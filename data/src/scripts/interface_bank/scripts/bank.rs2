[inv_button1,bank_main:inv] @bank_withdraw(last_slot, 1);
[inv_button2,bank_main:inv] @bank_withdraw(last_slot, 5);
[inv_button3,bank_main:inv] @bank_withdraw(last_slot, 10);
[inv_button4,bank_main:inv] @bank_withdraw(last_slot, ^max_32bit_int);
[inv_button5,bank_main:inv] @bank_withdraw(last_slot, null);
[inv_buttond,bank_main:inv] inv_movetoslot(bank, bank, last_slot, last_targetslot);
[if_button,bank_main:com_94] %bank_noted = 0;
[if_button,bank_main:com_93] %bank_noted = 1;
[if_close,bank_main] @closebank;

[label,openbank]
%bank_noted = 0;
~reorganize_inv(bank);
inv_transmit(inv, bank_side:inv);
inv_transmit(bank, bank_main:inv);
if_openmain_side(bank_main, bank_side);

[label,closebank]
inv_stoptransmit(bank_side:inv);
inv_stoptransmit(bank_main:inv);
queue(reorganize_bank, 0); // confirmed queued

// reorganize the bank like when you open the bank.
// this is because a player can move objs around in the bank
// to any slot that is available. so when they open the bank
// we reorganize all of the slots towards the left of the bank.
[queue,reorganize_bank]
~reorganize_inv(bank);

[label,bank_withdraw](int $slot, int $requested_number)
// Check if the slot was empty.
def_obj $item = inv_getobj(bank, $slot);
// How many did they want to withdraw?
if ($requested_number <= 0) {
    p_countdialog;
    if (last_int <= 0) return;
    $requested_number = last_int;
}
// How many have they got?
def_int $number = inv_total(bank, $item);
// How many should we withdraw?
if ($requested_number < $number) $number = $requested_number;
// Okay, withdraw it from the bank.
~bank_withdraw_request(inv, $item, $number, $slot);

[proc,bank_withdraw_request](inv $inv, obj $obj, int $amount, int $slot)
def_obj $cert_or_uncert = oc_uncert($obj);
if (%bank_noted ! 0) {
    $cert_or_uncert = oc_cert($obj);
}

if (%bank_noted = ^true & oc_cert($obj) = $obj) {
    mes("This item can not be withdrawn as a note."); // 2005
}

if (inv_itemspace($inv, $cert_or_uncert, $amount, inv_size($inv)) = false){
    if (inv_freespace($inv) = 0) {
        // https://youtu.be/Zv7Wh3TIDOc?t=22
        mes("You don't have enough inventory space.");
        return;
    } else if (oc_stackable($cert_or_uncert) = true) {
        // https://youtu.be/8vNeG5bHg0Q?t=443
        mes("You're not going to be able to carry all that!");
    } else {
        // https://youtu.be/HnEZFSGbYqM?t=79
        mes("You don't have enough inventory space to withdraw that many.");
    }
}

def_int $overflow = inv_itemspace2($inv, $cert_or_uncert, $amount, inv_size($inv));
def_int $withdraw = sub($amount, $overflow);
if ($withdraw > 0) {
    session_log(^log_moderator, "Withdrew <oc_debugname($obj)> x<tostring($withdraw)> from bank");
    switch_int (%bank_noted) {
        case 0 : inv_moveitem_uncert(bank, $inv, $obj, $withdraw);
        case default : inv_moveitem_cert(bank, $inv, $obj, $withdraw);
    }
}

[proc,bank_deposit_request](inv $inv, obj $obj, int $amount, int $slot)
def_int $overflow = inv_itemspace2(bank, oc_uncert($obj), $amount, ^max_32bit_int);
if (sub(^max_32bit_int, $amount) < inv_total(bank, $obj)) {
    mes("You already have a full stack of that item in the bank."); // 2024 osrs
} else if ($overflow >= $amount) {
    // https://youtu.be/2BgXLZD-xgo?t=28
    // https://youtu.be/Ep4gaI5ic1s?t=197
    mes("You don't have enough space in your bank account."); // osrs
    return;
}
def_int $deposit = sub($amount, $overflow);
if ($deposit > 0) {
    session_log(^log_moderator, "Deposited <oc_debugname($obj)> x<tostring($deposit)> into bank");
    inv_moveitem_uncert($inv, bank, $obj, $deposit);
}

[proc,bank_check_nobreak](obj $obj)(boolean)
if (~bank_find_obj_unbankable($obj) = true) {
    mes("A magical force prevents you from banking this item!"); // osrs
    return(true);
}

if (map_members = false) {
    // check for f2p bank limits.
    if ((inv_total(bank, oc_uncert($obj)) = 0) & (sub(inv_size(bank), inv_freespace(bank)) >= ^bank_free_slots)) {
        // https://youtu.be/yv-H5-IoRLs?t=9 (2006)
        // https://web.archive.org/web/20041126073829/http://img39.imageshack.us/img39/3412/firemaking.jpg (2004)
        mes("You need to subscribe for a members account if you want to");
        mes("store any more items in the bank.");
        return(true);
    }
}
return(false);

// returns if an obj is considered to be unbankable.
// experience lamps, quest items, etc.
[proc,bank_find_obj_unbankable](obj $obj)(boolean)
switch_obj ($obj) {
    case macro_genilamp : return(true); // cant bank xp lamps
    case default : return(false);
}
