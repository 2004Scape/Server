[inv_button1,bank_deposit:inv] // 1
def_namedobj $obj = inv_getobj(inv, last_slot);
~deposit_to_bank($obj, 1);

[inv_button2,bank_deposit:inv] // 5
def_namedobj $obj = inv_getobj(inv, last_slot);
~deposit_to_bank($obj, 5);

[inv_button3,bank_deposit:inv] // 10
def_namedobj $obj = inv_getobj(inv, last_slot);
~deposit_to_bank($obj, 10);

[inv_button4,bank_deposit:inv] // all
def_namedobj $obj = inv_getobj(inv, last_slot);
~deposit_to_bank($obj, ^max_32bit_int);

[inv_button5,bank_deposit:inv] // x
def_namedobj $obj = inv_getobj(inv, last_slot);
// TODO Withdraw-X

[inv_buttond,bank_deposit:inv] // dragging obj
inv_swap(inv, last_slot, last_useslot);

[proc,deposit_to_bank](namedobj $obj, int $amount)
if (~bank_find_obj_unbankable($obj) = true) {
    mes("A magical force prevents you from banking this item!");
    return;
}

$amount = min($amount, inv_total(inv, $obj));

def_int $free_to_play_count; def_int $members_count;
$free_to_play_count, $members_count = ~bank_count_objs_free_mems;

def_int $overflow = inv_itemspace2(bank, oc_uncert($obj), $amount, ^max_32bit_int);
if ($overflow = $amount) {
    if (map_members = false & $free_to_play_count >= 60 & oc_members($obj) = false) {
        mes("Your bank cannot hold your items."); // TODO Find real message for this.
        return;
    }
    // https://youtu.be/q1haFodDvPI?t=416
    mes("Your bank cannot hold your items.");
    return;
}

if ($overflow > 0) {
    // https://youtu.be/2BgXLZD-xgo?t=28
    // https://youtu.be/Ep4gaI5ic1s?t=197
    mes("You don't have enough space in your bank account.");
}
$amount = calc($amount - $overflow);

inv_moveitem_uncert(inv, bank, $obj, $amount);
