// TODO: Random Events handling, verify pick and ability to mine after stat boost wears off
// Sound: found_gem is used for gem rocks rather than finding a gem while mining

// Standard op
[oploc1,_mining_rock] @mining_firstswing;
// "hidden" op
[oploc3,_mining_rock] @mining_continue;

// Essence mine
[oploc1,loc_2491] @mining_firstswing;
[oploc3,loc_2491] @mining_continue;

[label,mining_firstswing]
def_int $is_empty = loc_param(mining_rock_empty);
if ($is_empty = ^true) {
    anim(null, 0);
    mes("There is currently no ore available in this rock.");
    sound_synth(prospect, 0, 0);
    return;
}
db_find(mining_table:rock, loc_type);
def_dbrow $data = db_findnext;
if ($data = null) {
    @nothing_interesting_happens;
}
if (inv_freespace(inv) < 1) {
    ~mesbox("Your inventory is too full to hold anymore <db_getfield($data, mining_table:ore_name, 0)>.");
    anim(null, 0);
    return;
}
def_int $levelreq = db_getfield($data, mining_table:rock_level, 0);;
if (stat(mining) < $levelreq) {
    anim(null, 0);
    ~mesbox("You need a mining level of <tostring($levelreq)> to mine this rock.");
    return;
}
def_obj $pickaxe = ~pickaxe_checker;
if ($pickaxe = null) {
    ~mesbox("You need a pickaxe to mine this rock. You do not have a pickaxe which you have the Mining level to use.");
    return;
}
def_int $mining_rate = oc_param($pickaxe, mining_rate);
if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + $mining_rate);
    if (%skill_sound = calc(map_clock + $mining_rate)) {
        sound_synth(mine_quick, 0, 0);
    }
    p_oploc(1);
} else {
    %skill_anim = %skill_clock;
    %skill_sound = calc(map_clock + $mining_rate + 1);
    anim(oc_param($pickaxe, mining_animation), 0);
    mes(" You swing your pick at the rock.");
    @mining_continue;
}


[label,mining_continue]
// mes("mining_continue: %skill_anim = <tostring(%skill_anim)>, %skill_sound = <tostring(%skill_sound)>");
// mes("%skill_clock = <tostring(%skill_clock)>, map_clock = <tostring(map_clock)>");
db_find(mining_table:rock, loc_type);
def_dbrow $data = db_findnext;
if ($data = null) {
    @nothing_interesting_happens;
}
if (inv_freespace(inv) < 1) {
    ~mesbox("Your inventory is too full to hold anymore <db_getfield($data, mining_table:ore_name, 0)>.");
    anim(null, 0);
    return;
}
def_int $levelreq = db_getfield($data, mining_table:rock_level, 0);
if (stat(mining) < $levelreq) {
    anim(null, 0);
    ~mesbox("You need a mining level of <tostring($levelreq)> to mine this rock.");
    return;
}
def_obj $pickaxe = ~pickaxe_checker;
if ($pickaxe = null) {
    ~mesbox("You need a pickaxe to mine this rock. You do not have a pickaxe which you have the Mining level to use.");
    return;
}
def_int $mining_rate = oc_param($pickaxe, mining_rate);

if (%skill_anim < calc(map_clock + 1)) {
    %skill_anim = calc(map_clock + $mining_rate + 1);
    %skill_sound = calc(map_clock + $mining_rate + 1);
    anim(oc_param($pickaxe, mining_animation), 0);
}
// if roll is due
if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + $mining_rate);
} else if (%skill_clock = map_clock) {
    def_int $successchance_low;
    def_int $successchance_high;
    $successchance_low, $successchance_high = db_getfield($data, mining_table:rock_successchance, 0);
    // roll for gem
    def_int $rand = 0;
    if (db_getfield($data, mining_table:allow_gem_roll, 0) = ^true) {
        def_obj $amulet = inv_getobj(worn, ^wearpos_front);
        def_int $chance = 256;
        if ($amulet = amulet_of_glory_1 | $amulet = amulet_of_glory_2 | $amulet = amulet_of_glory_3 | $amulet = amulet_of_glory_4) {
            $chance = 64;
        }
        $rand = random($chance);
        if ($rand = ^true) {
            def_namedobj $gem = ~mining_gem_table;
            def_string $gem_name = oc_param($gem, mining_gem_reward_name);
            inv_add(inv, $gem, 1);
            if (string_length($gem_name) > 0) {
                mes("You just found <$gem_name>!");
            }
        }
    }
    // if player rolls a success, and didnt roll a gem
    if (stat_random(stat(mining), $successchance_low, $successchance_high) = true & $rand ! ^true) {
        // delay player
        if (db_getfield($data, mining_table:rock_successdelay, 0) = ^true) {
            p_delay(0);
        }
        inv_add(inv, db_getfield($data, mining_table:rock_output, 0), 1);
        givexp(mining, db_getfield($data, mining_table:rock_exp, 0));
        // message
        if (string_length(db_getfield($data, mining_table:success_message, 0)) < 1) {
            mes("You mine some <db_getfield($data, mining_table:ore_name, 0)>.");
        } else {
            mes(db_getfield($data, mining_table:success_message, 0));
        }
        // deplete
        def_int $respawn = ~scale_by_playercount(db_getfield($data, mining_table:rock_respawnrate, 0));
        if ($respawn > 0) {
            // Suddent Silent says its sound_230
            sound_synth(sound_230, 0, 0);
            anim(null, 0);
            loc_del($respawn);
            if (loc_param(next_loc_stage) ! null) {
                loc_add(loc_coord, loc_param(next_loc_stage), loc_angle, loc_shape, $respawn);
            }
            return;
        }
    }
}
if (%skill_sound = calc(map_clock + $mining_rate)) {
    sound_synth(mine_quick, 0, 0);
}
if (%skill_sound < calc(map_clock + $mining_rate)) {
    sound_synth(mine_quick, 3, 0);
}
p_oploc(3);

[proc,mining_gem_table]()(namedobj)
def_int $rand = random(128);
if ($rand < 2) return (uncut_diamond);
if ($rand < 8) return (uncut_ruby);
if ($rand < 16) return (uncut_emerald);
if ($rand < 32) return (uncut_sapphire);