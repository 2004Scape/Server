[label,hetty_start]
~chatplayer("quiz", "I'm in search of a quest.");
~chatnpc("quiz", "Hmmmm... Maybe I can think of something for you.");
~chatnpc("quiz", "Would you like to become more proficient in the dark arts?");

def_int $option = ~p_choice3("Yes help me become one with my darker side.", 1, "No I have my principles and honour.", 2, "What, you mean improve my magic?", 3);
if($option = 1) {
    ~chatplayer("long", "Yes help me become one with my darker side.");
    @hetty_step_1;
} else if($option = 2) {
    ~chatplayer("quiz", "No I have my principles and honour.");
    ~chatnpc("sad", "Suit yourself, but you're missing out.");
} else if($option = 3) {
    ~chatplayer("default", "What, you mean improve my magic?");
    ~chatnpc("default", "Yes improve your magic.");
    ~chatnpc("default", "Do you have no sense of drama?");
    $option = ~p_choice3("Yes I'd like to improve my magic.", 1, "No, I'm not interested.", 2, "Show me the mysteries of the dark arts.", 3);
    if($option = 1) {

    } else if($option = 2) {

    } else if($option = 3) {

    }
}

[label,hetty_step_1]
%hetty_progress = 1;
~send_quest_progress(quest_journal:hetty, %hetty_progress, 3);

~chatnpc("neutral", "Okay, I'm going to make a potion to help bring out|your darker self.");
~chatnpc("quiz", "You will need certain ingredients.");
~chatplayer("quiz", "What do I need?");
~chatnpc("quiz", "You need an eye of newt, a rat's tail, an onion... Oh|and a peice of burnt meat.");
~chatplayer("long", "Great, I'll go and get them.");

[label,hetty_step_2]
~chatplayer("long", "I've been looking for those ingredients.");
~chatnpc("long", "So what have you found so far?");

if(inv_total(inv, obj_2146) > 0 & inv_total(inv, rats_tail) > 0 & inv_total(inv, eye_of_newt) > 0 & inv_total(inv, onion) > 0) {
    ~chatplayer("long", "In fact I have everything!");
    ~chatnpc("long", "Excellent, can I have them then?");
    ~mesbox("You pass the ingredients to Hetty and she puts them all into her|cauldron. Hetty closes her eyes and begins to chant. The cauldron|bubbles mysteriously.");
    inv_del(inv, obj_2146, 1);
    inv_del(inv, rats_tail, 1);
    inv_del(inv, eye_of_newt, 1);
    inv_del(inv, onion, 1);
    %hetty_progress = 2;
    ~chatplayer("quiz", "Well, is it ready?");
    ~chatnpc("long", "Ok, now drink from the cauldron.");
} else if(inv_total(inv, obj_2146) = 0 & inv_total(inv, rats_tail) = 0 & inv_total(inv, eye_of_newt) = 0 & inv_total(inv, onion) = 0){
    ~chatplayer("sad", "I'm afraid I don't have any of them yet.");
    ~chatnpc("sad", "Well I can't make the potion without them! Remember...|You need an eye of newt, a rat's tail, an onion, and a|piece of burnt meat. Off you go dear!");
} else {
    def_string $message = "";
    if(inv_total(inv, rats_tail) = 0) {
        $message = append($message, "I don't have a rat's tail, ");
    } else {
        $message = append($message, "I have the rat's tail (ewww), ");
    }

    if(inv_total(inv, obj_2146) = 0) {
        $message = append($message, "I don't have any burnt meat, ");
    } else {
        $message = append($message, "I have the burnt meat, ");
    }

    if(inv_total(inv, onion) = 0) {
        $message = append($message, "I|don't have an onion, ");
    } else {
        $message = append($message, "I|have an onion, ");
    }

    if(inv_total(inv, eye_of_newt) = 0) {
        $message = append($message, "and I don't have an eye of newt.");
    } else {
        $message = append($message, "and I have the eye of newt, yum!");
    }
    ~chatplayer("quiz", $message);
    ~chatnpc("quiz", "Great, but I'll need the other ingredients as well.");
}

[oploc1,loc_2024]
if(%hetty_progress = 2) {
    ~mesbox("You drink from the cauldron, it tastes horrible! You feel yourself imbued with power.");
    givexp(magic, 3250);
    %hetty_progress = 3;
    ~send_quest_complete(quest_journal:hetty, eye_of_newt, 1, "You have completed the\\nWitches Potion Quest!");
} else {
    ~chatplayer("quiz", "As nice as that looks I think I'll give it a miss for now.");
}