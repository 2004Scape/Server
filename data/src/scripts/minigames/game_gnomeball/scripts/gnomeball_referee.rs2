[label,trail_gnome_ball_ref]
if(inv_total(inv, trail_clue_medium_anagram008_challenge) > 0) {
    @trail_challengenpc_prompt("<p,neutral>Please enter the answer to the question.", "<p,happy>Well done!", "<p,angry>Wrong answer!",
    "The ref has given you another clue scroll!", trail_clue_medium_anagram008, trail_clue_medium_anagram008_challenge);
}
~chatnpc("<p,neutral>Right, you haven't finished quite yet.");
~chatplayer("<p,confused>Ugh.");
~chatnpc("<p,neutral>Answer this for me.");
inv_add(inv, trail_clue_medium_anagram008_challenge, 1);
~objbox(trail_clue_medium_anagram008_challenge, "The ref has given you a challenge scroll!", 250, 0, 0);

[opnpc1,gnome_ball_referee]
if(inv_total(inv, trail_clue_medium_anagram008) > 0) {
    @trail_gnome_ball_ref;
}
if (%gnomeball_progress = ^gnomeball_not_started) {
    @gnomeball_ref_first_start;
}
if (%gnomeball_progress = ^gnomeball_game_started) {
    if (~gnomeball_inplay(npc_uid) = true | inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) {
        ~chatnpc("<p,neutral>You have a ball in play.|Do you need to hear the rules again?");
        @multi2("Yes please.", gnomeball_hear_rules, "No thanks.", gnomeball_no_hear_rules);
    } else {
        ~chatnpc("<p,happy>Have a new ball!");
        ~gnomeball_game_start;
        return;
    }
    
}
~chatnpc("<p,neutral>Hi, welcome back to Gnomeball. Would you like to hear the rules again?");
@multi3("Yes please.", gnomeball_hear_rules, "I just want to play.", gnomeball_just_want_to_play, "No, I don't want to play.", gnomeball_dont_want_to_play);



// doesnt let you enter with a full inventory
// upon entering, it unequips right hand slot
// cant equip items with right hand slot when inside gnome ball area


[proc,gnomeball_game_start]
%gnomeball_progress = ^gnomeball_game_started;
if (inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) {
    ~chatnpc("<p,neutral>The ball you have in your hand is fine to play with...|Game on.");
    return;
} else if (inv_total(inv, ball_gnomeball_game) > 0) {
    ~chatnpc("<p,neutral>Ahh I see you have your own ball. We can't play with|that; I'll give it back when you leave.");
    inv_del(inv, ball_gnomeball_game, inv_total(inv, ball_gnomeball_game));
}
if_close;
npc_say("Peeeeeeeeeep");
sound_synth(whistle, 0, 0);
mes("The ref throws the ball into the air.");
npc_anim(midget_throw, 0);
~player_projectile(npc_coord, coord, uid, spotanim_55, 36, 40, 34, 60, 30, 11, 0);
anim(human_catch, 27);
p_delay(0);
mes("You jump up and catch it.");
sound_synth(catch, 0, 0);
p_delay(0);
inv_setslot(worn, ^wearpos_rhand, ball_gnomeball_game, 1);
facesquare(npc_coord);
~update_all(inv_getobj(worn, ^wearpos_rhand));

// other npcs complain about not getting passed to?
p_delay(0);


[label,gnomeball_ref_first_start]
~chatnpc("<p,happy>Hi, welcome to Gnomeball. It's pretty simple really: You take the ball from me, charge at the gnome defence and try to throw the ball through the net using your ranged skill.");
~chatnpc("<p,neutral>It's a rough game but great fun. It's also great for improving agility. So do you fancy a game?");
def_int $choice = ~p_choice2("Looks too dangerous for me!", 1, "Okay then, I'll have a go.", 2);

if ($choice = 1) {
    ~chatplayer("<p,neutral>Looks too dangerous for me!");
    ~chatnpc("<p,neutral>You may be right, we've seen humans die on this field.");
    return;
}

~chatplayer("<p,neutral>Okay then, I'll have a go.");
~chatnpc("<p,neutral>Great stuff! There are few rules to Gnomeball, so it can get a bit rough.");
~chatnpc("<p,neutral>You can pass to the green winger gnomes. Then you can make a run and they'll pass back. Otherwise, if you're feeling brave, you can just charge and dodge.");
~chatplayer("<p,neutral>Sounds easy enough.");
~chatnpc("<p,neutral>The main aim is to leave with no broken limbs. I think you should be fine.");
%gnomeball_progress = ^gnomeball_talked_to_ref;
~gnomeball_game_start;

[label,gnomeball_hear_rules]
~chatnpc("<p,happy>It's pretty simple really: You take the ball from me, charge at the gnome defence and try to throw the ball through the net using your ranged skill.");
~chatnpc("<p,neutral>It's a rough game but great fun. It's also great for improving agility.");
~chatnpc("<p,neutral>You can pass to the green winger gnomes. Then you can make a run and they'll pass back. Otherwise, if you're feeling brave, you can just charge and dodge.");
~chatplayer("<p,neutral>Sounds easy enough.");
~chatnpc("<p,neutral>The main aim is to leave with no broken limbs. I think you should be fine.");
~gnomeball_game_start;

[label,gnomeball_no_hear_rules]
~chatplayer("<p,neutral>No thanks.");
~chatnpc("<p,neutral>Okay, good luck.");

[label,gnomeball_just_want_to_play]
~chatplayer("<p,neutral>I just want to play.");
~chatnpc("<p,neutral>Okay... Let's play GNOMEBALL!");
~gnomeball_game_start;

[label,gnomeball_dont_want_to_play]
~chatplayer("<p,neutral>No, I don't want to play.");
~chatnpc("<p,neutral>Okay, never mind. Maybe next time...");