[ai_opplayer2,gnome_baller1] @npc_gnomeball_tackle;
[ai_opplayer2,gnome_baller2] npc_setmode(null);
[ai_opplayer2,gnome_baller3] npc_setmode(null);
[ai_opplayer2,gnome_baller4] npc_setmode(null);
[ai_opplayer2,gnome_baller5] @npc_gnomeball_tackle;
[ai_opplayer2,gnome_baller6] npc_setmode(null);
[ai_opplayer2,gnome_baller7] npc_setmode(null);
[ai_opplayer2,gnome_baller8] npc_setmode(null);
[ai_opplayer2,gnome_baller9] @npc_gnomeball_tackle;
[ai_opplayer2,gnome_baller10] npc_setmode(null);
[ai_opplayer2,gnome_baller11] npc_setmode(null);
[ai_opplayer2,gnome_baller12] npc_setmode(null);

[label,npc_gnomeball_tackle]
npc_setmode(null);
if (%npc_action_delay > map_clock | ~npc_can_attack_player = false) {
    return;
}
if_close;
if (p_finduid(uid) = true) {
    if (random(2) = 0 & inv_getobj(worn, ^wearpos_rhand) = gnomeball) { // 50% guess for now
        ~npc_gnomeball_tackle_success;
    } else {
        ~npc_gnomeball_tackle_fail;
    }
}

[proc,npc_gnomeball_tackle_fail]
def_npc $old = npc_type;
npc_anim(midget_seq_207, 0);
npc_changetype(npc_param(gnome_fallen));
npc_delay(5);
npc_changetype($old);
npc_anim(midget_seq_209, 0);
npc_delay(1);
%npc_aggressive_player = null;

[proc,npc_gnomeball_tackle_success]
queue(damage_player, 0, randominc(3));
anim(seq_779, 0);
npc_changetype(npc_param(gnome_hasball));
npc_anim(midget_seq_210, 0);
inv_delslot(worn, ^wearpos_rhand);
~update_all(gnomeball);
npc_setmode(wander);
%npc_aggressive_player = uid;
%npc_lastcombat = ^max_32bit_int;


[opnpc1,gnome_baller1] @player_gnomeball_tackle;
[opnpc1,gnome_baller5] @player_gnomeball_tackle;
[opnpc1,gnome_baller9] @player_gnomeball_tackle;

[label,player_gnomeball_tackle]
if (inv_getobj(worn, ^wearpos_rhand) = gnomeball) {
    mes("You may not tackle when you have a ball.");
    return;
}
if (add(%npc_lastcombat, 2) > map_clock) {
    mes("That gnome is being tackled.");
    return;
}
%npc_aggressive_player = null;
%npc_lastcombat = map_clock;
p_delay(0);
if (random(2) = 0) { // 50% guess for now
    ~player_gnomeball_tackle_success(npc_type);
} else {
    ~player_gnomeball_tackle_fail;
}


[proc,player_gnomeball_tackle_fail]
anim(seq_780, 33);
npc_anim(midget_seq_204, 33);
// SoundEffect(name = "gnomeball_tackled_gnome_miss")

[proc,player_gnomeball_tackle_success](npc $old)
anim(seq_778, 33);
npc_anim(midget_seq_203, 30);
// SoundEffect(name = "gnomeball_tackled_gnome")
npc_setmode(none);
p_delay(1);
~update_all(gnomeball);
npc_changetype(nc_param($old, gnome_tackled));
npc_delay(10);
npc_setmode(null);
npc_changetype($old);
npc_anim(midget_seq_206, 0);


[opnpc1,gnome_baller2] @player_gnomeball_tackle_carrier;
[opnpc1,gnome_baller6] @player_gnomeball_tackle_carrier;
[opnpc1,gnome_baller10] @player_gnomeball_tackle_carrier;

[label,player_gnomeball_tackle_carrier]
if (inv_getobj(worn, ^wearpos_rhand) = gnomeball) {
    mes("You may not tackle when you have a ball.");
    return;
}
if (add(%npc_lastcombat, 2) > map_clock) {
    mes("That gnome is being tackled.");
    return;
}
%npc_lastcombat = map_clock;
p_delay(0);
if (random(2) = 0) { // 50% guess for now
    inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
    npc_changetype(npc_param(gnome_original));
    ~player_gnomeball_tackle_success(npc_type);
    %npc_aggressive_player = null;
} else {
    ~player_gnomeball_tackle_fail;
}
