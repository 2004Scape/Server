[ai_opplayer2,gnome_baller1] @npc_gnomeball_tackle;
[ai_opplayer2,gnome_baller5] @npc_gnomeball_tackle;
[ai_opplayer2,gnome_baller9] @npc_gnomeball_tackle;

[label,npc_gnomeball_tackle]
npc_setmode(null);
npc_delay(0);
if_close;
if (p_finduid(uid) = true) {
    if (stat_random(stat(agility), 30, 200) = false & inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) { // 50% guess for now
        ~npc_gnomeball_tackle_success;
    } else {
        ~npc_gnomeball_tackle_fail;
    }
}

[proc,npc_gnomeball_tackle_fail]
npc_anim(midget_human_tackle_fail, 0);
npc_changetype_keepall(npc_param(gnome_fallen), 5);
npc_delay(5);
npc_anim(midget_human_tackle_fail_getup, 0);
npc_delay(0);
%npc_attacking_uid = null;

[proc,npc_gnomeball_tackle_success]
queue(damage_player, 0, add(random(2), 1));
anim(human_tackled, 0);
npc_changetype_keepall(npc_param(gnome_hasball), ^max_32bit_int); // perm? 
npc_anim(midget_human_tackle, 0);
inv_delslot(worn, ^wearpos_rhand);
~update_all(ball_gnomeball_game);
npc_setmode(null);
%npc_attacking_uid = uid;
%npc_lastcombat = ^max_32bit_int;


[opnpc1,gnome_baller1] @player_gnomeball_tackle(30, 200);
[opnpc1,gnome_baller5] @player_gnomeball_tackle(30, 200);
[opnpc1,gnome_baller9] @player_gnomeball_tackle(30, 220);
[opnpct,gnomeball:com_5] 
switch_npc (npc_type) {
    case gnome_baller1, gnome_baller5 : @player_gnomeball_tackle(30, 200);
    case gnome_baller9 : @player_gnomeball_tackle(30, 220);
    case gnome_baller2, gnome_baller6, gnome_baller10 : @player_gnomeball_tackle_carrier;
    case default : mes("You can only tackle gnome ballers."); // complete guess
}

[opplayert,gnomeball:com_5] 
mes("You can only tackle gnome ballers."); // complete guess

[label,player_gnomeball_tackle](int $low, int $high)
if (inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) {
    mes("You may not tackle when you have a ball.");
    return;
}
if (add(%npc_lastcombat, 4) > map_clock) {
    mes("That gnome is being tackled.");
    return;
}
%npc_attacking_uid = null;
%npc_lastcombat = map_clock;

npc_setmode(none);
npc_walk(npc_coord);
p_delay(0);
npc_facesquare(coord);
facesquare(npc_coord);
p_delay(0);
if (stat_random(stat(agility), $low, $high) = true) { // 50% guess for now
    ~player_gnomeball_tackle_success;
} else {
    ~player_gnomeball_tackle_fail;
}


[proc,player_gnomeball_tackle_fail]
anim(human_gnome_tackle_fail, 33);
npc_anim(midget_dodge_tackle, 33);
npc_delay(0);
// SoundEffect(name = "gnomeball_tackled_gnome_miss")

[proc,player_gnomeball_tackle_success]
anim(human_gnome_tackle, 33);
npc_anim(midget_tackled, 30);
p_delay(0);
~update_all(ball_gnomeball_game);
npc_changetype_keepall(npc_param(gnome_tackled), 9);
npc_delay(9);
npc_setmode(null);
npc_anim(midget_getup, 0);
npc_delay(0);


[opnpc1,gnome_baller2] @player_gnomeball_tackle_carrier;
[opnpc1,gnome_baller6] @player_gnomeball_tackle_carrier;
[opnpc1,gnome_baller10] @player_gnomeball_tackle_carrier;

[label,player_gnomeball_tackle_carrier]
if (inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) {
    mes("You may not tackle when you have a ball.");
    return;
}
if (add(%npc_lastcombat, 4) > map_clock) {
    mes("That gnome is being tackled.");
    return;
}
%npc_lastcombat = map_clock;
npc_setmode(none);
npc_walk(npc_coord);
p_delay(0);
npc_facesquare(coord);
facesquare(npc_coord);
p_delay(0);
if (stat_random(stat(agility), 30, 220) = true) { // 50% guess for now
    inv_setslot(worn, ^wearpos_rhand, ball_gnomeball_game, 1);
    npc_changetype_keepall(npc_param(gnome_original), ^max_32bit_int);
    ~player_gnomeball_tackle_success;
    %npc_attacking_uid = null;
} else {
    ~player_gnomeball_tackle_fail;
}
