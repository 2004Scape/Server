[if_button,gnomeball:com_6]
if_close;
if (p_finduid(uid) = true) {
    if (loc_find(^gnomeball_goal_coord, gnome_goal) = true) {
        @gnomeball_shoot;
    }
}

[label,gnomeball_shoot]
if (lineofsight(coord, loc_coord) = false) {
    return;
}
if (~inzone_coord_pair_table(gnomeball_zones, coord) = false) {
    mes("You may only score while on the pitch.");
    return;
}
if (inv_getobj(worn, ^wearpos_rhand) ! ball_gnomeball_game) {
    mes("You need a ball in your hand to throw.");
    return;
}
if (coordx(coord) >= coordx(loc_coord)) {
    mes("You must be in front of the goal.");
    return;
}
if (distance(loc_coord, coord) > 10) {
    mes("The goal is too far away from here."); // complete guess
    return;
}
// pos: 2404 3488
p_stopaction;
// https://youtu.be/LWfcA_C2bJY?si=1xvGzgrQ53VgBsAx&t=50
mes("You throw the ball at the goal.");
cam_moveto(movecoord(loc_coord, 3, 0, 0), 300, 100, 100);
cam_lookat(coord, 250, 100, 100);
p_delay(1);
facesquare(loc_coord);
anim(human_throw_gnomeball, 0);
sound_synth(throw, 0, 10);
def_coord $land_coord = ~gnomeball_shoot_random(loc_coord);
def_int $duration = ~coord_projectile(coord, $land_coord, spotanim_55, 40, 2, 41, 15, 0, 11, 5);

inv_delslot(worn, ^wearpos_rhand);
~update_all(ball_gnomeball_game);
p_delay(divide($duration, 30));
if ($land_coord = loc_coord) {
    mes("and score!"); // aug 2004, https://youtu.be/LWfcA_C2bJY?si=1xvGzgrQ53VgBsAx&t=50
    %gnomeball = min(add(max(%gnomeball, 0), 1), 5);
    if (%gnomeball = 5) {
        mes("You win!");
        inv_add(foodshop, ball_gnomeball_game, 1);
        %gnomeball = 0;
        stat_advance(ranged, 300);
        stat_advance(agility, 300);
    } else {
        stat_advance(ranged, multiply(add(%gnomeball, 3), 10));
    }
    cam_moveto(movecoord(loc_coord, -5, 0, 2), 300, 10, 20);
    cam_lookat(movecoord(loc_coord, -7, 0, 5), 250, 10, 20);
    // todo: goal jingle
    if (npc_find(^gnomeball_cheer_coord1, gnome_cheerleader, 0, 0) = true) {
        npc_say("GOAL");
    }
    if (npc_find(^gnomeball_cheer_coord2, gnome_cheerleader, 0, 0) = true) {
        npc_say("GOAL");
    }  
} else {
    mes("and miss."); // guess based off score mes
}
p_delay(3);
// cam_lookat(movecoord(loc_coord, -68, 0, -40), 0, 0, 0);
// cam_moveto(movecoord(loc_coord, 3, 0, 0), 300, 100, 0);
cam_reset;


[proc,gnomeball_shoot_random](coord $coord)(coord)
// from 100 goals at 95 ranged, 10 tiles away:
// - 95 goals, 5 misses
// from 100 goals at 1 ranged, 10 tiles away:
// - 9 goals, 91 misses
// ash says distance matters, but it doesnt seem like it matters a huge amount
if (stat_random(stat(ranged), 25, sub(300, multiply(distance(coord, $coord), 5))) = true) {
    return($coord);
}

def_int $x = add(random(1), 1);
def_int $z = add(random(1), 1);
if (random(2) = 0) {
    $x = multiply($x, -1);
}
if (random(2) = 0) {
    $z = multiply($z, -1);
}

return(movecoord($coord, $x, 0, $z));