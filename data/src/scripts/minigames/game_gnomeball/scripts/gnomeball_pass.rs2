[opplayeru,ball_gnomeball_game] @pvp_gnomeball_pass;
[applayeru,ball_gnomeball_game] @pvp_gnomeball_pass;
[opplayert,gnomeball:com_7] @pvp_gnomeball_pass;
[applayert,gnomeball:com_7] @pvp_gnomeball_pass;

[label,pvp_gnomeball_pass]
def_boolean $player1_ingame = ~inzone_coord_pair_table(gnomeball_zones, coord);
def_boolean $player2_ingame = ~.inzone_coord_pair_table(gnomeball_zones, .coord);
if (inv_total(worn, ball_gnomeball_game) < 1 & $player1_ingame = true) {
    mes("You need a ball in your hand to throw.");
    return;
}
if (inv_getobj(worn, ^wearpos_rhand) ! null & $player1_ingame = false) {
    mes("You need your right hand free to pass a ball."); // osrs
    return;
}
// https://oldschool.runescape.wiki/w/Update:Patch_Notes_(3_October_2013)
// if close early: "Players can no longer use a gnomeball to interrupt other players' Make-X actions without the gnomeball being thrown."
.if_close;
if (.p_finduid(.uid) = false) {
    mes("<.displayname> is busy."); // osrs
    return;
}
if ($player1_ingame = true & $player2_ingame = false) {
    mes("<.displayname> is not playing gnomeball.");
    return;
}
if ($player1_ingame = false & $player2_ingame = true) {
    mes("You cannot pass to someone in the game from here.");
    return;
}
if (.inv_getobj(worn, ^wearpos_rhand) ! null) {
    mes("<.displayname> has something in <.text_gender("his", "her")> hand.");
    return;
}
if ($player1_ingame = true & $player2_ingame = true) {
    if (~gnomeball_inplay(null) = true) {
        mes("They already have a ball in play.");
        return;
    }
    inv_delslot(worn, ^wearpos_rhand);
    ~update_all(ball_gnomeball_game);
} else {
    inv_del(inv, ball_gnomeball_game, 1);
    // mes("You throw the ball."); // didnt exist in 2005
}
mes("You throw the ball in play."); // was always this mes https://youtu.be/0lgauPFMzQ8?t=69

anim(human_throw_gnomeball, 0);
def_int $duration = ~player_projectile(coord, .coord, .uid, gnomeball_travel, 40, 40, 41, 15, 5, 11, 5);
sound_synth(throw, 0, 0);
.sound_synth(throw, 0, 0);

//.p_delay(0); todo: engine change for protected player 2 pointer
.facesquare(coord);
.anim(human_catch, 0);
.inv_setslot(worn, ^wearpos_rhand, ball_gnomeball_game, 1);
~.update_all(ball_gnomeball_game);
sound_synth(catch, 0, 0);
.sound_synth(catch, 0, 0);
.p_delay(1);