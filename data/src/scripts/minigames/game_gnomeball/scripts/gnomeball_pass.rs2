[opplayeru,gnomeball] @pvp_gnomeball_pass;
[applayeru,gnomeball] @pvp_gnomeball_pass;
[opplayert,gnomeball:com_7] @pvp_gnomeball_pass;
[applayert,gnomeball:com_7] @pvp_gnomeball_pass;

[label,pvp_gnomeball_pass]
if (.p_finduid(.uid) = false) {
    mes("<.displayname> is busy."); // osrs
    return;
}
def_boolean $player1_ingame = ~inzone_coord_pair_table(gnomeball_zones, coord);
def_boolean $player2_ingame = ~.inzone_coord_pair_table(gnomeball_zones, .coord);
if ($player1_ingame = true & $player2_ingame = false) {
    mes("<.displayname> is not playing gnomeball.");
    return;
}
if ($player1_ingame = false & $player2_ingame = true) {
    mes("You cannot pass to someone in the game from here.");
    return;
}
if (.inv_getobj(worn, ^wearpos_rhand) ! null) {
    mes("<.displayname> has something in <text_gender("his", "her")> hand.");
    return;
}
if ($player1_ingame = true & $player2_ingame = true) {
    if (~gnomeball_inplay(null) = true) {
        mes("They already have a ball in play.");
        return;
    }
    mes("You throw the ball in play.");
    inv_delslot(worn, ^wearpos_rhand);
    ~update_all(gnomeball);
} else {
    inv_del(inv, gnomeball, 1);
    mes("You throw the ball.");
}

anim(seq_783, 0);
def_int $duration = ~player_projectile(coord, .coord, .uid, spotanim_55, 40, 40, 41, 15, 5, 11, 5);
sound_synth(throw, 0, 0);
.sound_synth(throw, 0, 0);

//.p_delay(0); todo: engine change for protected player 2 pointer
.facesquare(coord);
.anim(seq_782, 0);
.inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
~.update_all(gnomeball);
sound_synth(sound_152, 0, 0);
.sound_synth(sound_152, 0, 0);
.p_delay(1);