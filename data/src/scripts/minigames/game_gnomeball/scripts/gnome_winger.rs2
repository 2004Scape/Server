[opnpc1,gnome_winger1]
mes("The gnome is not interested in chatting."); // osrs
[opnpc1,gnome_winger2] ~npc_gnomeball_pass;

[opnpct,gnomeball:com_7] @player_gnomeball_pass;
[apnpct,gnomeball:com_7] @player_gnomeball_pass;

[label,player_gnomeball_pass]
if (npc_type = gnome_winger2) {
    mes("The gnome already has a ball."); // complete guess. Shouldnt happen anyway?
    return;
}
if (npc_type ! gnome_winger1) {
    mes("You can only pass the ball to the wingers."); // complete guess
    return;
}
if (inv_getobj(worn, ^wearpos_rhand) ! gnomeball) {
    mes("You need a ball in your hand to throw."); // osrs
    return;
}

mes("You throw the ball.");
anim(seq_783, 0);
npc_anim(midget_seq_200, 28);

def_int $duration = ~npc_projectile(coord, npc_uid, spotanim_55, 40, 36, 41, 15, 5, 11, 5);
sound_synth(sound_152, 0, 28);
inv_delslot(worn, ^wearpos_rhand);
~update_all(gnomeball);
npc_facesquare(coord);
%npc_attacking_uid = uid;
npc_delay(divide($duration, 30));
npc_changetype(gnome_winger2);
npc_settimer(6);
// npc_queue(5, 0, 6);


[ai_timer,gnome_winger2]
if (finduid(%npc_attacking_uid) = true) {
    ~npc_gnomeball_pass;
}

[queue,player_catch_gnomeball]
inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
~update_all(gnomeball);

[proc,npc_gnomeball_pass]
if_close;
if (p_finduid(%npc_attacking_uid) = true) {
    if (~inzone_coord_pair_table(gnomeball_zones, coord) = true) {
        mes("The gnome passes the ball back.");
        anim(seq_782, 22);
        facesquare(npc_coord);

        npc_anim(midget_seq_201, 0);
        sound_synth(sound_152, 0, 22);
        sound_synth(throw, 0, 0);
        ~player_projectile(npc_coord, coord, uid, spotanim_55, 36, 40, 34, 15, 15, 11, 2);
        p_delay(0);
        npc_facesquare(coord);
        npc_changetype(gnome_winger1);
        p_delay(1);
        inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
        ~update_all(gnomeball);
    }
}