[opnpc1,gnome_winger1]
mes("The gnome is not interested in chatting."); // osrs
[opnpc1,gnome_winger2]
mes("The gnome is not interested in chatting."); // osrs

[opnpct,gnomeball:com_7] @player_gnomeball_pass;
[apnpct,gnomeball:com_7] @player_gnomeball_pass;

[label,player_gnomeball_pass]
if (inv_getobj(worn, ^wearpos_rhand) ! gnomeball) {
    mes("You need a ball in your hand to throw."); // osrs
    return;
}
if (npc_type = gnome_winger2) {
    mes("The gnome already has a ball."); // complete guess. Shouldnt happen anyway?
    return;
}
if (npc_type ! gnome_winger1) {
    mes("You can only pass the ball to the wingers."); // complete guess
    return;
}
if (add(%npc_lastcombat, 8) > map_clock) {
    mes("This gnome is helping someone else at the moment."); // osrs if he passes back almost instantly you can see this message
    return;
}

mes("You throw the ball.");
anim(seq_783, 0);
npc_anim(midget_seq_200, 28);

def_int $duration = ~npc_projectile(coord, npc_uid, spotanim_55, 40, 36, 41, 15, 5, 11, 5);
def_int $delay = sub(divide($duration, 30), 1);
sound_synth(sound_152, 0, 28);
inv_delslot(worn, ^wearpos_rhand);
~update_all(gnomeball);
npc_facesquare(coord);
%npc_attacking_uid = uid;
%npc_lastcombat = map_clock;

npc_delay($delay);
npc_changetype(gnome_winger2, ^max_32bit_int);

// no idea if this is how they do it but if you pass before 10 ticks after u first 
// passed to them, they will always pass 10 ticks later
def_int $throw = add(random(10), 2);
def_int $last = sub(add(%npc_lastattack, 10), map_clock);
if ($last > 1) {
    $throw = $last;
}
npc_settimer(add($throw, 1));

[queue,player_catch_gnomeball]
inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
~update_all(gnomeball);

[ai_timer,gnome_winger2]
if (finduid(%npc_attacking_uid) = false | ~inzone_coord_pair_table(gnomeball_zones, coord) = false) {
    if (add(%npc_lastcombat, 60) < map_clock & npc_type = gnome_winger2) {
        %npc_attacking_uid = null;
        npc_changetype(gnome_winger1, ^max_32bit_int);
        npc_del;
        return;
    }
}
if (finduid(%npc_attacking_uid) = true) {
    // yes it checks inv too, i checked with smuggling extra gnomeball in osrs with telegrab
    if (~inzone_coord_pair_table(gnomeball_zones, coord) = true & inv_getobj(worn, ^wearpos_rhand) = null & inv_total(inv, gnomeball) < 1) {
        ~npc_gnomeball_pass;
        return;
    }
}
npc_settimer(20);

[proc,npc_gnomeball_pass]
if_close;
if (p_finduid(%npc_attacking_uid) = true) {
    %npc_lastattack = map_clock;
    mes("The gnome passes the ball back.");
    anim(seq_782, 22);
    facesquare(npc_coord);

    npc_settimer(0);
    npc_anim(midget_seq_201, 0);
    sound_synth(sound_152, 0, 22);
    sound_synth(throw, 0, 0);
    ~player_projectile(npc_coord, coord, uid, spotanim_55, 36, 40, 34, 15, 15, 11, 2);
    p_delay(0);
    npc_facesquare(coord);
    p_delay(0);
    npc_changetype(gnome_winger1, ^max_32bit_int);
    p_delay(0);
    inv_setslot(worn, ^wearpos_rhand, gnomeball, 1);
    ~update_all(gnomeball);
    %npc_attacking_uid = null;
}
