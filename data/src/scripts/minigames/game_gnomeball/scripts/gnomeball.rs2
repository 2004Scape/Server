[mapzoneexit,0_37_54]
if (%gnomeball_progress = ^gnomeball_game_started) {
    mes("You hear the ref call you out of the game as you leave.");
    def_int $ball_inv = inv_total(inv, ball_gnomeball_game);
    def_int $ball_worn = inv_total(worn, ball_gnomeball_game);
    if ($ball_inv > 0) {
        inv_del(inv, ball_gnomeball_game, $ball_inv);
    }
    if ($ball_worn > 0) {
        inv_del(worn, ball_gnomeball_game, $ball_worn);
        ~update_all(null);
    }
    ~gnomeball_reset;
}

[oploc1,gnomeball_gate]
def_obj $rhand = inv_getobj(worn, ^wearpos_rhand);
def_obj $lhand = inv_getobj(worn, ^wearpos_lhand);
def_boolean $exiting = ~check_axis(coord, loc_coord, loc_angle);
if ($exiting = false) { 
    if (~inv_slotspace(worn, inv, worn_hands) = false) {
        mes("You do not have free inventory space for what you carry.");
        return;
    }
    if ($rhand ! null) {
        ~unequip(^wearpos_rhand);
    }
    if ($lhand ! null) {
        ~unequip(^wearpos_lhand);
    }
    ~open_and_close_door(loc_param(next_loc_stage), $exiting, false);
    if (inv_total(worn, ball_gnomeball_game) > 0 | inv_total(inv, ball_gnomeball_game) > 0) {
        mes("The ref takes your ball for safe keeping. You will get it back when you leave.");
        inv_delslot(worn, ^wearpos_rhand);
        inv_del(inv, ball_gnomeball_game, inv_total(inv, ball_gnomeball_game));
        %gnomeball_owedball = ^true;
    }
} else {
    ~open_and_close_door(loc_param(next_loc_stage), $exiting, false);
    if (%gnomeball_owedball = ^true) {
        // give free gnomeball
        if (inv_freespace(inv) = 0) {
            ~gnomeball_reset;
            inv_setslot(worn, ^wearpos_rhand, ball_gnomeball_game, 1);
            return; // https://youtu.be/acS5ZgHsZQM?t=190
        } else {
            inv_add(inv, ball_gnomeball_game, 1);
        }
    } else  {
        def_int $ball_inv = inv_total(inv, ball_gnomeball_game);
        def_int $ball_worn = inv_total(worn, ball_gnomeball_game);
        if ($ball_inv > 0) {
            inv_del(inv, ball_gnomeball_game, $ball_inv);
        }
        if ($ball_worn > 0) {
            inv_del(worn, ball_gnomeball_game, $ball_worn);
        }
    }
    ~gnomeball_reset;
}
~update_all($rhand);

[proc,gnomeball_reset]
if (%gnomeball_progress > ^gnomeball_not_started) {
    %gnomeball_progress = ^gnomeball_talked_to_ref;
}
if (inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game) {
    inv_delslot(worn, ^wearpos_rhand);
}
%gnomeball_owedball = ^false;
%gnomeball = 0;
npc_findallany(^gnomeball_centre_coord, 14, ^vis_off);
while (npc_findnext = true) {
    if (%npc_attacking_uid = uid) {
        if (npc_param(gnome_original) ! null & npc_type = nc_param(npc_param(gnome_original), gnome_hasball)) {
            npc_changetype_keepall(npc_param(gnome_original), ^max_32bit_int);
        }
    }
}

[proc,gnomeball_inplay]()(boolean)
.npc_findallany(^gnomeball_centre_coord, 14, ^vis_off);
while (.npc_findnext = true) {
    if (.%npc_attacking_uid = uid) {
        def_npc $original = nc_param(.npc_type, gnome_original);
        if ($original ! null & .npc_type = nc_param($original, gnome_hasball)) {
            return(true);
        }
    }
}
return(false);

[opheld5,ball_gnomeball_game]
if (~inzone_coord_pair_table(gnomeball_zones, coord) = true) {
    mes("The ball magically returns to the ref as you put it down.");
    inv_delslot(inv, last_slot);
    anim(human_pickupfloor, 0);
    sound_synth(pick, 0, 0);
    return;
}
~dropslot(last_slot);

[proc,gnomeball_login]
if (~inzone_coord_pair_table(gnomeball_zones, coord) = true) {
    p_telejump(0_37_54_14_32);
    // Test 1:
    // - enter with gnome ball
    // - x log
    // - result: keep gnomeball on login (equipped!!)

    // Test 2:
    // - enter without gnomeball
    // - score 5 points
    // - x log
    // - result: keep gnomeball on login (equipped!!)

    // Test 3:
    // - enter with gnomeball
    // - smuggle second gnomeball in inv
    // - x log 
    // result: keep gnomeball on login (equipped!!) & 2nd smuggled gnomeball!

    if (%gnomeball_progress > ^gnomeball_not_started) {
        %gnomeball_progress = ^gnomeball_talked_to_ref;
    }
    // for some reason on x log, and you're owed a gnomeball, it just gives it to you equipped
    if (inv_getobj(worn, ^wearpos_rhand) = ball_gnomeball_game & %gnomeball_owedball = ^false) {
        inv_delslot(worn, ^wearpos_rhand);
    }
    %gnomeball_owedball = ^false;
    %gnomeball = 0;
    npc_findallany(^gnomeball_centre_coord, 14, ^vis_off);
    while (npc_findnext = true) {
        if (%npc_attacking_uid = uid) {
            if (npc_param(gnome_original) ! null & npc_type = nc_param(npc_param(gnome_original), gnome_hasball)) {
                npc_changetype_keepall(npc_param(gnome_original), ^max_32bit_int);
            }
        }
    }
}

[debugproc,gb]
if (p_finduid(uid) = true) {
    p_telejump(0_37_54_14_32);
}