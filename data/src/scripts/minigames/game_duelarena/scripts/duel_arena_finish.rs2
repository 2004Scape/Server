[queue,player_death_duel]
%duelstatus = ^duelstatus_lost;
if (~in_duel_arena(coord) = true) {
    ~player_death;
}
clearqueue(duel_finish);
queue(duel_finish, 0);

if (.finduid(%duelpartner) = true) {
    session_log(^log_moderator, "Lost a duel against <.name>");
    .if_close;
    .%duelstatus = ^duelstatus_victory;
    .clearqueue(duel_finish);
    .queue(duel_finish, 0);
}
// ur able to get double mes if opponent kills u with knives: https://youtu.be/KRDZJKa1whI?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=12
midi_jingle(^death_jingle_2, ^death_jingle_2_millis);
// https://storage.googleapis.com/tannerdino/images/6543.jpg
// https://youtu.be/gWmwSYxzUfo
mes("Oh dear you are dead!");


[queue,duel_finish]
if (.finduid(%duelpartner) = true) {
    if (%duelstatus = ^duelstatus_victory) {
        if_settext(duel_win:com_99, "You are victorious!");
        mes("Well done! You have defeated <.displayname>!");
        session_log(^log_moderator, "Won a duel against <.name>");
        midi_jingle(^duel_win_2_jingle, ^duel_win_2_jingle_millis);
        ~duel_reset_all;
        ~duel_spoils;
        return;
    } else if (%duelstatus = ^duelstatus_opponent_resigned) {
        if_settext(duel_win:com_99, "You are victorious! Your opponent resigned!"); // 2006
        mes("Well done! <.displayname> resigned!"); // 2006
        session_log(^log_moderator, "Won a duel by forfeit against <.name>");
        midi_jingle(^duel_win_2_jingle, ^duel_win_2_jingle_millis);
        ~duel_reset_all;
        ~duel_spoils;
        return;
    }
}
~duel_reset_all;
[proc,duel_spoils]
midi_jingle(^duel_win_2_jingle, ^duel_win_2_jingle_millis);
if_openmain(duel_win);
~duel_adjust_scoreboard(displayname, ~player_combat_level, .displayname, ~.player_combat_level);
if_settext(duel_win:displayname, .displayname);
if_settext(duel_win:combat_level, tostring(~.player_combat_level));

~duel_give_stake_back;

// take stuff from other player's stakeinv
.both_moveinv(duelarrows, duelarrows);
inv_transmit(duelarrows, duel_win:spoils);

[proc,duel_reset_all]
if (~in_duel_arena(coord) = false) {
    return;
}
// get arrows back
def_int $i = 0;
while ($i < inv_size(tradeoffer)) {
    if (inv_getobj(tradeoffer, $i) ! null) {
        inv_movefromslot(tradeoffer, inv, $i);
    }
    $i = calc($i + 1);
}

buildappearance(worn);
~prayer_deactivate_all;
p_telejump(~duel_arena_finish_coord(coord));
anim(null, 0);
p_stopaction;

~stat_reset_all;
healenergy(10000);
~combat_clearqueue;
clearqueue(player_death_default);
~clear_pk_skull;
~clear_poison;
~update_all(inv_getobj(worn, ^wearpos_rhand));
hint_stop;
%duelstatus = ^duelstatus_reset;
%duelpartner = null;

[proc,duel_give_stake_back]
def_int $i = 0;
while ($i < inv_size(duelarrows)) {
    if (inv_getobj(duelarrows, $i) ! null) {
        //mes("<oc_debugname(inv_getobj(stakeinv, $i))>: <tostring(inv_getnum(tempinv, $i))>");
        inv_movefromslot(duelarrows, inv, $i);
    }
    $i = calc($i + 1);
}

[if_close,duel_win]
~duel_give_stake_back;
inv_stoptransmit(duel_win:spoils);

// https://youtu.be/QggKGBAl_X8?t=111
[proc,duel_arena_finish_coord](coord $coord)(coord)
def_coord $coord1 = 0_52_51_27_3;
def_coord $coord2 = 0_52_51_51_15;

def_int $x1 = coordx($coord1);
def_int $x2 = coordx($coord2);
def_int $z1 = coordz($coord1);
def_int $z2 = coordz($coord2);
def_int $i = 0;
def_coord $random_coord = movecoord($coord1, random(sub($x2, $x1)), 0, random(sub($z2, $z1)));

while (map_blocked($random_coord) = true) {
    $random_coord = movecoord($coord1, random(sub($x2, $x1)), 0, random(sub($z2, $z1)));
    $i = calc($i + 1);
    if ($i = 50) {
        return($coord); // no way this is reached
    }
}
return($random_coord);