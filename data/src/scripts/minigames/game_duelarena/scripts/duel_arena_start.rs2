[queue,duel_arena_prepare_start]
if (.finduid(%duelpartner) = true) {
    def_coord $random_coord1 = ~duel_arena_coord;
    def_coord $random_coord2 = $random_coord1;

    if ($random_coord1 = null) {
        %duelstatus = ^duelstatus_reset;
        %duelpartner = null;
        ~mesbox("All arenas of this type are full! Select another type of arena, try|again in a few minutes or try a different server."); // https://youtu.be/TcrIr9O92sw?t=89
        return;
    }
    p_telejump($random_coord1);
    def_int $i = 0;
    if (testbit(%duel_settings, ^no_movement) = ^true & .p_finduid(.uid) = true) {
        while (map_blocked($random_coord2) = true | $random_coord2 = $random_coord1 | lineofwalk($random_coord1, $random_coord2) = false & $i < 50){
            switch_int (random(4)) {
                case 0 : $random_coord2 = movecoord($random_coord1, 1, 0, 0);
                case 1 : $random_coord2 = movecoord($random_coord1, -1, 0, 0);
                case 2 : $random_coord2 = movecoord($random_coord1, 0, 0, 1);
                case 3 : $random_coord2 = movecoord($random_coord1, 0, 0, -1);
            }
            $i = calc($i + 1);
        }
        .p_telejump($random_coord2);
    }

    p_stopaction;
    // only deactive prayers if no prayers is selected. based off: https://imgur.com/ETH1z4L
    if (testbit(%duel_settings, ^no_prayer) = ^true) {
        ~prayer_deactivate_all;
    }
    // Doesnt rest stats.
    // - https://youtu.be/zMhQfzn4M0Y?list=PLn23LiLYLb1Zo7wVV7wY2Zj1VV4zz6LBK&t=64
    // But, reset buffs only if no potions is selected:
    // - https://web.archive.org/web/20050930213502/http://runevillage.com:80/rs2specialDuelingarena.php
    //      - "No Potions: This means you canâ€™t use Potions, if you used a Potion before the Duel its effects will be returned to normal."
    // - In 2006, if you had the "No drinks" option selected, the 2nd menu would show  "Boosted stats will be restored." https://imgur.com/ETH1z4L
    if (testbit(%duel_settings, ^no_potions) = ^true) {
        ~stat_reset_boosts;
    }

    if (testbit(%duel_settings, ^no_weapons) = ^true) {
        ~unequip_duel(duel_arena_weapon_slots);
    }
    if (testbit(%duel_settings, ^no_armour) = ^true) {
        ~unequip_duel(duel_arena_armour_slots);
    }
    if (testbit(%duel_settings, ^no_jewelry) = ^true) {
        ~unequip_duel(duel_arena_jewelry_slots);
    }

    hint_player(.uid);

    settimer(duel_arena_time_limit, 6000); // 60 minute time limit https://runescape.wiki/w/Update:Assist_System
    midi_jingle(^duel_start_jingle, ^duel_start_jingle_millis);
    ~duel_arena_countdown;
}

[proc,duel_arena_countdown]
world_delay(1);
if (~in_duel_arena(coord) = false) {
    return;
}
say("3");
world_delay(1);
if (~in_duel_arena(coord) = false) {
    return;
}
say("2");
world_delay(1);
if (~in_duel_arena(coord) = false) {
    return;
}
say("1");
world_delay(1);
if (~in_duel_arena(coord) = false) {
    return;
}
if (.finduid(%duelpartner) = false) {
    queue(duel_arena_disconnect, 0);
    return;
}
if (~.in_duel_arena(.coord) = false) {
    queue(duel_arena_disconnect, 0);
    .queue(duel_arena_disconnect, 0);
    .clearqueue(duel_arena_prepare_start);
    return;
}
say("FIGHT!");
%duelstatus = ^duelstatus_start;

[proc,duel_arena_coord]()(coord)
// if all arenas are full, return null
if (~duel_arena_obstacles_check = ^true) {
    def_int $i = 0;
    while ($i < db_getfieldcount(duel_arena_obstacle_fight_zones, coord_pair_table:coord_pair)) {
        if (map_playercount(db_getfield(duel_arena_obstacle_fight_zones, coord_pair_table:coord_pair, $i)) < 24) {
            return(~map_findsquare_inzone(db_getfield(duel_arena_obstacle_fight_zones, coord_pair_table:coord_pair, $i)));
        }
        $i = calc($i + 1);
    }
} else {
    def_int $i = 0;
    while ($i < db_getfieldcount(duel_arena_fight_zones, coord_pair_table:coord_pair)) {
        if (map_playercount(db_getfield(duel_arena_fight_zones, coord_pair_table:coord_pair, $i)) < 24) {
            return(~map_findsquare_inzone(db_getfield(duel_arena_fight_zones, coord_pair_table:coord_pair, $i)));
        }
        $i = calc($i + 1);
    }
}
return(null);

[walktrigger,duel_arena_no_move]
if (~duel_arena_movement_check = ^true & ~in_duel_arena(coord) = true) {
    p_walk(coord);
    walktrigger(duel_arena_no_move);
}

[timer,duel_arena_time_limit]
if (~in_duel_arena(coord) = false) {
    cleartimer(duel_arena_time_limit);
    return;
}
%duelstatus = ^duelstatus_reset;
%duelpartner = null;
~moveallinv(dueloffer, inv);
~duel_reset_all;
~mesbox("Your duel ended in an honourable draw."); // RS3 message: https://archive.org/details/runelibris
