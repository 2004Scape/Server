[opheld1,trail_clue_medium_map001]
~full_trail_readclue_map(trail_map03);

// This clue is flagged as unobtainable in information frmo @Hlwys.
[opheld1,trail_clue_medium_map002]
~full_trail_readclue_map(trail_map04);

[opheld1,_trail_clue_medium]
~full_trail_readclue(oc_param(last_item, trail_desc));

[opheld1,_trail_challenge]
~full_trail_readclue(oc_param(last_item, trail_desc));

[proc,progress_clue_medium](obj $current_clue, string $text)
~trail_clue_progress;
inv_del(inv, $current_clue, 1);
if(~trail_clue_medium_complete = true) {
    ~trail_clue_medium_reward;
} else {
    def_namedobj $new_clue = ~trail_clue_medium_getrandom;
    inv_add(inv, $new_clue, 1);
    ~objboxt($new_clue, $text);
}

// Key drop
[proc,trail_checkmediumdrop]
// this was changed from black heather -> ardougne monks at some point, not sure when though
// https://www.neoseeker.com/runescape/faqs/125252-treasure-trail.html, https://web.archive.org/web/20040813233712/http://runehq.com/viewguidedb.php?id=00350
if(npc_type = black_heather & inv_total(inv, trail_clue_medium_riddle_exp001) > 0 & ~obj_gettotal(trail_clue_medium_key_exp001) = 0) {
    obj_add(npc_coord, trail_clue_medium_key_exp001, 1, ^lootdrop_duration);
} else if(npc_type = guard_dog & inv_total(inv, trail_clue_medium_riddle_exp002) > 0 & ~obj_gettotal(trail_clue_medium_key_exp002) = 0) {
    obj_add(npc_coord, trail_clue_medium_key_exp002, 1, ^lootdrop_duration);
} else if(npc_type = guard_ardougne & inv_total(inv, trail_clue_medium_riddle_exp003) > 0 & ~obj_gettotal(trail_clue_medium_key_exp003) = 0) {
    obj_add(npc_coord, trail_clue_medium_key_exp003, 1, ^lootdrop_duration);
} else if(npc_category = chicken & inv_total(inv, trail_clue_medium_riddle_exp004) > 0 & ~obj_gettotal(trail_clue_medium_key_exp004) = 0) {
    obj_add(npc_coord, trail_clue_medium_key_exp004, 1, ^lootdrop_duration);
} else if(npc_category = citizen & inv_total(inv, trail_clue_medium_riddle_exp005) > 0 & ~obj_gettotal(trail_clue_medium_key_exp005) = 0) {
    obj_add(npc_coord, trail_clue_medium_key_exp005, 1, ^lootdrop_duration);
}

// TODO: not the same loc or clue, but basing the lock message off this, probably confirm on OSRS: https://youtu.be/cFwNqBvSHNg?si=poX8EWgEZIEfvie7&t=66
[label,riddle_loc_medium_exp001]
if(inv_total(inv, trail_clue_medium_riddle_exp001) > 0) {
    // https://www.youtube.com/watch?v=tfnYvJrHnqw
    if(inv_total(inv, trail_clue_medium_key_exp001) > 0) {
        p_arrivedelay;
        anim(human_openchest,0);
        p_delay(0);
        // need to find how long it changes for
        loc_change(loc_3368, 4);
        p_delay(0);
        inv_del(inv, trail_clue_medium_key_exp001, 1);
        ~progress_clue_medium(trail_clue_medium_riddle_exp001, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The chest is locked! A note on the top of the chest reads:|Property of Black Heather.");
}
mes("The chest is locked.");

// TODO: not the same drawer, but basing the lock message off this, probably confirm on OSRS: https://youtu.be/cFwNqBvSHNg?si=poX8EWgEZIEfvie7&t=66
[label,riddle_loc_medium_exp002]
if(inv_total(inv, trail_clue_medium_riddle_exp002) > 0) {
    // https://www.youtube.com/watch?v=tfnYvJrHnqw
    if(inv_total(inv, trail_clue_medium_key_exp002) > 0) {
        p_arrivedelay;
        anim(human_openchest,0);
        p_delay(0);
        // need to find how long it changes for
        loc_change(loc_3369, 4);
        p_delay(0);
        inv_del(inv, trail_clue_medium_key_exp002, 1);
        ~progress_clue_medium(trail_clue_medium_riddle_exp002, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked! A note on the top of the drawers reads:|Beware of the dog.");
}
mes("These drawers are locked.");

// TODO: not the same drawer, but basing the lock message off this, probably confirm on OSRS: https://youtu.be/cFwNqBvSHNg?si=poX8EWgEZIEfvie7&t=66
[label,riddle_loc_medium_exp003]
if(inv_total(inv, trail_clue_medium_riddle_exp003) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_key_exp003) > 0) {
        p_arrivedelay;
        anim(human_openchest,0);
        p_delay(0);
        // need to confirm how long it changes for, seems like 4t in video
        loc_change(loc_3369, 4);
        p_delay(0);
        inv_del(inv, trail_clue_medium_key_exp003, 1);
        ~progress_clue_medium(trail_clue_medium_riddle_exp003, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked! A note on the top of the drawers reads:|It's a guard's life.");
}
mes("These drawers are locked.");

// TODO: not the same drawer, but basing the lock message off this, probably confirm on OSRS: https://youtu.be/cFwNqBvSHNg?si=poX8EWgEZIEfvie7&t=66
[label,riddle_loc_medium_exp004]
if(inv_total(inv, trail_clue_medium_riddle_exp004) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_key_exp004) > 0) {
        p_arrivedelay;
        anim(human_openchest,0);
        p_delay(0);
        // need to confirm how long it changes for, seems like 4t in video
        loc_change(loc_3370, 4);
        p_delay(0);
        inv_del(inv, trail_clue_medium_key_exp004, 1);
        ~progress_clue_medium(trail_clue_medium_riddle_exp004, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked! A note on the top of the drawers reads:|Don't forget to feed the chickens.");
}
mes("These drawers are locked.");

[label,riddle_loc_medium_exp005]
if(inv_total(inv, trail_clue_medium_riddle_exp005) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_key_exp005) > 0) {
        p_arrivedelay;
        anim(human_openchest,0);
        p_delay(0);
        // need to confirm how long it changes for, seems like 4t in video
        loc_change(loc_3370, 4);
        p_delay(0);
        inv_del(inv, trail_clue_medium_key_exp005, 1);
        ~progress_clue_medium(trail_clue_medium_riddle_exp005, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The chest is locked! A note on the top of the chest reads:|Stand by your man.");
}
mes("The chest is locked.");


[proc,trail_clue_medium_complete](boolean)
if(~get_trail_progress >= ^trail_medium_maxsteps | (add(~get_trail_progress, 2) >= ^trail_medium_maxsteps & random(2) = 0)) {
    return (true);
}
return(false);

[opheld1,_trail_casket_medium] 
~progress_clue_medium(last_item, "You've found another clue scroll!");

[proc,trail_clue_medium_loc_interact](namedobj $clue)
~progress_clue_medium($clue, "You've found another clue scroll!");

[proc,trail_clue_medium_getrandom](namedobj)
return(enum(int, namedobj, trail_medium_enum, random(enum_getoutputcount(trail_medium_enum))));

[debugproc,ts](int $arg)
//if_setanim(trail_sextant:com_9, seq_934);
//if_setanim(trail_sextant:com_10, seq_997);
//if_setanim(trail_sextant:com_11, seq_934);
//if_setposition(trail_sextant:com_1, 0, -56);
if_setposition(trail_sextant:com_2, 0, $arg);
if_openmainmodal(trail_sextant);
