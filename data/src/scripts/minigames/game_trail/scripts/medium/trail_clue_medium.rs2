[opheld1,trail_clue_medium_map001]
~full_trail_readclue_map(trail_map03);

// This clue is flagged as unobtainable in information frmo @Hlwys.
[opheld1,trail_clue_medium_map002]
~full_trail_readclue_map(trail_map04);

[opheld1,_trail_clue_medium]
~full_trail_readclue(oc_param(last_item, trail_desc));

[opheld1,_trail_challenge]
~full_trail_readclue(oc_param(last_item, trail_desc));

[proc,progress_clue_medium](obj $current_clue, string $text)
~trail_clue_progress;
inv_del(inv, $current_clue, 1);
if(~trail_clue_medium_complete = true) {
    ~trail_clue_medium_reward;
} else {
    def_namedobj $new_clue = ~trail_clue_medium_getrandom;
    inv_add(inv, $new_clue, 1);
    ~objbox($new_clue, $text, 250, 0, 0);
}

// Key drop
[proc,trail_checkmediumdrop]
// this was changed from black heather -> ardougne monks at some point, not sure when though
// https://www.neoseeker.com/runescape/faqs/125252-treasure-trail.html, https://web.archive.org/web/20040813233712/http://runehq.com/viewguidedb.php?id=00350
if(npc_type = black_heather & inv_total(inv, trail_clue_medium_riddle001) > 0 & ~obj_gettotal(trail_clue_medium_riddle001_key) = 0) {
    obj_add(npc_coord, trail_clue_medium_riddle001_key, 1, ^lootdrop_duration);
} else if(npc_type = guarddog & inv_total(inv, trail_clue_medium_riddle002) > 0 & ~obj_gettotal(trail_clue_medium_riddle002_key) = 0) {
    obj_add(npc_coord, trail_clue_medium_riddle002_key, 1, ^lootdrop_duration);
} else if(npc_type = ardougne_guard & inv_total(inv, trail_clue_medium_riddle003) > 0 & ~obj_gettotal(trail_clue_medium_riddle003_key) = 0) {
    obj_add(npc_coord, trail_clue_medium_riddle003_key, 1, ^lootdrop_duration);
} else if(npc_category = chicken & inv_total(inv, trail_clue_medium_riddle004) > 0 & ~obj_gettotal(trail_clue_medium_riddle004_key) = 0) {
    obj_add(npc_coord, trail_clue_medium_riddle004_key, 1, ^lootdrop_duration);
} else if(compare(npc_name, "Man") = 0 & inv_total(inv, trail_clue_medium_riddle005) > 0 & ~obj_gettotal(trail_clue_medium_riddle005_key) = 0) {
    obj_add(npc_coord, trail_clue_medium_riddle005_key, 1, ^lootdrop_duration);
}

[label,riddle_loc_medium_exp001]
if(inv_total(inv, trail_clue_medium_riddle001) > 0) {
    // https://www.youtube.com/watch?v=tfnYvJrHnqw
    if(inv_total(inv, trail_clue_medium_riddle001_key) > 0) {
        p_arrivedelay;
        anim(human_openchest, 0);
        sound_synth(chest_open, 0, 0);
        p_delay(0);
        // Temp note: dur updated
        loc_change(chestopen_inactive, 5);
        p_delay(0);
        inv_del(inv, trail_clue_medium_riddle001_key, 1);
        ~progress_clue_medium(trail_clue_medium_riddle001, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The chest is locked!|An inscription on the chest says:|Property of Black Heather.");
}
mes("The chest is locked.");

[label,riddle_loc_medium_exp002]
if(inv_total(inv, trail_clue_medium_riddle002) > 0) {
    // https://www.youtube.com/watch?v=tfnYvJrHnqw
    if(inv_total(inv, trail_clue_medium_riddle002_key) > 0) {
        p_arrivedelay;
        anim(human_openchest, 0);
        sound_synth(cupboard_open, 0, 0);
        p_delay(0);
        // Temp note: dur updated
        loc_change(drawers1open_inactive, 5);
        p_delay(0);
        inv_del(inv, trail_clue_medium_riddle002_key, 1);
        ~progress_clue_medium(trail_clue_medium_riddle002, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked!  A note left on the drawers reads: Beware of the dog!");
}
mes("The drawers are locked.");

[label,riddle_loc_medium_exp003]
if(inv_total(inv, trail_clue_medium_riddle003) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_riddle003_key) > 0) {
        p_arrivedelay;
        anim(human_openchest, 0);
        sound_synth(cupboard_open, 0, 0);
        p_delay(0);
        // Temp note: dur updated
        loc_change(drawers1open_inactive, 5);
        p_delay(0);
        inv_del(inv, trail_clue_medium_riddle003_key, 1);
        ~progress_clue_medium(trail_clue_medium_riddle003, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked! A note is scratched on the top of the drawers: It's a guard's life.");
}
mes("The drawers are locked.");

[label,riddle_loc_medium_exp004]
if(inv_total(inv, trail_clue_medium_riddle004) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_riddle004_key) > 0) {
        p_arrivedelay;
        anim(human_openchest, 0);
        sound_synth(cupboard_open, 0, 0);
        p_delay(0);
        // Temp note: dur updated
        loc_change(drawers2open_inactive, 5);
        p_delay(0);
        inv_del(inv, trail_clue_medium_riddle004_key, 1);
        ~progress_clue_medium(trail_clue_medium_riddle004, "You've found another clue scroll!");
        return;
    }
    ~mesbox("The drawers are locked!|A note left on the drawers reads: Don't forget to feed the chickens.");
}
mes("The drawers are locked.");

[label,riddle_loc_medium_exp005]
if(inv_total(inv, trail_clue_medium_riddle005) > 0) {
    // https://www.youtube.com/watch?v=cxAvPqh4WjM
    if(inv_total(inv, trail_clue_medium_riddle005_key) > 0) {
        p_arrivedelay;
        anim(human_openchest, 0);
        sound_synth(chest_open, 0, 0);
        p_delay(0);
        // Temp note: dur updated
        loc_change(chestopen_inactive, 5);
        p_delay(0);
        inv_del(inv, trail_clue_medium_riddle005_key, 1);
        ~progress_clue_medium(trail_clue_medium_riddle005, "You've found another clue scroll!");
        return;
    }
    // intentional doublespace
    ~mesbox("The chest is locked!  An inscription on the chest reads: Stand by your man.");
}
mes("The chest is locked.");

[label,trail_challengenpc_prompt](string $prompt_chat, string $correct_chat, string $wrong_chat, string $progress_mes, namedobj $clue, namedobj $challenge)
~chatnpc($prompt_chat);
if_close;
p_delay(0); // old videos have this it seems, I think it's a workaround otherwise the old chatnpc will appear for a tick after the countdialog is closed
p_countdialog;
def_int $entered_value = last_int;
p_delay(0); // there seems to be a small delay after the countdialog as well
if($entered_value = oc_param($challenge, trail_challenge_answer)) {
    ~chatnpc($correct_chat);
    inv_del(inv, $challenge, 1);
    ~progress_clue_medium($clue, $progress_mes);
} else {
    ~chatnpc($wrong_chat);
}

[proc,trail_clue_medium_complete]()(boolean)
if(~get_trail_progress >= ^trail_medium_maxsteps | (add(~get_trail_progress, 2) >= ^trail_medium_maxsteps & random(2) = 0)) {
    return (true);
}
return(false);

[opheld1,_trail_casket_medium] 
~progress_clue_medium(last_item, "You've found another clue scroll!");

[proc,trail_clue_medium_loc_interact](namedobj $clue)
~progress_clue_medium($clue, "You've found another clue scroll!");

[proc,trail_clue_medium_getrandom]()(namedobj)
return(enum(int, namedobj, trail_medium_enum, random(enum_getoutputcount(trail_medium_enum))));