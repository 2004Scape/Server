[proc,trail_clue_easy_reward]
// Rolls math verification: https://www.youtube.com/watch?v=Er4RAFnmTvs
def_int $rolls = calc(2 + random(3));
def_int $roll = 0;
def_int $rare = 0;

while ($roll < $rolls) {
    // Rare drop chance was 1/12 in OSRS in 2019. @https://twitter.com/JagexTide/status/1131933683714330624
    // This was adjusted from 1/21 at clue expansion, according to Mod John C:
    //
    // "Previously it was 1/21 to jump to the rare item list then 1/67 at each of the rewards,
    // effectively 1/1407. To keep each reward the same rarity I made it 1/13 to jump to the rare table
    // which now has 108 rewards, effectively 1404, which is as close as I could make it."
    //
    // Assuming the rarity of any given object from the rare drop tables, have stayed consistent throughout
    // the years, this lands our chance to land on the rare drop table at 1/117.
    if(random(117) = 0) {
        $rare = calc($rare + 1);
        gosub(trail_clue_easy_rare);
    } else {
        gosub(trail_clue_easy_normal);
    }
    $roll = calc($roll + 1);
}
if ($rare > 0) {
    session_log(^log_adventure, "Completed an Easy Clue Scroll and received <tostring($rare)> Rare Reward(s)!");
} else {
    session_log(^log_adventure, "Completed an Easy Clue Scroll.");
}
~trail_complete;

[proc,trail_clue_easy_normal]
def_int $random = random(26);

switch_int ($random) {
    case 0 : inv_add(reward, coins, calc(50 + random(151)));
    case 1 : inv_add(reward, cert_trout, calc(6 + random(5)));
    case 2 : inv_add(reward, cert_salmon, calc(6 + random(5)));
    case 3 : inv_add(reward, airrune, calc(30 + random(21)));
    case 4 : inv_add(reward, firerune, calc(30 + random(21)));
    case 5 : inv_add(reward, waterrune, calc(30 + random(21)));
    case 6 : inv_add(reward, earthrune, calc(30 + random(21)));
    case 7 : inv_add(reward, mindrune, calc(30 + random(21)));
    case 8 : inv_add(reward, bodyrune, calc(30 + random(21)));
    case 9 : inv_add(reward, lawrune, calc(5 + random(6)));
    case 10 : inv_add(reward, chaosrune, calc(5 + random(6)));
    case 11 : inv_add(reward, naturerune, calc(5 + random(6)));
    case 12 : inv_add(reward, black_platebody, 1);
    case 13 : inv_add(reward, black_longsword, 1);
    case 14 : inv_add(reward, black_full_helm, 1);
    case 15 : inv_add(reward, black_platelegs, 1);
    case 16 : inv_add(reward, black_battleaxe, 1);
    case 17 : inv_add(reward, black_axe, 1);
    case 16 : inv_add(reward, black_dagger, 1);
    case 18 : inv_add(reward, steel_pickaxe, 1);
    case 19 : inv_add(reward, staff_of_air, 1);
    case 20 : inv_add(reward, studded_chaps, 1);
    case 21 : inv_add(reward, studded_body, 1);
    case 22 : inv_add(reward, willow_shortbow, 1);
    case 23 : inv_add(reward, coif, 1);
    case 24 : inv_add(reward, amulet_of_magic, 1);
    case 25 : inv_add(reward, willow_longbow, 1);
}

[proc,trail_clue_easy_rare]
def_int $random = random(12);

switch_int ($random) {
    case 0 : inv_add(reward, black_platebody_trim, 1);
    case 1 : inv_add(reward, black_platelegs_trim, 1);
    case 2 : inv_add(reward, black_full_helm_trim, 1);
    case 3 : inv_add(reward, black_kiteshield_trim, 1);
    case 4 : inv_add(reward, black_platebody_gold, 1);
    case 5 : inv_add(reward, black_platelegs_gold, 1);
    case 6 : inv_add(reward, black_full_helm_gold, 1);
    case 7 : inv_add(reward, black_kiteshield_gold, 1);
    case 8 : inv_add(reward, trail_highwayman_mask, 1);
    case 9 : inv_add(reward, trail_berret_blue, 1);
    case 10 : inv_add(reward, trail_berret_black, 1);
    case 11 : inv_add(reward, trail_berret_white, 1);
}