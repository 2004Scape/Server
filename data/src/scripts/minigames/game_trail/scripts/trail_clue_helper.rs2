[proc,trail_hasclue_all](boolean)
def_int $slot = 0;

while ($slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if(oc_category($slotobj) = trail_clue_easy | oc_category($slotobj) = trail_clue_medium | oc_category($slotobj) = trail_clue_hard) {
        return(true);
    }
    $slot = calc($slot + 1);
}
$slot = 0;

while ($slot < inv_size(bank)) {
    def_obj $slotobj = inv_getobj(bank, $slot);
    if(oc_category($slotobj) = trail_clue_easy | oc_category($slotobj) = trail_clue_medium | oc_category($slotobj) = trail_clue_hard) {
        return(true);
    }
    $slot = calc($slot + 1);
}
return(false);

[proc,trail_hasclue_inv](boolean)
def_int $slot = 0;

while ($slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if(oc_category($slotobj) = trail_clue_easy | oc_category($slotobj) = trail_clue_medium | oc_category($slotobj) = trail_clue_hard) {
        return(true);
    }
    $slot = calc($slot + 1);
}

return(false);

[proc,trail_getclue_inv](namedobj)
def_int $slot = 0;

while ($slot < inv_size(inv)) {
    def_namedobj $slotobj = inv_getobj(inv, $slot);
    if(oc_category($slotobj) = trail_clue_easy | oc_category($slotobj) = trail_clue_medium | oc_category($slotobj) = trail_clue_hard) {
        return($slotobj);
    }
    $slot = calc($slot + 1);
}

[proc,trail_clue_coord](namedobj $clue)(boolean)
def_coord $default_coord = 0_0_0_0_0;
if(oc_param($clue, trail_coord) ! $default_coord) {
    return(true);
} else {
    return(false);
}

[proc,trail_casket_obtained]
~objbox(trail_casket_hard, 200, "You've found a casket!");

[proc,trail_complete]
mes("Well done you've completed the Treasure Trail!");
midi_jingle(^treasure_hunt_win_jingle, ^treasure_hunt_win_jingle_millis);
inv_transmit(reward_inv, trail_reward:inv);
if_opentop(trail_reward);
queue(trail_give_reward, 0);

[queue,trail_give_reward]
def_int $slot = 0;

while ($slot < inv_size(reward_inv)) {
    def_namedobj $slotobj = inv_getobj(reward_inv, $slot);
    if($slotobj ! null) {
        if(inv_itemspace(inv, $slotobj, inv_total(reward_inv, $slotobj), inv_freespace(inv)) = true) {
            inv_add(inv, $slotobj, inv_getslotcount(reward_inv, $slot));
        } else {
            obj_add(coord, $slotobj, inv_getslotcount(reward_inv, $slot), 200);
        }

    }
    $slot = calc($slot + 1);
}
inv_clear(reward_inv);