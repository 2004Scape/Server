[proc,trail_hasclue_all]()(boolean)
if (~trail_hasclue_inv = true) {
    return (true);
}
if(inv_totalcat(bank, trail_clue_easy) > 0| inv_totalcat(bank, trail_clue_medium) > 0 | inv_totalcat(bank, trail_clue_hard) > 0 | 
    inv_totalcat(bank, trail_casket_easy) > 0 | inv_totalcat(bank, trail_casket_medium) > 0 | inv_totalcat(bank, trail_casket_hard) > 0) {
    return (true);
}
return(false);

[proc,trail_hasclue_inv]()(boolean)
if (inv_totalcat(inv, trail_clue_easy) > 0| inv_totalcat(inv, trail_clue_medium) > 0| inv_totalcat(inv, trail_clue_hard) > 0 | 
    inv_totalcat(inv, trail_casket_easy) > 0 | inv_totalcat(inv, trail_casket_medium) > 0 | inv_totalcat(inv, trail_casket_hard) > 0) {
        return (true);
}
return(false);

[proc,trail_getclue_inv]()(obj)
def_int $slot = 0;
while ($slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if ($slotobj ! null) {
        if(oc_category($slotobj) = trail_clue_easy | oc_category($slotobj) = trail_clue_medium | oc_category($slotobj) = trail_clue_hard) {
            return($slotobj);
        }
    }
    $slot = calc($slot + 1);
}
return (null);

[proc,trail_clue_coord](obj $clue)(boolean)
if($clue ! null & oc_param($clue, trail_coord) ! null) {
    return(true);
}
return(false);

[proc,trail_clue_progress]
if(testbit(%trail_clue, 4) = ^true) {
    %trail_clue = clearbit(%trail_clue, 4);
}
%trail_clue = setbit_range_toint(%trail_clue, calc(~get_trail_progress + 1), 0, 3);

// bits 0-3 will store the progress of the clue, bit 4 is used to flag if the clue guardian (just zamorak wizard in version 225)
// has been killed, bits 5-7 to store the progress of the "miniquest" to get sextant/chart/watch (possible this didnt use a varp in 225, it does in osrs
// although it doesn't use 292 (trail_clue))
// https://web.archive.org/web/20051214060247/http://www.tip.it/runescape/?page=treasure_trails.htm and runehq forums mention you can get the items back directly by talking
// to the npc once you've claimed the items before meaning it likely used varp
[proc,get_trail_progress]()(int)
return (getbit_range(%trail_clue, 0, 3));

// clears the trail progess, including the bit used for tracking the zamorak wizard
[proc,clear_trail_progress]
%trail_clue = clearbit_range(%trail_clue, 0, 4);

// 4 bits
[proc,get_chart_progress]()(int)
return (getbit_range(%trail_clue, 5, 8));

[proc,has_sextant_clue]()(boolean)
def_int $slot = 0;
while ($slot < inv_size(inv)) {
    def_obj $slotobj = inv_getobj(inv, $slot);
    if ($slotobj ! null) {
        if(oc_param($slotobj, trail_sextant) = true) {
            return (true);
        }
    }
    $slot = calc($slot + 1);
}
return (false);

[proc,trail_casket_obtained](namedobj $casket)
~objbox($casket, "You've found a casket!", 250, 0, divide(^objbox_height, 2));

[proc,trail_complete]
~clear_trail_progress;
mes("Well done you've completed the Treasure Trail!");
midi_jingle(^treasure_hunt_win_jingle, ^treasure_hunt_win_jingle_millis);
inv_transmit(trail_rewardinv, trail_reward:inv);
if_openmain(trail_reward);
queue(trail_give_reward, 0);

[queue,trail_give_reward]
def_int $slot = 0;
while ($slot < inv_size(trail_rewardinv)) {
    if (inv_getnum(trail_rewardinv, $slot) > 0) {
        inv_movefromslot(trail_rewardinv, inv, $slot);
    }
    $slot = calc($slot + 1);
}
inv_clear(trail_rewardinv);
