[proc,trawler_win](int $rolls)
huntall(^trawler_start_center_under, 8, 0);
while (huntnext = true) {
    if_close;
    %trawler_catch = $rolls;
    clearqueue(trawler_player_rip_net);
    clearqueue(trawler_player_sink);
    queue(trawler_player_win, 0);
}
huntall(^trawler_start_center, 8, 0);
while (huntnext = true) {
    if_close;
    %trawler_catch = $rolls;
    clearqueue(trawler_player_rip_net);
    clearqueue(trawler_player_sink);
    queue(trawler_player_win, 0);
}
huntall(^trawler_flood_center_under, 8, 0);
while (huntnext = true) {
    if_close;
    %trawler_catch = $rolls;
    clearqueue(trawler_player_rip_net);
    clearqueue(trawler_player_sink);
    queue(trawler_player_win, 0);
}
huntall(^trawler_flood_center, 8, 0);
while (huntnext = true) {
    if_close;
    %trawler_catch = $rolls;
    clearqueue(trawler_player_rip_net);
    clearqueue(trawler_player_sink);
    queue(trawler_player_win, 0);
}

[queue,trawler_player_win]
if (~inzone_coord_pair_table(trawler_game_zones, coord) = false) {
    return;
}
mes("Murphy turns the boat towards shore.");
%boat_takeoff = 12;
%trawler = ^trawler_finished;
midi_jingle(^sailing_journey_jingle, ^sailing_journey_jingle_millis);
if_openmainmodal(ship_journey);

p_delay(20);
if_close;
~update_all(null);
p_teleport(~coord_lineofwalk_radius(^trawler_lobby_murphy_spawn, 4));

[oploc1,game_trawler_reward_net]
if (%trawler < 3 | %trawler_catch = 0) {
    ~chatplayer("<p,neutral>I'd better not steal other people's fish!"); // osrs and rs3
    return;
}
def_int $level = stat(fishing);
if ($level < 15) { // osrs wiki says its unboostable but this is untrue.
    mes("You need to have a Fishing level of 15 to find fish here."); // osrs and rs3
    return;
}
anim(human_pickupfloor, 0);
p_delay(0);

inv_transmit(trawler_catch, trawler_catch:inv);
if_openmainmodal(trawler_catch);

def_int $i = 0;
while ($i < %trawler_catch) {
    ~trawler_reward($level);
    $i = add($i, 1);
}
%trawler = ^trawler_sailed;
%trawler_catch = 0;


[if_close,trawler_catch]
def_int $i = 0;
while ($i < inv_size(trawler_catch)) {
    if (inv_getobj(trawler_catch, $i) ! null) {
        inv_dropslot(trawler_catch, coord, $i, 100); // drops in osrs
    }
    $i = add($i, 1);
}
inv_stoptransmit(trawler_catch:inv);

// osrs drops on ground on full inv
[inv_button1,trawler_catch:inv] 
inv_moveitem(trawler_catch, inv, last_item, 1);
if (inv_freespace(trawler_catch) = inv_size(trawler_catch)) {
    if_close; // osrs
}
[inv_button2,trawler_catch:inv]
inv_movefromslot(trawler_catch, inv, last_slot);
if (inv_freespace(trawler_catch) = inv_size(trawler_catch)) {
    if_close; // osrs
}

// trawler reward was reworked early 2007. So its way different than osrs
// - https://imgur.com/gBvOHZl
// - https://imgur.com/0808GUW
// Based on these two screenshots, junk was more common and actually apart of the fishing roll
// low fish wasnt ultra rare at high fish levels
// tldr: my theory is that instead of low: 150, high: 200 for each fish (osrs), it was fishes normal lows and highs
[proc,trawler_reward](int $level)
if (stat_random($level, 150, 200) = false) { // junk https://web.archive.org/web/20040812231222/http://www.tip.it/runescape/index.php?page=rs2trawler.htm
    inv_add(trawler_catch, ~randomjunk);
    givexp(fishing, 12); // it says junk gives 1 xp each, except with boots giving 2. Im guessing its like 1.1 or 1.2 xp each
    return;
}
if ($level >= 81 & stat_random($level, 3, 40) = true) {
    inv_add(trawler_catch, raw_manta_ray, 1);
    givexp(fishing, 1150);
} else if ($level >= 80 & stat_random($level, 3, 40) = true) {
    inv_add(trawler_catch, raw_sea_turtle, 1);
    givexp(fishing, 1050);
} else if ($level >= 76 & stat_random($level, 3, 40) = true) {
    inv_add(trawler_catch, raw_shark, 1);
    givexp(fishing, 1100);
} else if ($level >= 60 & stat_random($level, 4, 48) = true) {
    inv_add(trawler_catch, raw_swordfish, 1);
    givexp(fishing, 1000);
} else if ($level >= 40 & stat_random($level, 6, 95) = true) {
    inv_add(trawler_catch, raw_lobster, 1);
    givexp(fishing, 900);
} else if ($level >= 35 & stat_random($level, 8, 64) = true) {
    inv_add(trawler_catch, raw_tuna, 1);
    givexp(fishing, 800);
} else if ($level >= 16 & stat_random($level, 10, 80) = true) {
    def_int $random = random(3);
    if ($random = 0) {
        inv_add(trawler_catch, oyster, 1);
        givexp(fishing, 100);
    } else if ($random = 1) {
        inv_add(trawler_catch, seaweed, 1);
        givexp(fishing, 12);
    } else if ($random = 2) {
        inv_add(trawler_catch, edible_seaweed, 1);
        givexp(fishing, 12);
    }
} else if (stat_random($level, 24, 128) = true) {
    inv_add(trawler_catch, raw_anchovies, 1);
    givexp(fishing, 400);
} else if (stat_random($level, 32, 192) = true) {
    inv_add(trawler_catch, raw_sardine, 1);
    givexp(fishing, 200);
} else if (stat_random($level, 48, 200) = true) {
    inv_add(trawler_catch, raw_shrimps, 1);
    givexp(fishing, 100);
} else {
    inv_add(trawler_catch, ~randomjunk);
    givexp(fishing, 12);
}
