[proc,trawler_flood]
if (.npc_find(^trawler_flood_center_under, murphy_flood, 8, 0) = false) {
    return;
}
.%npc_int2 = ^trawler_control_flooded;
%npc_int2 = ^trawler_control_flooded;

.%npc_int = %npc_int;
.%npc_trawler_flood_status = %npc_trawler_flood_status;
.%npc_trawler_fish_caught = %npc_trawler_fish_caught;

huntall(^trawler_start_center_under, 8, 0);
while (huntnext = true) {
    if_close;
    queue(trawler_player_flood, 0);
}
huntall(^trawler_start_center, 8, 0);
while (huntnext = true) {
    if_close;
    queue(trawler_player_flood, 0);
}

[proc,trawler_unflood]
if (.npc_find(^trawler_start_center_under, murphy_nonflood, 8, 0) = false) {
    return;
}
.%npc_int2 = ^trawler_control_nonflooded;
%npc_int2 = ^trawler_control_nonflooded;

.%npc_int = %npc_int;
.%npc_trawler_flood_status = %npc_trawler_flood_status;
.%npc_trawler_fish_caught = %npc_trawler_fish_caught;

huntall(^trawler_flood_center_under, 8, 0);
while (huntnext = true) {
    if_close;
    queue(trawler_player_unflood, 0);
}
huntall(^trawler_flood_center, 8, 0);
while (huntnext = true) {
    if_close;
    queue(trawler_player_unflood, 0);
}

[proc,trawler_leak] // requires active loc
// Temp note: dur does not need updated
loc_change(game_trawler_leak, 1000);
~sound_area(leak, 0, loc_coord, 5);

def_coord $flood_coord = movecoord(loc_coord, 128, 0, 0);
if (loc_find($flood_coord, game_trawler_hull) = true | loc_find($flood_coord, game_trawler_repaired_leak) = true) {
    // Temp note: dur does not need updated
    loc_change(game_trawler_leak, 1000);
    ~sound_area(leak, 0, $flood_coord, 5);
}

[queue,trawler_player_flood]
p_teleport(movecoord(coord, 128, 0, 0));
p_walk(coord);

[queue,trawler_player_unflood]
p_teleport(movecoord(coord, -128, 0, 0));
p_walk(coord);


[oploc1,game_trawler_leak] @trawler_fix_leak;
[oplocu,game_trawler_leak]
if (last_useitem = swamppaste) {
    @trawler_fix_leak;
} else {
    ~displaymessage(^dm_default);
}

// using paste on repaired leak has NIH message: https://youtu.be/XtcZ1VTN9ik?list=PLn23LiLYLb1b7Lj_u-lnuUOa40FGWslbp&t=25

[label,trawler_fix_leak]
if (loc_type = game_trawler_leak) {
    p_arrivedelay;
    if (inv_total(inv, swamppaste) < 1) {
        mes("You'll need some swamp paste to fill that.");
        return;
    }
    anim(human_pickuptable, 0);
    sound_synth(pick, 0, 0);
    p_delay(0);
    if (loc_type = game_trawler_leak) {
        // Temp note: dur does not need updated
        loc_change(game_trawler_repaired_leak, 1000);
        if (~inzone_coord_pair_table(trawler_flood_zones, loc_coord) = true) {
            if (random(5) = 0) {
                if (npc_find(^trawler_flood_center_under, murphy_flood, 8, 0) = true) {
                    npc_queue(7, 0, 0);
                }
            }
            if (loc_find(movecoord(loc_coord, -128, 0, 0), game_trawler_leak) = true) {
                // Temp note: dur does not need updated
                loc_change(game_trawler_repaired_leak, 1000);
            }
        } else {
            if (random(5) = 0) {
                if (npc_find(^trawler_start_center_under, murphy_nonflood, 8, 0) = true) {
                    npc_queue(7, 0, 0);
                }
            }
            if (loc_find(movecoord(loc_coord, 128, 0, 0), game_trawler_leak) = true) {
                // Temp note: dur does not need updated
                loc_change(game_trawler_repaired_leak, 1000);
            }
        } 
        inv_del(inv, swamppaste, 1);
    }
}