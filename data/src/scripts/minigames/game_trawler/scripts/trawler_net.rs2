[proc,trawler_net_ripped]()(boolean)
if (loc_find(^trawler_start_net1, game_trawler_fish_net_broken1) = true) {
    return(true);
}
if (loc_find(^trawler_start_net2, game_trawler_fish_net_broken2) = true) {
    return(true);
}
return(false);

[proc,trawler_rip_net]
if (~trawler_net_ripped = true) {
    return;
}
if (loc_find(^trawler_start_net1, game_trawler_fish_net1) = true) {
    loc_change(game_trawler_fish_net_broken1, 1000);
}
if (loc_find(^trawler_start_net2, game_trawler_fish_net2) = true) {
    loc_change(game_trawler_fish_net_broken2, 1000); 
}
if (loc_find(^trawler_flood_net1, game_trawler_fish_net1) = true) {
    loc_change(game_trawler_fish_net_broken1, 1000);
}
if (loc_find(^trawler_flood_net2, game_trawler_fish_net2) = true) {
    loc_change(game_trawler_fish_net_broken2, 1000); 
}
huntall(^trawler_start_center_under, 8, 0);
while (huntnext = true) {
    queue(trawler_player_rip_net, 0);
}
huntall(^trawler_start_center, 8, 0);
while (huntnext = true) {
    queue(trawler_player_rip_net, 0);
}
huntall(^trawler_flood_center_under, 8, 0);
while (huntnext = true) {
    queue(trawler_player_rip_net, 0);
}
huntall(^trawler_flood_center, 8, 0);
while (huntnext = true) {
    queue(trawler_player_rip_net, 0);
}

[queue,trawler_player_rip_net]
mes("The net has ripped!");
~mesbox("The net has ripped!|You can't catch anything whilst the net is ripped!"); // https://imgur.com/sIkyhcN

[oploc1,game_trawler_fish_net1]
anim(human_pickupfloor, 0);
p_delay(0);
mes("The net is not damaged.");

[oploc1,game_trawler_fish_net2]
anim(human_pickupfloor, 0);
p_delay(0);
mes("The net is not damaged.");

[oplocu,game_trawler_fish_net1] @trawler_net_use;
[oplocu,game_trawler_fish_net2] @trawler_net_use;

[oploc1,game_trawler_fish_net_broken1] @trawler_fix_net;
[oploc1,game_trawler_fish_net_broken2] @trawler_fix_net;
[oplocu,game_trawler_fish_net_broken1] @trawler_net_use;
[oplocu,game_trawler_fish_net_broken2] @trawler_net_use;

[label,trawler_net_use]
if (last_useitem = rope) {
    ~chatplayer("<p,neutral>I need to inspect it first, I'll fix it if it needs doing."); // rs3
} else {
    ~displaymessage(^dm_default);
}

[label,trawler_fix_net]
if (inv_total(inv, rope) < 1) {
    anim(human_pickupfloor, 0);
    p_delay(0);
    mes("You'll need some rope to fix it."); // osrs
    return;
}
if (stat_random(crafting, 30, 300) = false) { // stat random is mostly guessed, i think osrs adjusted theirs at some point. I used 30 or so attempts from rs3
    anim(human_pickupfloor, 0);
    p_delay(0);
    mes("You fail to repair the net in the harsh conditions."); // https://youtu.be/scKmu6QETRw?t=347
    return;
}
if (loc_find(^trawler_start_net1, game_trawler_fish_net_broken1) = true) {
    // Temp note: dur updated
    loc_change(game_trawler_fish_net1, 2);
}
if (loc_find(^trawler_start_net2, game_trawler_fish_net_broken2) = true) {
    // Temp note: dur updated
    loc_change(game_trawler_fish_net2, 2); 
}
if (loc_find(^trawler_flood_net1, game_trawler_fish_net_broken1) = true) {
    // Temp note: dur updated
    loc_change(game_trawler_fish_net1, 2);
}
if (loc_find(^trawler_flood_net2, game_trawler_fish_net_broken2) = true) {
    // Temp note: dur updated
    loc_change(game_trawler_fish_net2, 2); 
}
anim(human_pickupfloor, 0);
p_delay(0);
mes("You manage to fix the net."); // osrs
stat_advance(crafting, 50);
inv_del(inv, rope, 1);