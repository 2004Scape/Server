[oploc1,loc_2417]
~open_chest(loc_2418);

[oploc2,loc_2418]
inv_transmit(partyroom_dropinv, party_drop_chest:inv);
inv_transmit(partyroom_tempinv, party_drop_chest:playerinv);
inv_transmit(inv, party_drop_chest_side:inv);
if_openmain_side(party_drop_chest, party_drop_chest_side);

[if_close,party_drop_chest]
inv_stoptransmit(party_drop_chest_side:inv);
inv_stoptransmit(party_drop_chest:inv);
inv_stoptransmit(party_drop_chest:playerinv);
~moveallinv(partyroom_tempinv, inv);

[oploc3,loc_2418]
~close_chest(loc_2417);

[inv_button1,party_drop_chest:playerinv] @withdraw_partychest(last_slot, 1);
[inv_button2,party_drop_chest:playerinv] @withdraw_partychest(last_slot, 5);
[inv_button3,party_drop_chest:playerinv] @withdraw_partychest(last_slot, 10);
[inv_button4,party_drop_chest:playerinv] @withdraw_partychest(last_slot, ^max_32bit_int);
[inv_button5,party_drop_chest:playerinv] @withdraw_partychest(last_slot, null);

[label,withdraw_partychest](int $slot, int $amount)
if ($amount <= 0) {
    p_countdialog;
    if (last_int <= 0) return;
    $amount = last_int;
}
def_obj $obj = inv_getobj(partyroom_tempinv, $slot);
def_int $total = inv_total(partyroom_tempinv, $obj);
if ($amount < $total) $total = $amount;
inv_moveitem(partyroom_tempinv, inv, $obj, $total);

[inv_button1,party_drop_chest_side:inv] @deposit_partychest(last_slot, 1);
[inv_button2,party_drop_chest_side:inv] @deposit_partychest(last_slot, 5);
[inv_button3,party_drop_chest_side:inv] @deposit_partychest(last_slot, 10);
[inv_button4,party_drop_chest_side:inv] @deposit_partychest(last_slot, ^max_32bit_int);
[inv_button5,party_drop_chest_side:inv] @deposit_partychest(last_slot, null);
[inv_buttond,party_drop_chest_side:inv] inv_movetoslot(inv, inv, last_slot, last_targetslot);

[label,deposit_partychest](int $slot, int $amount)
if ($amount <= 0) {
    p_countdialog;
    if (last_int <= 0) return;
    $amount = last_int;
}

def_obj $obj = inv_getobj(inv, $slot);
if (oc_tradeable($obj) = false) { // also map_members check (after room is moved to f2p probably)
    mes("You can't offer this item.");
    return;
}
def_int $total = inv_total(inv, $obj);
if ($amount < $total) $total = $amount;
if(oc_stackable($obj) = false & $total > inv_freespace(partyroom_tempinv)) $total = inv_freespace(partyroom_tempinv);
if($total = 0 | inv_itemspace(partyroom_tempinv, $obj, $total, inv_size(partyroom_tempinv)) = false) {
    mes("You must accept the current items before adding any more."); // 2006 https://www.youtube.com/watch?v=Ve5UF8B3BMw&t=194s
    return;
}
inv_moveitem(inv, partyroom_tempinv, $obj, $total);

[if_button,party_drop_chest:com_94]
def_int $i = 0;
def_boolean $moveditem = false;
if(inv_freespace(partyroom_tempinv) = inv_size(partyroom_tempinv)) {
    return;
}
while($i < inv_size(partyroom_tempinv)) {
    def_obj $obj = inv_getobj(partyroom_tempinv, $i);
    def_int $count = inv_getnum(partyroom_tempinv, $i);
    if ($obj ! null) {
        if(inv_itemspace(partyroom_dropinv, $obj, $count, inv_size(partyroom_dropinv)) = true) {
            inv_moveitem(partyroom_tempinv, partyroom_dropinv, $obj, $count);
            wealth_log(^wealth_lost, calc($count * oc_cost($obj)), "Deposited <oc_debugname($obj)> x<tostring($count)> in the party room chest");
            wealth_event(^wealth_partyroom, oc_debugname($obj), $count, calc($count * oc_cost($obj)));
            $moveditem = true;
        }
    }
    $i = add($i, 1);
}
if($moveditem = false) {
    mes("The chest cannot hold your offer at the moment.");
}
