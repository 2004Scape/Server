[opheldu,bowl_water]
switch_obj (last_useitem)
{
    case pot_flour : @dough_interface(last_item);
    case clay : @make_softclay(bowl_water);
    case bucket_empty, jug_empty : @water_fill(last_useslot, last_slot);
    case potato, cooked_meat, cooked_chicken : @make_incomplete_stew(last_useslot);
    case default : ~displaymessage(^dm_default);
};

[opheldu,bucket_water]
switch_obj (last_useitem)
{
    case newbie_pot_flour : @tut_make_dough;
    case pot_flour : @dough_interface(last_item);
    case clay : @make_softclay(bucket_water);
    case jug_empty, bowl_empty : @water_fill(last_useslot, last_slot);
    case default : ~displaymessage(^dm_default);
};

[opheldu,jug_water]
switch_obj (last_useitem)
{
    case grapes : @make_wine;
    case pot_flour : @dough_interface(last_item);
    case clay : @make_softclay(jug_water);
    case bucket_empty, bowl_empty : @water_fill(last_useslot, last_slot);
    case default : ~displaymessage(^dm_default);
};

[opheldu,bucket_empty]
switch_obj (last_useitem)
{
    case jug_water, bowl_water : @water_fill(last_useslot, last_slot);
    case default : ~displaymessage(^dm_default);
};

[opheldu,jug_empty]
switch_obj (last_useitem)
{
    case bucket_water, bowl_water : @water_fill(last_useslot, last_slot);
    case default : ~displaymessage(^dm_default);
};

[opheldu,bowl_empty]
switch_obj (last_useitem)
{
    case onion, tomato, cooked_ugthanki_meat : @make_bowl_mixture(last_useslot);
    case bucket_water, jug_water : @water_fill(last_useslot, last_slot);
    case default : ~displaymessage(^dm_default);
};

[label,water_fill](int $use_slot, int $last_slot)
def_obj $used_on = inv_getobj(inv, $last_slot);
def_obj $used_with = inv_getobj(inv, $use_slot);

inv_setslot(inv, $last_slot, oc_param($used_on, next_obj_stage), 1); //new water source
inv_setslot(inv, $use_slot, oc_param($used_with, next_obj_stage), 1); //new empty source

[oplocu,_watersource]
switch_obj (last_useitem) {
    case bucket_empty, jug_empty, vial_empty, bowl_empty, goldbowl_empty, goldbowlbless_empty : @fill_container_water(last_useitem);
    case default : ~displaymessage(^dm_default);
}

[label,fill_container_water](obj $container)
def_string $source_name = lowercase(loc_name);
def_string $container_name = lowercase(oc_name($container));
if(compare($source_name, "well") = 0 & ($container = goldbowl_empty | $container = goldbowlbless_empty)) {
    mes("If I drop my bowl down there I don't think I'm likely to get it back.");
    return;
}
if(compare($source_name, "well") = 0 & $container = bucket_empty) {
    ~displaymessage(^dm_default);
    return;
}
inv_del(inv, $container, 1);
inv_add(inv, oc_param($container, next_obj_stage), 1);
anim(human_pickuptable, 0);
sound_synth(tap_fill, 0, 0);
mes("You fill the <$container_name> from the <$source_name>.");
// strongqueued in osrs but that was likely changed in Sept 2005 
p_delay(0);
