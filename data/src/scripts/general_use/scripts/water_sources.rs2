[opheldu,bowl_water]
switch_obj (last_useitem)
{
    case pot_of_flour : @dough_interface(last_slot);
    case clay : @make_softclay(bowl_water);
    case bucket_empty, jug_empty : @water_fill(last_useslot, last_slot);
    case potato, cooked_meat, cooked_chicken : @make_incomplete_stew(last_useslot);
    case default : @nothing_interesting_happens;
};

[opheldu,bucket_water]
switch_obj (last_useitem)
{
    case pot_of_flour : @dough_interface(last_slot);
    case clay : @make_softclay(bucket_water);
    case jug_empty, bowl_empty : @water_fill(last_useslot, last_slot);
    case default : @nothing_interesting_happens;
};

[opheldu,jug_of_water]
switch_obj (last_useitem)
{
    case grapes : @make_wine;
    case pot_of_flour : @dough_interface(last_slot);
    case clay : @make_softclay(jug_of_water);
    case bucket_empty, bowl_empty : @water_fill(last_useslot, last_slot);
    case default : @nothing_interesting_happens;
};

[opheldu,bucket_empty]
switch_obj (last_useitem)
{
    case jug_of_water, bowl_water : @water_fill(last_useslot, last_slot);
    case default : @nothing_interesting_happens;
};

[opheldu,jug_empty]
switch_obj (last_useitem)
{
    case bucket_water, bowl_water : @water_fill(last_useslot, last_slot);
    case default : @nothing_interesting_happens;
};

[opheldu,bowl_empty]
switch_obj (last_useitem)
{
    case onion, tomato, ugthanki_meat : @make_bowl_mixture(last_useslot);
    case bucket_water, jug_of_water : @water_fill(last_useslot, last_slot);
    case default : @nothing_interesting_happens;
};

[label,water_fill](int $use_slot, int $last_slot)
def_obj $used_on = inv_getobj(inv, $last_slot);
def_obj $used_with = inv_getobj(inv, $use_slot);

inv_setslot(inv, $last_slot, oc_param($used_on, changesto), 1); //new water source
inv_setslot(inv, $use_slot, oc_param($used_with, changesto), 1); //new empty source

[oplocu,_watersource]
switch_obj (last_useitem) {
    case bucket_empty, jug_empty, vial_empty, bowl_empty: @fill_container_water(last_useitem);
    case default: @nothing_interesting_happens;
}

[label,fill_container_water](obj $container)
def_string $source_name = lowercase(loc_name);
def_string $container_name = lowercase(oc_name($container));
if(compare($source_name, "well") = 0 & compare($container_name, "bucket") ! 0) {
    @nothing_interesting_happens;
}
inv_del(inv, $container, 1);
inv_add(inv, oc_param($container, changesto), 1);
anim(human_pickuptable, 0);
sound_synth(tap_fill, 0, 0);
mes("You fill the <$container_name> from the <$source_name>.");
// strongqueued in osrs but that was likely changed in Sept 2005 
p_delay(0);