[proc,gate_set_open](int $angle, boolean $outer)(int, int)
def_int $x = 0;
def_int $z = 0;
switch_int($angle) {
    case ^loc_west : $z = -1;
    case ^loc_north : $x = -1;
    case ^loc_east : $z = 1;
    case ^loc_south : $x = 1;
}

if($outer = true) {
    switch_int($angle) {
        case ^loc_west : return (calc($x + 1), $z);
        case ^loc_north : return ($x, calc($z - 1));
        case ^loc_east : return (calc($x - 1), $z);
        case ^loc_south : return ($x, calc($z + 1));
    }   
}
return($x, $z);

[proc,gate_set_close](int $angle, int $mod)(int, int)
def_int $x = 0;
def_int $z = 0;
switch_int($angle) {
    case ^loc_west : $x = -1;
    case ^loc_north : $z = 1;
    case ^loc_east : $x = 1;
    case ^loc_south : $z = -1;
}
return(calc($x * $mod), calc($z * $mod));

[proc,get_pair_coord](coord $main_coord, int $angle, boolean $outer)(coord)
def_int $dir = 1;
if($outer = true) {
    $dir = -1;
}
switch_int($angle) {
    case ^loc_west : return (movecoord($main_coord, 0, 0, calc($dir * 1)));
    case ^loc_north : return (movecoord($main_coord, calc($dir * 1), 0, 0));
    case ^loc_east : return (movecoord($main_coord, 0, 0, calc($dir * -1)));
    case ^loc_south : return (movecoord($main_coord, calc($dir * -1), 0, 0));
}   
return ($main_coord);

[proc,open_gate]
def_int $orig_angle = loc_angle; 
def_coord $orig_coord = loc_coord;
def_coord $main_open = ~movecoord_loc_return(~gate_set_close($orig_angle, 1));
def_coord $outer_open = ~movecoord_loc_return(~gate_set_close($orig_angle, 2));
//todo: Figure out if these use door_open/close, the gate sounds werent added until ~2005?
sound_synth(door_open, 0, 0); 
// Temp note: dur does not need updated
loc_del(500);
loc_add($main_open, loc_param(next_loc_stage), modulo(add(loc_angle, 3), 4), wall_straight, 500);
loc_findallzone(~get_pair_coord($orig_coord, $orig_angle, false));
while (loc_findnext = true) {
    if (distance($orig_coord, loc_coord) <= 1 & loc_category = gate_outer_closed) {
        // Temp note: dur does not need updated
        loc_del(500);
        loc_add($outer_open, loc_param(next_loc_stage), modulo(add($orig_angle, 3), 4), wall_straight, 500);
        return;
    }
}

[proc,close_gate]
def_int $orig_angle = loc_angle; 
def_coord $orig_coord = loc_coord;
def_coord $main_closed = ~movecoord_loc_return(~gate_set_open($orig_angle, false));
def_coord $outer_closed = ~movecoord_loc_return(~gate_set_open($orig_angle, true));
sound_synth(door_close, 0, 0); 
// Temp note: dur does not need updated
loc_del(500);
loc_add($main_closed, loc_param(next_loc_stage), modulo(add(loc_angle, 1), 4), wall_straight, 500);
loc_findallzone(~get_pair_coord($orig_coord, $orig_angle, false));
while (loc_findnext = true) {
    if (distance($orig_coord, loc_coord) <= 1 & loc_category = gate_outer_open) {
        // Temp note: dur does not need updated
        loc_del(500);
        loc_add($outer_closed, loc_param(next_loc_stage), modulo(add($orig_angle, 1), 4), wall_straight, 500);
        return;
    }
}

[oploc1,_gate_main_closed] ~open_gate;
[oploc1,_gate_outer_closed]
def_coord $orig_coord = loc_coord;
loc_findallzone(~get_pair_coord($orig_coord, loc_angle, true));
while (loc_findnext = true) {
    if (distance($orig_coord, loc_coord) <= 1 & loc_category = gate_main_closed) {
        ~open_gate;
        return;
    }
}

[oploc1,_gate_main_open] ~close_gate;
[oploc1,_gate_outer_open]
def_coord $orig_coord = loc_coord;
loc_findallzone(~get_pair_coord($orig_coord, loc_angle, true));
while (loc_findnext = true) {
    if (distance($orig_coord, loc_coord) <= 1 & loc_category = gate_main_open) {
        ~close_gate;
        return;
    }
}