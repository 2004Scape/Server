[opheld1,mithril_seeds]
p_stopaction;
if (~inzone_coord_pair_table(bank_zones, coord) = true | map_locaddunsafe(coord) = true) {
    mes("You can't plant your flowers here."); // OSRS
    return;
} 
if (~inzone_coord_pair_table(party_room_zones, coord) = true) {
    // osrs
    mes("Party Pete won't be happy if you plant your flowers here.");
    return;
}
if (~duel_arena_movement_check = true & ~in_duel_arena(coord) = true) {
    // https://youtu.be/L3OwjH-iXQU
    //  - could still fm in the duel arena. 
    //  - its just that you cant firemake if movement is restricted
    // https://oldschool.runescape.wiki/w/Update:RS2_-_more_fixes/changes
    //  - "Fixed: You could select a duel with the no movement option and then plant flowers or light a fire so you weren't standing next to your opponent."
    mes("You can't plant your flowers here.");
    return;
}
mes("You open the small mithril case.");
mes("You drop a seed by your feet.");
inv_del(inv, mithril_seeds, 1);
loc_add(coord, ~random_flowers, 0, centrepiece_straight, 500);
def_coord $move_coord = ~free_mithseed_spot;
p_teleport($move_coord);
p_delay(1);
facesquare(loc_coord);
def_int $option = ~p_choice2("Pick the flowers.", 1, "Leave the flowers.", 2);
if($option = 2) {
    return;
}
if(inv_freespace(inv) = 0) {
    mes("You have no free space in your pack."); // OSRS
    return;
}
anim(human_pickupfloor, 0);
inv_add(inv, ~flowers_obj(loc_type), 1);
loc_del(1);

[proc,random_flowers](loc)
def_int $rand = random(1001);
if($rand < 150) {
    return (loc_2986);
} else if($rand < 300) {
    return (loc_2981);
} else if($rand < 450) {
    return (loc_2982);
} else if($rand < 600) {
    return (loc_2983);
} else if($rand < 750) {
    return (loc_2984);
} else if($rand < 900) {
    return (loc_2985);
} else if($rand < 998) {
    return (loc_2980);
} else if($rand < 999) {
    return (loc_2987);
} else if($rand < 1001) {
    return (loc_2988);
}

[proc,flowers_obj](loc $flowers_loc)(namedobj)
switch_loc($flowers_loc) {
    case loc_2980: return (assorted_flowers);
    case loc_2981: return (red_flowers);
    case loc_2982: return (blue_flowers);
    case loc_2983: return (yellow_flowers);
    case loc_2984: return (purple_flowers);
    case loc_2985: return (orange_flowers);
    case loc_2986: return (mixed_flowers);
    case loc_2987: return (white_flowers);
    case loc_2988: return (black_flowers);
    case default: return (mixed_flowers);
}

[proc,free_mithseed_spot](coord)
if (~in_duel_arena(coord) = true) {
        // https://youtu.be/L3OwjH-iXQU
        //  - could still fm in the duel arena. 
        //  - It just didnt move the player at all
        // https://oldschool.runescape.wiki/w/Update:RS2_-_more_fixes/changes
        //  - "Fixed: You could select a duel with the no movement option and then plant flowers or light a fire so you weren't standing next to your opponent."
        return(coord);
    }
if (lineofwalk(coord, movecoord(coord, -1, 0, 0)) = true) { // E
    return (movecoord(coord, -1, 0, 0));
} else if (lineofwalk(coord, movecoord(coord, 1, 0, 0)) = true) { // W
    return (movecoord(coord, 1, 0, 0));
} else if (lineofwalk(coord, movecoord(coord, 0, 0, -1)) = true) { // S
    return (movecoord(coord, 0, 0, -1));
} else if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true) { // N
    return (movecoord(coord, 0, 0, 1));
}
return (coord);