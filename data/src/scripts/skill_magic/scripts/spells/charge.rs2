[if_button,magic:charge]
if_close;
if (p_finduid(uid) = true) {
    if (%magearena < ^mage_arena_staff_given) {
        mes("This spell can only be learnt in the Mage Arena.");
        return;
    }
    // this was added sometime in 2005 https://www.youtube.com/watch?v=CqzZ_Qsw7RE
    // if (%charge_god_spell > 300) {
    //     mes("You can't recast that yet, your current Charge is too strong.");
    //     return;
    // }

    def_dbrow $spell_data = ~get_spell_data(^charge);
    if (~check_spell_requirements($spell_data) = false) {
        return;
    }
    %charge_god_spell = 350;
    // timer in osrs, can pause with interface
    settimer(charge, 2);
    mes("You feel charged with magic power.");
    anim(human_casting, 0);
    spotanim_pl(teleport_casting, 128, 32);
    sound_synth(charge, 0, 0);
    ~delete_spell_runes($spell_data);
    ~give_spell_xp($spell_data);
}

[timer,charge]
%charge_god_spell = sub(%charge_god_spell, 1);
if (%charge_god_spell < 1) {
    mes("Your magical charge fades away."); // https://youtu.be/nRiXrgWEJ40?t=12
    sound_synth(charge_gone, 0, 0);
    cleartimer(charge);
    return;
}
