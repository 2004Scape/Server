[if_button,magic:charge]
if_close;
if (p_finduid(uid) = true) {
    if (%magearena_progress < ^mage_arena_staff_given) {
        mes("This spell can only be learnt in the Mage Arena.");
        return;
    }

    if (%charge_god_spell > 300) {
        mes("You can't recast that yet, your current Charge is too strong.");
        return;
    }

    def_dbrow $spell_data = ~get_spell_data(^charge);
    if (~check_spell_requirements($spell_data) = false) {
        return;
    }
    %charge_god_spell = 350;
    // timer in osrs, can pause with interface
    settimer(charge, 2);
    mes("You feel charged with magic power.");
    anim(human_casting, 0);
    spotanim_pl(teleport_casting, 128, 32);
    sound_synth(charge, 0, 0);
    ~delete_spell_runes($spell_data);
    ~give_spell_xp($spell_data);
}


[timer,charge]
%charge_god_spell = sub(%charge_god_spell, 1);
if (%charge_god_spell < 1) {
    mes("Your magical charge fades away."); // https://youtu.be/nRiXrgWEJ40?t=12
    sound_synth(charge_gone, 0, 0);
    cleartimer(charge);
    return;
}


[proc,has_god_cape_and_staff]()(boolean)
if (inv_total(worn, saradomincape) > 0 & inv_total(worn, saradominstaff) > 0) {
    return (true);
}
if (inv_total(worn, guthixcape) > 0 & inv_total(worn, guthixstaff) > 0) {
    return (true);
}
if (inv_total(worn, zamorakcape) > 0 & inv_total(worn, zamorakstaff) > 0) {
    return (true);
}
return (false);