[label,magic_spell_charge_orb](component $spell, loc $loc)
def_dbrow $spell_data = ~get_spell_data($spell);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check if they have orb in inv
if (inv_total(inv, stafforb) < 1) {
    mes("You must be holding an orb to enchant it.");
    return;
}
// check runes, and spell reqs
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);
// check if used on correct loc
if (db_getfield($spell_data, magic_spell_table:loc_type, 0) ! $loc) {
    mes("This spell needs to be cast on <~add_article(lc_param(loc_type, charge_orb_name))>");
    return;
}
def_namedobj $final_obj;
def_seq $anim;
def_spotanim $spotanim;
def_synth $sound;
$final_obj, $anim, $spotanim, $sound = ~magic_spell_search_convertobj($spell_data, stafforb);
// delete old, add new, play animations
inv_del(inv, stafforb, 1);
~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
inv_add(inv, $final_obj, 1);
~give_spell_xp($spell_data);
anim($anim, 0);
spotanim_pl($spotanim, 92, 0);
sound_synth($sound, 0, 0);
// delay for 3-4 ticks
// https://youtu.be/M7foXShcyBY&t=113
// this could be 4 but not sure. osrs is 3 ticks
p_delay(2);