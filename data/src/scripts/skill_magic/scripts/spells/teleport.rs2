[if_button,magic:varrock_teleport] @magic_teleport(^varrock_teleport);
[if_button,magic:lumbridge_teleport] @magic_teleport(^lumbridge_teleport);
[if_button,magic:falador_teleport] @magic_teleport(^falador_teleport);
[if_button,magic:camelot_teleport] @magic_teleport(^camelot_teleport);
[if_button,magic:ardougne_teleport] @magic_teleport(^ardougne_teleport);
[if_button,magic:watchtower_teleport] @magic_teleport(^watchtower_teleport);

[label,magic_teleport](int $spell)
if_close;
if (p_finduid(uid) = true) {
    // look for spell in db
    def_dbrow $spell_data = ~get_spell_data($spell);
    if (~check_spell_requirements($spell_data)= false) {
        return;
    }
    // TODO: 
    // - check if player has plague city completed for ardy teleport
    // - check if player has watchtower quest completed for watchtower teleport
    // - check for additional reqs, BEFORE deleting runes
    if (~pre_tele_checks(coord) = false) {
        return;
    }
    if (~wilderness_level(coord) > 20) {
        mes("A mysterious force blocks your teleport spell!");
        mes("You can't use this teleport after level 20 wilderness.");
        return;
    }
    if($spell = ^ardougne_teleport & %elena_progress < ^elena_complete_read_scroll) {
        mes("You must have completed Plague City to use this spell.");
        return;
    }
    if (afk_event = ^true) {
        ~macro_event_general_spawn(~macro_event_set_random);
        return;
    }
    // delete runes and give xp
    ~delete_spell_runes($spell_data);
    ~give_spell_xp($spell_data);
    // give additional affects
    // teleport player. Always a radius of 2
    ~player_teleport_normal(~coord_lineofwalk_radius(db_getfield($spell_data, magic_spell_table:tele_coord, 0), 2));
}

[proc,player_teleport_normal](coord $coord)
if_close;
p_stopaction;
sound_synth(teleport_all, 0, 0);
anim(human_castteleport, 0);
spotanim_pl(teleport_casting, 92, 0);
p_delay(3);
~p_telejump_safe($coord);

[proc,p_telejump_safe](coord $coord)
p_telejump($coord);
anim(null, 0);
// TODO: ensure this proc is used for other teleports too
def_int $rum = inv_total(inv, karamja_rum);
if ($rum > 0) {
    inv_del(inv, karamja_rum, $rum);
    mes("Your Karamjan rum gets broken and spilled.");
}
if (%gnomeball_progress = ^gnomeball_game_started) {
    mes("You hear the ref call you out of the game as you leave.");
    def_int $ball_inv = inv_total(inv, gnomeball);
    def_int $ball_worn = inv_total(worn, gnomeball);
    if ($ball_inv > 0) {
        inv_del(inv, gnomeball, $ball_inv);
    }
    if ($ball_worn > 0) {
        inv_del(worn, gnomeball, $ball_worn);
    }
    ~gnomeball_reset;
}
~update_all(null);

[proc,pre_tele_checks](coord $coord)(boolean)
if (~inzone_coord_pair_table(trawler_wreck_zones, $coord) = true) {
    ~mesbox("Water and magic don't mix!|You can't teleport whilst swimming."); // osrs
    return(false);
}
if (~inzone_coord_pair_table(trawler_game_zones, $coord) = true) {
    ~mesbox("You're too far from shore to teleport!"); // osrs
    return(false);
}
if (~in_duel_arena($coord) = true) {
    mes("You can't teleport during a duel."); // guess based on: https://youtu.be/xLE8au7T50Q?t=227
    return(false);
}
return(true);
