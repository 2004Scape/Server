[if_button,magic:varrock_teleport] @magic_teleport(^varrock_teleport);
[if_button,magic:lumbridge_teleport] @magic_teleport(^lumbridge_teleport);
[if_button,magic:falador_teleport] @magic_teleport(^falador_teleport);
[if_button,magic:camelot_teleport] @magic_teleport(^camelot_teleport);
[if_button,magic:ardougne_teleport] @magic_teleport(^ardougne_teleport);
[if_button,magic:watchtower_teleport] @magic_teleport(^watchtower_teleport);

[label,magic_teleport](int $spell)
if_close;
if (p_finduid(uid) = true) {
    // look for spell in db
    def_dbrow $spell_data = ~get_spell_data($spell);
    if (~check_spell_requirements($spell_data)= false) {
        return;
    }
    if (~wilderness_level(coord) > 20) {
        mes("A mysterious force blocks your teleport spell!");
        mes("You can't use this teleport after level 20 wilderness.");
        return;
    }
    if($spell = ^ardougne_teleport & %elena_progress < ^elena_complete) {
        mes("You must have completed Plague City to use this spell.");
        return;
    }
    if($spell = ^watchtower_teleport & %itwatchtower_progress < ^itwatchtower_complete) {
        mes("You must have completed Watch Tower to use this spell.");
        return;
    }
    // https://www.reddit.com/r/2007scape/comments/b4vdki/watchtower_teleport_not_working_despite/, https://runescape.salmoneus.net/forums/topic/212538-cant-do-ardougne-tele-why-is-it-so/
    if(($spell = ^ardougne_teleport & %elena_progress < ^elena_complete_read_scroll) | ($spell = ^watchtower_teleport & %itwatchtower_progress < ^itwatchtower_complete_read_scroll)) {
        mes("You havn't learnt how to cast this spell yet.");
        return;
    }
    if (~pre_tele_checks(coord) = false) {
        return;
    }
    if (afk_event = ^true) {
        ~macro_event_general_spawn(~macro_event_set_random);
        return;
    }
    // delete runes and give xp
    ~delete_spell_runes($spell_data);
    ~give_spell_xp($spell_data);
    // give additional affects
    // teleport player. Always a radius of 2
    ~player_teleport_normal(map_findsquare(db_getfield($spell_data, magic_spell_table:tele_coord, 0), 0, 2, ^map_findsquare_lineofwalk));
}

[proc,player_teleport_normal](coord $coord)
if_close;
p_stopaction;
sound_synth(teleport_all, 0, 0);
anim(human_castteleport, 0);
spotanim_pl(teleport_casting, 92, 0);
p_delay(2);
~p_telejump_safe($coord);

[proc,p_telejump_safe](coord $coord)
if (stat(hitpoints) = 0) {
    return;
}
p_telejump($coord);
anim(null, 0);
// todo: ensure this proc is used for other teleports too
def_int $rum = inv_total(inv, karamja_rum);
if ($rum > 0) {
    inv_del(inv, karamja_rum, $rum);
    mes("Your Karamjan rum gets broken and spilled.");
}
if(inv_total(inv, plaguesample) > 0) {
    inv_del(inv, plaguesample, ^max_32bit_int);
    mes("The plague sample is too delicate...it disintegrates in the crossing.");
    return;
}

[proc,pre_tele_checks](coord $coord)(boolean)
if(inv_total(inv, thanainabarrel) > 0) {
    ~chatnpc_specific("Ana (in a Barrel)", anabarrel, "<p,idle>@dbl@-- You can't teleport while holding the barrel, it's just @dbl@too difficult to concentrate. --");
    return(false);
}
if (~inzone_coord_pair_table(trawler_wreck_zones, $coord) = true) {
    ~mesbox("Water and magic don't mix!|You can't teleport whilst swimming."); // osrs
    return(false);
}
if (~inzone_coord_pair_table(trawler_game_zones, $coord) = true) {
    ~mesbox("You're too far from shore to teleport!"); // osrs
    return(false);
}
if (~in_duel_arena($coord) = true) {
    mes("Coward! You can't teleport from a duel."); //RS3 message: https://archive.org/details/runelibris
    //Colored in #AB0908 in RS3, but was likely not colored in 2004
    return(false);
}
return(true);
