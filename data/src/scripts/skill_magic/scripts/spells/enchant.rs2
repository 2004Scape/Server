[opheldt,magic:com_3] @magic_spell_enchant;
[opheldt,magic:com_13] @magic_spell_enchant;
[opheldt,magic:com_24] @magic_spell_enchant;
[opheldt,magic:com_28] @magic_spell_enchant;
[opheldt,magic:com_35] @magic_spell_enchant;

[label,magic_spell_enchant]
// dont continue if another magic utility is already queued
if (getqueue(magic_utility_queue) > 0) {
    return;
}
// look for spell in db
def_dbrow $spell_data = ~get_spell_data(last_com);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);

def_namedobj $initial_obj = inv_getobj(inv, last_slot);
def_namedobj $final_obj;
def_seq $anim;
def_spotanim $spotanim;
def_sound $sound;
$final_obj, $anim, $spotanim, $sound = ~search_convertobj($spell_data, $product);
// if you cant enchant the item, display this message
if ($final_obj = null) {
    mes("This spell can only be cast on unenchanted <db_getfield($spell_data, magic_spell_table:additional_reqmessage, 0)> rings and amulets.");
    return;
}
// if they use a spell on a valid obj, but its p2p check it here
// --
// i know this isnt how jagex does it, since ring of forging is f2p in osrs but unenchantable in f2p.
// They likely have another label for enchanting rings. However, the way i do it works better IMO, we're unlikely to make 
// a piece of enchanted jewelry thats f2p and not enchantable in f2p.
// I guess if someone wants to make a ring or something f2p in the future, i suggest just making it enchantable as well.
if (oc_members($final_obj) = ^true) {
    ~require_members_make_item;
}
inv_setslot(inv, last_slot, $final_obj, 1);
anim($anim, 0);
spotanim($spotanim, 92, 0);
sound($sound, 0, 0);
mes("You successfully enchant the <lower(oc_name($initial_obj))>.");
queue(magic_utility_queue, 2, 0);