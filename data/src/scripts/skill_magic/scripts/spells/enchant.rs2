[opheldt,magic:enchant_lvl1]@magic_spell_enchant(^enchant_lvl1, last_slot);
[opheldt,magic:enchant_lvl2]@magic_spell_enchant(^enchant_lvl2, last_slot);
[opheldt,magic:enchant_lvl3]@magic_spell_enchant(^enchant_lvl3, last_slot);
[opheldt,magic:enchant_lvl4]@magic_spell_enchant(^enchant_lvl4, last_slot);
[opheldt,magic:enchant_lvl5]@magic_spell_enchant(^enchant_lvl5, last_slot);

[label,magic_spell_enchant](int $spell, int $slot)
def_obj $initial_obj = inv_getobj(inv, $slot);
// if player has casted a spell recently then return
if (map_clock < %magic_utility_delay | inv_total(inv, $initial_obj) < 1) {
    return;
}
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);

def_namedobj $final_obj;
def_seq $anim;
def_spotanim $spotanim;
def_synth $sound;
$final_obj, $anim, $spotanim, $sound = ~magic_spell_search_convertobj($spell_data, $initial_obj);
// if you cant enchant the item, display this message
if ($final_obj = null) {
    // this checks if initial_obj has a specific message with the current spell
    ~magic_spell_specificobj_reqmessage($spell_data, $initial_obj); 
    mes(db_getfield($spell_data, magic_spell_table:additional_reqmessage, 0));
    return;
}
// if they use a spell on a valid obj, but its p2p check it here
// --
// i know this isnt how jagex does it, since ring of forging is f2p in osrs but unenchantable in f2p.
// They likely have another label for enchanting rings
if (oc_members($final_obj) = true) {
    // not sure what the members message is
    ~require_members_make_item;
}
// enchanting always sets slot instead of deleting
inv_setslot(inv, $slot, $final_obj, 1);
~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
~give_spell_xp($spell_data);

anim($anim, 0);
spotanim_pl($spotanim, 92, 0);
sound_synth($sound, 0, 0);
if ($initial_obj = dragonstone_amulet) {
    mes("You successfully enchant the dragonstone amulet.");
} else {
    mes("You successfully enchant the <lowercase(oc_name($initial_obj))>.");
}
// delay magic utility by 3 ticks
%magic_utility_delay = calc(map_clock + 3);

[opheld4,amulet_of_glory_4] @amulet_of_glory_interface("Your amulet has three charges left.");
[opheld4,amulet_of_glory_3] @amulet_of_glory_interface("Your amulet has two charges left.");
[opheld4,amulet_of_glory_2] @amulet_of_glory_interface("Your amulet has one charge left.");
[opheld4,amulet_of_glory_1] @amulet_of_glory_interface("You use your amulet's last charge.");
[opheld4,amulet_of_glory] mes("The amulet has lost its charge.");

[label,amulet_of_glory_interface](string $message)
// https://youtu.be/Zv7Wh3TIDOc
mes("You rub the amulet...");
def_int $choice = ~p_choice5_header("Edgeville", 1, "Karamja", 2, "Draynor Village", 3, "Al Kharid", 4, "Nowhere", 5, "Where would you like to teleport to?");
// https://i.imgur.com/HsKKBnr.png
if ($choice = 5) {
    mes("You remain where you were.");
    return;
}
if (~wilderness_level(coord) > 30) {
    // osrs
    mes("A mysterious force blocks your teleport.");
    mes("You can't use this teleport after level 30 wilderness.");
    return;
}
mes($message);
switch_int($choice) {
    case 1: ~player_teleport_normal(0_48_54_15_40);
    case 2: ~player_teleport_normal(0_45_49_38_40);
    case 3: ~player_teleport_normal(0_48_50_33_51);
    case 4: ~player_teleport_normal(0_51_49_29_27);
}
inv_setslot(inv, last_slot, oc_param(last_item, next_obj_stage), 1);


[opheld4,_category_136]
// https://youtu.be/vGH1Nyp4IBU
mes("You rub the ring...");
if (~wilderness_level(coord) > 20) {
    // yes it actually says this in osrs
    mes("A mysterious force blocks your teleport spell!");
    mes("You can't use this teleport after level 20 wilderness.");
    return;
}
~player_teleport_normal(~coord_lineofwalk_radius(0_51_50_51_35, 2));
def_namedobj $new_ring = oc_param(last_item, next_obj_stage);
if ($new_ring = null) {
    inv_delslot(inv, last_slot);
    mes("Your ring of dueling crumbles to dust.");
} else {
    inv_setslot(inv, last_slot, $new_ring, 1);
    mes("Your ring of dueling has <~pluralise(oc_param($new_ring, charges), "use")> left.");
}

