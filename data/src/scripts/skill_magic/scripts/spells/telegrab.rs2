[opobjt,magic:telekinetic_grab] @magic_spell_telegrab;
[apobjt,magic:telekinetic_grab] @magic_spell_telegrab;

// TODO: telegrabbing
[label,magic_spell_telegrab]
// look for spell in db
def_dbrow $spell_data = ~get_spell_data(^telekinetic_grab);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);

// display tea
if (obj_type = obj_712) {
    ~chatnpc_specific(nc_name(tea_seller), tea_seller, angry, "Hey! Put that back! Those are for display only!");
    return;
}
// start anim, sound, and spotanim
anim(human_casttelegrab, 0);
sound_synth(telegrab_all, 0, 0);
spotanim_pl(spotanim_142, 92, 0);

// delete runes
~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
// delay character for 4 ticks, give item, delay character for 3 ticks
// play mapanim
def_int $duration = ~coord_projectile(coord, obj_coord, spotanim_144, 35, 0, 48, 16, -2, 64, 10);
spotanim_map(spotanim_144, obj_coord, 0, $duration);
p_stopaction;
// mes("<tostring(map_clock)>: You start telegrabbing.");
p_delay(2);
~give_spell_xp($spell_data);
if (obj_count < 1) {
    mes("Too late - it's gone!");
} else {
    inv_add(inv, obj_type, obj_count);
    obj_del();
}
p_delay(1);
// mes("<tostring(map_clock)>: You finish telegrabbing.");