[opobjt,magic:telekinetic_grab] @magic_spell_telegrab;
[apobjt,magic:telekinetic_grab] @magic_spell_telegrab;

// TODO: telegrabbing
[label,magic_spell_telegrab]
// look for spell in db
def_dbrow $spell_data = ~get_spell_data(^telekinetic_grab);
if (~check_spell_requirements($spell_data) = false) {
    return;
}
if (afk_event = ^true) {
    ~macro_event_general_spawn(~macro_event_set_random);
    return;
}
// display tea
if (obj_type = obj_712) {
    ~chatnpc_specific(nc_name(tea_seller), tea_seller, angry, "Hey! Put that back! Those are for display only!");
    return;
}
// These use a different mes compared to obj's with the telegrab_disabled param (maybe make a 2nd param)
if(obj_type = holy_grail | obj_type = ghost_skull | obj_type = obj_78 | obj_type = boy_ball) {
    mes("I can't use Telekinetic Grab on this object.");
    return;
}
if(oc_param(obj_type, telegrab_disabled) = true) {
    mes("You can't cast this spell on that object.");
    return;
}
// start anim, sound, and spotanim
anim(human_casttelegrab, 0);
sound_synth(telegrab_all, 0, 0);
spotanim_pl(telegrab_casting, 92, 0);

// delete runes
~delete_spell_runes($spell_data);
// delay character for 4 ticks, give item, delay character for 3 ticks
// play mapanim
def_int $duration = ~coord_projectile(coord, obj_coord, telegrab_travel, 35, 0, 48, 16, -2, 64, 10);
spotanim_map(telegrab_impact, obj_coord, 0, $duration);
p_stopaction;
// mes("<tostring(map_clock)>: You start telegrabbing.");
p_delay(2);
~give_spell_xp($spell_data);
if (obj_count < 1) {
    mes("Too late - it's gone!");
} else {
    inv_add(inv, obj_type, obj_count);
    obj_del();
}
p_delay(1);
// mes("<tostring(map_clock)>: You finish telegrabbing.");