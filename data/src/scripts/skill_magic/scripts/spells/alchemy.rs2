[opheldt,magic:highlvl_alchemy]@magic_spell_high_alch(^highlvl_alchemy, last_item);
[opheldt,magic:lowlvl_alchemy]@magic_spell_low_alch(^lowlvl_alchemy, last_item);

[label,magic_spell_high_alch](int $spell, obj $item)
if_close;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
if (~check_spell_requirements($spell_data) = false) {
    return;
}
// prevent alchemy
if (~is_alchable($item, $spell_data) = false) {
    return;
}
// play sound and anim
sound_synth(high_alchemy, 0, 0);
anim(human_casthighlvlalchemy, 0);
spotanim_pl(highlvlalchemy_casting, 92, 0);
// delete runes and give xp
~delete_spell_runes($spell_data);
~give_spell_xp($spell_data);
// give 0.6x of item's value and delete item
def_int $profit = max(scale(6, 10, oc_cost($item)), 1);
wealth_log(^wealth_gained, $profit, "High alching <oc_debugname($item)>");
inv_del(inv, $item, 1);
inv_add(inv, coins, $profit);
// delay by 5 ticks
if (afk_event = ^true) {
    ~macro_event_general_spawn(~macro_event_set_random);
}
// https://oldschool.runescape.wiki/w/Update:Patch_Notes_(11_April_2013), in osrs it weakqueues the spell. Early osrs it was delay'd
// https://www.neoseeker.com/forums/2410/t960058-useless-rs-facts/2.htm
// > When you high alch while your cannon is spinning, it stops spinning until you finish alching
// Also this video: https://www.youtube.com/watch?v=P_o3Cxg73Tg&t=91s
if_settabactive(^tab_magic);
p_delay(3);


[label,magic_spell_low_alch](int $spell, obj $item)
if_close;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
if (~check_spell_requirements($spell_data) = false) {
    return;
}
// prevent alchemy
if (~is_alchable($item, $spell_data) = false) {
    return;
}
// play sound and anim
sound_synth(low_alchemy, 0, 0);
anim(human_castlowlvlalchemy, 0);
spotanim_pl(lowlvlalchemy_casting, 92, 0);
// delete runes and give xp
~delete_spell_runes($spell_data);
~give_spell_xp($spell_data);
// give 0.4x of item's value and delete item
def_int $profit = max(scale(4, 10, oc_cost($item)), 1);
wealth_log(^wealth_gained, $profit, "Low alching <oc_debugname($item)>");
inv_del(inv, $item, 1);
inv_add(inv, coins, $profit);
if_settabactive(^tab_magic);
if (afk_event = ^true) {
    ~macro_event_general_spawn(~macro_event_set_random);
}
// delay by 3 ticks
p_delay(1);

[proc,is_alchable](obj $item, dbrow $spell_data)(boolean)
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~staff_runes($spell_data);
switch_obj($item) {
    case naturerune :
        if (sub(inv_total(inv, naturerune), $rune_count1) < 1) {
            mes("You do not have enough Nature Runes to cast this spell.");
            return(false);
        }
    case firerune :
        if (sub(inv_total(inv, firerune), $rune_count2) < 1) {
            mes("You do not have enough Fire Runes to cast this spell.");
            return(false);
        }
    case anainabarrel :
        mes("You can't do that to the poor girl!");
        return(false);
    case coins :
        mes("Coins are already made of gold.");
        return(false);
    case default :
        if(oc_category($item) = kitten) {
            mes("You can't do that to a defenceless kitten!");
            return(false);
        }
        if(oc_category($item) = cat) { // you can alch witches cat (RS3)
            mes("You can't do that to a defenceless cat!");
            return(false);
        }
        if (oc_param($item, no_alchemy) = ^true) {
            mes("You cannot use alchemy on that item.");
            return(false);
        }
}
return(true);