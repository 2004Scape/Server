[opheldt,magic:highlvl_alchemy]@magic_spell_high_alch(^highlvl_alchemy, last_item);
[opheldt,magic:lowlvl_alchemy]@magic_spell_low_alch(^lowlvl_alchemy, last_item);

[label,magic_spell_high_alch](int $spell, obj $item)
if_close;
// if player has casted a spell recently then return
// https://oldschool.runescape.wiki/w/Update:Patch_Notes_(11_April_2013), in osrs it weakqueues the spell instead of returning
if (map_clock < %magic_utility_delay) {
    return;
}
//p_stopaction;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);
// prevent alchemy
if (~is_alchable($item) = false) {
    return;
}
// play sound and anim
sound_synth(high_alchemy, 0, 0);
anim(human_casthighlvlalchemy, 0);
spotanim_pl(spotanim_113, 92, 0);
// delete runes and give xp
~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
~give_spell_xp($spell_data);
// give 0.6x of item's value and delete item
inv_del(inv, $item, 1);
inv_add(inv, coins, max(scale(6, 10, oc_cost($item)), 1));
// delay by 5 ticks
%magic_utility_delay = calc(map_clock + 5);


[label,magic_spell_low_alch](int $spell, obj $item)
if_close;
//p_stopaction;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);
// if player has casted a spell recently then return
// https://oldschool.runescape.wiki/w/Update:Patch_Notes_(11_April_2013), in osrs it weakqueues the spell instead of returning
if (map_clock < %magic_utility_delay) {
    return;
}
// prevent alchemy
if (~is_alchable($item) = false) {
    return;
}
// play sound and anim
sound_synth(low_alchemy, 0, 0);
anim(human_castlowlvlalchemy, 0);
spotanim_pl(spotanim_112, 92, 0);
// delete runes and give xp
~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
~give_spell_xp($spell_data);
// give 0.4x of item's value and delete item
inv_del(inv, $item, 1);
inv_add(inv, coins, max(scale(4, 10, oc_cost($item)), 1));
// delay by 3 ticks
%magic_utility_delay = calc(map_clock + 3);


[proc,is_alchable](obj $item)(boolean)
switch_obj($item) {
    case obj_1842 : 
        mes("unimplimented");
        return(false);
    case coins : 
        mes("Coins are already made of gold.");
        return(false);
    case default : 
        if (oc_param($item, no_alchemy) = ^true) {
            mes("You cannot use alchemy on that item.");
            return(false);
        }
}
return(true);