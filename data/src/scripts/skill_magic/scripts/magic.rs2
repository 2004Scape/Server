[opheldt,magic:lowlvl_alchemy]
@magic_spell_low_alch(magic:lowlvl_alchemy, last_item);

[opheldt,magic:highlvl_alchemy]
@magic_spell_high_alch(magic:highlvl_alchemy, last_item);

[opheldt,magic:superheat_item]
@magic_spell_superheat(magic:superheat_item, last_item);

[opheldt,magic:enchant_lvl1] @magic_spell_enchant(magic:enchant_lvl1, last_slot);
[opheldt,magic:enchant_lvl2] @magic_spell_enchant(magic:enchant_lvl2, last_slot);
[opheldt,magic:enchant_lvl3] @magic_spell_enchant(magic:enchant_lvl3, last_slot);
[opheldt,magic:enchant_lvl4] @magic_spell_enchant(magic:enchant_lvl4, last_slot);
[opheldt,magic:enchant_lvl5] @magic_spell_enchant(magic:enchant_lvl5, last_slot);

[oploct,magic:water_orb] @magic_spell_charge_orb(magic:water_orb, loc_type);
[oploct,magic:earth_orb] @magic_spell_charge_orb(magic:earth_orb, loc_type);
[oploct,magic:fire_orb] @magic_spell_charge_orb(magic:fire_orb, loc_type);
[oploct,magic:air_orb] @magic_spell_charge_orb(magic:air_orb, loc_type);

[apobjt,magic:telekinetic_grab] @magic_spell_telegrab(magic:telekinetic_grab, last_obj);
[opobjt,magic:telekinetic_grab] @magic_spell_telegrab(magic:telekinetic_grab, last_obj);

[if_button,magic:bones_to_bananas] @magic_spell_convert_bones; //bones to bananas
[if_button,magic:varrock_teleport] @magic_teleport;
[if_button,magic:lumbridge_teleport] @magic_teleport;
[if_button,magic:falador_teleport] @magic_teleport;
[if_button,magic:camelot_teleport] @magic_teleport;
[if_button,magic:ardougne_teleport] @magic_teleport;
[if_button,magic:watchtower_teleport] @magic_teleport;

[label,magic_level_fail]
mes("Your Magic level is not high enough for this spell.");

[label,magic_runes_fail](namedobj $rune)
mes("You do not have enough <~string_removeright(oc_name($rune), 5)> Runes to cast this spell.");

[proc,get_spell_data](component $spell)(dbrow)
// look for spell in db
db_find(magic_spell_table:spell, $spell);
def_dbrow $spell_data = db_findnext;
if ($spell_data = null) {
    @nothing_interesting_happens;
}
return ($spell_data);

// takes all 3 runes and their counts as arguments, checks if player has required runes
// accounts for staff and returns runes and their adjusted counts.
[proc,check_spell_requirements](dbrow $spell_data)(namedobj, int, namedobj, int, namedobj, int)
// check if members
if (db_getfield($spell_data, magic_spell_table:members, 0) = true) {
    ~require_members_spell;
}
// check if high enough level
if (stat(magic) < db_getfield($spell_data, magic_spell_table:levelrequired, 0)) {
    @magic_level_fail;
}
// define spells
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = db_getfield($spell_data, magic_spell_table:runesrequired, 0);
// check if wielding staff and look for staff in db
def_obj $staff = inv_getobj(worn, ^wearpos_rhand);
db_find(magic_staff_table:staff, $staff);
def_dbrow $staff_data = db_findnext;
def_namedobj $staff_rune = null;
if ($staff_data ! null) {
    $staff_rune = db_getfield($staff_data, magic_staff_table:rune, 0);
}
// remove the number of runes required if the rune matches with staff rune
if ($staff_rune = $rune1) {
    $rune_count1 = null;
}
if ($staff_rune = $rune2) {
    $rune_count2 = null;
}
if ($staff_rune = $rune3) {
    $rune_count3 = null;
}
// check if player has required runes
if (inv_total(inv, $rune1) < $rune_count1 & $rune1 ! null) {
    @magic_runes_fail($rune1);
}
if (inv_total(inv, $rune2) < $rune_count2 & $rune2 ! null) {
    @magic_runes_fail($rune2);
}
if (inv_total(inv, $rune3) < $rune_count3 & $rune3 ! null) {
    @magic_runes_fail($rune3);
}
return ($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);

[proc,delete_spell_runes](namedobj $rune1, int $rune_count1, namedobj $rune2, int $rune_count2, namedobj $rune3, int $rune_count3)
if ($rune_count1 > 0) {
    inv_del(inv, $rune1, $rune_count1);
}
if ($rune_count2 > 0) {
    inv_del(inv, $rune2, $rune_count2);
}
if ($rune_count3 > 0) {
    inv_del(inv, $rune3, $rune_count3);
}

[proc,give_spell_xp](dbrow $spell_data)
givexp(magic, db_getfield($spell_data, magic_spell_table:experience, 0));

// spells that convert one item into another are generalized
// spell dbrows with the convertobj field are spells that convertobj
[proc,magic_spell_search_convertobj](dbrow $data, obj $obj)(namedobj, seq, spotanim, synth)
def_int $i = 0;
while ($i <= db_getfieldcount($data, magic_spell_table:convertobj)) {
    def_obj $initial_obj;
    def_namedobj $final_obj;
    def_seq $seq;
    def_spotanim $spotanim;
    def_synth $sound;
    $initial_obj, $final_obj, $seq, $spotanim, $sound = db_getfield($data, magic_spell_table:convertobj, $i);
    if ($initial_obj = $obj) {
        return($final_obj, $seq, $spotanim, $sound);
    } else {
        $i = calc($i + 1);
    }
}
return(null, null, null, null);

// for opheldt spells that have specific messages for specific items
[proc,magic_spell_specificobj_reqmessage](dbrow $data, obj $obj)
def_int $i = 0;
while ($i <= db_getfieldcount($data, magic_spell_table:specificobj_reqmessage)) {
    def_obj $db_obj;
    def_string $string;
    $db_obj, $string = db_getfield($data, magic_spell_table:specificobj_reqmessage, $i);
    if ($db_obj = $obj) {
        mes($string);
        @exit;
    }
    $i = calc($i + 1);
}
return;