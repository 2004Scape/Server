[proc,magic_has_unlocks](int $level)(boolean)
if ($level = 3 | $level = 5) {
    return(true);
}
return(false);

[label,show_magic_unlocks](int $level)
if ($level = 3) {
    // TODO: objbox, proper text
    ~mesbox("You can now cast Confuse.");
} else if ($level = 5) {
    ~mesbox("You can now cast Water Strike.");
}

[queue,magic_unlocks]
def_int $level = stat_base(magic);

if_settext(levelup_magic:line1, "@dbl@Congratulations, you just advanced a magic level.");
if_settext(levelup_magic:line2, "Your magic level is now <tostring($level)>.");
if_openbottom(levelup_magic);
p_pausebutton;

if (~magic_has_unlocks($level) = true) {
    @show_magic_unlocks($level);
}

[levelup,magic]
def_int $level = stat_base(magic);

mes("Congratulations, you've just advanced your magic level. You are now level <tostring($level)>.");
spotanim_pl(levelup_anim, 124, 0);
sound_synth(firework, 0, 0);

def_boolean $unlocks = ~magic_has_unlocks($level);
if ($unlocks = false) {
    midi_jingle(^advance_magic_jingle, ^advance_magic_millis);
} else {
    midi_jingle(^advance_magic_2_jingle, ^advance_magic_2_millis);
}

queue(magic_unlocks, 0);
