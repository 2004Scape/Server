[oplocu,_cooking_oven] @attempt_cook_item;
[oplocu,_cooking_fire] @attempt_cook_item;


[label,attempt_cook_item]
// get item data from db
mes(oc_debugname(last_item));
db_find(cooking_generic:cooking_obj, last_item);
def_dbrow $data = db_findnext;
if ($data = null) {
    ~mesbox("You can't cook that.");
    return;
}
def_namedobj $cooked_item = db_getfield($data,cooking_generic:cooked,0);
def_namedobj $uncooked_item = db_getfield($data,cooking_generic:uncooked,0);
if (oc_param($uncooked_item, cooking_range_only) = ^true & lc_category(loc_type) ! cooking_oven) {
    ~mesbox("You need a proper oven to cook that.");
    return;
}
if ($uncooked_item = raw_oomlie) {
    ~mesbox("The meat is far too delicate to cook like this. Perhaps you should wrap it up to protect it from the heat.");
    return;
}
// check if mems obj
if (oc_members($cooked_item) = true | oc_members($uncooked_item) = true) {
    // complete guess btw, i have no idea what sorta message they'd use here. Perhaps theres early osrs footage of this but ive not found any.
    ~require_members_make_item;
}
// check if player has high enough level
def_int $levelrequired = db_getfield($data,cooking_generic:levelrequired,0);
if (stat(cooking) < $levelrequired) {
    ~mesbox("You need a Cooking level of <tostring($levelrequired)> to cook <lowercase(oc_name($cooked_item))>.");
    return;
}
// def variables
def_string $cookmessage;
def_string $succesmessage = db_getfield($data,cooking_generic:successmessage,0);
def_string $burnmessage = db_getfield($data,cooking_generic:burnmessage,0);
def_seq $anim;
// field can specify "0" to not show any message at all. Only applies to heating up cocktails AFAIK
// else if not specified at all it just shows the default message below.
if (string_length($succesmessage) < 1 & compare($succesmessage, "0") ! 0) {
    $succesmessage = "The <lowercase(oc_name($cooked_item))> is now nicely cooked.";
}
if (string_length($burnmessage) < 1 & compare($burnmessage, "0") ! 0) {
    $burnmessage = "You accidentally burn the <lowercase(oc_name($cooked_item))>.";
}
// diff loc categories give diff messages
def_int $low;
def_int $high;
$low, $high = db_getfield($data,cooking_generic:successchance,0);
def_int $low_range;
def_int $high_range;
$low_range, $high_range = db_getfield($data,cooking_generic:successchance_range,0);
def_int $low_gauntlets;
def_int $high_gauntlets;
$low_gauntlets, $high_gauntlets = db_getfield($data,cooking_generic:successchance_gauntlets,0);
def_int $low_cookomatic;
def_int $high_cookomatic;
$low_cookomatic, $high_cookomatic = db_getfield($data,cooking_generic:successchance_cookomatic,0);

def_category $cooking_source = lc_category(loc_type);
if ($cooking_source = cooking_oven) {
    $cookmessage = db_getfield($data,cooking_generic:cookmessage_range,0);
    if ($low_range > null | $high_range > null) {
        $low = $low_range;
        $high = $high_range;
    }
    $anim = human_cooking;
    if (string_length($cookmessage) < 1 & compare($cookmessage, "0") ! 0) {
        $cookmessage = "You put the <lowercase(oc_name($cooked_item))> on the stove.";
    }
} else if ($cooking_source = cooking_fire) {
    $cookmessage = db_getfield($data,cooking_generic:cookmessage_fire,0);
    $anim = human_firecooking;
    if (string_length($cookmessage) < 1 & compare($cookmessage, "0") ! 0) {
        $cookmessage = "You put the <lowercase(oc_name($cooked_item))> on the fire.";
    }
}
// cooking gauntlets prioritize over cook o matic it seems
if (inv_total(worn, gauntlets_of_cooking) > 0 & ($low_gauntlets > null | $high_gauntlets > null)) {
    $low = $low_gauntlets;
    $high = $high_gauntlets;
} else if (loc_type = cook_range & ($low_cookomatic > null | $high_cookomatic > null)) { // lumbridge cook-o-matic 100
    $low = $low_cookomatic;
    $high = $high_cookomatic;
}
def_boolean $passes_roll;
// field can specify 0 to always burn, or 1 to always cook
if ($low = 0 & $high = 0) {
    $passes_roll = false;
} else if ($low = 1 & $high = 1) {
    $passes_roll = true;
} else {
    $passes_roll = stat_random(stat(cooking), $low, $high);
}
def_namedobj $burnt_item = db_getfield($data,cooking_generic:burnt,0);
def_namedobj $additional_item = db_getfield($data,cooking_generic:additional,0);
def_int $experience = db_getfield($data,cooking_generic:experience,0);
@cook_item($passes_roll, $anim, $cookmessage, $succesmessage, $burnmessage, $uncooked_item, $cooked_item, $burnt_item, $additional_item, $experience);


// i put this in a label so its easier to transition to weak queues later on
[label,cook_item](boolean $passes_roll, seq $anim, string $cookmessage, string $succesmessage, string $burnmessage, namedobj $uncooked_item, namedobj $cooked_item, namedobj $burnt_item, namedobj $additional_item, int $experience)
mes($cookmessage);
anim($anim, 0);
sound_synth(fry, 0, 0);
// could be p_delay(2);
p_delay(3);
inv_del(inv, $uncooked_item, 1);

if ($passes_roll = true) {
    inv_add(inv, $cooked_item, 1);
    mes($succesmessage);
    givexp(cooking, $experience);
} else {
    inv_add(inv, $burnt_item, 1);
    mes($burnmessage);
}
// i think literally only cakes (gives cake tin) use this, but not sure.
if ($additional_item ! null) {
    inv_add(inv, $additional_item, 1);
}