[opheldu,pot_flour]
if(oc_category(last_useitem) = murder_proofobj) {
    @flour_proofobj(last_useitem);
}
switch_obj (last_useitem) {
    case cake_tin : @make_uncooked_cake;
    case swamp_tar : @make_swamp_paste;
    case default :
        if (oc_param(last_useitem, is_water_source) = ^true) {
            @dough_interface(last_useitem);
        }
        ~displaymessage(^dm_default);
        return;
}

// https://www.youtube.com/watch?v=Tix1wbx3YCc
// https://youtu.be/n79WMvBkFTc
[label,dough_interface](obj $water_source)
if (inv_freespace(inv) < 1) {
    mes("You dont have enough inventory space to make any dough.");
    return;
}
def_namedobj $choice; 
def_string $end_string = "some";
switch_int (~p_choice4_header("Bread dough.", 1, "Pastry dough.", 2, "Pizza dough.", 3, "Pitta dough.", 4,  "What sort of dough do you wish to make?")) {
    case 1 : $choice = bread_dough;
    case 2 : $choice = pastry_dough;
    case 3 :
        $choice = pizza_base;
        $end_string = "a"; // only pizza adds an article
    case 4 : 
        $choice = uncooked_pitta_bread;
    case default :
        ~displaymessage(^dm_default);
        return;
}

if (map_members = ^false & oc_members($choice) = true) {
    mes(^mes_members_do_that); // guess... osrs has no option on f2p worlds for pitta :(
    return;
}

inv_del(inv, pot_flour, 1);
inv_del(inv, $water_source, 1);

inv_add(inv, pot_empty, 1);
if (oc_param($water_source, next_obj_stage) ! null) {
    inv_add(inv, oc_param($water_source, next_obj_stage), 1);
}
inv_add(inv, $choice, 1);
mes("You mix the water and flour to make <$end_string> <lowercase(oc_name($choice))>.");