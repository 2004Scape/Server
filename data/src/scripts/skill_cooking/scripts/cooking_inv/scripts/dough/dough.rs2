[opheldu,pot_of_flour]
if(oc_category(last_useitem) = murder_proofobj) {
    @flour_proofobj(last_useitem);
}
switch_obj (last_useitem) {
    case cake_tin : @make_uncooked_cake;
    case swamp_tar: @make_swamp_paste;
    case bowl_water, jug_of_water, bucket_water :
        if (%tutorial_progress < ^tutorial_complete) {
            @tut_make_dough;
        } else {
            @dough_interface(last_useslot);
        }
    case default : @nothing_interesting_happens;
}

[label,dough_interface](int $last_slot)
if (inv_freespace(inv) < 1) {
    mes("You dont have enough inventory space to make any dough.");
    return;
}
def_namedobj $choice; 
def_string $end_string = "some";
switch_int (~p_choice3_header("Bread dough.", 1, "Pastry dough.", 2, "Pizza dough.", 3, "What sort of dough do you wish to make?")) {
    case 1 : $choice = bread_dough;
    case 2 : $choice = pastry_dough;
    case 3 : 
        $choice = pizza_base;
        $end_string = "a";
    case default : @nothing_interesting_happens;
}
def_obj $water_source = inv_getobj(inv, $last_slot);

inv_del(inv, pot_of_flour, 1);
inv_del(inv, $water_source, 1);

inv_add(inv, pot_empty, 1);
if (oc_param($water_source, changesto) ! null) {
    inv_add(inv, oc_param($water_source, changesto), 1);
}
inv_add(inv, $choice, 1);
mes("You mix the water and flour to make <$end_string> <lowercase(oc_name($choice))>.");