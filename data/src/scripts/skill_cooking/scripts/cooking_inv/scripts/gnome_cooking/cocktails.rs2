[opheld1,cocktail_shaker]
def_int $total_steps = ~get_cocktail_ingredient_step;
if ($total_steps  < 1) {
    mes("The cocktail shaker is empty.");
    return;
}
def_string $message = "The cocktail shaker contains ";
def_string $ingredient_name;
def_int $step = 0;
def_int $ingredient_count = 0;
def_int $new_line_count = 0;
while ($step < calc($total_steps + 1)) {
    $ingredient_name, $ingredient_count = ~get_cocktail_ingredient_name($step);
    $new_line_count = calc($new_line_count + 1);
    if ($step ! 0 & $total_steps > 1) $message = append($message, ", ");
    $step = calc($step + $ingredient_count);

    // new line on 4rd ingredient
    if ($new_line_count = 4) {
        mes($message);
        $message = "";
    }
    if ($step = $total_steps & $total_steps > 1) {
        mes("<$message>and <$ingredient_name>.");
        return;
    }
    $message = append($message, $ingredient_name);
}
mes("<$message>.");

[opheldu,cocktail_shaker]
@add_cocktail_ingredient(last_useslot);

[label,add_cocktail_ingredient](int $slot)
def_obj $item = inv_getobj(inv, $slot);
def_string $item_name = lowercase(oc_name($item));
def_string $message = "You put nothing";
switch_obj($item) {
    case lemon, orange, pineapple, lime, dwellberry : $message = "You squeeze the juice from the <$item_name>";
    case vodka, gin, brandy, whisky, bucket_milk : $message = "You pour the <$item_name>";
    case equa_leaves : $message = "You sprinkle the equa leaves";
    case default : @nothing_interesting_happens;
}
def_int $step = ~get_cocktail_ingredient_step;
switch_int($step) {
    case 0 : %gnome_drink_ingredient1 = $item;
    case 1 : %gnome_drink_ingredient2 = $item;
    case 2 : %gnome_drink_ingredient3 = $item;
    case 3 : %gnome_drink_ingredient4 = $item;
    case 4 : %gnome_drink_ingredient5 = $item;
    case 5 : %gnome_drink_ingredient6 = $item;
    case 6 : %gnome_drink_ingredient7 = $item;
    case default : 
        mes("The cocktail shaker is too full to fit anymore ingredients.");
        return;
}
mes("<$message> into your cocktail shaker...");
inv_setslot(inv, $slot, null, 1);


[proc,get_cocktail_ingredient_step]()(int)
if (%gnome_drink_ingredient1 = null | %gnome_drink_ingredient1 = obj_0) {
    return(0);
}
if (%gnome_drink_ingredient2 = null | %gnome_drink_ingredient2 = obj_0) {
    return(1);
}
if (%gnome_drink_ingredient3 = null | %gnome_drink_ingredient3 = obj_0) {
    return(2);
}
if (%gnome_drink_ingredient4 = null | %gnome_drink_ingredient4 = obj_0) {
    return(3);
}
if (%gnome_drink_ingredient5 = null | %gnome_drink_ingredient5 = obj_0) {
    return(4);
}
if (%gnome_drink_ingredient6 = null | %gnome_drink_ingredient6 = obj_0) {
    return(5);
}
if (%gnome_drink_ingredient7 = null | %gnome_drink_ingredient7 = obj_0) {
    return(6);
}
return(7);

[proc,get_cocktail_ingredient_name](int $step)(string, int)
def_obj $ingredient;
switch_int($step) {
    case 0 : $ingredient = %gnome_drink_ingredient1;
    case 1 : $ingredient = %gnome_drink_ingredient2;
    case 2 : $ingredient = %gnome_drink_ingredient3;
    case 3 : $ingredient = %gnome_drink_ingredient4;
    case 4 : $ingredient = %gnome_drink_ingredient5;
    case 5 : $ingredient = %gnome_drink_ingredient6;
    case 6 : $ingredient = %gnome_drink_ingredient7;
    case default : $ingredient = %gnome_drink_ingredient1;
}
def_int $count = 0;
if (%gnome_drink_ingredient1 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient2 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient3 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient4 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient5 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient6 = $ingredient) $count = calc($count + 1);
if (%gnome_drink_ingredient7 = $ingredient) $count = calc($count + 1);
if ($count < 1) {
    return("", $count);
}
def_string $ingredient_name = oc_name($ingredient);
if (string_length(oc_param($ingredient, singular_name)) > 0) {
    $ingredient_name = oc_param($ingredient, singular_name);
}
def_string $ingredient_name_shot = "<$ingredient_name> shot";
if ($count > 1) {
    $ingredient_name = append_char($ingredient_name, 's');
    $ingredient_name_shot = append_char($ingredient_name_shot, 's');
    if (string_length(oc_param($ingredient, plural_name)) > 0) {
        $ingredient_name = oc_param($ingredient, plural_name);
    }
}
switch_obj ($ingredient) {
    case lemon, orange, pineapple, lime, dwellberry, equa_leaves, bucket_milk :
        return("<tostring($count)> <lowercase($ingredient_name)>", $count);
    case vodka, gin, brandy, whisky : 
        return("<tostring($count)> <lowercase($ingredient_name_shot)>", $count);
    case default : 
        return("", $count);
}

[debugproc,cock]
mes(oc_name(%gnome_drink_ingredient1));
mes(oc_name(%gnome_drink_ingredient2));
mes(oc_name(%gnome_drink_ingredient3));
mes(oc_name(%gnome_drink_ingredient4));
mes(oc_name(%gnome_drink_ingredient5));
mes(oc_name(%gnome_drink_ingredient6));
mes(oc_name(%gnome_drink_ingredient7));