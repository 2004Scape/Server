[oplocu,loc_2728]
~cook_check_range;

[oplocu,loc_2729]
~cook_check_range;

[oplocu,loc_2730]
~cook_check_range;

[oplocu,loc_2731]
~cook_check_range;

[proc,cook_check_range]
switch_obj (last_item)
{
    case raw_oomlie, seaweed : @cook_item_range (last_slot, loc_type);
    case default :
        if (oc_category(last_item) = cooking_range_only | oc_category(last_item) = cooking_generic | oc_category(last_item) = category_57) {
            @cook_item_range (last_slot, loc_type);
        }
        ~mesbox("You can't cook that.");
};


[label,cook_item_range] (int $last_slot, loc $loc)
def_namedobj $uncooked_item = inv_getobj(inv, $last_slot);
// get item data from db
db_find(cooking_generic:uncooked, $uncooked_item);
def_dbrow $data = db_findnext;
if ($data = null) {
    @nothing_interesting_happens;
}
def_namedobj $product = db_getfield($data,cooking_generic:cooked,0);
// check if mems obj
if (oc_members($product) = true) {
    ~require_members_make_item;
}
// check if player has high enough level
def_int $levelrequired = db_getfield($data,cooking_generic:levelrequired,0);
if (stat(cooking) < $levelrequired) {
    ~mesbox("You need a Cooking level of <tostring($levelrequired)> to cook <lowercase(oc_name($product))>.");
    return;
}
//get info from struct
def_struct $struct = oc_param($product, cooking_generic_struct);
def_string $processmessage_range = "";
def_string $productmessage = "";
def_string $burnmessage = "";
if ($struct ! null) {
    $processmessage_range = struct_param($struct, processmessage_range);
    $productmessage = struct_param($struct, productmessage);
    $burnmessage = struct_param($struct, burnmessage);
}
// prevent from spamming, (Furnaces do the same thing)
if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + 3);
    anim(seq_883, 0);
    sound_synth(fry,0,0);
    //if process message isnt defined, then display default. else display define process message
    if (string_length($processmessage_range) < 3) {
        mes("You put the <lowercase(oc_name($product))> on the stove.");
    } else {
        mes($processmessage_range);
    }
    weakqueue(cooking_item_range, 1, $data, $product, $loc, $processmessage_range, $burnmessage, $productmessage);
} else {
    weakqueue(start_cooking_range, calc(%skill_clock - map_clock - 1), $data, $product, $loc, $processmessage_range, $burnmessage, $productmessage);
}

//--- set skill clock, and start cooking
[weakqueue,start_cooking_range](dbrow $data, namedobj $product, loc $loc, string $processmessage_range, string $burnmessage, string $productmessage)
%skill_clock = calc(map_clock + 3);
anim(seq_883, 0);
sound_synth(fry,0,0);
//if process message isnt defined, then display default. else display define process message
if (string_length($processmessage_range) < 3) {
    mes("You put the <lowercase(oc_name($product))> on the stove.");
} else {
    mes($processmessage_range);
}
weakqueue(cooking_item_range, 3, $data, $product, $loc, $processmessage_range, $burnmessage, $productmessage);
//--------------------------------

//--- Weakqueue for cooking 1 item, on a range
[weakqueue,cooking_item_range](dbrow $data, namedobj $product, loc $loc, string $processmessage_range, string $burnmessage, string $productmessage)
// check if cooking gauntlets
def_int $low;
def_int $high;
if (inv_total(worn, gauntlets_of_cooking) > 0) {
    $low, $high = db_getfield($data,cooking_generic:successchance_gauntlets,0);
} else if ($loc = cook_range){
    $low, $high = db_getfield($data,cooking_generic:successchance_cookomatic,0);
} else {
    $low, $high = db_getfield($data,cooking_generic:successchance_range,0);
}
// play anim again? Furnaces do this, not sure for cooking
anim(seq_883, 0);
// delete raw item
inv_del(inv, db_getfield($data,cooking_generic:uncooked,0), 1);
// check if cooked or burnt
if (stat_random(stat(cooking), $low, $high) = true) {
    // add cooked
    inv_add(inv, $product, 1);
    // display message
    if ($product = burnt_oomlie) {
        //cooked product of oomlie is burnt oomlie
        ~mesbox("The meat is far too delicate to cook like this. Perhaps you should wrap it up to protect it from the heat.");
        return;
    }
    //if product message isnt defined, then display default. else display define product message
    if (string_length($productmessage) < 3) {
        mes("The <lowercase(oc_name($product))> is now nicely cooked.");
    } else {
        mes($productmessage);
    }
    // add exp
    givexp(cooking, db_getfield($data,cooking_generic:experience,0));
} else {
    // add burnt
    inv_add(inv, db_getfield($data,cooking_generic:burnt,0), 1);
    // display message
    //if burn message isnt defined, then display default. else display define burn message
    if (string_length($burnmessage) < 3) {
        mes("You accidentally burn the <lowercase(oc_name($product))>.");
    } else {
        mes($burnmessage);
    }
}
//if cake, add cake tin
if ($product = cake) {
    inv_add(inv, cake_tin, 1);
}
//-------------------------------

