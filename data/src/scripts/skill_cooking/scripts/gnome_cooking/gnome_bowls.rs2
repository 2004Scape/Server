[opheld1,half_baked_gnomebowl]
if (~gnome_bowl ! odd_gnomebowl) {
    mes("The gnome bowl is ready to bake.");
    return;
}
@bowl_prep_choose;

[label,bowl_prep_choose]
@multi4_header(
    "Chocolate Bomb", bowl_prep_chocolate_bomb,
    "Tangled Toad's Legs", bowl_prep_tangled_toad,
    "Worm Hole", bowl_prep_worm_hole,
    "Veg Ball", bowl_prep_veg_ball,
    "What would you like to prepare?"
);


[label,bowl_prep_chocolate_bomb]
if (inv_total(inv, chocolate_bar) < 4 | inv_total(inv, equa_leaves) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the bowl.");
inv_del(inv, chocolate_bar, 4);
inv_del(inv, equa_leaves, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_bowl_chocolate_bomb, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit);


[label,bowl_prep_veg_ball]
if (inv_total(inv, onion) < 2 | inv_total(inv, potato) < 2 | inv_total(inv, gnome_spice) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the bowl.");
inv_del(inv, onion, 2);
inv_del(inv, potato, 2);
// dont delete spice!
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_bowl_veg_ball, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit);


[label,bowl_prep_worm_hole]
if (inv_total(inv, king_worm) < 4 | inv_total(inv, onion) < 2 | inv_total(inv, gnome_spice) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the bowl.");
inv_del(inv, king_worm, 4);
inv_del(inv, onion, 2);
// dont delete spice!
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_bowl_worm_hole, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit);


[label,bowl_prep_tangled_toad]
if (inv_total(inv, toad_legs) < 4 | inv_total(inv, cheese) < 2 | inv_total(inv, equa_leaves) < 2 | inv_total(inv, gnome_spice) < 1 | inv_total(inv, dwellberry) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the bowl.");
inv_del(inv, toad_legs, 4);
inv_del(inv, cheese, 2);
inv_del(inv, equa_leaves, 2);
// dont delete spice!
inv_del(inv, dwellberry, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_bowl_tangled_toad_legs, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit);

[proc,gnome_bowl]()(namedobj)
return(enum(int, namedobj, gnome_bowls, getbit_range(%gnome_cuisine_var, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit)));

[proc,gnome_bowl_cook]()(namedobj)
def_namedobj $gnome_bowl = ~gnome_bowl;
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cuisine_empty, ^gnome_bowl_start_bit, ^gnome_bowl_end_bit);
return($gnome_bowl);