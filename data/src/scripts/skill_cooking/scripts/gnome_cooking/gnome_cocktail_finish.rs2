[label,gnome_drink_add](obj $gnome_drink, obj $ingredient, namedobj $req_ingredient, namedobj $product, namedobj $fail)
if (oc_param($ingredient, gnome_topping) = ^false) {
    ~displaymessage(^dm_default);
    return;
}

if (string_length(oc_param($ingredient, cocktail_message)) < 1) {
    mes("You add the <lowercase(oc_name($ingredient))> to the drink."); // guess
} else {
    mes(oc_param($ingredient, cocktail_message));
}

inv_del(inv, $gnome_drink, 1);

if ($ingredient ! gnome_spice) { // gnome spice is infinite
    inv_del(inv, $ingredient, 1);
}
if ($ingredient ! $req_ingredient) {
    inv_add(inv, $fail, 1);
    return;
}
inv_add(inv, $product, 1);

if (oc_category($product) = gnome_cocktail) { // finished
    stat_advance(cooking, 600);
}

[opheldu,_gnome_unf_odd_cocktail] @finish_cocktail(last_item, last_useitem);

[label,finish_cocktail](obj $cocktail, obj $ingredient)
switch_obj ($cocktail) {
    case unfinished_pineapple_punch1 : @gnome_drink_add($cocktail, $ingredient, pineapple_chunks, unfinished_pineapple_punch2, odd_cocktail2);
    case unfinished_pineapple_punch2 : @gnome_drink_add($cocktail, $ingredient, lime_chunks, unfinished_pineapple_punch3, odd_cocktail3);
    case unfinished_pineapple_punch3 : @gnome_drink_add($cocktail, $ingredient, orange_slices, pineapple_punch, odd_cocktail4);
    case unfinished_blurberry_special1 : @gnome_drink_add($cocktail, $ingredient, orange_chunks, unfinished_blurberry_special2, odd_cocktail2);
    case unfinished_blurberry_special2 : @gnome_drink_add($cocktail, $ingredient, lemon_chunks, unfinished_blurberry_special3, odd_cocktail3);
    case unfinished_blurberry_special3 : @gnome_drink_add($cocktail, $ingredient, lime_slices, unfinished_blurberry_special4, odd_cocktail3);
    case unfinished_blurberry_special4 : @gnome_drink_add($cocktail, $ingredient, equa_leaves, blurberry_special, odd_cocktail4);
    case unfinished_chocolate_saturday1 : @gnome_drink_add($cocktail, $ingredient, chocolate_bar, unfinished_chocolate_saturday2, odd_cocktail2);
    case unfinished_chocolate_saturday2 : @gnome_drink_add($cocktail, $ingredient, null, unfinished_chocolate_saturday2, odd_cocktail2);
    case unfinished_chocolate_saturday3 : @gnome_drink_add($cocktail, $ingredient, pot_of_cream, unfinished_chocolate_saturday4, odd_cocktail3);
    case unfinished_chocolate_saturday4 : @gnome_drink_add($cocktail, $ingredient, chocolate_dust, chocolate_saturday, odd_cocktail4);
    case unfinished_drunk_dragon1 : @gnome_drink_add($cocktail, $ingredient, pineapple_chunks, unfinished_drunk_dragon2, odd_cocktail2);
    case unfinished_drunk_dragon2 : @gnome_drink_add($cocktail, $ingredient, pot_of_cream, unfinished_drunk_dragon3, odd_cocktail3);
    case unfinished_drunk_dragon3 : @gnome_drink_add($cocktail, $ingredient, null, unfinished_drunk_dragon3, odd_cocktail3);
    case unfinished_fruit_blast1 : @gnome_drink_add($cocktail, $ingredient, lemon_slices, fruit_blast, odd_cocktail2);
    case unfinished_short_green_guy1 : @gnome_drink_add($cocktail, $ingredient, equa_leaves, unfinished_short_green_guy2, odd_cocktail2);
    case unfinished_short_green_guy2 : @gnome_drink_add($cocktail, $ingredient, lime_slices, short_green_guy, odd_cocktail2);
    case unfinished_wizard_blizzard1 : @gnome_drink_add($cocktail, $ingredient, pineapple_chunks, unfinished_wizard_blizzard2, odd_cocktail2);
    case unfinished_wizard_blizzard2 : @gnome_drink_add($cocktail, $ingredient, lime_slices, wizard_blizzard, odd_cocktail3);
    case default : ~displaymessage(^dm_default);
}