[opheld1,half_baked_batta]
if (~gnome_batta ! odd_batta) {
    mes("The gnome batta is ready to bake.");
    return;
}
@batta_prep_choose;

[label,batta_prep_choose]
@multi5_header(
    "Worm Batta", batta_prep_worm,
    "Toad's Legs Batta", batta_prep_toad,
    "Cheese & Tomato Batta", batta_prep_cheese,
    "Fruit Batta", batta_prep_fruit,
    "Vegetable Batta", batta_prep_veg,
    "What would you like to prepare?"
);

[label,batta_prep_worm]
if (inv_total(inv, spicy_worm) < 1 | inv_total(inv, cheese) < 1) {
    mes("You don't have the ingredients to prepare this cuisine."); // guess
    return;
}
mes("You put the ingredients into the batta."); // guess
inv_del(inv, spicy_worm, 1);
inv_del(inv, cheese, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_batta_worm, ^gnome_batta_start_bit, ^gnome_batta_end_bit);


[label,batta_prep_toad]
if (inv_total(inv, seasoned_toad_legs) < 1 | inv_total(inv, cheese) < 1) {
    mes("You don't have the ingredients to prepare this cuisine."); // guess
    return;
}
mes("You put the ingredients into the batta."); // guess
inv_del(inv, seasoned_toad_legs, 1);
inv_del(inv, cheese, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_batta_toad_legs, ^gnome_batta_start_bit, ^gnome_batta_end_bit);


[label,batta_prep_cheese]
if (inv_total(inv, cheese) < 1 | inv_total(inv, tomato) < 1) {
    mes("You don't have the ingredients to prepare this cuisine."); // guess
    return;
}
mes("You put the ingredients into the batta."); // guess
inv_del(inv, cheese, 1);
inv_del(inv, tomato, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_batta_cheese_tomato, ^gnome_batta_start_bit, ^gnome_batta_end_bit);


[label,batta_prep_fruit]
if (inv_total(inv, equa_leaves) < 4) {
    mes("You don't have the ingredients to prepare this cuisine."); // guess
    return;
}
mes("You put the ingredients into the batta."); // guess
inv_del(inv, equa_leaves, 4);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_batta_fruit, ^gnome_batta_start_bit, ^gnome_batta_end_bit);


[label,batta_prep_veg]
if (inv_total(inv, tomato) < 2 | inv_total(inv, onion) < 1 | inv_total(inv, cabbage) < 1 | inv_total(inv, dwellberry) < 1 | inv_total(inv, cheese) < 1) {
    mes("You don't have the ingredients to prepare this cuisine."); // guess
    return;
}
mes("You put the ingredients into the batta."); // guess
inv_del(inv, tomato, 2);
inv_del(inv, onion, 1);
inv_del(inv, cabbage, 1);
inv_del(inv, dwellberry, 1);
inv_del(inv, cheese, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_batta_vegetable, ^gnome_batta_start_bit, ^gnome_batta_end_bit);

[proc,gnome_batta]()(namedobj)
return(enum(int, namedobj, gnome_battas, getbit_range(%gnome_cuisine_var, ^gnome_batta_start_bit, ^gnome_batta_end_bit)));

[proc,gnome_batta_cook]()(namedobj)
def_namedobj $gnome_batta = ~gnome_batta;
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cuisine_empty, ^gnome_batta_start_bit, ^gnome_batta_end_bit);
return($gnome_batta);