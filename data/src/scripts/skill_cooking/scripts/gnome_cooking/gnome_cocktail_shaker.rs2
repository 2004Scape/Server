[opheldu,cocktail_shaker] 
switch_obj(last_useitem) {
    case cocktail_glass : @pour_cocktail_shaker;
    case default : ~displaymessage(^dm_default);
}

[opheldu,cocktail_glass]
switch_obj(last_useitem) {
    case cocktail_shaker : @pour_cocktail_shaker;
    case default : ~displaymessage(^dm_default);
}

[opheld1,cocktail_shaker] // no idea what this is supposed to do
if (~gnome_cocktail ! null) {
    mes("The cocktail shaker is ready to pour.");
    return;
}
@cocktail_mix_choose;

[label,cocktail_mix_choose]
@multi5_header(
    "Blurberry Special", cocktail_mix_blurberry_special,
    "Chocolate Saturday", cocktail_mix_chocolate_saturday,
    "Drunk Dragon", cocktail_mix_drunk_dragon,
    "Fruit Blast", cocktail_mix_fruit_blast,
    "More...", cocktail_mix_more,
    "What would you like to mix?"
);

[label,cocktail_mix_more]
@multi4_header(
    "Pineapple Punch", cocktail_mix_pineapple_punch,
    "Short Green Guy (SGG)", cocktail_mix_short_green_guy,
    "Wizard Blizzard", cocktail_mix_wizard_blizzard,
    "Previous...", cocktail_mix_choose,
    "What would you like to mix?"
);

[label,cocktail_mix_blurberry_special]
if (inv_total(inv, vodka) < 1 | inv_total(inv, gin) < 1 | inv_total(inv, brandy) < 1 | inv_total(inv, lemon) < 2 | inv_total(inv, orange) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, vodka, 1);
inv_del(inv, gin, 1);
inv_del(inv, brandy, 1);
inv_del(inv, lemon, 2);
inv_del(inv, orange, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_blurberry_special, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_chocolate_saturday]
if (inv_total(inv, whisky) < 1 | inv_total(inv, equa_leaves) < 1 | inv_total(inv, bucket_milk) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, whisky, 1);
inv_del(inv, equa_leaves, 1);
inv_del(inv, bucket_milk, 1);
inv_add(inv, bucket_empty, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_chocolate_saturday, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_drunk_dragon]
if (inv_total(inv, vodka) < 1 | inv_total(inv, gin) < 1 | inv_total(inv, dwellberry) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, vodka, 1);
inv_del(inv, gin, 1);
inv_del(inv, dwellberry, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_drunk_dragon, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_fruit_blast]
if (inv_total(inv, pineapple) < 1 | inv_total(inv, orange) < 1 | inv_total(inv, lemon) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, pineapple, 1);
inv_del(inv, orange, 1);
inv_del(inv, lemon, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_fruit_blast, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_pineapple_punch]
if (inv_total(inv, pineapple) < 2 | inv_total(inv, orange) < 1 | inv_total(inv, lemon) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, pineapple, 2);
inv_del(inv, orange, 1);
inv_del(inv, lemon, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_pineapple_punch, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_short_green_guy]
if (inv_total(inv, vodka) < 1 | inv_total(inv, lime) < 3) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, vodka, 1);
inv_del(inv, lime, 3);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_ssg, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);


[label,cocktail_mix_wizard_blizzard]
if (inv_total(inv, vodka) < 2 | inv_total(inv, gin) < 1 | inv_total(inv, pineapple) < 1 | inv_total(inv, orange) < 1 | inv_total(inv, lime) < 1 | inv_total(inv, lemon) < 1) {
    mes("You don't have the ingredients to mix this drink."); // guess
    return;
}
mes("You mix the ingredients into the shaker."); // guess
inv_del(inv, vodka, 2);
inv_del(inv, gin, 1);
inv_del(inv, pineapple, 1);
inv_del(inv, orange, 1);
inv_del(inv, lime, 1);
inv_del(inv, lemon, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cocktail_wizzard_blizzard, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);

[label,pour_cocktail_shaker]
def_namedobj $cocktail = ~gnome_cocktail;
if ($cocktail = null) {
    mes("You need to put some contents into the shaker."); // rsc
    return;
}
mes("You pour the contents into a glass."); // rsc
inv_del(inv, cocktail_glass, 1);
inv_add(inv, $cocktail, 1);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cuisine_empty, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit);

[proc,gnome_cocktail]()(namedobj)
return(enum(int, namedobj, gnome_cocktails, getbit_range(%gnome_cuisine_var, ^gnome_cocktail_start_bit, ^gnome_cocktail_end_bit)));