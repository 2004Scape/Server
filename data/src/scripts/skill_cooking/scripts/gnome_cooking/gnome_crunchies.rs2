// val=5,Worm Crunchies
// val=6,Choc Chip Crunchies
// val=7,Spicy Crunchies
// val=8,Toad Crunchies
[opheld1,half_baked_crunchies]
if (~gnome_crunchies ! odd_crunchies) {
    mes("The gnome crunchies are ready to be baked.");
    return;
}
@crunchies_prep_choose;

[label,crunchies_prep_choose]
@multi4_header(
    "Worm Crunchies", crunchies_prep_worm,
    "Choc Chip Crunchies", crunchies_prep_choc_chip,
    "Spicy Crunchies", crunchies_prep_spicy,
    "Toad Crunchies", crunchies_prep_toad,
    "What would you like to prepare?"
);


[label,crunchies_prep_spicy]
if (inv_total(inv, gnome_spice) < 1 | inv_total(inv, equa_leaves) < 2) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the crunchies."); // guess
inv_del(inv, equa_leaves, 2);
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_crunchies_spicy, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit);


[label,crunchies_prep_toad]
if (inv_total(inv, toad_legs) < 2 | inv_total(inv, gnome_spice) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the crunchies."); // guess
inv_del(inv, toad_legs, 2);
// dont delete gnome spice!
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_crunchies_toad_legs, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit);


[label,crunchies_prep_worm]
if (inv_total(inv, king_worm) < 2 | inv_total(inv, equa_leaves) < 1 | inv_total(inv, gnome_spice) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the crunchies."); // guess
inv_del(inv, king_worm, 2);
inv_del(inv, equa_leaves, 1);
// dont delete gnome spice!
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_crunchies_worm, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit);


[label,crunchies_prep_choc_chip]
if (inv_total(inv, chocolate_bar) < 2 | inv_total(inv, gnome_spice) < 1) {
    ~mesbox("You don't have all the ingredients."); // guess based on osrs
    return;
}
mes("You put the ingredients into the crunchies."); // guess
inv_del(inv, chocolate_bar, 2);
// dont delete gnome spice!
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_crunchies_choc_chip, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit);


[proc,gnome_crunchies]()(namedobj)
return(enum(int, namedobj, gnome_crunchies, getbit_range(%gnome_cuisine_var, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit)));

[proc,gnome_crunchies_cook]()(namedobj)
def_namedobj $gnome_crunchies = ~gnome_crunchies;
%gnome_cuisine_var = setbit_range_toint(%gnome_cuisine_var, ^gnome_cuisine_empty, ^gnome_crunchies_start_bit, ^gnome_crunchies_end_bit);
return($gnome_crunchies);