[if_button,prayer:prayer_superhumanstrength]
if_close;
if (p_finduid(uid) = true) {
    @activate_prayer_superhumanstrength;
} else {
    queue(prayer_activate, 0, 5);
}

[label,activate_prayer_superhumanstrength]
def_dbrow $data = ~get_prayer_data(^prayer_superhumanstrength);
if ($data = null) {
    ~displaymessage(^dm_default);
    return;
}
if (%prayer_superhumanstrength = ^true) {
    %prayer_drain_effect = sub(%prayer_drain_effect, enum(int, int, prayer_drain_effects, ^prayer_superhumanstrength));
    %prayer_superhumanstrength = ^false;
    sound_synth(cancel_prayer, 0, 0);
    def_int $headicon = enum(int, int, prayer_headicons, ^prayer_superhumanstrength);
    if ($headicon > 0) {
        ~headicon_del($headicon);
    }
    if (~prayers_are_active = false) {
        cleartimer(drain_prayer);
        //clearqueue(start_draining);
    }
} else {
    if (~prayer_checks($data) = false) {
        %prayer_superhumanstrength = ^false;
    } else if (stat_base(prayer) < db_getfield($data, prayers:level, 0)){
        %prayer_superhumanstrength = ^false;
        sound_synth(prayer_off, 0, 0);
        ~mesbox("You need a @dbl@Prayer@bla@ level of <tostring(db_getfield($data, prayers:level, 0))> to use @dbl@<db_getfield($data, prayers:name, 0)>@bla@.");
    } else {
        %prayer_superhumanstrength = ^true;
        ~prayer_activate($data);
    }
}