[if_button,prayer:prayer_rapidheal]
if_close;
if (p_finduid(uid) = true) {
    @activate_prayer_rapidheal;
} else {
    queue(prayer_activate, 0, 11);
}

[label,activate_prayer_rapidheal]
def_dbrow $data = ~get_prayer_data(^prayer_rapidheal);
if ($data = null) {
    ~displaymessage(^dm_default);
    return;
}
if (%prayer_rapidheal = ^true) {
    %prayer_drain_effect = sub(%prayer_drain_effect, enum(int, int, prayer_drain_effects, ^prayer_rapidheal));
    %prayer_rapidheal = ^false;
    sound_synth(cancel_prayer, 0, 0);
    def_int $headicon = enum(int, int, prayer_headicons, ^prayer_rapidheal);
    if ($headicon > 0) {
        ~headicon_del($headicon);
    }
    if (~prayers_are_active = false) {
        cleartimer(drain_prayer);
        //clearqueue(start_draining);
    }
    cleartimer(health_regen);
    settimer(health_regen, 100);
} else {
    if (~prayer_checks($data) = false) {
        %prayer_rapidheal = ^false;
    } else if (stat_base(prayer) < db_getfield($data, prayers:level, 0)){
        %prayer_rapidheal = ^false;
        sound_synth(prayer_off, 0, 0);
        ~mesbox("You need a @dbl@Prayer level of <tostring(db_getfield($data, prayers:level, 0))> to use <db_getfield($data, prayers:name, 0)>.");
    } else {
        %prayer_rapidheal = ^true;
        cleartimer(health_regen);
        settimer(health_regen, 50);
        ~prayer_activate($data);
    }
}