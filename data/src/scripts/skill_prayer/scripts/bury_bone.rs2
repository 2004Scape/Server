[opheld1,bones]
if (%tutorial_progress < ^tutorial_complete) {
    @tut_bury_bones(last_slot);
} else {
    @bury_bones(last_slot);
}
[opheld1,burnt_bones] @bury_bones(last_slot);
[opheld1,bat_bones] @bury_bones(last_slot);
[opheld1,big_bones] @bury_bones(last_slot);
[opheld1,babydragon_bones] @bury_bones(last_slot);
[opheld1,dragon_bones] @bury_bones(last_slot);
[opheld1,wolf_bones] @bury_bones(last_slot);

[label,bury_bones](int $slot)
if (getqueue(finish_bury) > 0) {
    return;
}

mes("You dig a hole in the ground...");
anim(human_pickupfloor, 0);
sound_synth(bones_down, 1, 0);
def_obj $last_item = inv_getobj(inv, $slot);
inv_delslot(inv, $slot);
// this is strong queue in later revs
p_stopaction;
queue(finish_bury, 1, $last_item);

[queue,finish_bury](obj $last_item)
if_close;
givexp(prayer, oc_param($last_item, bone_exp));
mes("You bury the bones.");

if (afk_event = ^false | ~macro_event_general_active = true) {
    return;
}
@macro_event_prayer;