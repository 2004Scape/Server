[login,_]
mes("Welcome to RuneScape.");
cam_reset;
//if_close; // come back to this if this proves to be problematic, commented so welcome screen is visible

~update_quests;
~update_bas;
~update_bonuses;
~update_weight;
~p_combat_stat; // update combat varps

%music_mode = 1; // set to auto on login
if (%game_brightness = 0) {
    // sane defaults for game controls
    %game_brightness = 2;
    %music_volume = 2;
    %sound_volume = 2;
}

// register the soft timer that handles replenishing stats
softtimer(stat_replenish, 100);

if (%tutorial_progress < ^tutorial_complete) {
    @start_tutorial;
} else {
    last_login_info;
    ~initalltabs;
}

[proc,initalltabs]
~update_weapon_category(inv_getobj(worn, ^wearpos_rhand));
if_settab(skills, ^tab_skills);
if_settab(quest_journal, ^tab_quest_journal);

inv_transmit(inv, inventory:inv);
if_settab(inventory, ^tab_inventory);

inv_transmit(worn, wornitems:wear);
if_settab(wornitems, ^tab_wornitems);

if_settab(prayer, ^tab_prayer);
if_settab(magic, ^tab_magic);
if_settab(friends, ^tab_friends);
if_settab(ignore, ^tab_ignore);
if_settab(logout, ^tab_logout);
if_settab(player_controls, ^tab_player_controls);
// todo: support low detail modes
if_settab(game_options, ^tab_game_options);
if_settab(musicplayer, ^tab_musicplayer);

[mapenter,_]
~music_playbyregion(coord);
