[label,consume_effect_poison_chalice]
sound_synth(liquid, 0, 0);
anim(human_eat, 0);
inv_setslot(inv, last_slot, cocktail_glass, 1);
mes("You drink the strange green liquid.");
def_int $hitpoints = stat(hitpoints);
def_int $random = random(128);
if ($random < 40 & $hitpoints >= stat_base(hitpoints)) { // 40/128 chance, doesnt activate if health is full
    mes("It heals some health");
    stat_heal(hitpoints, 6, 0);
} else if ($random < 72) { // 32/128 chance
    mes("You feel a little strange.");
    // Attack, Strength, Defence go down by 1, Crafting goes up by 1
    stat_sub(attack, 1, 0);
    stat_sub(strength, 1, 0);
    stat_sub(defence, 1, 0);
    ~stat_addclamp(crafting, 1, 0);
} else if ($random < 80) { // 8/128 chance
    mes("That tasted a bit dodgy.  You feel a bit ill."); // extra space, not a typo
    if ($hitpoints < 5 & $hitpoints > 2) {
        ~damage_self(sub($hitpoints, 2));
    } else {
        ~damage_self(4);
    }
} else if ($random < 84) { // 4/128 chance
    mes("That tasted very dodgy. You feel very ill.");
    def_int $damage;
    // deal up to 49 damage
    if ($hitpoints > 10) {
        $damage = divide(sub($hitpoints, 1), 10);
        $damage = min(sub(multiply($damage, 10), 1), 49);
        $damage = min(sub($hitpoints, 2), $damage);
    }  else if ($hitpoints < 10) {
        $damage = 4;
    } else {
        $damage = 6;
    }
    if ($damage = 0) {
        return;
    }
    ~damage_self($damage);
    stat_sub(attack, min(calc(1 + scale(5, 100, stat(attack))), 3), 0);
    stat_sub(strength, min(calc(1 + scale(5, 100, stat(strength))), 3), 0);
    stat_sub(defence, min(calc(1 + scale(5, 100, stat(defence))), 3), 0);
} else if ($random < 120) {
    mes("You feel a lot better."); // 36/128 chance
    // heal by 14% + 6
    stat_heal(hitpoints, calc(scale(14, 100, stat_base(hitpoints)) + 6), 0);
    ~stat_addclamp(thieving, 1, 0);
} else if ($random < 124) { // 4/128 chance
    mes("Wow! That was amazing! You feel really invigorated.");
    // heal by 24% + 7
    stat_heal(hitpoints, calc(scale(24, 100, stat_base(hitpoints)) + 7), 0);
    ~stat_addclamp(attack, 4, 0);
    ~stat_addclamp(strength, 4, 0);
    ~stat_addclamp(defence, 4, 0);
} else { // 4/128 chance of doing nothing, unless full health, then its 44/128
    mes("It has a slight taste of apricot.");
}
