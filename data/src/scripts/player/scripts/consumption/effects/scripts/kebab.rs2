[proc,consume_effect_kebab]
sound_synth(eat, 0, 0);

// wiki data from 17229 kebabs
def_int $chance = 128;
def_int $rand = randominc($chance);
// 78/128 chance
$chance = calc($chance - 78);
if ($rand > $chance) {
    mes("It heals some health.");
    stat_heal(hitpoints, 10, 0);
    return;
}
// 34/128 chance
$chance = calc($chance - 34);
if ($rand > $chance) {
    mes("That was a good kebab. You feel a lot better.");
    // not sure if it can heal 10-20 or 11-20
    stat_heal(hitpoints, calc(10 + random(11)), 0);
    return;
}
// 4/128 chance
$chance = calc($chance - 4);
if ($rand > $chance) {
    // increases melee stats by 2
    mes("Wow, that was an amazing kebab!  You feel really invigorated.");
    stat_heal(hitpoints, 30, 0);
    ~stat_addclamp(attack, 2, 0);
    ~stat_addclamp(strength, 2, 0);
    ~stat_addclamp(defence, 2, 0);
    return;
}
// 4/128 chance
$chance = calc($chance - 4);
if ($rand > $chance) {
    mes("That tasted very dodgy. You feel very ill.");
    // reduces melees by 3, and reduces a random stat 4
    stat_sub(attack, 3, 0);
    stat_sub(strength, 3, 0);
    stat_sub(defence, 3, 0);
    def_stat $random_stat = enum(int, stat, stats, random(enum_getoutputcount(stats)));
    // has a weird check for stats close to 1. Minimum it can reduce to is 2
    // it always reduces by 4 unless stats are low, if stat is low it reduces it to level 2
    // never reduces to level 1 or lower. Doesnt show 2nd message if stat is 1 or 2
    if (stat($random_stat) > 5) {
        stat_sub($random_stat, 4, 0);
        mes("Eating the kebab has done damage to some of your stats.");
    } else if (stat($random_stat) > 2){
        stat_sub($random_stat, calc(stat($random_stat) - 2), 0);
        mes("Eating the kebab has done damage to some of your stats.");
    }
    return;
}
// 4/128 chance
$chance = calc($chance - 4);
if ($rand > $chance) {
    mes("That kebab didn't seem to do a lot.");
    return;
}
// 4/128 chance
$chance = calc($chance - 4);
if ($rand > $chance) {
    mes("That tasted a bit dodgy. You feel a bit ill.");
    // reduces random stat by 3
    def_stat $random_stat = enum(int, stat, stats, random(enum_getoutputcount(stats)));
    if (stat($random_stat) > 4) {
        stat_sub($random_stat, 3, 0);
        mes("Eating the kebab has damaged your <~stat_name_for_stat($random_stat)> stat.");
    } else if (stat($random_stat) > 2){
        stat_sub($random_stat, calc(stat($random_stat) - 2), 0);
        mes("Eating the kebab has damaged your <~stat_name_for_stat($random_stat)> stat.");
    }
    return;
}