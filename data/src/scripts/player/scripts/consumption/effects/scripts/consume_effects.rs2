
[label,consume_effect_stat]
def_obj $consumable = last_item;
~consume_effect_apply($consumable, false);

// change item to its next stage. default is null
inv_setslot(inv, last_slot, oc_param($consumable, next_obj_stage), 1);


// stat2 uses inv_delslot and inv_add
// this is used for cocktails
[label,consume_effect_stat2]
def_obj $consumable = last_item;
~consume_effect_apply($consumable, false);

inv_delslot(inv, last_slot, 1);
inv_add(inv, oc_param($consumable, next_obj_stage), 1);


[label,consume_effect_stat_say]
def_obj $consumable = last_item;
~consume_effect_apply($consumable, true);

// change item to its next stage. default is null
inv_setslot(inv, last_slot, oc_param($consumable, next_obj_stage), 1);


[proc,consume_effect_apply](obj $consumable, boolean $say)
def_int $hitpoints = stat(hitpoints);
db_find(consume_table:consumable, $consumable);
def_dbrow $effect_data = db_findnext;
if ($effect_data = null) {
    @nothing_interesting_happens;
}
// stat increase/decrease
def_int $constant;
def_int $percent;
def_stat $stat;
def_int $i = 0;
while ($i < db_getfieldcount($effect_data, consume_table:stat_change)) {
    $stat, $constant, $percent = db_getfield($effect_data, consume_table:stat_change, $i);
    if ($constant < 0 | $percent < 0) {
        // decrease
        // if hitpoints, damage instead
        if ($stat = hitpoints) {
            // not sure if it uses multiple hit sounds, or if theres a 15 client tick delay
            def_int $damage = calc(scale(abs($percent), 100, stat(hitpoints)) + abs($constant));
            damage(uid, min($damage, 1), $damage);
            sound_synth(human_hit4, 0, 15);
        } else {
            stat_sub($stat, abs($constant), abs($percent));
        }
    } else {
        // increase
        ~stat_addclamp($stat, $constant, $percent);
    }
    $i = calc($i + 1);
}
// stat restore
$i = 0;
while ($i < db_getfieldcount($effect_data, consume_table:stat_restore)) {
    stat_heal(db_getfield($effect_data, consume_table:stat_restore, $i));
    $i = calc($i + 1);
}
// start sound
~consume_effect_sound(db_getfield($effect_data, consume_table:consume_sound, 0));
// display messages
~consume_effect_message($consumable, $hitpoints, $say);

[proc,consume_effect_sound](synth $sound)
if ($sound = null) {
    sound_synth(liquid, 0, 0);
} else {
    sound_synth($sound, 0, 0);
}

[proc,consume_effect_message](obj $consumable, int $hitpoints, boolean $say)
db_find(consume_messages_table:consumable, $consumable);
def_dbrow $message_data = db_findnext;
def_string $consume_message1 = "You eat the <lowercase(oc_name($consumable))>.";
def_string $consume_message2 = "It heals some health.";
def_string $restore_message = "0";
if ($message_data ! null) {
    if (string_length(db_getfield($message_data, consume_messages_table:consume_message1, 0)) > 0) {
        $consume_message1 = db_getfield($message_data, consume_messages_table:consume_message1, 0);
    }
    if (string_length(db_getfield($message_data, consume_messages_table:consume_message2, 0)) > 0) {
        $consume_message2 = db_getfield($message_data, consume_messages_table:consume_message2, 0);
    }
    if (string_length(db_getfield($message_data, consume_messages_table:restore_message, 0)) > 0) {
        $restore_message = db_getfield($message_data, consume_messages_table:restore_message, 0);
    }
}
if (compare($consume_message1, "0") ! 0) {
    mes($consume_message1);
}
if (oc_category($consumable) = category_69) {
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have finished your potion");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 1 dose of potion left.");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 2 doses of potion left.");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 3 doses of potion left.");
        return;
    }
}
if (compare($consume_message2, "0") ! 0) {
    if ($say = true) {
        say($consume_message2);
    } else {
        mes($consume_message2);
    }
}
if (compare($restore_message, "0") ! 0 & $hitpoints < stat(hitpoints)) {
    mes("<tostring($hitpoints)>, <tostring(stat(hitpoints))>, <$restore_message>");
}