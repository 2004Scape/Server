
[label,consume_effect_stat]
def_obj $consumable = last_item;

// change item to its next stage. default is null
inv_delslot(inv, last_slot);
if (oc_param($consumable, next_obj_stage) ! null) {
    inv_setslot(inv, last_slot, oc_param($consumable, next_obj_stage), 1);
}
anim(human_eat, 0);
~consume_effect_apply($consumable, false);


// stat2 uses inv_delslot and inv_add
// this is used for cocktails
[label,consume_effect_stat2]
def_obj $consumable = last_item;

inv_delslot(inv, last_slot);
if (oc_param($consumable, next_obj_stage) ! null) {
    inv_add(inv, oc_param($consumable, next_obj_stage), 1);
}
anim(human_eat, 0);
~consume_effect_apply($consumable, false);


[label,consume_effect_stat_say]
def_obj $consumable = last_item;

// change item to its next stage. default is null
inv_delslot(inv, last_slot);
if ($consumable ! null & oc_param($consumable, next_obj_stage) ! null) {
    inv_setslot(inv, last_slot, oc_param($consumable, next_obj_stage), 1);
}
anim(human_eat, 0);
~consume_effect_apply($consumable, true);


[proc,consume_effect_apply](obj $consumable, boolean $say)
def_int $hitpoints = stat(hitpoints);
db_find(consume_table:consumable, $consumable);
def_dbrow $effect_data = db_findnext;
if ($effect_data = null) {
    return;
}
// stat increase/decrease
def_int $constant;
def_int $percent;
def_stat $stat;
def_int $i = 0;
while ($i < db_getfieldcount($effect_data, consume_table:stat_change)) {
    $stat, $constant, $percent = db_getfield($effect_data, consume_table:stat_change, $i);
    if ($constant < 0 | $percent < 0) {
        // decrease
        // if hitpoints, damage instead
        if ($stat = hitpoints) {
            // not sure if it uses multiple hit sounds, or if theres a 15 client tick delay
            def_int $damage = calc(scale(abs($percent), 100, stat(hitpoints)) + abs($constant));
            ~damage_self($damage);
        } else {
            stat_drain($stat, abs($constant), abs($percent));
        }
    } else {
        // increase
        stat_boost($stat, $constant, $percent);
    }
    $i = calc($i + 1);
}
// stat heal
$i = 0;
while ($i < db_getfieldcount($effect_data, consume_table:stat_heal)) {
    stat_heal(db_getfield($effect_data, consume_table:stat_heal, $i));
    $i = calc($i + 1);
}

// start sound
~consume_effect_sound(db_getfield($effect_data, consume_table:consume_sound, 0));
// display messages
~consume_effect_messages($consumable, $hitpoints, $say);

[proc,consume_effect_sound](synth $sound)
if ($sound = null) {
    sound_synth(liquid, 0, 0);
} else {
    sound_synth($sound, 0, 0);
}

[proc,consume_effect_messages](obj $consumable, int $hitpoints, boolean $say)
db_find(consume_messages_table:consumable, $consumable);
def_dbrow $message_data = db_findnext;
def_string $consume_message1;
def_string $consume_message2;
def_string $restore_message1;
def_string $restore_message2;
if ($message_data ! null) {
    if (string_length(db_getfield($message_data, consume_messages_table:consume_message1, 0)) > 0) {
        $consume_message1 = db_getfield($message_data, consume_messages_table:consume_message1, 0);
    }
    if (string_length(db_getfield($message_data, consume_messages_table:consume_message2, 0)) > 0) {
        $consume_message2 = db_getfield($message_data, consume_messages_table:consume_message2, 0);
    }
    if (string_length(db_getfield($message_data, consume_messages_table:restore_message1, 0)) > 0) {
        $restore_message1 = db_getfield($message_data, consume_messages_table:restore_message1, 0);
    }
    if (string_length(db_getfield($message_data, consume_messages_table:restore_message2, 0)) > 0) {
        $restore_message2 = db_getfield($message_data, consume_messages_table:restore_message2, 0);
    }
}

// if potion then display doses left instead of consume_message2
if ($consumable ! null & oc_category($consumable) = category_69) {
    $consumable = oc_param($consumable, next_obj_stage);
    if (compare($consume_message1, "0") ! 0) {
        if (string_length($consume_message1) > 0) {
            mes($consume_message1);
        } else {
            mes("You drink the <lowercase(oc_name($consumable))>.");
        }
    }
    // potions have delay: https://www.youtube.com/watch?v=s5sDZdBzapk&t=151s
    if (db_getfieldcount($message_data, consume_messages_table:message_delay) > 0) {
        p_delay(db_getfield($message_data, consume_messages_table:message_delay, 0));
    }
    if ($consumable = vial_empty) {
        mes("You have finished your potion.");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 1 dose of potion left.");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 2 doses of potion left.");
        return;
    }
    $consumable = oc_param($consumable, next_obj_stage);
    if ($consumable = vial_empty) {
        mes("You have 3 doses of potion left.");
        return;
    }
}
// display consume_message1 on line one
// display consume_message2 on line two
// restore_message1 replaces consume_message1 when the item restores health
// restore_message2 replaces consume_message2 when the item restores health

if (compare($restore_message1, "0") ! 0 & $hitpoints < stat(hitpoints) & string_length($restore_message1) > 0) {
    mes($restore_message1);
} else if (compare($consume_message1, "0") ! 0) {
    // display default if consume_message1 is not defined
    if (string_length($consume_message1) > 0) {
        mes($consume_message1);
    } else {
        mes("You eat the <lowercase(oc_name($consumable))>.");
    }
}

// gnome food probably uses this, since karambwans and gnome food are the same in osrs
// https://oldschool.runescape.wiki/w/Update:Chat_update_and_%27Operate%27
// https://youtu.be/Z-e_O0O6A50?t=33
if ($message_data ! null & db_getfieldcount($message_data, consume_messages_table:message_delay) > 0) {
    p_delay(db_getfield($message_data, consume_messages_table:message_delay, 0));
}

if (compare($restore_message2, "0") ! 0 & $hitpoints < stat(hitpoints)) {
    // display default if restore_message2 is not defined
    if (string_length($restore_message2) > 0) {
        mes($restore_message2);
    } else {
        mes("It heals some health.");
    }
}
// say always replaces consume_message2
if ($say = true) {
    say($consume_message2);
} else if (compare($consume_message2, "0") ! 0 & string_length($consume_message2) > 0 & (string_length($restore_message2) < 0 | $hitpoints >= stat(hitpoints) | compare($restore_message2, "0") = 0)) {
    mes($consume_message2); // kinda scuffed but since restore_message2 has a default i think this is required
}