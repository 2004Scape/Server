// ************************* TRIGGERS **************************
// normal food
[opheld1,_category_5]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,_category_58]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,_category_59]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,_category_86]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,_category_120]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,_category_131]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,vegetable_batta]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,pineapple]
mes("You cant eat it whole; maybe you should cut it up.");
[opheld1,_alcoholic_drinks]@player_consume_item(consume_effect_stat, 2, 3);

// Kebabs
[opheld1,kebab]@player_consume_item(consume_effect_kebab, 2, 3);
[opheld1,dodgy_ugthanki_kebab]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,ugthanki_kebab]@player_consume_item(consume_effect_stat_say, 2, 3);

// gnome food
// impossible to know how category_15 worked, i assume they work the same way as other gnome food
[opheld1,_category_15]
switch_obj(last_item) {
    case odd_cocktail1, odd_cocktail2, odd_cocktail3, odd_cocktail4 : 
        inv_delslot(inv, last_slot);
        inv_add(inv, oc_param(last_item, next_obj_stage), 1);
        mes("You empty the cocktail.");
    case default : @player_consume_item(consume_effect_stat, null, null); // odd gnome food, unfinished cocktails
}
[opheld1,_category_123]@player_consume_item(consume_effect_stat, null, null);
[opheld1,_category_125]@player_consume_item(consume_effect_stat, null, null);
[opheld1,_category_127]@player_consume_item(consume_effect_stat, null, null);
[opheld1,_category_128]@player_consume_item(consume_effect_stat, null, null);

// cock tails
[opheld1,_category_83]
switch_obj(last_item) { 
    // i really dont know if these should be delayed like gnome food
    case premade_fruit_blast, fruit_blast, pineapple_punch, pineapple_punch : @player_consume_item(consume_effect_stat, 2, 3);
    case default : @player_consume_item(consume_effect_stat2, 2, 3);
}
//odd cocktail
[opheld3,_category_15]@player_consume_item(consume_effect_stat2, null, null);

// pies
[opheld1,_category_129]
def_int $eat_delay = 0;
if (oc_param(last_item, next_obj_stage) = piedish) {
    $eat_delay = 1;
}
@player_consume_item(consume_effect_stat, $eat_delay, 3);

// cakes
[opheld1,_category_130]
def_int $eat_delay = 1;
if (oc_param(last_item, next_obj_stage) = null) {
    $eat_delay = 2;
}
@player_consume_item(consume_effect_stat, $eat_delay, 3);

// pizzas
[opheld1,_category_132]
def_int $eat_delay = 0;
if (oc_param(last_item, next_obj_stage) = null) {
    $eat_delay = 1;
}
@player_consume_item(consume_effect_stat, $eat_delay, 3);

// potions
[opheld1,jangerberries]@player_consume_item(consume_effect_stat, 2, 3);
[opheld1,obj_197]@player_consume_item(consume_effect_poison_chalice, null, null);
[opheld1,cup_of_tea]@player_consume_item(consume_effect_stat_say, 2, 3);
[opheld1,_category_69]
p_stopaction; // https://oldschool.runescape.wiki/w/Update:Patch_Notes_(19_September_2013)
switch_obj(last_item) {
    // anti dragon
    case 1dose1antidragon, 2dose1antidragon, 3dose1antidragon, 4dose1antidragon: 
        @player_consume_item(consume_effect_antidragon, null, null);
    // anti posions
    case 1doseantipoison, 2doseantipoison, 3doseantipoison, 4doseantipoison: 
        @player_consume_item(consume_effect_antipoison, null, null);
    case 1dose2antipoison, 2dose2antipoison, 3dose2antipoison, 4dose2antipoison: 
        @player_consume_item(consume_effect_super_antipoison, null, null);
    case default : @player_consume_item(consume_effect_stat, null, null);
}
[opheld1,strange_fruit]@player_consume_item(consume_effect_strange_fruit, 2, 3);
// ************************* TRIGGERS **************************

// all normal foods use %eat_delay
// some foods use %eat_delay2, like karambwans (later rev) or in our case gnome food
// potions use %drink_delay
[label,player_consume_item](label $consume_effect, int $eat_delay, int $skill_delay)
def_obj $obj = inv_getobj(inv, last_slot);

// check if item is members
if (oc_members($obj) = true) {
    // complete guess
    ~require_members_use_item;
}

if ($eat_delay ! null) {
    if (%eat_delay >= map_clock) {
        return;
    }
    %eat_delay = calc(map_clock + $eat_delay);
}

if ($skill_delay ! null) {
    %action_delay = calc(%action_delay + $skill_delay);
}

// consume effect
jump($consume_effect);

// vials dont even use this yet. Pies and odd cocktails have an empty option with their own scuffed messages
[label,player_empty_item]
inv_setslot(inv, last_slot, oc_param(last_item, next_obj_stage), 1);
mes("You empty the <oc_name(oc_param(last_item, next_obj_stage))>.");
