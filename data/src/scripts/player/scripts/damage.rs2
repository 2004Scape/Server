[debugproc,hit](int $amount)
~damage_self($amount);

[proc,combat_clearqueue]
clearqueue(playerhit_n_retaliate);

[queue,player_death]
p_stopaction;
midi_jingle(^death_jingle_2, ^death_jingle_2_millis);
anim(human_death, 0);
p_delay(6);

// todo: save 3 items + protect items prayer
// todo: pk skull lose all items
def_int $i = 0;
def_int $size = inv_size(inv);
def_obj $prev_wep = inv_getobj(worn, ^wearpos_rhand);
while ($i < $size) {
    if (inv_getnum(inv, $i) > 0) {
        if (oc_category(inv_getobj(inv, $i)) = armour_godcape) {
            mes(oc_param(inv_getobj(inv, $i), lose_cape_message));
            inv_delslot(inv, $i);
        } else {
            inv_dropslot(inv, coord, $i, 200);
        }
    }
    $i = calc($i + 1);
}

$i = 0;
$size = inv_size(worn);
while ($i < $size) {
    if (inv_getnum(worn, $i) > 0) {
        if (oc_category(inv_getobj(worn, $i)) = armour_godcape) {
            mes(oc_param(inv_getobj(worn, $i), lose_cape_message));
            inv_delslot(worn, $i);
        } else {
            inv_dropslot(worn, coord, $i, 200);
        }
    }
    $i = calc($i + 1);
}
buildappearance(worn);

p_telejump(0_50_50_22_22); // todo: randomize
anim(null, 0);
mes("Oh dear you are dead!"); // no comma in early rs2. https://storage.googleapis.com/tannerdino/images/6543.jpg

stat_heal(hitpoints, 99, 0);
stat_heal(prayer, 99, 0);
healenergy(10000);
~combat_clearqueue;
~clear_pk_skull;
// todo: end poison timer ~player_end_poison
~update_all($prev_wep);

[proc,damage_self](int $amount)
if_close;
if ($amount > 0) {
    damage(uid, 1, $amount);
    ~human_hit_sound;
} else {
    damage(uid, 0, 0);
}

if (stat(hitpoints) = 0 & getqueue(player_death) < 1){
    queue(player_death, 0);
}


[proc,human_hit_sound]
def_int $rand;
def_synth $sound;
if (gender() = 0) {
        $rand = random(2);
        switch_int($rand) {
            case 0 : $sound = human_hit2;
            case 1 : $sound = human_hit3;
            case 2 : $sound = human_hit4;
            case default : $sound = human_hit2;
        }
    } else {
        $rand = random(1);
        switch_int($rand) {
            case 0 : $sound = female_hit;
            case 1 : $sound = female_hit2;
            case default : $sound = female_hit;
        }
    }
sound_synth($sound, 0, 20);


[proc,.damage_self](int $amount)
.if_close;
if ($amount > 0) {
    .damage(.uid, 1, $amount);
    ~.human_hit_sound;
} else {
    .damage(.uid, 0, 0);
}

// if (.stat(hitpoints) = 0 & .getqueue(player_death) < 1){
//     .queue(player_death, 0);
// }
if (.stat(hitpoints) = 0){
    .say("Pvp death is not implimented. Need support for .queue.");
} 


[proc,.human_hit_sound]
def_int $rand;
def_synth $sound;
if (.gender() = 0) {
        $rand = random(2);
        switch_int($rand) {
            case 0 : $sound = human_hit2;
            case 1 : $sound = human_hit3;
            case 2 : $sound = human_hit4;
            case default : $sound = human_hit2;
        }
    } else {
        $rand = random(1);
        switch_int($rand) {
            case 0 : $sound = female_hit;
            case 1 : $sound = female_hit2;
            case default : $sound = female_hit;
        }
    }
.sound_synth($sound, 0, 20);