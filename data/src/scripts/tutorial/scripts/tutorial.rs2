// Sources:
// https://www.youtube.com/watch?v=Quwc_TfPW8Q&t=433s
// https://www.youtube.com/watch?v=yjxsitgrHpE&list=PL9FE4AE865F753045&index=61&t=72s
// https://www.youtube.com/watch?v=xZ0pz8ZUit4&list=PL9FE4AE865F753045&index=28
// https://www.youtube.com/watch?v=L15kCsQZNiA - Feb 23, 2013 OSRS - Guy fishes forever
// https://www.youtube.com/watch?v=9NEh8kBrjkk - March 1, 2013 OSRS
// https://www.youtube.com/watch?v=TSx2-jjPeIs - July 31, 2013 OSRS - Guy does a lot of accidental not step related things
// https://www.youtube.com/watch?v=ui3I-OPLqzE - March 5, 2013 OSRS - Does some scrolling on scrollbar chat interfaces
// https://www.youtube.com/watch?v=b786yUpSUnU - March 13, 2013 OSRS - Gets level 3 in all skills
// https://www.youtube.com/watch?v=wrA5lcCcBts - October 29, 2007 - Dies on Tutorial Island

// Varp info: https://oldschool.runescape.wiki/w/RuneScape:Varplayer/281

[label,start_tutorial]
// logging into a new character might show old tabs so we're going to clear them
if_settab(null, 0);
if_settab(null, 1);
if_settab(null, 2);
if_settab(null, 3);
if_settab(null, 4);
if_settab(null, 5);
if_settab(null, 6);
if_settab(null, 8);
if_settab(null, 9);
if_settab(logout, 10);
if_settab(game_options, ^tab_game_options);
if_settab(null, 12);
if_settab(null, 13);

~tutorial_set_active_tabs;
~tutorial_set_npc_hints;

if (%tutorial_progress = 0) {
    p_telejump(0_48_48_22_34);
    if_close; // workaround w/ receivedFirstClose
    if_openmainmodal(player_design);
    allowdesign(true);
}

switch_int(%tutorial_progress) {
    // Runescape Guide's House
    case 0, 1: ~tutorial_step_1_getting_started;
    // Not Relelvant until October 2006
    case 2: ~tutorial_step_2_player_controls_spanner;
    case 3: ~tutorial_step_3_player_controls_left_click;
    case 4: ~tutorial_step_4_interact_with_scenery;
    case 10: ~tutorial_step_10_moving_around;

    // Survival Tutor
    case 20: ~tutorial_step_11_view_inventory;
    case 30: ~tutorial_step_12_cut_tree;
    case 40: ~tutorial_step_13_build_fire;
    case 50: ~tutorial_step_14_you_gained_experience;
    case 60: ~tutorial_step_15_view_stats;
    case 70: ~tutorial_step_16_catch_shrimp;
    case 80: ~tutorial_step_17_cook_shrimp;
    case 90: ~tutorial_step_18_burn_shrimp;
    case 120: ~tutorial_step_19_survival_recap;

    // Master Chef
}

[proc,tutorial_set_active_tabs]
if (%tutorial_progress > 0) {
    if_settab(game_options, 11);
}

if (%tutorial_progress > 20) {
    inv_transmit(inv, inventory:inv);
    if_settab(inventory, ^tab_inventory);
}

if (%tutorial_progress > 60) {
    if_settab(skills, ^tab_skills);
}

[proc,tutorial_set_npc_hints]
if (%tutorial_progress < 4) {
    npc_findallzone(0_48_48_21_35);
    while (npc_findnext = true) {
        if (npc_type = runescape_guide) {
            hint_npc(npc_uid);
        }
    }
}

if (%tutorial_progress = 70) {
    npc_findallzone(0_48_48_29_20);
    while (npc_findnext = true) {
        if (npc_coord = 0_48_48_29_20 & npc_type = npc_952) {
            hint_npc(npc_uid);
        }
    }
}

if (%tutorial_progress = 10) {
   ~tutorial_step_10_moving_around;
}

[if_button,player_design:accept]
if_close;

[if_close,player_design]
%tutorial_progress = 1;
allowdesign(false);


[if_flashing_tab,_]
if (%tutorial_progress = 2) {
    %tutorial_progress = 3;
    ~tutorial_step_3_player_controls_left_click;
} else if (%tutorial_progress = 20) {
    inv_add(inv, bronze_axe, 1);
    inv_add(inv, tinderbox, 1);
    %tutorial_progress = 30;
    ~tutorial_step_12_cut_tree;
} else if (%tutorial_progress = 50) {
    %tutorial_progress = 60;
    ~tutorial_step_15_view_stats;
}

[label,tutorial_step20]
~tutorialstep("Find your next instructor.", "Follow the path until you get to the door with the yellow arrow|above it. Click on the door to open it. Notice the mini-map in|the top right, this shows a top down view of the area around|you. This can also be used for navigation.");

[proc,set_tutorial_progress]
switch_int(%tutorial_progress) {
    // Runescape Guide's House
    case 0, 1: ~tutorial_step_1_getting_started;
    // Not Relelvant until October 2006
    case 2: ~tutorial_step_2_player_controls_spanner;
    case 3: ~tutorial_step_3_player_controls_left_click;
    case 4: ~tutorial_step_4_interact_with_scenery;
    case 10: ~tutorial_step_10_moving_around;

    // Survival Tutor
    case 20: ~tutorial_step_11_view_inventory;
    case 30: ~tutorial_step_12_cut_tree;
    case 40: ~tutorial_step_13_build_fire;
    case 50: ~tutorial_step_14_you_gained_experience;
    case 60: ~tutorial_step_15_view_stats;
    case 70: ~tutorial_step_16_catch_shrimp;
    case 80: ~tutorial_step_17_cook_shrimp;
    case 90: ~tutorial_step_18_burn_shrimp;
    case 120: ~tutorial_step_19_survival_recap;

    // Master Chef
}

[proc,tutorial_give_xp](stat $skill, int $xp)
if (stat_base($skill) >= 3) {
    return;
}
givexp($skill, $xp);

[label,tutorial_complete]
if_closesticky();

%tutorial_progress = ^tutorial_complete;
p_telejump(0_50_50_22_22);

inv_clear(inv);
inv_add(inv, bronze_axe, 1);
inv_add(inv, tinderbox, 1);
inv_add(inv, net, 1);
inv_add(inv, shrimps, 1);
inv_add(inv, bucket_empty, 1);
inv_add(inv, pot_empty, 1);
inv_add(inv, bread, 1);
inv_add(inv, bronze_pickaxe, 1);
inv_add(inv, bronze_dagger, 1);
inv_add(inv, bronze_sword, 1);
inv_add(inv, wooden_shield, 1);
inv_add(inv, shortbow, 1);
inv_add(inv, bronze_arrow, 25);
inv_add(inv, airrune, 25);
inv_add(inv, mindrune, 16);
inv_add(inv, waterrune, 6);
inv_add(inv, earthrune, 4);
inv_add(inv, bodyrune, 2);

~initalltabs;
~update_all(inv_getobj(worn, ^wearpos_rhand));
