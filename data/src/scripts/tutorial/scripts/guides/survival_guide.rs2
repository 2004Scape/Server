[opnpc1,npc_943]
switch_int(%tutorial_progress) {
    case 10: @survival_guide_start;
    case 20: @survival_guide_look_at_menu;
    case 30, 40: @survival_guide_fire;
    case 50: @survival_guide_look_at_menu;
    case 60: @survival_guide_fishing;
    case 70, 80: @survival_guide_fishing_after_burning;
    case default: @surivial_guide_recap;
}

[label,survival_guide_start]
~chatnpc(neutral, "Hello there newcomer. My name is Brynna. My job is|to teach you a few survival tips and tricks. First off|we're going to start with the most basic survival skill of|all; making a fire.");
~objbox2(tinderbox, bronze_axe, "The Survival Guide gives you a @blu@Tinderbox@bla@ and a|@blu@Bronze Axe@bla@!");
%tutorial_progress = 20;
~tutorial_step_11_view_inventory;

[label,survival_guide_look_at_menu]
~chatnpc(neutral, "Hello again. You should take a look at that menu before we continue.");

[label,survival_guide_fire]
~chatnpc(neutral, "Next up you need to make a fire. First, you'll need to cut down a tree to get some logs.");
@survival_guide_replace_items;

[label,survival_guide_replace_items]
if (inv_total(inv, bronze_axe) < 1 & inv_total(inv, tinderbox) < 1) {
    inv_add(inv, bronze_axe, 1);
    inv_add(inv, tinderbox, 1);
    ~objbox2(tinderbox, bronze_axe, "The Survival Guide gives you a @dbl@tinderbox@bla@ and a @dbl@bronze axe@bla@!");
}

if (inv_total(inv, bronze_axe) < 1) {
    inv_add(inv, bronze_axe, 1);
    ~objboxt(bronze_axe, "The Survival Guide gives you a @dbl@bronze axe@bla@!");
}

if (inv_total(inv, tinderbox) < 1) {
    inv_add(inv, tinderbox, 1);
    ~objboxt(tinderbox, "The Survival Guide gives you a @dbl@tinderbox@bla@!");
}

[label,survival_guide_fishing]
~chatnpc(neutral, "There's nothing like a good fire to warm the bones.|Next thing is getting food in our bellies. We'll need|something to cook. There are shrimp in the pond there.|So let's catch and cook some.");
npc_findallzone(0_48_48_29_20);
while (npc_findnext = true) {
    if (npc_coord = 0_48_48_29_20 & npc_type = npc_952) {
        hint_npc(npc_uid);
    }
}
~objbox(net, "The Survival Guide gives you a @blu@Net@bla@!");
inv_add(inv, net, 1);
%tutorial_progress = 70;
~tutorial_step_16_catch_shrimp;

// https://youtu.be/TSx2-jjPeIs?t=31
[label,survival_guide_fishing_after_burning]
~chatnpc(neutral, "Hello again. Next thing is getting food in our bellies. We'll need something to cook. There are shrimp in the pond there. So let's catch and cook some.");

[label,surivial_guide_recap]
~chatnpc(quiz, "Hello again. Is there something you'd like to hear more about?");
~survival_recap_previous_options;

[proc,survival_guide_recap_response](int $choice)
switch_int ($choice) {
    case 1:
        ~survival_recap_skills;
        ~survival_recap_questions($choice);
    case 2:
        ~survival_recap_woodcutting;
        ~survival_recap_questions($choice);
    case 3:
        ~survival_recap_firemaking;
        ~survival_recap_questions($choice);
    case 4:
        ~survival_recap_more_options;
    case 5:
        ~survival_recap_fishing;
        ~survival_recap_questions($choice);
    case 6:
        ~survival_recap_cooking;
        ~survival_recap_questions($choice);
    case 7:
        ~survival_recap_nothing_thanks;
    case 8:
        ~survival_recap_previous_options;
}

[proc,survival_recap_questions](int $choice)
~chatnpc(quiz, "Is there anything else you'd like to hear more about?");

switch_int($choice) {
    case 1: ~survival_guide_recap_response(~p_choice5("Tell me about Woodcutting again.", 2, "Tell me about Firemaking again.", 3, "Tell me about Fishing again", 5, "Tell me about Cooking again", 6, "Nothing thanks.", 7));
    case 2: ~survival_guide_recap_response(~p_choice5("Tell me about my skills again.", 1, "Tell me about Firemaking again.", 3, "Tell me about Fishing again", 5, "Tell me about Cooking again", 6, "Nothing thanks.", 7));
    case 3: ~survival_guide_recap_response(~p_choice5("Tell me about my skills again.", 1, "Tell me about Woodcutting again.", 2, "Tell me about Fishing again", 5, "Tell me about Cooking again", 6, "Nothing thanks.", 7));
    case 5: ~survival_guide_recap_response(~p_choice5("Tell me about my skills again.", 1, "Tell me about Woodcutting again.", 2, "Tell me about Firemaking again", 3, "Tell me about Cooking again", 6, "Nothing thanks.", 7));
    case 6: ~survival_guide_recap_response(~p_choice5("Tell me about my skills again.", 1, "Tell me about Woodcutting again.", 2, "Tell me about Firemaking again", 3, "Tell me about Fishing again", 5, "Nothing thanks.", 7));
}

[proc,survival_recap_skills]
~chatplayer("default", "Tell me about my skills again.");
~chatnpc(neutral, "Every skill is listed in the skills menu. Here you can see what your current levels are and how much experience you have.");
~chatnpc(neutral, "As you move your mouse over the various skills it will show you the exact amount of experience you have and how much is needed to get to the next level.");
// Re-enable this line when we get skill menus.
//~chatnpc("default", "You can also click on a skill to open the relevant skillguide. In the skillguide, you can see all of the unlocks available in that skill. ");

[proc,survival_recap_woodcutting]
~chatplayer("default", "Tell me about Woodcutting again.");
~chatnpc(neutral, "Woodcutting, eh? Don't worry, newcomer, it's really very easy. Simply equip your axe and click on a nearby tree to chop away.");
~chatnpc(neutral, "As you explore the mainland you will discover many different kinds of trees that will require different Woodcutting levels to chop down.");
~chatnpc(neutral, "Logs are not only useful for making fires. Many archers use the skill known as Fletching to craft their own bows and arrows from trees.");

[proc,survival_recap_fishing]
~chatplayer("default", "Tell me about Fishing again.");
~chatnpc(neutral, "Ah, yes. Fishing! Fishing is undoubtedly one the more popular hobbies here in Runescape!");
~chatnpc(neutral, "Whenever you see sparking waters, you can be sure there's probably some good fishing to be had there!");
~chatnpc(neutral, "Not only are fish absolutely delicious when cooked, they will also heal lost health.");
~chatnpc(neutral, "I would recommend everybody has a go at Fishing at least once in their lives!");

[proc,survival_recap_firemaking]
~chatplayer("default", "Tell me about Firemaking again.");
~chatnpc(neutral, "Certainly, newcomer. When you have logs simply use your tinderbox on then. If successful, you will start a fire.");
~chatnpc(neutral, "You can also set fire to logs you find lying on the floor already, and some other things can also be set alight...");
~chatnpc(neutral, "A tinderbox is always a useful item to keep around!");

[proc,survival_recap_cooking]
~chatplayer("default", "Tell me about Cooking again.");
~chatnpc(neutral, "Yes, the most basic of survival techniques. Most simple foods can be used on a fire to cook them. If you're feeling a bit fancier, you can also use a range to cook the food instead.");
~chatnpc(neutral, "Eating cooked food will restore lost health. The harder something is to cook, the more it will heal you.");

[proc,survival_recap_nothing_thanks]
~chatplayer(quiz, "Nothing thanks.");

[proc,survival_recap_previous_options]
~survival_guide_recap_response(~p_choice4("Tell me about my skills again.", 1, "Tell me about Woodcutting again.", 2, "Tell me about Firemaking again.", 3, "More options...", 4));

[proc,survival_recap_more_options]
~survival_guide_recap_response(~p_choice4("Tell me about Fishing again.", 5, "Tell me about Cooking again.", 6, "Nothing thanks.", 7, "Previous options...", 8));

[label,set_hint_icon_surivial_guide]
npc_findallzone(0_48_48_29_24);
while (npc_findnext = true) {
    if (npc_type = npc_943) {
        hint_npc(npc_uid);
    }
}

npc_findallzone(0_48_48_33_25);
while (npc_findnext = true) {
    if (npc_type = npc_943) {
        hint_npc(npc_uid);
    }
}

npc_findallzone(0_48_48_32_21);
while (npc_findnext = true) {
    if (npc_type = npc_943) {
        hint_npc(npc_uid);
    }
}

npc_findallzone(0_48_48_29_21);
while (npc_findnext = true) {
    if (npc_type = npc_943) {
        hint_npc(npc_uid);
    }
}

