[proc,tut_island_survial_gate](loc $loc, coord $loc_coord, int $angle)
if (%tutorial_progress < 120) {
    ~mesbox("You need to talk to the Survival Guide and complete her tasks before you are allowed to proceed through this gate.");
    return;
}
if (%tutorial_progress = 120) {
    %tutorial_progress = 130;
    ~set_tutorial_progress;
}
if ($loc = loc_3015) {
    ~tut_open_gate;
} else {
    def_coord $orig_coord = $loc_coord;
    loc_findallzone(~get_pair_coord($orig_coord, $angle, true));
    while (loc_findnext = true) {
        if (distance($orig_coord, $loc_coord) <= 1 & loc_category = gate_main_closed) {
            ~tut_open_gate;
        }
    }
}

def_boolean $is_outside = ~check_axis(coord, $loc_coord, $angle);
if ($is_outside = true) {
    if (coord ! $loc_coord) {
        p_teleport($loc_coord);
        p_delay(1);
    }
    p_teleport(movecoord($loc_coord, 1, 0, 0));
} else {
    if (coord ! $loc_coord) {
        p_teleport($loc_coord);
        p_delay(1);
    }
}

[proc,tut_open_gate]
def_int $orig_angle = loc_angle;
def_coord $orig_coord = loc_coord;
def_coord $main_open = ~movecoord_loc_return(~gate_set_close($orig_angle, 1));
def_coord $outer_open = ~movecoord_loc_return(~gate_set_close($orig_angle, 2));
//TODO: Figure out if these use door_open/close, the gate sounds werent added until ~2005?
sound_synth(door_open, 0, 0);
loc_del(2);
loc_add($main_open, loc_param(next_loc_stage), modulo(add(loc_angle, 3), 4), wall_straight, 2);
loc_findallzone(~get_pair_coord($orig_coord, $orig_angle, false));
while (loc_findnext = true) {
    if (distance($orig_coord, loc_coord) <= 1 & loc_category = gate_outer_closed) {
        loc_del(2);
        loc_add($outer_open, loc_param(next_loc_stage), modulo(add($orig_angle, 3), 4), wall_straight, 2);
        return;
    }
}

// Survival Tutor Gate
[oploc1,loc_3015] ~tut_island_survial_gate(loc_type, loc_coord, loc_angle);
[oploc1,loc_3016] ~tut_island_survial_gate(loc_type, loc_coord, loc_angle);

// Runescape guide door
[oploc1,loc_3014]
if (%tutorial_progress < 4) {
    ~mesbox("You need to talk to the 'Runescape Guide' before you are allowed to proceed through this door.");
    return;
}

hint_stop();
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);
if (%tutorial_progress = 4) {
    %tutorial_progress = 10;
    ~tutorial_step_moving_around;
}

// Door to Master Chef
[oploc1,loc_3017]
hint_stop();
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);
if (%tutorial_progress = 130) {
    %tutorial_progress = ^chef_start;
    ~set_tutorial_progress;
}

// Door exiting Master Chef's house
[oploc1,loc_3018]
if (%tutorial_progress < 180) {
    ~mesbox("You need to finish the Cooking Guide's tasks first.");
    return;
}

hint_stop();
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);
if (%tutorial_progress = 180) {
    %tutorial_progress = 190;
    ~set_tutorial_progress;
}

// Door to Quest Guide's house
[oploc1,loc_3019]
hint_stop();
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);
if (%tutorial_progress = 200) {
    %tutorial_progress = ^quest_guide_start;
    ~set_tutorial_progress;
}

// Ladder to Mine
[oploc1,loc_3029]
if (%tutorial_progress < 250) {
    npc_findallzone(0_48_48_12_52);
    while (npc_findnext = true) {
        if (npc_type = npc_949) @quest_guide_not_ready_to_leave_yet;
    }

    npc_findallzone(0_48_48_16_48);
    while (npc_findnext = true) {
        if (npc_type = npc_949) @quest_guide_not_ready_to_leave_yet;
    }
    return;
}
@tutorial_complete;
