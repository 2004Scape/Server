// Tutorial Island Tree
[oploc1,loc_3033]
if (%tutorial_progress < 30) {
    ~mesbox("You cannot cut down this tree yet.|You must progress further in the tutorial.");
    return;
}

if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("Your inventory is too full to hold any more logs.|To drop an item right-click on it and select drop.");
    return;
}

if (%tutorial_progress > 120) {
    ~mesbox("Perhaps you've done enough woodcutting now.");
    return;
}

if (inv_total(inv, logs) > 2) {
    ~mesbox("Perhaps you've got enough logs for now.");
    return;
}

if (inv_total(inv, bronze_axe) = 0) {
    ~mesbox("You need an axe to chop this tree.|Talk to the survival expert to get one.");
    return;
}

p_arrivedelay;
~tutorial_please_wait_woodcutting;
~tut_woodcutting_animation_and_sound;
~tut_woodcutting_swap_loc;
~tutorial_give_xp(woodcutting, 250);
if (%tutorial_progress = 30) {
    @tut_advance_woodcutting;
} else {
    @give_logs;
}

[proc,tut_woodcutting_animation_and_sound]
anim(human_woodcutting_bronze_axe, 0);
sound_synth(woodchop_4, 0, 18);
p_delay(1);
sound_synth(woodchop_4, 0, 0);
p_delay(0);
sound_synth(woodchop_4, 0, 8);
p_delay(0);
anim(human_woodcutting_bronze_axe, 0);
sound_synth(woodchop_4, 0, 25);
p_delay(0);
sound_synth(woodchop_4, 0, 18);
p_delay(1);
sound_synth(woodchop_4, 0, 8);
p_delay(0);
sound_synth(woodchop_4, 0, 25);
p_delay(1);
anim(human_woodcutting_bronze_axe, 0);
sound_synth(woodchop_4, 0, 25);
p_delay(0);

[proc,tut_woodcutting_swap_loc]
def_int $respawnrate = ~scale_by_playercount(60);
loc_del($respawnrate);
loc_add(loc_coord, loc_param(next_loc_stage), loc_angle, centrepiece_straight, $respawnrate);
// set skill anim so it doesnt continue after depletion
anim(null, $respawnrate);

[label,tut_advance_woodcutting]
%tutorial_progress = 40;
hint_stop();
inv_add(inv, logs, 1);
~tutorial_step_13_build_fire;
~objboxt(logs, "You get some logs.");

[label,give_logs]
inv_add(inv, logs, 1);
~set_tutorial_progress;
~objboxt(logs, "You get some logs.");

[oploc1,loc_3037]
if (%tutorial_progress < 30) {
    ~mesbox("You cannot cut down this tree yet.|You must progress further in the tutorial.");
    return;
} else {
    ~mesbox("You won't be able to chop oak trees until you have a Woodcutting level of 15. You'll advance to higher Woodcutting levels by chopping down normal trees. At higher Woodcutting levels you will find even more trees you can cut.");
    return;
}