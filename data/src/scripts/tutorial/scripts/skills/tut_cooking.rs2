[oplocu,loc_3039]
if (last_useitem = bread_dough) {
    anim(human_cooking, 0);
    sound_synth(fry, 0, 0);
    p_delay(2);
    if (%tutorial_progress = 160) {
        inv_del(inv, bread_dough, 1);
        inv_add(inv, bread, 1);
        ~tutorial_give_xp(cooking, 400);
        %tutorial_progress = 170;
        ~set_tutorial_progress;
    } else {
        inv_del(inv, bread_dough, 1);
        def_int $rand = random(10);
        if ($rand < calc(3 + stat_base(cooking))) {
            inv_add(inv, bread, 1);
            ~tutorial_give_xp(cooking, 400);
        } else {
            inv_add(inv, burnt_bread, 1);
        }
    }
} else if (last_useitem = raw_shrimps) {
    anim(human_cooking, 0);
    sound_synth(fry, 0, 0);
    p_delay(2);
    inv_del(inv, raw_shrimps, 1);
    @tut_random_shrimp_chance;
} else {
    @nothing_interesting_happens;
}

[label,tut_attempt_cook_item](obj $uncooked_item)
if ($uncooked_item = raw_shrimps) {
    p_arrivedelay;
    if (%tutorial_progress = 80) {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        inv_add(inv, burnt_shrimp_anchovies, 1);
        %tutorial_progress = 90;
        ~tutorial_step_burn_shrimp;
    } else if (%tutorial_progress = 90) {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        inv_add(inv, shrimps, 1);
        ~tutorial_give_xp(cooking, 300);
        %tutorial_progress = 120;
        ~tutorial_step_survival_recap;
    } else {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        @tut_random_shrimp_chance;
    }
} else {
    @nothing_interesting_happens;
}

[label,tut_random_shrimp_chance]
def_int $rand = random(10);
if ($rand < calc(3 + stat_base(cooking))) {
    inv_add(inv, shrimps, 1);
    ~tutorial_give_xp(cooking, 300);
} else {
    inv_add(inv, burnt_shrimp_anchovies, 1);
}

[label,tut_make_dough]
// TODO: See what happens on rs if you have a full invent
// On a quick test, I made the dough, got that in my invent first, then an empty pot, and no bucket to be found anywhere.
inv_del(inv, pot_of_flour, 1);
inv_del(inv, bucket_water, 1);
inv_add(inv, pot_empty, 1);
inv_add(inv, bucket_empty, 1);
inv_add(inv, bread_dough, 1);
if (%tutorial_progress = 150) {
    %tutorial_progress = 160;
}
~set_tutorial_progress;