[oplocu,loc_3039]
if (last_useitem = bread_dough) {
    anim(human_cooking, 0);
    sound_synth(fry, 0, 0);
    p_delay(2);
    if (%tutorial_progress = ^chef_mixed_dough) {
        inv_del(inv, bread_dough, 1);
        inv_add(inv, bread, 1);
        ~tutorial_give_xp(cooking, 400);
        %tutorial_progress = ^chef_baked_bread;
        ~set_tutorial_progress;
    } else {
        inv_del(inv, bread_dough, 1);
        def_int $rand = random(10);
        if ($rand < calc(3 + stat_base(cooking))) {
            mes("You remove the bread from the oven.");
            inv_add(inv, bread, 1);
            ~tutorial_give_xp(cooking, 400);
        } else {
            mes("You accidentally burn the bread.");
            inv_add(inv, burnt_bread, 1);
        }
    }
} else if (last_useitem = raw_shrimps) {
    anim(human_cooking, 0);
    sound_synth(fry, 0, 0);
    p_delay(2);
    inv_del(inv, raw_shrimps, 1);
    @tut_random_shrimp_chance;
} else if (last_useitem = raw_rat_meat) {
      anim(human_cooking, 0);
      sound_synth(fry, 0, 0);
      p_delay(2);
      inv_del(inv, raw_rat_meat, 1);
      @tut_random_meat_chance;
  }else {
    @nothing_interesting_happens;
}

[label,tut_attempt_cook_item](obj $uncooked_item)
if ($uncooked_item = raw_shrimps) {
    p_arrivedelay;
    if (%tutorial_progress = ^survival_guide_cook_shrimps) {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        inv_add(inv, burnt_shrimp_anchovies, 1);
        %tutorial_progress = ^survival_guide_burnt_shrimps;
        ~tutorial_step_burn_shrimp;
    } else if (%tutorial_progress = ^survival_guide_burnt_shrimps) {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        inv_add(inv, shrimps, 1);
        ~tutorial_give_xp(cooking, 300);
        %tutorial_progress = ^survival_guide_complete;
        ~tutorial_step_survival_recap;
    } else {
        anim(human_firecooking, 0);
        sound_synth(fry, 0, 0);
        p_delay(2);
        inv_del(inv, raw_shrimps, 1);
        @tut_random_shrimp_chance;
    }
} else if ($uncooked_item = raw_rat_meat) {
    anim(human_firecooking, 0);
    sound_synth(fry, 0, 0);
    p_delay(2);
    inv_del(inv, raw_rat_meat, 1);
    @tut_random_meat_chance;
} else {
    @nothing_interesting_happens;
}

[label,tut_random_shrimp_chance]
def_int $rand = random(10);
if ($rand < calc(3 + stat_base(cooking))) {
    mes("The shrimps are now nicely cooked.");
    inv_add(inv, shrimps, 1);
    ~tutorial_give_xp(cooking, 300);
} else {
    mes("You accidentally burn the shrimps.");
    inv_add(inv, burnt_shrimp_anchovies, 1);
}

[label,tut_random_meat_chance]
def_int $rand = random(10);
if ($rand < calc(3 + stat_base(cooking))) {
    mes("The meat is now nicely cooked.");
    inv_add(inv, cooked_meat, 1);
    ~tutorial_give_xp(cooking, 300);
} else {
    mes("You accidentally burn the meat.");
    inv_add(inv, burnt_meat, 1);
}

[label,tut_make_dough]
// TODO: See what happens on rs if you have a full invent
// On a quick test, I made the dough, got that in my invent first, then an empty pot, and no bucket to be found anywhere.
inv_del(inv, pot_of_flour, 1);
inv_del(inv, bucket_water, 1);
inv_add(inv, pot_empty, 1);
inv_add(inv, bucket_empty, 1);
inv_add(inv, bread_dough, 1);
if (%tutorial_progress = ^chef_make_dough) {
    %tutorial_progress = ^chef_mixed_dough;
}
~set_tutorial_progress;