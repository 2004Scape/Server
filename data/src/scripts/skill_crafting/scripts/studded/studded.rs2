[opheldu,steel_studs]
switch_obj (last_useitem)
{
    case leather_chaps : 
        ~require_members_feature;
        @craft_studded(studded_chaps);
    case leather_body :
        ~require_members_feature;
        @craft_studded(studded_body);
    case default : ~displaymessage(^dm_default);
};

[opheldu,leather_chaps]
if (last_useitem = steel_studs) {
    ~require_members_feature;
    @craft_studded(studded_chaps);
}
~displaymessage(^dm_default);

[opheldu,leather_body]
if (last_useitem = steel_studs) {
    ~require_members_feature;
    @craft_studded(studded_body);
}
~displaymessage(^dm_default);


[label,craft_studded](obj $studded_obj)
def_struct $struct = oc_param($studded_obj, crafting_studded_struct);

// check if player has level req
if (stat(crafting) < struct_param($struct, levelrequired)) {
    mes(struct_param($struct, levelfailure));
    return;
}

// delete items
inv_del(inv, struct_param($struct, ingredient), 1);
inv_del(inv, steel_studs, 1);

// add item
inv_add(inv, struct_param($struct, product), 1);

stat_advance(crafting, struct_param($struct, productexp));

// display game message
mes(struct_param($struct, productmessage));