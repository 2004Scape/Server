[oplocu,spinning_wheel]
switch_obj (last_useitem) 
{
    case wool : @craft_spinning(last_useitem);
    case flax :
        if (map_members = ^false) {
            mes(^mes_members_feature);
            return;
        }
        @craft_spinning(last_useitem);
    case ball_of_wool : ~objbox(ball_of_wool, "You have already spun this wool.", 250, 0, divide(^objbox_height, 2));
    case bow_string : ~objbox(bow_string, "You have already spun this bow string.", 250, 0, divide(^objbox_height, 2));
    case default : ~displaymessage(^dm_default);
};

[label,craft_spinning](obj $spinning_obj)
def_struct $struct = oc_param($spinning_obj, crafting_spinning_struct);
// check if we have the required level
def_int $level = struct_param($struct, levelrequire);
if (stat(crafting) < $level) {
    ~mesbox("You need level <tostring($level)> Crafting to make that.");
    return;
}
// macro event
if (afk_event = ^true) {
    ~macro_event_general_spawn(~macro_event_set_random);
    return;
}
// play human animation
anim(human_spinningwheel, 0);
loc_anim(spinningwheel);
// if sound is found, play here
inv_del(inv, struct_param($struct, ingredient), 1);
inv_add(inv, struct_param($struct, product), 1);
mes(struct_param($struct, processmessage));
stat_advance(crafting, struct_param($struct, productexp));
p_delay(3);
//weakqueue(crafting_spinning, 2, $struct);

/**
//May have not used weakqueue for this back then https://youtu.be/eQEYx8hwA_I?t=410
// todo flax spinning must have been queue because of 2 diff messages: https://youtu.be/HQEpeWAUMbI?t=51
// todo one for in queue and one out of queue. -jordan
[weakqueue,crafting_spinning](struct $struct)
// delete item
inv_del(inv, struct_param($struct, ingredient), 1);

// add item
inv_add(inv, struct_param($struct, product), 1);
// display game message
mes(struct_param($struct, processmessage));

stat_advance(crafting, struct_param($struct, productexp));
*/


