// trigger for logs on ground
[opobj4,_category_22]
    @light_logs_ground;

[opobju,_category_22]
if (last_useitem = tinderbox) {
    @light_logs_ground;
} else {
    ~displaymessage(^dm_default);
}

// opheldu for logs is handled in fletching
[opheldu,tinderbox]
if (last_useitem = tutorial_logs) {
    @tut_light_logs_inv(last_useslot);
} else if(oc_param(last_useitem, unlit_arrow) = ^true) { // might not work on every fire?
    @light_firearrow(last_useitem);
} else switch_category(oc_category(last_useitem)) {
    case category_22 : @light_logs_inv(last_useslot);
    case light_source_unlit: @ignite_light_source(last_useitem);
    case default : ~displaymessage(^dm_default);
}

[label,light_logs_ground]
def_obj $log = obj_type;
// check level first
def_int $level = oc_param($log, levelrequire);
if (stat(firemaking) < $level) {
    mes("You need a Firemaking level of <tostring($level)> to burn <oc_name($log)>.");
    return;
}
if (~area_allow_loc_add(obj_coord) = false) {
    mes("You can't light a fire here.");
    return;
}
// check if they have tinderbox
if(inv_total(inv, tinderbox) < 1) {
    mes("You need a tinderbox to light a fire.");
    return;
}
// party pete goes here, after tinderbox check
if (~inzone_coord_pair_table(party_room_zones, coord) = true) {
    mes("Party Pete won't be happy if you light a fire here."); // osrs
    return;
}
// 4t cycle
// mes(tostring(sub(map_clock, %action_delay)));
if (%action_delay < map_clock) {
    p_stopaction;
    anim(human_createfire, 0);
    sound_synth(tinderbox_strike, 0, 0);
    mes("You attempt to light the logs.");
    %action_delay = calc(map_clock + 3);
} else if (%action_delay = map_clock) {
    if(stat_random(stat(firemaking), 64, 512) = true) { // firemaking roll
        %action_delay = calc(map_clock + 4);
        // attempt to firemaking each direction: west east south north
        // if player isnt standing on the log anymore, the fire is lit
        ~push_player(obj_coord);
        // if fully blocked then just light the log and dont move
        ~firemaking_success(obj_coord, $log);
        return;
    } else {
        // if fails, then reattempt to light
        anim(human_createfire, 0);
        sound_synth(tinderbox_strike, 0, 0);
        %action_delay = calc(map_clock + 4);
    }
}
// mes("<tostring(map_clock)>: p_opobj(4)");
p_opobj(4); // interact with obj again

[label,light_logs_inv](int $slot)
def_obj $log = inv_getobj(inv, $slot);
def_int $level = oc_param($log, levelrequire);

if (stat(firemaking) < $level) {
    mes("You need a Firemaking level of <tostring($level)> to burn <oc_name($log)>.");
    return;
}
// check if the coord is already occupied by an interactable loc
if (~area_allow_loc_add(coord) = false) {
    mes("You can't light a fire here.");
    return;
}
// check if they have tinderbox
if(inv_total(inv, tinderbox) < 1) {
    mes("You need a tinderbox to light a fire.");
    return;
}
// party pete goes here, after tinderbox check
if (~inzone_coord_pair_table(party_room_zones, coord) = true) {
    // osrs
    mes("Party Pete won't be happy if you light a fire here.");
    return;
}
inv_dropslot(inv, coord, $slot, ^lootdrop_duration);
if (%action_delay < map_clock) {
    p_stopaction;
    anim(human_createfire, 0);
    sound_synth(tinderbox_strike, 0, 0);
    mes("You attempt to light the logs.");
    %action_delay = calc(map_clock + 3);
} else if (%action_delay = map_clock) {
    if(stat_random(stat(firemaking), 64, 512) = true) { // firemaking roll
        %action_delay = calc(map_clock + 4);
        ~push_player_from_inv(obj_coord);
        ~firemaking_success(obj_coord, $log); // if fully blocked then just light the log and dont move
        return;
    } else {
        // if fails, then reattempt to light
        anim(human_createfire, 0);
        sound_synth(tinderbox_strike, 0, 0);
        %action_delay = calc(map_clock + 4);
    }
}
p_opobj(4);

[proc,firemaking_success](coord $fire_coord, obj $log)
obj_del;
stat_advance(firemaking, oc_param($log, productexp));
sound_synth(fire_lit, 0, 0);
facesquare($fire_coord);
//mes("<tostring(map_clock)>, <tostring(%action_delay)>: The fire catches and the logs begin to burn.");
mes("The fire catches and the logs begin to burn.");
def_int $delay = calc(100 + random(100)); // firemaking length: https://archive.is/zF5hb
loc_add($fire_coord, loc_2732, 1, centrepiece_straight, $delay);
world_delay($delay);
obj_add($fire_coord, ashes, 1, 100);

[proc,area_allow_loc_add](coord $coord)(boolean)
if (map_locaddunsafe($coord) = true) {
    return(false);
}
if (map_blocked($coord) = true) {
    return(false);
}
if (~inzone_coord_pair_table(bank_zones, coord) = true) { // zone based checks use coord instead of loc_coord
    return(false);
}
return(true);


[debugproc,pt]
if (p_finduid(uid) = true) {
    p_teleport(movecoord(coord, 1, 0, 0));
}

[proc,push_player](coord $origin_coord)
anim(null, 0);
if (~in_duel_arena($origin_coord) = true) {
    p_delay(0);
    return;
}
if (lineofwalk(coord, movecoord(coord, -1, 0, 0)) = true & coord = $origin_coord) { // check west
    p_teleport(movecoord(coord, -1, 0, 0));
    p_delay(0);
}
if (lineofwalk(coord, movecoord(coord, 1, 0, 0)) = true & coord = $origin_coord) { // check east
    p_teleport(movecoord(coord, 1, 0, 0));
    p_delay(0);
}
if (lineofwalk(coord, movecoord(coord, 0, 0, -1)) = true & coord = $origin_coord) { // check south
    p_teleport(movecoord(coord, 0, 0, -1));
    p_delay(0);
}
if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true & coord = $origin_coord) { // check north
    p_teleport(movecoord(coord, 0, 0, 1));
    p_delay(0);
}

[proc,push_player_from_inv](coord $origin_coord)
anim(null, 0);
if (~in_duel_arena($origin_coord) = true) {
    // https://youtu.be/L3OwjH-iXQU
    //  - could still fm in the duel arena. 
    //  - It just didnt move the player at all
    // https://oldschool.runescape.wiki/w/Update:RS2_-_more_fixes/changes
    //  - "Fixed: You could select a duel with the no movement option and then plant flowers or light a fire so you weren't standing next to your opponent."
    p_delay(1);
    return;
}
if (lineofwalk(coord, movecoord(coord, -1, 0, 0)) = true & coord = $origin_coord) { // check west
    p_teleport(movecoord(coord, -1, 0, 0));
    p_delay(1);
    if (lineofwalk(coord, movecoord(coord, 1, 0, 0)) = true & coord = $origin_coord) { // check east
        p_teleport(movecoord(coord, 1, 0, 0));
        p_delay(0);
    }
    if (lineofwalk(coord, movecoord(coord, 0, 0, -1)) = true & coord = $origin_coord) { // check south
        p_teleport(movecoord(coord, 0, 0, -1));
        p_delay(0);
    }
    if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true & coord = $origin_coord) { // check north
        p_teleport(movecoord(coord, 0, 0, 1));
        p_delay(0);
    }
} else if (lineofwalk(coord, movecoord(coord, 1, 0, 0)) = true & coord = $origin_coord) { // check east
    p_teleport(movecoord(coord, 1, 0, 0));
    p_delay(1);
    if (lineofwalk(coord, movecoord(coord, 0, 0, -1)) = true & coord = $origin_coord) { // check south
        p_teleport(movecoord(coord, 0, 0, -1));
        p_delay(0);
    }
    if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true & coord = $origin_coord) { // check north
        p_teleport(movecoord(coord, 0, 0, 1));
        p_delay(0);
    }
} else if (lineofwalk(coord, movecoord(coord, 0, 0, -1)) = true & coord = $origin_coord) { // check south
    p_teleport(movecoord(coord, 0, 0, -1));
    p_delay(1);
    if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true & coord = $origin_coord) { // check north
        p_teleport(movecoord(coord, 0, 0, 1));
        p_delay(0);
    }
} else if (lineofwalk(coord, movecoord(coord, 0, 0, 1)) = true & coord = $origin_coord) { // check north
    p_teleport(movecoord(coord, 0, 0, 1));
    p_delay(1);
}