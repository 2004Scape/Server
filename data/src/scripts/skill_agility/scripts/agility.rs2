// Delay and 'climb' to a coord, similar to ladders. 
// Generally used for net obstacles
[proc,agility_climb_up](int $xp, coord $coord)
anim(human_reachforladdertop, 0);
p_delay(0);
givexp(agility, $xp);
p_telejump($coord);

// force walk to a specific location, this isn't the 
// same as an exactmove 
[proc,agility_force_move](int $xp, seq $walk_seq, coord $dest)
bas_walk_f($walk_seq);
buildappearance(worn);
def_int $change_x = calc(coordx($dest) - coordx(coord));
def_int $change_z = calc(coordz($dest) - coordz(coord));
~agility_walk($change_x, $change_z);
givexp(agility, $xp);

[proc,agility_walk](int $change_x, int $change_z)
if($change_x = 0 & $change_z = 0) {
    ~update_bas;
    return;
}
def_int $move_x = 0;
def_int $move_z = 0;
if ($change_x >= 1) {
    $move_x = 1;
} else if ($change_x <= -1) {
    $move_x = -1;
}
if ($change_z >= 1) {
    $move_z = 1;
} else if ($change_z <= -1) {
    $move_z = -1;
}
// Perform movement and re-calc
def_coord $movement_coord = movecoord(coord, $move_x, 0, $move_z);
p_teleport(movecoord(coord, $move_x, 0, $move_z));
$change_x = calc($change_x - $move_x);
$change_z = calc($change_z - $move_z);
p_delay(0);
// recursive call
~agility_walk($change_x, $change_z);

// Proc for combining all general parts of an exactmove used in the agility skill (animation, delay, locmerge)
[proc,agility_exactmove](seq $anim, int $anim_delay, int $movement_delay, coord $start_coord, coord $end_coord, int $start_cycle, int $end_cycle, int $direction, boolean $merge_with_loc)
anim($anim, $anim_delay);
if($merge_with_loc = true) {
    p_locmerge($start_cycle, $end_cycle, $start_coord, $end_coord); 
}
p_exactmove($start_coord, $end_coord, $start_cycle, $end_cycle, $direction);
p_delay($movement_delay);

// Standard delayed fail used in agility
[proc,agility_delay_fail](coord $fall_face, coord $fail_coord, int $damage, string $message)
anim(human_sidestep_fall, 0);
facesquare($fall_face);
p_delay(1);
p_telejump($fail_coord);
damage(uid, 1, $damage);
if (gender() = 0) {
    sound_synth(human_hit2, 0, 20);
} else {
    sound_synth(female_hit2, 0, 20);
}
mes($message);
p_delay(0);

// forces the player to walk to a certain coord, ignoring their current run setting, can be used to setup obstacles
// or for cutscenes
[proc,forcewalk](coord $dest_coord)
if(coord = $dest_coord) {
    return;
}
def_int $dx = abs(calc(coordx(coord) - coordx($dest_coord)));
def_int $dz = abs(calc(coordz(coord) - coordz($dest_coord)));
def_int $delay = calc($dx + $dz - 1);
if(%player_run = 1) {
    $delay = calc($delay / 2);
}
p_delay(0);
p_walk($dest_coord);
p_delay($delay);

// Forces the player to walk to a certain coord. Basically p_teleport but doesnt telejump from a distance.
// doesnt respect clipping
[proc,forcemove](coord $dest_coord)
~agility_force_move(0, human_walk_f, $dest_coord);

// Instant fail, not used as often
[proc,agility_instant_fail](coord $fail_coord, int $damage, string $message)
p_telejump($fail_coord);
~damage_self($damage);
mes($message);

// Finds an identical loc on the $merge_pos coord to switch active_loc to
[proc,change_merged_loc](coord $merge_pos)
loc_findallzone($merge_pos);
while (loc_findnext = true) {
    if (loc_find($merge_pos, loc_type) = true) {
        return;
    }
}