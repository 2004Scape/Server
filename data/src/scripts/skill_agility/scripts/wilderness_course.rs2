[oploc1,loc_2309]
if(stat(agility) < 52) {
    ~mesbox("You need an Agility level of 52 to attempt balancing on the ridge.");
    return;
}
~open_entrance_gate;
mes("You go through the gate and try to edge over the ridge...");
// walk to start
p_teleport(0_46_61_54_13);
p_delay(0);
~agility_force_move(0, human_walk_logbalance, movecoord(coord, 0, 0, 6));
// todo: actual rate
if(stat_random(stat(agility), 180, 245) = false) {
    @fail_ridge;
}
~agility_force_move(150, human_walk_logbalance, movecoord(coord, 0, 0, 6));
~open_course_gates;
p_teleport(movecoord(coord, 0, 0, 2));
mes("You skillfully balance across the ridge...");

[oploc1,_wilderness_course_main_gate]
mes("You go through the gate and try to edge over the ridge...");
~open_course_gates;
p_teleport(0_46_61_54_25);
p_delay(0);
~agility_force_move(0, human_walk_logbalance, movecoord(coord, 0, 0, -6));
// todo: actual rate
if(stat_random(stat(agility), 180, 245) = false) {
    @fail_ridge;
}
~agility_force_move(150, human_walk_logbalance, movecoord(coord, 0, 0, -6));
~open_entrance_gate;
p_teleport(movecoord(coord, 0, 0, -1));
mes("You skillfully balance across the ridge...");

[oploc1,wilderness_obstacle_pipe] 
if(coordz(coord) >= 3939) {
    mes("You can't enter the pipe from this side.");
    return;
}
if (stat(agility) < 49) {
    ~mesbox("You need an Agility level of 49 to squeeze through the pipe.");
    return;
}
if(coord ! movecoord(loc_coord, 0, 0, -1)) {
    p_delay(0);
    p_walk(movecoord(loc_coord, 0, 0, -1));
}
// todo: set var here and clear after the first exact move
if(%wilderness_obstacle_pipe_used >= map_clock) {
    mes("The pipe is being used"); // todo: confirm, using the same as gnome course
    return;
}
%wilderness_obstacle_pipe_used = calc(map_clock + 4); 
p_delay(0);
~agility_exactmove(human_doublepipesqueeze, 30, 3, movecoord(loc_coord, 0, 0, -1), movecoord(loc_coord, 0, 0, 2), 30, 126, ^exact_north, true);
p_delay(0);
p_telejump(movecoord(loc_coord, 0, 0, 9)); // jump so we can switch active loc
p_delay(0);
~change_merged_loc(movecoord(coord, 0, 0, 1));
~agility_exactmove(human_doublepipesqueeze, 30, 3, movecoord(loc_coord, 0, 0, -1), movecoord(loc_coord, 0, 0, 2), 30, 126, ^exact_north, true);
stat_advance(agility, 125);
~update_wilderness_varp(1);

[oploc1,wilderness_rope_swing]
def_coord $start_pos = movecoord(loc_coord, 0, 0, 1);
def_int $delay = distance(coord, $start_pos);
if(coordz(coord) > coordz($start_pos)) {
    mes("You cannot do that from here.");
    return;
}
p_delay(0);
p_teleport($start_pos);
if($delay > 0) p_delay($delay);
if(stat_random(stat(agility), 175, 245) = false) {
    // todo: find actual rates, this is 82% at 52, 96% at 99
    ~agility_instant_fail(0_46_161_60_53, calc(((stat(hitpoints) * 15) / 100) + 1), "You slip and fall to the pit below.");
    return;
}
if(%wilderness_ropeswing_used >= map_clock) {
    ~mesbox("The rope swing is being used");
    return;
}
%wilderness_ropeswing_used = calc(map_clock + 2);
loc_anim(ropeswing_long);
~agility_exactmove(human_ropeswing_long, 20, 1, $start_pos, movecoord($start_pos, 0, 0, 5), 45, 70, ^exact_north, false);
mes("You skillfully swing across.");
stat_advance(agility, 200);
~update_wilderness_varp(2);

[oploc1,wilderness_stepping_stone]
mes("You carefully start crossing the stepping stones...");
~agility_exactmove(human_spot_jump, 0, 1, movecoord(loc_coord, 1, 0, 0), loc_coord, 15, 30, ^exact_west, false);
~agility_exactmove(human_spot_jump, 0, 1, loc_coord, movecoord(loc_coord, -1, 0, 0), 15, 30, ^exact_west, false);
~agility_exactmove(human_spot_jump, 0, 1, movecoord(loc_coord, -1, 0, 0), movecoord(loc_coord, -2, 0, 0), 15, 30, ^exact_west, false);
if(stat_random(stat(agility), 185, 240) = false) {
    // todo: find actual rates, this is 83% at 52, 94% at 99
    ~agility_delay_fail(movecoord(coord, 0, 0, 2), movecoord(loc_coord, 1, 0, 3), calc(((stat(hitpoints) * 216) / 1000) + 1), "...You lose your footing and fall into the lava.");
    sound_synth(sizzle, 0, 0);
    return;
}
~agility_exactmove(human_spot_jump, 0, 1, movecoord(loc_coord, -2, 0, 0), movecoord(loc_coord, -3, 0, 0), 15, 30, ^exact_west, false);
~agility_exactmove(human_spot_jump, 0, 1, movecoord(loc_coord, -3, 0, 0), movecoord(loc_coord, -4, 0, 0), 15, 30, ^exact_west, false);
~agility_exactmove(human_spot_jump, 0, 1, movecoord(loc_coord, -4, 0, 0), movecoord(loc_coord, -5, 0, 0), 15, 30, ^exact_west, false);
stat_advance(agility, 200);
mes("...You safely cross to the other side.");
~update_wilderness_varp(3);

[oploc1,wilderness_log_balance]
p_teleport(movecoord(loc_coord, 1, 0, 0));
p_delay(0);
mes("You walk carefully across the slippery log...");
~agility_force_move(0, human_walk_logbalance, movecoord(coord, -4, 0, 0));
if(stat_random(stat(agility), 170, 250) = false) {
    // todo: find actual rates, this is 82% at 52, 98% at 99
    ~agility_delay_fail(movecoord(coord, 0, 0, 2), 0_46_161_54_42, calc(((stat(hitpoints) * 15) / 100) + 1), "You slip and fall onto the spikes below.");
    p_teleport(movecoord(coord, 0, 0, -1));
    return;
}
~agility_force_move(200, human_walk_logbalance, movecoord(coord, -4, 0, 0));
mes("You skillfully edge across the gap.");
~update_wilderness_varp(4);

[oploc1,wilderness_rocks]
~set_readywalk_bas(human_climbing_ready, human_climbing);
~forcemove(loc_coord);
~forcemove(movecoord(coord, 0, 0, -3));
~update_bas;
~update_wilderness_varp(5);

[label,fail_ridge]
anim(human_walk_logbalance_stumble, 0);
p_delay(1);
p_telejump(movecoord(coord, 2, 0, 0));
mes("You lose your footing and fall into the wolf pit.");

// todo: These double gate doors are broken
[proc,open_course_gates]
loc_findallzone(coord);
while(loc_findnext = true) {
     if(loc_category = wilderness_course_main_gate) {
            def_coord $central_coord = loc_coord;
            def_int $orig_angle = loc_angle;
            // Temp note: dur updated
            loc_del(4);
            if(loc_type = loc_2308) {
                // Temp note: dur updated
                loc_add(movecoord($central_coord, 0, 0, -1), loc_1563, 0, loc_shape, 4);
                loc_add(movecoord($central_coord, 1, 0, -1), loc_1562, 2, loc_shape, 4); 
            }
            // Temp note: dur updated
            loc_add($central_coord, inviswall, $orig_angle, loc_shape, 4);
     }
}
sound_synth(door_open, 0, 0);

[proc,open_entrance_gate]
loc_findallzone(coord);
while(loc_findnext = true) {
    if(loc_type = loc_2309) {
        // Temp note: dur updated
        loc_change(inviswall, 3);
        loc_add(movecoord(loc_coord, 0, 0, -1), loc_1541, 0, loc_shape, 3);
        sound_synth(door_open, 0, 0);
    }
}

[proc,update_wilderness_varp](int $completed)
if(calc(%wilderness_course_progress + 1) >= $completed) {
    %wilderness_course_progress = $completed;
}
if(%wilderness_course_progress = 5) {
    stat_advance(agility, 4989);
    %wilderness_course_progress = 0;
}