[oploc1,gnome_log_balance]
p_arrivedelay;
mes("You walk carefully across the slippery log...");
~gnome_trainer_say("Okay get over that log, quick quick!");
~agility_force_move(75, human_walk_logbalance, movecoord(coord, 0, 0, -7));
mes("...You make it safely to the other side.");
~update_gnome_varp(1);

[oploc1,gnome_obstacle_net_1]
mes("You climb the netting...");
~gnome_trainer_say("Move it, move it, move it!");
~agility_climb_up(75, movecoord(coord, 0, 1, -2));
~update_gnome_varp(2);

[oploc1,gnome_tree_branch_1]
mes("You climb the tree..");
~gnome_trainer_say("That's it - straight up.");
~agility_climb_up(50, movecoord(loc_coord, 0, 1, -2));
mes("...To the platform above.");
~update_gnome_varp(3);

[oploc1,gnome_balancing_rope]
p_arrivedelay;
mes("You carefully cross the tightrope.");
~gnome_trainer_say("Come on scaredy cat, get across that rope!");
~agility_force_move(75, human_walk_logbalance, movecoord(coord, 6, 0, 0));
~update_gnome_varp(4);

[oploc1,gnome_tree_branch_2]
mes("You climb down the tree.");
~agility_climb_up(50, movecoord(loc_coord, 1, -2, 1));
mes("You land on the ground.");
~update_gnome_varp(5);

[oploc1,gnome_obstacle_net_2]
if(coordz(coord) >= coordz(loc_coord)) {
    mes("You can not do that from here.");
    return;
}
mes("You climb the netting...");
~gnome_trainer_say("My Granny can move faster than you.");
~agility_climb_up(75, movecoord(coord, 0, 0, 2));
~update_gnome_varp(6);

[oploc1,gnome_obstacle_pipe]
if(coordz(coord) > coordz(loc_coord)) {
    mes("You can't enter the pipe from this side.");
    return;
}
// Enter tube
~forcewalk(movecoord(loc_coord, 0, 0, -1));
if(%gnome_obstacle_pipe_used >= map_clock) {
    mes("The pipe is being used"); 
    return;
}
%gnome_obstacle_pipe_used = calc(map_clock + 5);
~agility_exactmove(human_pipesqueeze, 30, 4, movecoord(loc_coord, 0, 0, -1), movecoord(loc_coord, 0, 0, 2), 30, 126, ^exact_east, true);
~agility_exactmove(human_pipesqueeze, 30, 4, movecoord(loc_coord, 0, 0, 2), movecoord(loc_coord, -3, 0, 2), 30, 158, ^exact_north, false);
p_teleport(movecoord(loc_coord, -3, 0, 3));
p_delay(0);
~change_merged_loc(movecoord(coord, 0, 0, 1));
~agility_exactmove(human_pipesqueeze, 30, 4, coord, movecoord(coord, 0, 0, 3), 30, 126, ^exact_east, true);
givexp(agility, 75);
~update_gnome_varp(7);

[proc,gnome_trainer_say](string $line)
npc_findallzone(loc_coord);
while (npc_findnext = true) {
    if (npc_type = gnome_trainer) {
        npc_say($line);
        return;
    }
}

[proc,update_gnome_varp](int $completed)
if(calc(%gnome_course_progress + 1) >= $completed) {
    %gnome_course_progress = $completed;
}
if(%gnome_course_progress = 7) {
    givexp(agility, 390);
    %gnome_course_progress = 0;
}