[oploc2,loc_635] // Tea stall
@attempt_steal_from_stall;

[oploc2,loc_2560] // Silk stall
@attempt_steal_from_stall;

[oploc2,loc_2561] // Baker's stall
@attempt_steal_from_stall;

[oploc2,loc_2562] // Gem stall
@attempt_steal_from_stall;

[oploc2,loc_2563] // Fur stall
@attempt_steal_from_stall;

[oploc2,loc_2564] // Spice stall
@attempt_steal_from_stall;

[oploc2,loc_2565] // Silver stall
@attempt_steal_from_stall;

[label,attempt_steal_from_stall]
def_string $name = loc_name();
db_find(stealing:name, $name);
def_dbrow $data = db_findnext;
if ($data = null) {
    @nothing_interesting_happens;
}

p_arrivedelay;

def_string $stall = db_getfield($data, stealing:stall, 0);

if (db_getfieldcount($data, stealing:stall) > 1) {
    def_string $stall_message = db_getfield($data, stealing:stall, 1);
    // This message will still show even if you do not meet the level requirement.
    mes("You attempt to steal <$stall_message> from the <$stall>.");
}

def_int $current_level = stat(thieving);
def_int $thieving_level = db_getfield($data, stealing:level, 0);
if ($current_level < $thieving_level) {
    ~mesbox("You need to be level <tostring($thieving_level)> to steal from the <$stall>.");
    return;
}

anim(seq_832, 0);
p_delay(0);

def_int $experience = db_getfield($data, stealing:experience, 0);
def_int $respawn_ticks = calc(db_getfield($data, stealing:respawn_ticks, 0) - 1);

~stealing_check_for_reward($data);
~steal_from_stall($experience, $respawn_ticks);

[proc,stealing_check_for_reward](dbrow $data)
def_int $loot_size = db_getfieldcount($data, stealing:loot);

while ($loot_size >= 0) {
    def_namedobj $reward;
    def_int $reward_quantity_min;
    def_int $reward_quantity_max;
    def_int $reward_rarity_numerator;
    def_int $reward_rarity_denominator;
    def_string $reward_message;
    $reward, $reward_quantity_min, $reward_quantity_max, $reward_rarity_numerator, $reward_rarity_denominator, $reward_message = db_getfield($data, stealing:loot, $loot_size);

    def_int $roll = random($reward_rarity_denominator);
    if ($reward_rarity_numerator >= $roll) {
        def_int $quantity_roll = ~random_range($reward_quantity_min, $reward_quantity_max);
        inv_add(inv, $reward, $quantity_roll);
        // https://youtu.be/ZyiWFTAt_1o?t=2
        mes("You steal <$reward_message>.");
        $loot_size = -1; // Break the loop if we get an item.
    }

    $loot_size = calc($loot_size - 1);
}
