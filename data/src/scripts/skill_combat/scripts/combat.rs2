// https://oldschool.runescape.wiki/w/Damage_per_second/Melee

// effective levels
[proc,combat_effective_stat](int $stat_level, int $prayerbonus)(int)
return(scale(max(100, $prayerbonus), 100, $stat_level));

[proc,combat_maxhit](int $combat_stat)(int)
return(calc(($combat_stat + 320) / 640));

// defence rolls
[proc,combat_stat](int $effective_stat, int $bonus)(int)
return(calc($effective_stat * ($bonus + 64)));

// returns the dbrow from the combat style enum.
[proc,combat_get_weapon_style_data](obj $weapon)(dbrow)
if ($weapon = null | (map_members = ^false & oc_members($weapon) = true)) {
    return(weapon_unarmed_table);
}
// todo: allow outputtype=dbrow
// return(enum(category, dbrow, combat_style_enum, oc_category($weapon)));
switch_category(oc_category($weapon)) {
    case weapon_2h_sword : return(weapon_2h_sword_table);
    case weapon_axe : return(weapon_axe_table);
    case weapon_blunt : return(weapon_blunt_table); // https://i.imgur.com/jFiwBP8.png
    case weapon_pickaxe : return(weapon_pickaxe_table);
    case weapon_scythe : return(weapon_scythe_table);
    case weapon_slash : return(weapon_slash_table);
    case weapon_spear : return(weapon_spear_table);
    case weapon_spiked : return(weapon_spiked_table);
    case weapon_stab : return(weapon_stab_table);
    case weapon_bow : return(weapon_bow_table);
    case weapon_crossbow : return(weapon_crossbow_table);
    case weapon_thrown : return(weapon_thrown_table);
    case weapon_javelin : return(weapon_thrown_table);
    case weapon_staff : return(weapon_staff_table);
    case default : return(weapon_unarmed_table);
}

// returns the combat style type for a weapon and a selected style.
// example: unarmed punch style returns crush style.
[proc,combat_get_damagetype](dbrow $category_data, int $attackstyle)(int)
return(db_getfield($category_data, combat_style_table:damagetype, $attackstyle));

[proc,combat_get_damagestyle](dbrow $category_data, int $attackstyle)(int)
return(db_getfield($category_data, combat_style_table:damagestyle, $attackstyle));

// takes int $damagestyle (example: ^style_melee_accurate)
// returns attack, strength, defence, ranged bonus.
[proc,combat_get_damagestyle_bonuses](int $damagestyle)(int, int, int, int)
switch_int($damagestyle) {
    // (attack, strength, defence, ranged)
    case ^style_melee_accurate : return(3, 0, 0, 0);
    case ^style_melee_aggressive : return(0, 3, 0, 0);
    case ^style_melee_defensive : return(0, 0, 3, 0);
    case ^style_melee_controlled : return(1, 1, 1, 0);
    case ^style_ranged_accurate : return(0, 0, 0, 3);
    case ^style_ranged_longrange : return(0, 0, 3, 0);
    case default : return(0, 0, 0, 0);
}

// returns the swing anim to use for this weapon and selected style.
[proc,combat_swing_anim_and_synth](obj $weapon, int $damagetype, int $damagestyle, int $attackstyle)(seq, synth)
// unarmed
if ($weapon = null | (map_members = ^false & oc_members($weapon) = true) | oc_category($weapon) = null) {
    switch_int ($attackstyle) {
        case 0 : return(human_unarmedpunch, unarmed_punch);
        case 1 : return(human_unarmedkick, unarmed_kick);
        case 2 : return(human_unarmedpunch, unarmed_punch);
    }
}
// manually handle scythe defensive
if (oc_category($weapon) = weapon_scythe) {
    if ($damagestyle = ^style_melee_defensive & $damagetype = ^slash_style) {
        return(human_scythe_spin, oc_param($weapon, slash_sound));
    }
}
// everything else
switch_int ($damagetype) {
    case ^stab_style : return(oc_param($weapon, stabattack_anim), oc_param($weapon, stab_sound));
    case ^slash_style : return(oc_param($weapon, slashattack_anim), oc_param($weapon, slash_sound));
    case ^crush_style : return(oc_param($weapon, crushattack_anim), oc_param($weapon, crush_sound));
    case ^ranged_style : return(oc_param($weapon, rangeattack_anim), oc_param($weapon, rangeattack_sound));
}

// default multiplier is 1000
[proc,give_combat_experience](int $damagestyle, int $damage, int $multiplier)
def_int $base = multiply($damage, 10);
// not a multiplier (some npcs have that), but exactly 1xp * hit for all cb styles (including controlled), prob a better way of doing this
// not sure if this is the only npc that has this mechanic (not 100% sure if this existed in 2004 either)
if(npc_type = black_knight_titan) { 
    switch_int($damagestyle) {
        case ^style_melee_accurate :
            stat_advance(attack, $base);
        case ^style_melee_aggressive :
            stat_advance(strength, $base);
        case ^style_melee_defensive :
            stat_advance(defence, $base);
        case ^style_melee_controlled :
            stat_advance(attack, $base);
            stat_advance(strength, $base);
            stat_advance(defence, $base);
        case ^style_ranged_accurate, ^style_ranged_rapid :
            stat_advance(ranged, $base);
        case ^style_ranged_longrange :
            stat_advance(ranged, $base);
            stat_advance(defence, $base);
        case ^style_magic_normal :
            stat_advance(magic, $base);
        case ^style_magic_defensive :
            stat_advance(magic, $base);
            stat_advance(defence, $base);
        case default : return;
    }
    stat_advance(hitpoints, scale(133, 100, $base));
    return;
}
if (%tutorial_progress < ^tutorial_complete) {
    switch_int($damagestyle) {
        case ^style_melee_accurate :
            ~tutorial_give_xp(attack, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_aggressive :
            ~tutorial_give_xp(strength, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_defensive :
            ~tutorial_give_xp(defence, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_controlled :
            ~tutorial_give_xp(attack, scale($multiplier, 1000, scale(133, 100, $base)));
            ~tutorial_give_xp(strength, scale($multiplier, 1000, scale(133, 100, $base)));
            ~tutorial_give_xp(defence, scale($multiplier, 1000, scale(133, 100, $base)));
        case ^style_ranged_accurate, ^style_ranged_rapid :
            ~tutorial_give_xp(ranged, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_ranged_longrange :
            ~tutorial_give_xp(ranged, scale($multiplier, 1000, scale(200, 100, $base)));
            ~tutorial_give_xp(defence, scale($multiplier, 1000, scale(200, 100, $base)));
        case ^style_magic_normal :
            ~tutorial_give_xp(magic, scale($multiplier, 1000, scale(200, 100, $base)));
        case ^style_magic_defensive :
            ~tutorial_give_xp(magic, scale($multiplier, 1000, scale(133, 100, $base)));
            ~tutorial_give_xp(defence, scale($multiplier, 1000, scale(100, 100, $base)));
        case default : return;
    }
    ~tutorial_give_xp(hitpoints, scale($multiplier, 1000, scale(133, 100, $base)));
} else {
    switch_int($damagestyle) {
        case ^style_melee_accurate :
            stat_advance(attack, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_aggressive :
            stat_advance(strength, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_defensive :
            stat_advance(defence, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_melee_controlled :
            stat_advance(attack, scale($multiplier, 1000, scale(133, 100, $base)));
            stat_advance(strength, scale($multiplier, 1000, scale(133, 100, $base)));
            stat_advance(defence, scale($multiplier, 1000, scale(133, 100, $base)));
        case ^style_ranged_accurate, ^style_ranged_rapid :
            stat_advance(ranged, scale($multiplier, 1000, scale(400, 100, $base)));
        case ^style_ranged_longrange :
            stat_advance(ranged, scale($multiplier, 1000, scale(200, 100, $base)));
            stat_advance(defence, scale($multiplier, 1000, scale(200, 100, $base)));
        case ^style_magic_normal :
            stat_advance(magic, scale($multiplier, 1000, scale(200, 100, $base)));
        case ^style_magic_defensive :
            stat_advance(magic, scale($multiplier, 1000, scale(133, 100, $base)));
            stat_advance(defence, scale($multiplier, 1000, scale(100, 100, $base)));
        case default : return;
    }
    stat_advance(hitpoints, scale($multiplier, 1000, scale(133, 100, $base)));
}


// returns the defend anim to use for this combat
[proc,combat_defend_anim](obj $weapon)(seq)
if ($weapon = null | (map_members = ^false & oc_members($weapon) = true)) {
    return(human_unarmedblock);
}
return(oc_param($weapon, defend_anim));