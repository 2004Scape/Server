[queue,poison_player](int $severity)
if (map_members = false) {
    // nothing poisons in f2p
    return;
}
if (%antipoison < map_clock) { 
    if (%poison = 0) {
        mes("You have been poisoned!");
    }
    settimer(poison, 30);
}
%poison = $severity;

[timer,poison]
if (%poison = 0 | %antipoison > map_clock) {
    cleartimer(poison);
    return;
}
~poison_damage(calc((%poison + 4)/5));

%poison = calc(%poison - 1);
settimer(poison, 30);

[debugproc,poison](int $severity)
if_close;
queue(poison_player,0,$severity);

[proc,poison_damage](int $damage)
if_close;
def_synth $sound;
def_int $rand;
// not sure if this damage is supposed to be queued or not
damage(uid, ^hitmark_poison, $damage);
if (gender() = 0) {
    $rand = random(2);
    switch_int($rand) {
        case 0 : $sound = human_hit2;
        case 1 : $sound = human_hit3;
        case 2 : $sound = human_hit4;
        case default : $sound = human_hit2;
    }
} else {
    $rand = random(1);
    switch_int($rand) {
        case 0 : $sound = female_hit;
        case 1 : $sound = female_hit2;
        case default : $sound = female_hit;
    }
}
sound_synth($sound, 0, 20);

if (stat(hitpoints) = 0 & getqueue(player_death) < 1) {
    queue(player_death, 0);
}


[proc,clear_poison]
%poison = 0;
cleartimer(poison);