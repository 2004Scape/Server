[label,pvp_ranged_attack]
if (~duel_arena_ranged_check = false) {
    return;
}
if (.stat(hitpoints) = 0) {
    p_stopaction;
    return;
}
if (%action_delay > map_clock) {
    p_opplayer(2);
    return;
}
// this can be either the quiver or rhand.
def_obj $rhand = inv_getobj(worn, ^wearpos_rhand);
def_obj $ammo = ~player_ranged_check_ammo($rhand);
if ($ammo = null) {
    p_stopaction;
    ~update_all($rhand);
    return;
}

p_opplayer(2);
~set_pk_vars;

def_int $damage = 0;
// not sure if xp multipler exists for us or not?!
// https://oldschool.runescape.wiki/w/Combat#PvP_bonus_experience
if (~pvp_hit_roll(%damagetype) = true) {
    def_int $maxhit = %com_maxhit;
    if (~.check_protect_prayer(^ranged_style) = true) {
        $maxhit = scale(6, 10, $maxhit); // reduction of 40%
    }
    $damage = min(randominc($maxhit), .stat(hitpoints)); // tick eating existed! https://oldschool.runescape.wiki/w/Update:The_Wintertodt
    ~give_combat_experience_pvp(%damagestyle, $damage, ~pvp_xp_multiplier(~.player_combat_level));
    both_heropoints($damage);
}
def_int $delay = ~pvp_ranged_use_weapon($rhand, $ammo);
anim(%com_attackanim, 0);
sound_synth(%com_attacksound, 0, 0);
.sound_synth(%com_attacksound, 0, 0);

.queue(pvp_retaliate, calc($delay / 30), uid);
~.pvp_damage(calc($delay / 30), $damage);

.anim(.%com_defendanim, $delay);
// .sound_synth(.%com_defendsound, 0, 20);
// sound_synth(.%com_defendsound, 0, 20);

if ($rhand = null) {
    %action_delay = add(map_clock, 4);
} else {
    %action_delay = add(map_clock, oc_param($rhand, attackrate));
    def_int $poison_severity = oc_param($rhand, poison_severity);
    if ($damage > 0 & $poison_severity > 0 & random(4) = 0) { // 1/4 chance to poison
        .queue(poison_player, 0, $poison_severity);
    }
}
if (%damagestyle = ^style_ranged_rapid) {
    %action_delay = sub(%action_delay, 1);
}

~ranged_dropammo_pvp($ammo, 5, sub(divide($delay, 30), 2));


[proc,pvp_ranged_use_weapon](obj $rhand, obj $ammo)(int)
spotanim_pl(oc_param($ammo, proj_launch), 96, 0);
if (~in_duel_arena(coord) = true) {
    inv_moveitem(worn, tradeoffer, $ammo, 1); // give rangers their arrows back from duels
} else {
    inv_del(worn, $ammo, 1);
}
def_int $delay = 30;
switch_category(oc_category($rhand)) {
    case weapon_bow, weapon_crossbow :
        $delay = ~player_projectile(coord, .coord, .uid, oc_param($ammo, proj_travel), 40, 36, 32, 15, 0, 11, 5);
    case weapon_thrown, weapon_javelin :
        if (inv_total(worn, $ammo) = 0) {
            mes("That was your last one!");
            ~update_all($rhand);
        }
        $delay = ~player_projectile(coord, .coord, .uid, oc_param($ammo, proj_travel), 40, 36, 32, 15, 0, 11, 5);
}
return($delay);

[proc,ranged_dropammo_pvp](obj $ammo, int $chance, int $delay)
if (~in_duel_arena(coord) = true) {
    if (random($chance) = 0) {
        inv_del(tradeoffer, $ammo, 1);
    }
    return;
}
if (random($chance) ! 0) {
    world_delay($delay);
    if (map_blocked(.coord) = false) {
        obj_add(.coord, enum(obj, namedobj, ranged_ammo, $ammo), 1, 200);
    }
}