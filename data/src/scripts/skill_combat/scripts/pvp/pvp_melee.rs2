[label,pvp_melee_attack]
if (~duel_arena_melee_check = false) {
    return;
}
if (%action_delay > map_clock) {
    p_opplayer(2); // it is guaranteed here that we are already within op distance
    return;
}

if (.stat(hitpoints) = 0) {
    p_stopaction;
    return;
}

p_opplayer(2);
~set_pk_vars;

~player_combat_stat;
~.player_combat_stat;

def_int $damage = 0;
// not sure if xp multipler exists for us or not?!
// https://oldschool.runescape.wiki/w/Combat#PvP_bonus_experience
if (~pvp_hit_roll(%damagetype) = true) {
    def_int $maxhit = %com_maxhit;
    if (~.check_protect_prayer(^melee_style) = true) {
        $maxhit = scale(6, 10, $maxhit); // reduction of 40%
    }
    $damage = min(randominc($maxhit), .stat(hitpoints)); // tick eating existed! https://oldschool.runescape.wiki/w/Update:The_Wintertodt
    ~give_combat_experience_pvp(%damagestyle, $damage, ~pvp_xp_multiplier(~.player_combat_level));
}
.if_close;
anim(%com_attackanim, 0);
sound_synth(%com_attacksound, 0, 0);
.sound_synth(.%com_attacksound, 0, 0);

queue2(pvp_retaliate, 0, uid);
queue2(pvp_damage, 0, $damage);

both_heropoints($damage);

.anim(.%com_defendanim, 20);
// .sound_synth(.%com_defendsound, 0, 20);
// sound_synth(.%com_defendsound, 0, 20);

def_obj $weapon = inv_getobj(worn, ^wearpos_rhand);
if ($weapon = null) {
    %action_delay = add(map_clock, 4);
} else {
    %action_delay = add(map_clock, oc_param($weapon, attackrate));
    def_int $poison_severity = oc_param($weapon, poison_severity);
    if ($damage > 0 & $poison_severity > 0 & random(4) = 0) { // 1/4 chance to poison
        queue2(poison_player, 0, $poison_severity);
    }
}