[opplayert,magic:flames_of_zamorak] ~pvp_flames_of_zamorak;
[applayert,magic:flames_of_zamorak] ~pvp_flames_of_zamorak;

[proc,pvp_flames_of_zamorak]
// look for spell in db
def_dbrow $spell_data = ~get_spell_data(^flames_of_zamorak);
if (~pvp_combat_spell_checks($spell_data) = false) {
    return;
}
if (%flames_of_zamorak_casts < ^mage_arena_spell_finished & ~inzone_coord_pair_table(mage_arena, coord) = false) {
    // https://youtu.be/TrxuRyogdEo?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o&t=60
    mes("You are not yet experienced enough to use this spell outside the");
    mes("Mage Arena.");
    return;
}

~set_pk_vars;
~pvp_spell_cast($spell_data);
// spell hit
if (~pvp_hit_roll(^magic_style) = true) {
    def_int $maxhit = ~magic_spell_maxhit($spell_data);
    if (inv_total(worn, zamorak_cape) > 0 & inv_total(worn, zamorak_staff) > 0) {
        if (.stat(magic) = .stat_base(magic)) { // https://oldschool.runescape.wiki/w/Update:Contact!
            .stat_drain(magic, 1, 5);
            .mes("You feel your magical powers weaken.");
        }
        if (%charge_god_spell > 0) {
            $maxhit = scale(3, 2, $maxhit);
        }
    }
    ~pvp_spell_success($spell_data, $maxhit, 64);
} else {
    ~pvp_spell_fail($spell_data, 64);
}