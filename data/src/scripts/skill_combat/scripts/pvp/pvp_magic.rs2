[label,pvp_magic_attack]
def_dbrow $spell_data = ~get_spell_data(%autocast_spell);
if (~check_spell_requirements($spell_data) = false) {
    if (%autocast_spell = db_getfield($spell_data, magic_spell_table:spell, 0)) { // matches osrs behavior
        %attackstyle_magic = 0;
    }
    return;
}
p_opplayert(db_getfield($spell_data, magic_spell_table:spellcom, 0));

[applayert,magic:wind_strike] ~pvp_default_spell(^wind_strike);
[applayert,magic:water_strike] ~pvp_default_spell(^water_strike);
[applayert,magic:earth_strike] ~pvp_default_spell(^earth_strike);
[applayert,magic:fire_strike] ~pvp_default_spell(^fire_strike);
[applayert,magic:wind_bolt] ~pvp_default_spell(^wind_bolt);
[applayert,magic:water_bolt] ~pvp_default_spell(^water_bolt);
[applayert,magic:earth_bolt] ~pvp_default_spell(^earth_bolt);
[applayert,magic:fire_bolt] ~pvp_default_spell(^fire_bolt);
[applayert,magic:wind_blast] ~pvp_default_spell(^wind_blast);
[applayert,magic:water_blast] ~pvp_default_spell(^water_blast);
[applayert,magic:earth_blast] ~pvp_default_spell(^earth_blast);
[applayert,magic:fire_blast] ~pvp_default_spell(^fire_blast);
[applayert,magic:wind_wave] ~pvp_default_spell(^wind_wave);
[applayert,magic:water_wave] ~pvp_default_spell(^water_wave);
[applayert,magic:earth_wave] ~pvp_default_spell(^earth_wave);
[applayert,magic:fire_wave] ~pvp_default_spell(^fire_wave);
[applayert,magic:curse] ~pvp_default_spell(^curse);
[applayert,magic:weaken] ~pvp_default_spell(^weaken);
[applayert,magic:confuse] ~pvp_default_spell(^confuse);
[applayert,magic:bind] ~pvp_default_spell(^bind);
[applayert,magic:snare] ~pvp_default_spell(^snare);
[applayert,magic:entangle] ~pvp_default_spell(^entangle);
[applayert,magic:vulnerability] ~pvp_default_spell(^vulnerability);
[applayert,magic:enfeeble] ~pvp_default_spell(^enfeeble);
[applayert,magic:stun] ~pvp_default_spell(^stun);
[applayert,magic:crumble_undead] ~pvp_default_spell(^crumble_undead);

[proc,pvp_default_spell](int $spell)
def_dbrow $spell_data = ~get_spell_data($spell);
if (~pvp_combat_spell_checks($spell_data) = false) {
    return;
}
if (~pvp_freeze_allowed($spell_data) = false) {
    return;
}
if (~pvp_debuff_allowed($spell_data) = false) {
    return;
}

def_int $duration = ~pvp_spell_cast($spell_data);

// spell hit
if (~pvp_hit_roll(^magic_style) = true) {
    ~pvp_spell_success($spell_data, ~magic_spell_maxhit($spell_data), $duration);
    ~pvp_stat_change_effect($spell_data);
    ~pvp_freeze_effect($spell_data);
} else {
    ~pvp_spell_fail($spell_data, $duration);
}

[proc,pvp_combat_spell_checks](dbrow $spell_data)(boolean)
if_close;
p_stopaction;
if (.stat(hitpoints) = 0) {
    return(false);
}
if (map_clock < %action_delay) {
    p_opplayert(db_getfield($spell_data, magic_spell_table:spellcom, 0));
    return(false);
}
if (~check_spell_requirements($spell_data) = false) {
    if (%autocast_spell = db_getfield($spell_data, magic_spell_table:spell, 0)) { // matches osrs behavior
        %attackstyle_magic = 0;
    }
    return(false);
}
// these go after rune check
if (~pvp_is_attackable_opt = false) {
    return(false);
}
return(true);


[proc,pvp_freeze_allowed](dbrow $spell_data)(boolean)
// check if player is already frozen
if (db_getfield($spell_data, magic_spell_table:freeze_time, 0) > 0) {
    if (.%frozen > map_clock) {
        mes("Your target is already held by a magical force.");
        return(false);
    }
    // immune message existed in 2005 https://oldschool.runescape.wiki/w/Update:Magic_and_Wilderness_Updates
    if (calc(.%frozen + 5) > map_clock) {
        // osrs wiki trivia says that snare didnt have this? No proof though
        // ive found proof of bind and entangle having this in 2006: https://imgur.com/a/jBb1awE
        mes("Your target is currently immune to that spell.");
        return(false);
    }
}
return(true);


[proc,pvp_debuff_allowed](dbrow $spell_data)(boolean)
// check if player can be stat reduced
def_stat $stat;
def_int $constant;
def_int $percent;
def_int $i = 0;
def_int $spell = db_getfield($spell_data, magic_spell_table:spell, 0);
while ($i < db_getfieldcount($spell_data, magic_spell_table:stat_change)) {
    $stat, $constant, $percent = db_getfield($spell_data, magic_spell_table:stat_change, $i);
    if (.stat($stat) < .stat_base($stat)) {
        // in osrs, confuse and weaken spell have a different message
        if (($stat = attack & $spell = ^confuse)) {
            // yes theres an extra period. only for pvp
            mes("Your foe's <lowercase(~stat_name($stat))> has already been lowered..");
        } else if ($stat = strength & $spell = ^weaken) {
            mes("Your foe's <lowercase(~stat_name($stat))> has already been lowered.");
        } else {
            mes("Your foe's <lowercase(~stat_name($stat))> has already been weakened.");
        }
        p_stopaction;
        return(false);
    }
    $i = add($i, 1);
}
return(true);

[proc,pvp_stat_change_effect](dbrow $spell_data)
def_stat $stat;
def_int $constant;
def_int $percent;
def_int $i = 0;
while ($i < db_getfieldcount($spell_data, magic_spell_table:stat_change)) {
    $stat, $constant, $percent = db_getfield($spell_data, magic_spell_table:stat_change, $i);
    if (.stat($stat) >= .stat_base($stat)) {
        if ($constant > 0 & $percent > 0) {
            // if both positive assume statadd
            // nothing ever uses this but i guess if someone wants to add some heal spell or something
            .stat_add($stat, $constant, $percent);
        } else {
            if ($constant < 0) $constant = multiply($constant, -1);
            if ($percent < 0) $percent = multiply($percent, -1);
            .mes("You feel weakened.");
            .stat_sub($stat, $constant, $percent);
        }
    }
    $i = add($i, 1);
}

[proc,pvp_freeze_effect](dbrow $spell_data)
if (~high_priority_walktrigger_exists = true) {
    return;
}
def_int $freeze_time = db_getfield($spell_data, magic_spell_table:freeze_time, 0);
if ($freeze_time > 0) {
    if (~.check_protect_prayer(^magic_style) = true) {
        $freeze_time = divide($freeze_time, 2); // 50% reduction https://oldschool.runescape.wiki/w/Update:RS2_bugfixes_-_part_4
    }
    if (.p_finduid(.uid) = true) {
        .p_walk(.coord);
    }
    .%pk_binder = uid;
    .queue(pvp_freeze_player, 0, $freeze_time);
}

[proc,pvp_spell_success](dbrow $spell_data, int $maxhit, int $duration)
def_obj $weapon = inv_getobj(worn, ^wearpos_rhand);
def_int $damagestyle = ^style_magic_normal;
if (%damagestyle = ^style_melee_defensive) { // check varp directly
    $damagestyle = ^style_magic_defensive; // casting a spell with defensive on gives mage + def xp
}
def_synth $sound = db_getfield($spell_data, magic_spell_table:sound_success, 0);
sound_synth($sound, 0, 0);
.sound_synth($sound, 0, 0);

if ($maxhit = null) {
    ~.pvp_damage(calc($duration / 30 + 1), null); // delayed an extra tick: https://youtu.be/eEdE0mvglM4?t=62
    return;
}

if (~.check_protect_prayer(^magic_style) = true) {
    $maxhit = scale(6, 10, $maxhit); // reduction of 40%
}
def_int $damage = randominc($maxhit); // tick eating existed! https://oldschool.runescape.wiki/w/Update:The_Wintertodt
$damage = min($damage, .stat(hitpoints));
~.pvp_damage(calc($duration / 30 + 1), $damage); // delayed an extra tick: https://youtu.be/eEdE0mvglM4?t=62
// didnt have a flinch anim until sept 20 2006: https://oldschool.runescape.wiki/w/Update:Slug_Menace
~give_combat_experience_pvp($damagestyle, $damage, ~pvp_xp_multiplier(~.player_combat_level));
both_heropoints($damage);

.spotanim_pl(db_getfield($spell_data, magic_spell_table:spotanim_target, 0), $duration);
.queue(pvp_retaliate, calc($duration / 30 + 1), uid);

[proc,pvp_spell_fail](dbrow $spell_data, int $duration)
// splash
def_synth $sound;
if (db_getfieldcount($spell_data, magic_spell_table:sound_fail) > 0) {
    $sound = db_getfield($spell_data, magic_spell_table:sound_fail, 0);
} else {
    // reduce stat, and freeze spells dont have fail sounds, so i assume they just play their normal sound
    // every time?
    $sound = db_getfield($spell_data, magic_spell_table:sound_success, 0);
}

sound_synth($sound, 0, 0);
.sound_synth($sound, 0, 0);
// no splash sound?
.spotanim_pl(failedspell_impact, 92, $duration);
.queue(pvp_retaliate, 0, uid);

[proc,pvp_spell_cast](dbrow $spell_data)(int)
~set_pk_vars;
.if_close;
~delete_spell_runes($spell_data);
~give_spell_xp($spell_data);

// spell anim
anim(db_getfield($spell_data, magic_spell_table:anim, 0), 0);
// player spell visual effect
if (db_getfieldcount($spell_data, magic_spell_table:spotanim_origin) > 0) {
    spotanim_pl(db_getfield($spell_data, magic_spell_table:spotanim_origin, 0), 92, 0);
}
// shoot spell projectile
%action_delay = add(map_clock, 5);
def_int $duration = 0;
if (db_getfieldcount($spell_data, magic_spell_table:spotanim_proj) > 0) {
    $duration = ~player_projectile(coord, .coord, .uid, db_getfield($spell_data, magic_spell_table:spotanim_proj, 0));
}

// continue by autocast
if (~player_autocast_enabled = true & db_getfield($spell_data, magic_spell_table:continue_by_autocast, 0) = true) {
    p_opplayert(db_getfield(~get_spell_data(%autocast_spell), magic_spell_table:spellcom, 0));
}
return($duration);

[proc,magic_spell_maxhit](dbrow $spell_data)(int)
def_int $maxhit = db_getfield($spell_data, magic_spell_table:maxhit, 0);
def_int $spell = db_getfield($spell_data, magic_spell_table:spell, 0);
if (inv_total(worn, gauntlets_of_chaos) > 0 & $spell >= ^wind_bolt & $spell <= ^fire_bolt) {
    $maxhit = add($maxhit, 3);
}
return($maxhit);

[queue,pvp_freeze_player](int $duration)
mes("You've been frozen!");
%frozen = calc(map_clock + $duration);
walktrigger(pvp_frozen);


[walktrigger,pvp_frozen]
if (.finduid(%pk_binder) = true) {
    if (distance(coord, .coord) > 10) { // https://youtu.be/SC4KF1JWVl8&t=76
        return;
    }
    if (map_clock < %frozen) {
        mes("A magical force stops you from moving.");
        p_walk(coord);
        walktrigger(pvp_frozen);
    }
}
