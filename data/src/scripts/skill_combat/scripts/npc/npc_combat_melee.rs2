// returns the calculated accuracy for this npc
[proc,npc_melee_accuracy]()(int)
if (~check_protect_prayer(^melee) = true) {
    return(0);
}
def_int $attack_level = npc_stat(attack);
def_int $attackbonus = npc_param(attackbonus);
//npcs are essentially always on controlled
return(~combat_melee_accuracy(~combat_stat_attack(1, $attack_level, 0), $attackbonus));

// returns the calculated max hit for this npc
[proc,npc_melee_maxhit]()(int)
def_int $strength_level = npc_stat(strength);
def_int $strengthbonus = npc_param(strengthbonus);
//npcs are essentially always on controlled
return(~combat_melee_maximum_hit(~combat_stat_strength(1, $strength_level, 0), $strengthbonus));

// returns the defend sound for this npc
[proc,npc_combat_defend_sound]()(synth)
if (npc_param(gender) = ^gender_female) {
    return(female_hit);
}
return(npc_param(defend_sound));

// returns the death sound for this npc
[proc,npc_combat_death_sound]()(synth)
if (npc_param(gender) = ^gender_female) {
    return(female_death);
}
return(npc_param(death_sound));

// returns the defence roll for an attacking style.
[proc,npc_effective_defence](int $style)(int)
def_int $defence_level = npc_stat(defence);
def_int $defencebonus = ~npc_combat_defencebonus($style);
return(~combat_stat_defence(1, $defence_level, $defencebonus));

// get the npc bonus depending on the attacking style
[proc,npc_combat_defencebonus](int $style)(int)
switch_int ($style) {
    case ^stab: return(npc_param(stabdefence));
    case ^slash: return(npc_param(slashdefence));
    case ^crush: return(npc_param(crushdefence));
    case ^ranged: return(npc_param(rangedefence));
    case ^magic: return(npc_param(magicdefence));
}
error("style of <tostring($style)> not defined in switch for npc_combat_defencebonus");
