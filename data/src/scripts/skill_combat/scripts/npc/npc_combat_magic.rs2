[proc,npc_magic_attack_roll]()(int)
if (~check_protect_prayer(^magic_style) = true) {
    return(0);
}
def_int $magicattack = npc_param(magicattack);
def_int $effective_magic = ~combat_effective_stat(1, npc_stat(magic), 100); // no prayerbonus
return(~combat_stat($effective_magic, $magicattack));

[proc,npc_magic_attack_roll_no_prot]()(int)
def_int $magicattack = npc_param(magicattack);
def_int $effective_magic = ~combat_effective_stat(1, npc_stat(magic), 100); // no prayerbonus
return(~combat_stat($effective_magic, $magicattack));

[walktrigger,frozen]
if (map_clock < %stunned) {
    mes("A magical force stops you from moving.");
    p_stopaction;
    walktrigger(frozen);
}

[proc,npc_cast_spell](int $spell, int $attackspeed)
~npc_cast_spell_with_forced_max_hit($spell,$attackspeed,null);

[proc,npc_cast_spell_with_forced_max_hit](int $spell, int $attackspeed, int $defined_max_hit)
if (%npc_action_delay > map_clock) {
    npc_setmode(applayer2);
    return;
}
if (~npc_can_attack_player = false) {
    npc_setmode(null);
    return;
}
if_close;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);

// spell anim
if (npc_param(magicattack_anim) = null) {
    npc_anim(db_getfield($spell_data, magic_spell_table:anim, 0), 0);
} else {
    npc_anim(npc_param(magicattack_anim), 0);
}
// origin spell visual effect
if (db_getfieldcount($spell_data, magic_spell_table:spotanim_origin) > 0) {
    spotanim_npc(db_getfield($spell_data, magic_spell_table:spotanim_origin, 0), 92, 0);
}
// shoot spell projectile
%npc_action_delay = add(map_clock, 5);
def_int $duration = 64;
if (db_getfieldcount($spell_data, magic_spell_table:spotanim_proj) > 0) {
    $duration = ~player_projectile(npc_coord, coord, uid, db_getfield($spell_data, magic_spell_table:spotanim_proj, 0), 51, 16, -5, 64, 10);
}
def_int $maxhit;
if($defined_max_hit ! null) {
    $maxhit = $defined_max_hit;
} else {
    $maxhit = db_getfield($spell_data, magic_spell_table:maxhit, 0);
}

if (~npc_player_hit_roll(^magic_style) = true) {
    if_close; // close the player interface when taking a hit.
    // play spell success sound
    sound_synth(db_getfield($spell_data, magic_spell_table:sound_success, 0), 0, 0);
    // roll 0-maxhit
    def_int $damage = randominc($maxhit);
    // if maxhit is defined then damage npc
    if ($maxhit ! null) {
        // damage player, isnt delayed an extra tick like using spells on npc (osrs)
        if (getqueue(player_death) < 1) {
            queue(damage_player, calc($duration / 30), $damage);
            if (npc_param(poison_severity) > 0 & $damage > 0) {
                queue(poison_player, 0, npc_param(poison_severity));
            }
        }
        // player anim for flinching doesnt exist, even in osrs
        // anim(~combat_defend_anim(inv_getobj(worn, ^wearpos_rhand)), $duration);
    }
    // spell visual
    spotanim_pl(db_getfield($spell_data, magic_spell_table:spotanim_target, 0), $duration);
    // player auto retal
    if (getqueue(player_death) < 1) {
        queue(playerhit_n_retaliate, calc($duration / 30), npc_uid);
    }

    // other effects:
    // reduce stat
    def_int $i = 0;
    def_stat $stat;
    def_int $constant;
    def_int $percent;
    while ($i < db_getfieldcount($spell_data, magic_spell_table:stat_change)) {
        $stat, $constant, $percent = db_getfield($spell_data, magic_spell_table:stat_change, $i);
        if ($constant > 0 & $percent > 0) {
            // if both positive assume statadd
            // nothing ever uses this but i guess if someone wants to add some heal spell or something
            //mes("Increased stat: <~stat_name($stat)>, <tostring($constant)>, <tostring($percent)>");
            stat_add($stat, $constant, $percent);
        } else {
            if ($constant < 0) $constant = multiply($constant, -1);
            if ($percent < 0) $percent = multiply($percent, -1);
            //mes("Reduced stat: <~stat_name($stat)>, <tostring($constant)>, <tostring($percent)>");
            stat_sub($stat, $constant, $percent);
            mes("You feel weakened.");
        }
        $i = add($i, 1);
    }
    // freeze player if spell has freeze effect
    if (db_getfield($spell_data, magic_spell_table:freeze_time, 0) > 0) {
        queue(freeze_player, 0, db_getfield($spell_data, magic_spell_table:freeze_time, 0));
    }
} else {
    if_close; // close the player interface when taking a hit.
    // splash
    if (db_getfieldcount($spell_data, magic_spell_table:sound_fail) > 0) {
        sound_synth(db_getfield($spell_data, magic_spell_table:sound_fail, 0), 0, 0);
    } else {
        // reduce stat, and freeze spells dont have fail sounds, so i assume they just play their normal sound
        // every time?
        sound_synth(db_getfield($spell_data, magic_spell_table:sound_success, 0), 0, 0);
    }
    // splash sound would play here, doesnt exist in this rev?
    // sound_synth(, 0, $duration);
    spotanim_pl(failedspell_impact, 92, $duration);
    // splashing in osrs causes insta auto retal
    queue(playerhit_n_retaliate, 0, npc_uid);
}
~npc_set_attack_vars;


// todo: dunno if this should be queued but protected access lol
[queue,freeze_player](int $duration)
mes("You've been frozen!");
%stunned = calc(map_clock + $duration + 1);
walktrigger(frozen);
