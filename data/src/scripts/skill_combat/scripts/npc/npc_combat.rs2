[ai_queue1,_](int $arg) gosub(npc_default_retaliate);
[ai_queue2,_](int $damage) ~npc_default_damage($damage);
[ai_queue3,_](int $arg) gosub(npc_default_death);
[ai_opplayer2,_] gosub(npc_default_attack);

// a default melee retaliate script.
[proc,npc_default_retaliate]
if (finduid(%npc_attacking_uid) = false) {
    return;
}
// npc flinch
if (~npc_out_of_combat = true) {
    %npc_action_delay = add(map_clock, divide(npc_param(attackrate), 2));
}
npc_setmode(opplayer2);

// a default melee attack script.
[proc,npc_default_attack]
if (stat(hitpoints) = 0) {
    return;
}
if (%npc_action_delay > map_clock) return;
anim(%com_defendanim, 0);

npc_anim(npc_param(attack_anim), 0);
sound_synth(npc_param(attack_sound), 0, 0);
~npc_meleeattack;

// a default damage script.
[proc,npc_default_damage](int $damage)
// if the npc is already dead
if (npc_stat(hitpoints) = 0) {
    return;
}
// damage the npc
if ($damage = 0) {
    npc_damage(^hitmark_block, 0);
} else {
    npc_damage(^hitmark_damage, $damage);
}
if (npc_stat(hitpoints) > 0) {
    return;
}
// if the npc died from this damage
npc_setmode(none);
npc_queue(3, 0, 0);

// a default death script.
[proc,npc_default_death]
gosub(npc_death);
def_obj $drop = npc_param(death_drop);
if ($drop ! null & npc_findhero = true) {
    obj_add(npc_coord, $drop, 1, 200);
}

[proc,npc_player_hit_roll](int $damagetype)(boolean)
def_int $attack_roll = ~npc_attack_roll_specific($damagetype);
def_int $defence_roll = ~player_defence_roll_specific($damagetype);
//mes("Clock: <tostring(map_clock)>, Player A: <tostring($attack_roll)>, NPC D: <tostring($defence_roll)>, Player Max: <tostring(%com_maxhit)>");
if (randominc($attack_roll) > randominc($defence_roll)) {
    return(true);
}
return(false);

[proc,npc_is_attackable](npc $npc)(boolean)
// so this way is more authentic, allows any npc with op2 defined to be attacked
// https://web.archive.org/web/20060828222012/http://www.trillionareguild.com/runescape/dragon.php at the very end of the guide
// it says "You have no reason to do that.", so its prob this osrs message.
// every guide says you have to kill wormbrain, and monster db's say he drops bones. so theres no
// beating up message most likely.
if (npc_type = wormbrain & (~quest_dragon_getting_map_parts = false | inv_total(inv, crandor_map_part2) > 0 | %dragon_goblin ! ^quest_dragon_knows_about_goblin)) {
    mes("The goblin is already in prison. You have no reason to attack him.");
    // if (%dragon_progress < 3) {
    //     mes("The goblin is already in prison. You have no reason to attack him.");
    // } else {
    //     mes("You have already taken Wormbrain's map piece. There is no use in beating him up"); //osrs
    //     mes("further.");
    // }
    return (false);
}
if (string_length(nc_op($npc, 2)) > 0) {
    return (true);
}
// this is a better way to do it but over the years osrs has had bugs where you could attack npcs that just had an op2 defined
// if (compare(nc_op($npc, 2), "Attack") = ^true) {
//     return (true);
// }
return (false);

[proc,npc_is_attackable_message](npc $npc)(boolean)
if (~npc_is_attackable($npc) = false) {
    mes("You can't attack this npc.");
    return (false);
}
return (true);

[proc,npc_out_of_combat]()(boolean)
if (add(%npc_action_delay, 8) < map_clock) {
    return (true);
}
return (false);

// returns the defence roll for an attacking style.
[proc,npc_defence_roll_specific](int $damagetype)(int)
def_npc_stat $stat = defence;
if ($damagetype = ^magic_style) {
    $stat = magic;
}
def_int $defencebonus = ~npc_combat_defencebonus($damagetype);
def_int $effective_defence = ~combat_effective_stat(1, npc_stat($stat), 100); // no prayerbonus
def_int $defence_roll = ~combat_stat($effective_defence, $defencebonus);
return($defence_roll);

[proc,npc_attack_roll_specific](int $damagetype)(int)
switch_int ($damagetype) {
    case ^stab_style, ^slash_style, ^crush_style: return(~npc_melee_attack_roll);
    case ^ranged_style: return(~npc_ranged_attack_roll);
    case ^magic_style: return(~npc_magic_attack_roll);
}
return(0);

// get the npc bonus depending on the attacking style
[proc,npc_combat_defencebonus](int $style)(int)
switch_int ($style) {
    case ^stab_style: return(npc_param(stabdefence));
    case ^slash_style: return(npc_param(slashdefence));
    case ^crush_style: return(npc_param(crushdefence));
    case ^ranged_style: return(npc_param(rangedefence));
    case ^magic_style: return(npc_param(magicdefence));
}
error("style of <tostring($style)> not defined in switch for npc_combat_defencebonus");

//  this should be strong queued. Some damage is queued, some isnt
[queue,damage_player](int $damage)
~damage_self($damage);

[proc,npc_retaliate](int $queue_delay)
npc_queue(1, 0, $queue_delay);
%npc_attacking_uid = uid;
%npc_lastcombat = map_clock;
