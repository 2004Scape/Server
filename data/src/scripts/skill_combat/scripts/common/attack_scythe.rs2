// reap -> accurate/slash
// chop -> aggressive/stab
// jab -> aggressive/crush
// block -> defensive/slash

[proc,attack_scythe_swing](obj $obj, int $style)(seq, synth)
switch_int ($style) {
    case ^style_scythe_reap, ^style_scythe_chop, ^style_scythe_block:
        return(oc_param($obj, slashattack_anim), oc_param($obj, slash_sound)); // TODO
    case ^style_scythe_jab:
        return(oc_param($obj, crushattack_anim), oc_param($obj, crush_sound)); // TODO
}
error("style of <tostring($style)> not defined in switch for attack_scythe_swing");

[proc,attack_scythe_strengthbonus](int $style)(int)
switch_int ($style) {
    case ^style_scythe_reap, ^style_scythe_block:
        return(0);
    case ^style_scythe_chop, ^style_scythe_jab:
        return(3);
}
error("style of <tostring($style)> not defined in switch for attack_scythe_strengthbonus");

[proc,attack_scythe_accuracybonus](int $style)(int)
switch_int ($style) {
    case ^style_scythe_reap:
        return(3);
    case ^style_scythe_chop, ^style_scythe_jab, ^style_scythe_block:
        return(0);
}
error("style of <tostring($style)> not defined in switch for attack_scythe_accuracybonus");

[proc,attack_scythe_style](int $style)(int)
switch_int ($style) {
    case ^style_scythe_reap, ^style_scythe_block:
        return(^slash);
    case ^style_scythe_chop:
        return(^stab);
    case ^style_scythe_jab:
        return(^crush);
}
error("style of <tostring($style)> not defined in switch for attack_scythe_style");
