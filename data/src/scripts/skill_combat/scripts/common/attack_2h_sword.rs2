// chop -> accurate/slash
// slash -> aggressive/slash
// smash -> aggressive/crush
// block -> defensive/slash

[proc,attack_2h_sword_swing](obj $obj, int $style)(seq, synth)
switch_int ($style) {
    case ^style_2h_sword_chop, ^style_2h_sword_slash, ^style_2h_sword_block:
        return(oc_param($obj, slashattack_anim), oc_param($obj, slash_sound));
    case ^style_2h_sword_smash:
        return(oc_param($obj, crushattack_anim), oc_param($obj, crush_sound));
}
error("style of <tostring($style)> not defined in switch for attack_2h_sword_swing");

[proc,attack_2h_sword_strengthbonus](int $style)(int)
switch_int ($style) {
    case ^style_2h_sword_chop, ^style_2h_sword_block:
        return(0);
    case ^style_2h_sword_slash, ^style_2h_sword_smash:
        return(3);
}
error("style of <tostring($style)> not defined in switch for attack_2h_sword_strengthbonus");

[proc,attack_2h_sword_accuracybonus](int $style)(int)
switch_int ($style) {
    case ^style_2h_sword_chop:
        return(3);
    case ^style_2h_sword_slash, ^style_2h_sword_smash, ^style_2h_sword_block:
        return(0);
}
error("style of <tostring($style)> not defined in switch for attack_2h_sword_accuracybonus");

[proc,attack_2h_sword_style](int $style)(int)
switch_int ($style) {
    case ^style_2h_sword_chop, ^style_2h_sword_slash, ^style_2h_sword_block:
        return(^slash);
    case ^style_2h_sword_smash:
        return(^crush);
}
error("style of <tostring($style)> not defined in switch for attack_2h_sword_style");
