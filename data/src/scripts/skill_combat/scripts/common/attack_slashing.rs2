// chop -> accurate/slash
// slash -> aggressive/slash
// lunge -> controlled/stab
// block -> defensive/slash

[proc,attack_slashing_swing](obj $obj, int $style)(seq, synth)
switch_int ($style) {
    case ^style_slashing_chop, ^style_slashing_slash, ^style_slashing_block:
        return(oc_param($obj, slashattack_anim), oc_param($obj, slash_sound));
    case ^style_slashing_lunge:
        return(oc_param($obj, stabattack_anim), oc_param($obj, stab_sound));
}
error("style of <tostring($style)> not defined in switch for attack_slashing_swing");

[proc,attack_slashing_strengthbonus](int $style)(int)
switch_int ($style) {
    case ^style_slashing_chop, ^style_slashing_block:
        return(0);
    case ^style_slashing_lunge:
        return(1);
    case ^style_slashing_slash:
        return(3);
}
error("style of <tostring($style)> not defined in switch for attack_slashing_strengthbonus");

[proc,attack_slashing_accuracybonus](int $style)(int)
switch_int ($style) {
    case ^style_slashing_chop:
        return(3);
    case ^style_slashing_lunge:
        return(1);
    case ^style_slashing_slash, ^style_slashing_block:
        return(0);
}
error("style of <tostring($style)> not defined in switch for attack_slashing_accuracybonus");

[proc,attack_slashing_style](int $style)(int)
switch_int ($style) {
    case ^style_slashing_chop, ^style_slashing_slash, ^style_slashing_block:
        return(^slash);
    case ^style_slashing_lunge:
        return(^stab);
}
error("style of <tostring($style)> not defined in switch for attack_slashing_style");
