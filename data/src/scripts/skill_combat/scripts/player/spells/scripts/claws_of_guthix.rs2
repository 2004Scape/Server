[opnpct,magic:claws_of_guthix] ~pvm_claws_of_guthix;
[apnpct,magic:claws_of_guthix] ~pvm_claws_of_guthix;

[proc,pvm_claws_of_guthix]
def_dbrow $spell_data = ~get_spell_data(^claws_of_guthix);
if (~pvm_combat_spell_checks($spell_data) = false) {
    return;
}
if (%claws_of_guthix_casts < ^mage_arena_spell_finished & ~inzone_coord_pair_table(mage_arena, coord) = false) {
    // https://youtu.be/TrxuRyogdEo?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o&t=60
    mes("You are not yet experienced enough to use this spell outside the");
    mes("Mage Arena.");
    return;
}
~pvm_spell_cast($spell_data);

if (~player_npc_hit_roll(^magic_style) = true) {
    def_int $maxhit = ~magic_spell_maxhit($spell_data);
    if (inv_total(worn, guthix_cape) > 0 & inv_total(worn, guthix_staff) > 0) {
        if (npc_stat(defence) = npc_basestat(defence)) { // https://oldschool.runescape.wiki/w/Update:Contact!
            npc_statsub(defence, 1, 5); // npc_statdrain?
        }
        if (%charge_god_spell > 0) {
            $maxhit = scale(3, 2, $maxhit);
        }
    }
    ~pvm_spell_success($spell_data, $maxhit, 64);
} else {
    ~pvm_spell_fail($spell_data, 64);
}
if (%claws_of_guthix_casts = ^mage_arena_spell_last_cast) {
    mes("You can now cast Claws of Guthix outside the Arena.");
    %claws_of_guthix_casts = ^mage_arena_spell_finished;
    ~mesbox("Congratulations. Your ability to summon the Claws of Guthix has|improved. You may now use this spell outside of the Arena.");
} else if (%claws_of_guthix_casts < ^mage_arena_spell_last_cast) {
    %claws_of_guthix_casts = add(%claws_of_guthix_casts, 1);
}