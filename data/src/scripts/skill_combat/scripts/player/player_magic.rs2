// to add a new combat spell:
// create a constant (^spellname) in ../configs/magic/spells.constant
// add it to the spell table in ../configs/magic/magic_combat_spells.dbrow
// create an opnpct and apnpct trigger for it in this script

[opnpct,magic:wind_strike] @player_manual_spell(^wind_strike);
[opnpct,magic:water_strike] @player_manual_spell(^water_strike);
[opnpct,magic:earth_strike] @player_manual_spell(^earth_strike);
[opnpct,magic:fire_strike] @player_manual_spell(^fire_strike);
[opnpct,magic:wind_bolt] @player_manual_spell(^wind_bolt);
[opnpct,magic:water_bolt] @player_manual_spell(^water_bolt);
[opnpct,magic:earth_bolt] @player_manual_spell(^earth_bolt);
[opnpct,magic:fire_bolt] @player_manual_spell(^fire_bolt);
[opnpct,magic:wind_blast] @player_manual_spell(^wind_blast);
[opnpct,magic:water_blast] @player_manual_spell(^water_blast);
[opnpct,magic:earth_blast] @player_manual_spell(^earth_blast);
[opnpct,magic:fire_blast] @player_manual_spell(^fire_blast);
[opnpct,magic:wind_wave] @player_manual_spell(^wind_wave);
[opnpct,magic:water_wave] @player_manual_spell(^water_wave);
[opnpct,magic:earth_wave] @player_manual_spell(^earth_wave);
[opnpct,magic:fire_wave] @player_manual_spell(^fire_wave);
[opnpct,magic:curse] @player_manual_spell(^curse);
[opnpct,magic:weaken] @player_manual_spell(^weaken);
[opnpct,magic:confuse] @player_manual_spell(^confuse);
[opnpct,magic:bind] @player_manual_spell(^bind);
[opnpct,magic:snare] @player_manual_spell(^snare);
[opnpct,magic:entangle] @player_manual_spell(^entangle);
[opnpct,magic:vulnerability] @player_manual_spell(^vulnerability);
[opnpct,magic:enfeeble] @player_manual_spell(^enfeeble);
[opnpct,magic:stun] @player_manual_spell(^stun);
[opnpct,magic:crumble_undead] @player_manual_spell(^crumble_undead);

[apnpct,magic:wind_strike] @player_manual_spell(^wind_strike);
[apnpct,magic:water_strike] @player_manual_spell(^water_strike);
[apnpct,magic:earth_strike] @player_manual_spell(^earth_strike);
[apnpct,magic:fire_strike] @player_manual_spell(^fire_strike);
[apnpct,magic:wind_bolt] @player_manual_spell(^wind_bolt);
[apnpct,magic:water_bolt] @player_manual_spell(^water_bolt);
[apnpct,magic:earth_bolt] @player_manual_spell(^earth_bolt);
[apnpct,magic:fire_bolt] @player_manual_spell(^fire_bolt);
[apnpct,magic:wind_blast] @player_manual_spell(^wind_blast);
[apnpct,magic:water_blast] @player_manual_spell(^water_blast);
[apnpct,magic:earth_blast] @player_manual_spell(^earth_blast);
[apnpct,magic:fire_blast] @player_manual_spell(^fire_blast);
[apnpct,magic:wind_wave] @player_manual_spell(^wind_wave);
[apnpct,magic:water_wave] @player_manual_spell(^water_wave);
[apnpct,magic:earth_wave] @player_manual_spell(^earth_wave);
[apnpct,magic:fire_wave] @player_manual_spell(^fire_wave);
[apnpct,magic:curse] @player_manual_spell(^curse);
[apnpct,magic:weaken] @player_manual_spell(^weaken);
[apnpct,magic:confuse] @player_manual_spell(^confuse);
[apnpct,magic:bind] @player_manual_spell(^bind);
[apnpct,magic:snare] @player_manual_spell(^snare);
[apnpct,magic:entangle] @player_manual_spell(^entangle);
[apnpct,magic:vulnerability] @player_manual_spell(^vulnerability);
[apnpct,magic:enfeeble] @player_manual_spell(^enfeeble);
[apnpct,magic:stun] @player_manual_spell(^stun);
[apnpct,magic:crumble_undead] @player_manual_spell(^crumble_undead);

[label,player_manual_spell](int $spell)
if (%action_delay > map_clock) {
    %spell = null;
    return;
}
// later need to add weakqueue behavior when casting spells manually.
// specifically when action_delay is higher than map_clock.
// I believe this behavior didnt exist until weakqueues were added late 2004 
@player_magic_spell_attack($spell);


[label,player_magic_spell_attack](int $spell)
p_stopaction;
if_close;
if (npc_stat(hitpoints) = 0) {
    p_stopaction;
    return; // this means the npc is not avail to fight i.e dead
}
if (%action_delay > map_clock | %autocast_delay > map_clock) {
    %autocast_delay = add(map_clock, 1);
    %spell = $spell;
    //mes("p_opnpc(2)");
    p_opnpc(2);
    return;
}
// reset spell VERY IMPORTANT!
%spell = null;
// look for spell in db
def_dbrow $spell_data = ~get_spell_data($spell);
// define spell runes
def_namedobj $rune1;
def_int $rune_count1;
def_namedobj $rune2;
def_int $rune_count2;
def_namedobj $rune3;
def_int $rune_count3;
// check runes
$rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3 = ~check_spell_requirements($spell_data);
// mes("<tostring(map_clock)>: Cast");
// check if npc is attackable
if (~npc_is_attackable_message(npc_type) = false) {
    p_stopaction;
    return;
}

if (npc_type = delrith) {
    if (%demon_progress < ^demon_complete) {
        if (inv_getobj(worn, 3) ! silverlight) {
           return;
        }
    } else {
        return;
    }
}

// check if crumble undead & npc is undead
if ($spell = ^crumble_undead & npc_param(undead) = ^false) {
    mes("This spell only affects skeletons, zombies, ghosts and shades.");
    p_stopaction;
    return;
}

// check if npc is already frozen
if (db_getfield($spell_data, magic_spell_table:freeze_time, 0) > 0) {
    if (%npc_stunned > map_clock) {
        mes("Your target is already held by a magical force.");
        p_stopaction;
        return;
    }
    // immune message existed in 2005 https://oldschool.runescape.wiki/w/Update:Magic_and_Wilderness_Updates
    if (calc(%npc_stunned + 5) > map_clock) {
        // osrs wiki trivia says that snare didnt have this? No proof though
        // ive found proof of bind and entangle having this in 2006: https://imgur.com/a/jBb1awE
        mes("Your target is currently immume to that spell.");
        p_stopaction;
        return;
    }
}

// check if npc can be stat reduced
def_stat $stat;
def_int $constant;
def_int $percent;
def_int $i = 0;
while ($i < db_getfieldcount($spell_data, magic_spell_table:stat_change)) {
    $stat, $constant, $percent = db_getfield($spell_data, magic_spell_table:stat_change, $i);
    def_npc_stat $npc_stat = ~convert_stat_to_npc_stat($stat);
    if (npc_stat($npc_stat) < npc_basestat($npc_stat)) {
        // in osrs, confuse and weaken spell have a different message
        if (($npc_stat = attack & $spell = ^confuse) | ($npc_stat = strength & $spell = ^weaken)) {
            mes("Your foe's <lowercase(~stat_name($stat))> has already been lowered.");
        } else {
            mes("Your foe's <lowercase(~stat_name($stat))> has already been weakened.");
        }
        p_stopaction;
        return;
    }
    $i = add($i, 1);
}

~player_combat_stat;

~delete_spell_runes($rune1, $rune_count1, $rune2, $rune_count2, $rune3, $rune_count3);
~give_spell_xp($spell_data);
// damage npc if spell has max hit.
// give xp for damage
// if auto cast isnt defined, then just assume they're using a spell straight from the spellbook
def_int $damagestyle = %damagestyle;
// in osrs you can only defensive cast with a staff. Not sure if this is the case in 2004
// Might be confusing for players if they just so happen to have their non-magic weapon on defensive, and end up gaining def xp from manual casting.
// so maybe jagex did it this way?
if ($damagestyle = ^style_melee_defensive & oc_category(inv_getobj(worn, ^wearpos_rhand)) = weapon_staff) {
    $damagestyle = ^style_magic_defensive; // casting a spell with defensive on gives mage + def xp
} else {
    $damagestyle = ^style_magic_normal; // if this script is running they have to be using a spell, so assume magic style
}
// spell anim
anim(db_getfield($spell_data, magic_spell_table:anim, 0), 0);
// player spell visual effect
spotanim_pl(db_getfield($spell_data, magic_spell_table:spotanim_origin, 0), 92, 0);
// shoot spell projectile
%action_delay = add(map_clock, 5);
def_int $duration = 0;
if (db_getfieldcount($spell_data, magic_spell_table:spotanim_proj) > 0) {
    $duration = ~npc_projectile(coord, npc_uid, db_getfield($spell_data, magic_spell_table:spotanim_proj, 0), 51, 16, -5, 64, 10);
}

def_int $maxhit = db_getfield($spell_data, magic_spell_table:maxhit, 0);
def_int $damage = 0;
// if player DOESNT splash:
if (~player_npc_hit_roll(^magic_style) = true) {
    // play spell success sound
    sound_synth(db_getfield($spell_data, magic_spell_table:sound_success, 0), 0, 0);
    // roll 0-maxhit
    $damage = randominc($maxhit);
    // if maxhit is defined then damage npc
    if ($maxhit ! null) {
        // damage npc
        // mes("Duration: <tostring($duration)>, Duration (ticks): <tostring(calc($duration / 30))>");
        npc_queue(2, $damage, calc($duration / 30 + 1)); // delayed an extra tick in osrs
        // npcs didnt have a flinch anim until sept 20 2006. https://youtu.be/UFgMqvI4to8
        // https://oldschool.runescape.wiki/w/Update:Slug_Menace
        // npc_anim(npc_param(defend_anim), $duration); // delay npc this tick
        // npc sound
        sound_synth(npc_param(defend_sound), 0, $duration); // delay 1 client tick for the hit queue
        ~give_combat_experience($damagestyle, min($damage, npc_stat(hitpoints)));
    }
    // spell visual
    spotanim_npc(db_getfield($spell_data, magic_spell_table:spotanim_target, 0), 124, $duration);
    // npc auto retal
    ~npc_retaliate(calc($duration / 30 + 1)); // delayed an extra tick in osrs

    // other effects:
    // reduce stat
    $i = 0;
    while ($i < db_getfieldcount($spell_data, magic_spell_table:stat_change)) {
        $stat, $constant, $percent = db_getfield($spell_data, magic_spell_table:stat_change, $i);
        def_npc_stat $npc_stat = ~convert_stat_to_npc_stat($stat);
        if ($constant > 0 & $percent > 0) {
            // if both positive assume statadd
            // nothing ever uses this but i guess if someone wants to add some heal spell or something
            npc_statadd($npc_stat, $constant, $percent);
        } else {
            if ($constant < 0) $constant = multiply($constant, -1);
            if ($percent < 0) $percent = multiply($percent, -1);
            npc_statsub($npc_stat, $constant, $percent);
        }
        $i = add($i, 1);
    }
    // freeze npc if spell has freeze effect
    if (db_getfield($spell_data, magic_spell_table:freeze_time, 0) > 0) {
        %npc_stunned = calc(map_clock + db_getfield($spell_data, magic_spell_table:freeze_time, 0) + 1);
        npc_setmovecheck(npc_stunned_magic);
    }
} else {
    // splash
    if (db_getfieldcount($spell_data, magic_spell_table:sound_fail) > 0) {
        sound_synth(db_getfield($spell_data, magic_spell_table:sound_fail, 0), 0, 0);
    } else {
        // reduce stat, and freeze spells dont have fail sounds, so i assume they just play their normal sound
        // every time?
        sound_synth(db_getfield($spell_data, magic_spell_table:sound_success, 0), 0, 0);
    }
    // splash sound would play here, doesnt exist in this rev?
    // sound_synth(, 0, $duration);
    spotanim_npc(spotanim_85, 92, $duration);
    // splashing in osrs causes insta auto retal
    ~npc_retaliate(0);
}
npc_heropoints($damage);

// debug code
// if(testbit(%attackstyle_magic, 0) = true) {
//     mes("Auto cast turned on");
// }
// if (%attackstyle_magic = 3) {
//     mes("Using autocast spell");
// }
// if (db_getfield($spell_data, magic_spell_table:continue_by_autocast, 0) = true) {
//     mes("Spell can continue by autocast");
// }

// OSRS:
// only continue fighting if auto cast is turned on
// freezes, and stat reductions dont continue ever, even if you have autocast enabled.
// crumble undead continues autocast for some reason
if (testbit(%attackstyle_magic, 0) = true & %attackstyle_magic = 3) {
    // 
    %spell = %autocast_spell;
    if (db_getfield($spell_data, magic_spell_table:continue_by_autocast, 0) = true) {
        %autocast_delay = add(map_clock, 1);
        p_opnpc(2);
        return;
    }
}

[ai_movecheck,npc_stunned_magic]()(boolean)
if (map_clock < %npc_stunned) {
    // npc_say("<tostring(map_clock)>");
    return(false);
}

return(true);