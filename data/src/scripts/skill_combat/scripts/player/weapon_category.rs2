// in osrs attack styles are strongqueued
// for us its normal queued. Its likely queued because if you're delayed 
// and setting attack style, it wont go through at all. Queueing allows
// for it to go through after delay is over
[if_button,attack_2h_sword:com_5] ~set_attackstyle(0);
[if_button,attack_2h_sword:com_8] ~set_attackstyle(1);
[if_button,attack_2h_sword:com_7] ~set_attackstyle(2);
[if_button,attack_2h_sword:com_6] ~set_attackstyle(3);

[if_button,attack_axe:com_5] ~set_attackstyle(0);
[if_button,attack_axe:com_8] ~set_attackstyle(1);
[if_button,attack_axe:com_7] ~set_attackstyle(2);
[if_button,attack_axe:com_6] ~set_attackstyle(3);

[if_button,attack_blunt:com_7] ~set_attackstyle(0);
[if_button,attack_blunt:com_6] ~set_attackstyle(1);
[if_button,attack_blunt:com_5] ~set_attackstyle(2);

[if_button,attack_bow:com_7] ~set_attackstyle(0);
[if_button,attack_bow:com_6] ~set_attackstyle(1);
[if_button,attack_bow:com_5] ~set_attackstyle(2);

[if_button,attack_crossbow:com_7] ~set_attackstyle(0);
[if_button,attack_crossbow:com_6] ~set_attackstyle(1);
[if_button,attack_crossbow:com_5] ~set_attackstyle(2);

[if_button,attack_pickaxe:com_5] ~set_attackstyle(0);
[if_button,attack_pickaxe:com_8] ~set_attackstyle(1);
[if_button,attack_pickaxe:com_7] ~set_attackstyle(2);
[if_button,attack_pickaxe:com_6] ~set_attackstyle(3);

[if_button,attack_scythe:com_5] ~set_attackstyle(0);
[if_button,attack_scythe:com_8] ~set_attackstyle(1);
[if_button,attack_scythe:com_7] ~set_attackstyle(2);
[if_button,attack_scythe:com_6] ~set_attackstyle(3);

[if_button,attack_slashing:com_5] ~set_attackstyle(0);
[if_button,attack_slashing:com_8] ~set_attackstyle(1);
[if_button,attack_slashing:com_7] ~set_attackstyle(2);
[if_button,attack_slashing:com_6] ~set_attackstyle(3);

[if_button,attack_spear:com_5] ~set_attackstyle(0);
[if_button,attack_spear:com_8] ~set_attackstyle(1);
[if_button,attack_spear:com_7] ~set_attackstyle(2);
[if_button,attack_spear:com_6] ~set_attackstyle(3);

[if_button,attack_spiked:com_5] ~set_attackstyle(0);
[if_button,attack_spiked:com_8] ~set_attackstyle(1);
[if_button,attack_spiked:com_7] ~set_attackstyle(2);
[if_button,attack_spiked:com_6] ~set_attackstyle(3);

[if_button,attack_stabbing:com_5] ~set_attackstyle(0);
[if_button,attack_stabbing:com_8] ~set_attackstyle(1);
[if_button,attack_stabbing:com_7] ~set_attackstyle(2);
[if_button,attack_stabbing:com_6] ~set_attackstyle(3);

[if_button,attack_thrown:com_7] ~set_attackstyle(0);
[if_button,attack_thrown:com_6] ~set_attackstyle(1);
[if_button,attack_thrown:com_5] ~set_attackstyle(2);

[if_button,attack_unarmed:com_4] ~set_attackstyle(0);
[if_button,attack_unarmed:com_6] ~set_attackstyle(1);
[if_button,attack_unarmed:com_5] ~set_attackstyle(2);

[if_button,attack_magic_battlestaff:com_7] ~set_attackstyle(0);
[if_button,attack_magic_battlestaff:com_6] ~set_attackstyle(1);
[if_button,attack_magic_battlestaff:com_5] ~set_attackstyle(2);

[if_button,attack_magic_battlestaff:com_17]
if_close;
if (p_finduid(uid) = true) {
    %attackstyle_magic = togglebit(%attackstyle_magic, 0);
    %spell = %autocast_spell;
    return;
}
queue(set_attack_style_magic, 0);


[proc,set_attackstyle](int $style)
if_close;
if (p_finduid(uid) = true) {
    %attackstyle = $style;
    return;
}
queue(set_attackstyle, 0, $style);

[queue,set_attackstyle](int $style)
%attackstyle = $style;

[queue,set_attack_style_magic]
%attackstyle_magic = togglebit(%attackstyle_magic, 0);
%spell = %autocast_spell;

// updates the attack style tab depending on the
// weapon currently equipped or not by the player
[proc,update_weapon_category](obj $previous)
// dont remove attack style if the equipted item is not a weapon
if ($previous ! null & oc_wearpos($previous) ! ^wearpos_rhand) {
    return;
}
def_obj $obj = inv_getobj(worn, ^wearpos_rhand);
if ($previous = null | $obj = null | oc_category($previous) ! oc_category($obj)) {
    // TODO somehow confirm this
    // osrs keeps track of selected per category
    %attackstyle_magic = 0;
    %spell = null;
    %autocast_spell = null;
    %attackstyle = 0;
}
if ($obj = null | (map_members = false & oc_members($obj) = true)) {
    ~weapon_category_tab_attack_unarmed;
    return;
}
switch_category (oc_category($obj)) {
    // melee
    case weapon_2h_sword: ~weapon_category_tab_attack(attack_2h_sword:com_0, attack_2h_sword:com_2, attack_2h_sword, $obj);
    case weapon_axe: ~weapon_category_tab_attack(attack_axe:com_0, attack_axe:com_2, attack_axe, $obj);
    case weapon_blunt: ~weapon_category_tab_attack(attack_blunt:com_0, attack_blunt:com_2, attack_blunt, $obj);
    case weapon_pickaxe: ~weapon_category_tab_attack(attack_pickaxe:com_0, attack_pickaxe:com_2, attack_pickaxe, $obj);
    case weapon_scythe: ~weapon_category_tab_attack(attack_scythe:com_0, attack_scythe:com_2, attack_scythe, $obj);
    case weapon_slash: ~weapon_category_tab_attack(attack_slashing:com_0, attack_slashing:com_2, attack_slashing, $obj);
    case weapon_spear: ~weapon_category_tab_attack(attack_spear:com_0, attack_spear:com_2, attack_spear, $obj);
    case weapon_spiked: ~weapon_category_tab_attack(attack_spiked:com_0, attack_spiked:com_2, attack_spiked, $obj);
    case weapon_stab: ~weapon_category_tab_attack(attack_stabbing:com_0, attack_stabbing:com_2, attack_stabbing, $obj);
    // ranged
    case weapon_bow: ~weapon_category_tab_attack(attack_bow:com_0, attack_bow:com_2, attack_bow, $obj);
    case weapon_crossbow: ~weapon_category_tab_attack(attack_crossbow:com_0, attack_crossbow:com_2, attack_crossbow, $obj);
    case weapon_thrown, weapon_javelin: ~weapon_category_tab_attack(attack_thrown:com_0, attack_thrown:com_2, attack_thrown, $obj);
    // magic
    case weapon_staff: ~weapon_category_tab_attack(attack_magic_battlestaff:com_0, attack_magic_battlestaff:com_2, attack_magic_battlestaff, $obj);
    // unarmed everything else
    case default: ~weapon_category_tab_attack_unarmed;
}

[proc,weapon_category_tab_attack](component $component1, component $component2, interface $interface, obj $obj)
if_setobject($component1, $obj, 200);
if_settext($component2, "<oc_name($obj)>");
if_settab($interface, 0);

// sends the unarmed interface tab
[proc,weapon_category_tab_attack_unarmed]
// yes this is actually "unarmed" between beta and 2005-ish.
if_settext(attack_unarmed:com_1, "unarmed");
if_settab(attack_unarmed, 0);