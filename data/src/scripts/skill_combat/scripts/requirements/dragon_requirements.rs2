[opheld2,dragon_dagger] @dragon_req_zanaris_quest(last_slot);
[opheld2,dragon_dagger_p] @dragon_req_zanaris_quest(last_slot);
[opheld2,dragon_mace] @dragon_req_heroes_quest(last_slot);
[opheld2,dragon_longsword] @dragon_req_zanaris_quest(last_slot);
[opheld2,dragon_battleaxe] @dragon_req_heroes_quest(last_slot);
[opheld2,dragon_spear] @dragon_equip_req_attack(last_slot);
[opheld2,dragon_spear_p] @dragon_equip_req_attack(last_slot);

[opheld2,dragon_med_helm] @dragon_equip_req_defence(last_slot);
[opheld2,dragon_sq_shield] @dragon_req_legends_quest(last_slot);
// [opheld2,dragonfire_shield] @dragon_req_start_dragon_slayer_quest(last_slot); osrs has this. possibly not 2004.

[label,dragon_req_heroes_quest](int $slot)
if (%hero_progress ! ^hero_complete) {
    mes("You have not earned the right to wield this yet.");
    mes("You need to complete the Heroes' Guild entry quest."); // TODO confirm for 2004
    return;
}
@dragon_equip_req_attack($slot);

[label,dragon_req_legends_quest](int $slot)
if (%legends_progress ! ^legends_complete) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to complete the Legends' Quest."); // TODO confirm for 2004
    return;
}
@dragon_equip_req_defence($slot);

[label,dragon_req_zanaris_quest](int $slot)
if (%zanaris_progress ! ^zanaris_complete) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to complete the Lost City of Zanaris Quest."); // TODO confirm for 2004
    return;
}
@dragon_equip_req_attack($slot);

[label,dragon_req_start_dragon_slayer_quest](int $slot)
if (%dragon_progress = 0) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to have started the Dragon Slayer quest to equip this shield."); // TODO confirm for 2004
    return;
}
@equip($slot);

[label,dragon_equip_req_attack](int $slot)
if (stat_base(attack) < 60) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Attack level of 60.");
    return;
}
@equip($slot);

[label,dragon_equip_req_defence](int $slot)
if (stat_base(defence) < 60) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a Defence level of 60.");
    return;
}
@equip($slot);
