[levelup,attack] queue(levelup, 0, enum(int, stat, stats, ^attack));
[levelup,strength] queue(levelup, 0, enum(int, stat, stats, ^strength));
[levelup,ranged] queue(levelup, 0, enum(int, stat, stats, ^ranged));
[levelup,magic] queue(levelup, 0, enum(int, stat, stats, ^magic));
[levelup,defence] queue(levelup, 0, enum(int, stat, stats, ^defence));
[levelup,hitpoints] queue(levelup, 0, enum(int, stat, stats, ^hitpoints));
[levelup,prayer] queue(levelup, 0, enum(int, stat, stats, ^prayer));
[levelup,agility] queue(levelup, 0, enum(int, stat, stats, ^agility));
[levelup,herblore] queue(levelup, 0, enum(int, stat, stats, ^herblore));
[levelup,thieving] queue(levelup, 0, enum(int, stat, stats, ^thieving));
[levelup,crafting] queue(levelup, 0, enum(int, stat, stats, ^crafting));
[levelup,runecraft] queue(levelup, 0, enum(int, stat, stats, ^runecraft));
[levelup,mining] queue(levelup, 0, enum(int, stat, stats, ^mining));
[levelup,smithing] queue(levelup, 0, enum(int, stat, stats, ^smithing));
[levelup,fishing] queue(levelup, 0, enum(int, stat, stats, ^fishing));
[levelup,cooking] queue(levelup, 0, enum(int, stat, stats, ^cooking));
[levelup,firemaking] queue(levelup, 0, enum(int, stat, stats, ^firemaking));
[levelup,woodcutting] queue(levelup, 0, enum(int, stat, stats, ^woodcutting));
[levelup,fletching] queue(levelup, 0, enum(int, stat, stats, ^fletching));

[queue,levelup](stat $stat)
db_find(levelup:stat, $stat);
def_dbrow $unlock_row = db_findnext;
if ($unlock_row = null) {
    return;
}
def_enum $general_unlocks = db_getfield($unlock_row, levelup:unlocks, 0);
if ($general_unlocks = null) {
    return;
}
def_int $level = stat_base($stat);
def_string $message = db_getfield($unlock_row, levelup:message, 0);
// play the fireworks
spotanim_pl(levelup_anim, 124, 0);
sound_synth(firework, 0, 0);
// play the jingle
def_string $jingle;
def_int $jingle_millis;
def_struct $unlocks = enum(int, struct, $general_unlocks, $level);
if ($unlocks = null) {
    $jingle = db_getfield($unlock_row, levelup:levelup_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:levelup_jingle_millis, 0);
} else {
    $jingle = db_getfield($unlock_row, levelup:unlocks_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:unlocks_jingle_millis, 0);
}
midi_jingle($jingle, $jingle_millis);
// the level up dialogue box
if_settext(db_getfield($unlock_row, levelup:title, 0), "@dbl@<$message>");
if_settext(db_getfield($unlock_row, levelup:body, 0), "<db_getfield($unlock_row, levelup:level_prefix, 0)> <tostring($level)>.");
if_openbottom(db_getfield($unlock_row, levelup:interface, 0));
p_pausebutton;
if ($unlocks = null) {
    return;
}
def_string $levelup_message = struct_param($unlocks, levelup_message);
if (~string_isnullorempty($levelup_message) = true) {
    return;
}
// objbox2
def_namedobj $levelup_objbox = struct_param($unlocks, levelup_objbox);
def_namedobj $levelup_objbox2 = struct_param($unlocks, levelup_objbox2);
if ($levelup_objbox2 ! null & $levelup_objbox ! null) {
    ~objbox2($levelup_objbox, $levelup_objbox2, $levelup_message);
    return;
}
// objbox
if ($levelup_objbox ! null) {
    ~objbox($levelup_objbox, $levelup_message);
    return;
}
// objboxb
def_namedobj $levelup_objboxb = struct_param($unlocks, levelup_objboxb);
if ($levelup_objboxb ! null) {
    ~objboxb($levelup_objboxb, $levelup_message);
    return;
}
def_namedobj $levelup_objboxt = struct_param($unlocks, levelup_objboxt);
if ($levelup_objboxt ! null) {
    ~objboxt($levelup_objboxt, $levelup_message);
    return;
}
// mesbox
~mesbox($levelup_message);