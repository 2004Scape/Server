// https://www.youtube.com/watch?v=cz0FGYbfzzk
// https://www.youtube.com/watch?v=IC2n8t76DDY
[advancestat,attack] @levelup(enum(int, stat, stats, ^attack));
[advancestat,strength] @levelup(enum(int, stat, stats, ^strength));
[advancestat,ranged] @levelup(enum(int, stat, stats, ^ranged));
[advancestat,magic] @levelup(enum(int, stat, stats, ^magic));
[advancestat,defence] @levelup(enum(int, stat, stats, ^defence));
[advancestat,hitpoints] @levelup(enum(int, stat, stats, ^hitpoints));
[advancestat,prayer] @levelup(enum(int, stat, stats, ^prayer));
[advancestat,agility] @levelup(enum(int, stat, stats, ^agility));
[advancestat,herblore] @levelup(enum(int, stat, stats, ^herblore));
[advancestat,thieving] @levelup(enum(int, stat, stats, ^thieving));
[advancestat,crafting] @levelup(enum(int, stat, stats, ^crafting));
[advancestat,runecraft] @levelup(enum(int, stat, stats, ^runecraft));
[advancestat,mining] @levelup(enum(int, stat, stats, ^mining));
[advancestat,smithing] @levelup(enum(int, stat, stats, ^smithing));
[advancestat,fishing] @levelup(enum(int, stat, stats, ^fishing));
[advancestat,cooking] @levelup(enum(int, stat, stats, ^cooking));
[advancestat,firemaking] @levelup(enum(int, stat, stats, ^firemaking));
[advancestat,woodcutting] @levelup(enum(int, stat, stats, ^woodcutting));
[advancestat,fletching] @levelup(enum(int, stat, stats, ^fletching));

[label,levelup](stat $stat)
db_find(levelup:stat, $stat);
def_dbrow $unlock_row = db_findnext;
if ($unlock_row = null) {
    return;
}
def_int $level = stat_base($stat);
def_string $message = db_getfield($unlock_row, levelup:message, 0);
mes($message);
// play the fireworks
spotanim_pl(levelup_anim, 124, 0);
// sound_synth(firework, 0, 0); Was an osrs addition apparently
// https://secure.runescape.com/m=news/shades-of-mortton-rework?oldschool=1
// https://www.reddit.com/r/2007scape/comments/l7yh5p/the_new_level_up_fireworks_noise_is_extremely/
// https://www.reddit.com/r/2007scape/comments/l9bp4q/when_did_they_implement_insanely_annoying/ 
def_enum $unlocks_levels = db_getfield($unlock_row, levelup:unlocks_levels, 0);
def_boolean $has_unlocks = false;
if ($unlocks_levels ! null) {
    $has_unlocks = ~enum_containsintvalue($unlocks_levels, $level);
}
// play the jingle
def_string $jingle;
def_int $jingle_millis;
if ($has_unlocks = false) {
    $jingle = db_getfield($unlock_row, levelup:levelup_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:levelup_jingle_millis, 0);
} else {
    $jingle = db_getfield($unlock_row, levelup:unlocks_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:unlocks_jingle_millis, 0);
}
midi_jingle($jingle, $jingle_millis);
// the level up dialogue box
if_settext(db_getfield($unlock_row, levelup:title, 0), "@dbl@<$message>");
if_settext(db_getfield($unlock_row, levelup:body, 0), "<db_getfield($unlock_row, levelup:level_prefix, 0)> <tostring($level)>.");
if_openchat(db_getfield($unlock_row, levelup:interface, 0));
p_pausebutton;
if ($has_unlocks = false) {
    return;
}
jump(~levelup_unlocks($stat));
