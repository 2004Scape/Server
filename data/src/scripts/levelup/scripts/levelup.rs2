// https://www.youtube.com/watch?v=cz0FGYbfzzk
// https://www.youtube.com/watch?v=IC2n8t76DDY
[advancestat,attack] queue(levelup, 0, enum(int, stat, stats, ^attack));
[advancestat,strength] queue(levelup, 0, enum(int, stat, stats, ^strength));
[advancestat,ranged] queue(levelup, 0, enum(int, stat, stats, ^ranged));
[advancestat,magic] queue(levelup, 0, enum(int, stat, stats, ^magic));
[advancestat,defence] queue(levelup, 0, enum(int, stat, stats, ^defence));
[advancestat,hitpoints] queue(levelup, 0, enum(int, stat, stats, ^hitpoints));
[advancestat,prayer] queue(levelup, 0, enum(int, stat, stats, ^prayer));
[advancestat,agility] queue(levelup, 0, enum(int, stat, stats, ^agility));
[advancestat,herblore] queue(levelup, 0, enum(int, stat, stats, ^herblore));
[advancestat,thieving] queue(levelup, 0, enum(int, stat, stats, ^thieving));
[advancestat,crafting] queue(levelup, 0, enum(int, stat, stats, ^crafting));
[advancestat,runecraft] queue(levelup, 0, enum(int, stat, stats, ^runecraft));
[advancestat,mining] queue(levelup, 0, enum(int, stat, stats, ^mining));
[advancestat,smithing] queue(levelup, 0, enum(int, stat, stats, ^smithing));
[advancestat,fishing] queue(levelup, 0, enum(int, stat, stats, ^fishing));
[advancestat,cooking] queue(levelup, 0, enum(int, stat, stats, ^cooking));
[advancestat,firemaking] queue(levelup, 0, enum(int, stat, stats, ^firemaking));
[advancestat,woodcutting] queue(levelup, 0, enum(int, stat, stats, ^woodcutting));
[advancestat,fletching] queue(levelup, 0, enum(int, stat, stats, ^fletching));

[queue,levelup](stat $stat)
db_find(levelup:stat, $stat);
def_dbrow $unlock_row = db_findnext;
if ($unlock_row = null) {
    return;
}
def_int $level = stat_base($stat);
def_string $message = db_getfield($unlock_row, levelup:message, 0);
mes($message);
// play the fireworks
spotanim_pl(levelup_anim, 124, 0);
// sound_synth(firework, 0, 0); Was an osrs addition apparently
// https://secure.runescape.com/m=news/shades-of-mortton-rework?oldschool=1
// https://www.reddit.com/r/2007scape/comments/l7yh5p/the_new_level_up_fireworks_noise_is_extremely/
// https://www.reddit.com/r/2007scape/comments/l9bp4q/when_did_they_implement_insanely_annoying/ 
def_enum $unlocks_levels = db_getfield($unlock_row, levelup:unlocks_levels, 0);
def_boolean $has_unlocks = false;
if ($unlocks_levels ! null) {
    $has_unlocks = ~enum_containsintvalue($unlocks_levels, $level);
}
// play the jingle
def_string $jingle;
def_int $jingle_millis;
if ($has_unlocks = false) {
    $jingle = db_getfield($unlock_row, levelup:levelup_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:levelup_jingle_millis, 0);
} else {
    $jingle = db_getfield($unlock_row, levelup:unlocks_jingle, 0);
    $jingle_millis = db_getfield($unlock_row, levelup:unlocks_jingle_millis, 0);
}
midi_jingle($jingle, $jingle_millis);
// the level up dialogue box
if_settext(db_getfield($unlock_row, levelup:title, 0), "@dbl@<$message>");
if_settext(db_getfield($unlock_row, levelup:body, 0), "<db_getfield($unlock_row, levelup:level_prefix, 0)> <tostring($level)>.");
if_openchat(db_getfield($unlock_row, levelup:interface, 0));
p_pausebutton;
if ($has_unlocks = false) {
    return;
}
jump(~levelup_unlocks($stat));
