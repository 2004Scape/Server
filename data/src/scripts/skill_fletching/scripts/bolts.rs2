// bolts
[opheldu,_category_530]
switch_obj(last_useitem) {
    case bolt : @make_bolts(last_item);
    case default : ~displaymessage(^dm_default);
}
[opheldu,bolt]
switch_obj(last_useitem) {
    case default :
        if (oc_category(last_useitem) = category_530) {
            @make_bolts(last_useitem);
        } else {
            ~displaymessage(^dm_default);
        }
}

[label,make_bolt_tips](obj $item)
if (map_members = ^false) {
    mes(^mes_members_fletching); // guess
    return;
}
def_dbrow $data = ~get_fletching_data($item);
if ($data = null) {
    ~displaymessage(^dm_default);
    return;
}
def_int $level = db_getfield($data, fletching_table:level, 0);
if (stat(fletching) < $level) {
    // osrs
    ~mesbox("You need a Fletching level of <tostring($level)> to do that.");
    return;
}
inv_del(inv, $item, 1);
// includes count
inv_add(inv, db_getfield($data, fletching_table:product, 0));
stat_advance(fletching, db_getfield($data, fletching_table:experience, 0));
// osrs
def_seq $anim = db_getfield($data, fletching_table:skill_anim, 0);
def_synth $sound = db_getfield($data, fletching_table:skill_sound, 0);
if ($anim ! null) {
    anim($anim, 0);
}
if ($sound ! null) {
    sound_synth($sound,0 , 0);
}
mes("You use your chisel to fletch small bolt tips.");


[label,make_bolts](obj $bolt_tips)
if (map_members = ^false) {
    mes(^mes_members_fletching); // guess
    return;
}
def_dbrow $data = ~get_fletching_data($bolt_tips);
if ($data = null) {
    ~displaymessage(^dm_default);
    return;
}
def_int $level = db_getfield($data, fletching_table:level, 0);
if (stat(fletching) < $level) {
    // osrs
    ~doubleobjbox(bolt, $bolt_tips, "You need a Fletching level of <tostring($level)> to make that kind of bolt.", 200);
    return;
}
// bolts are made through smithing, they can be upgraded with fletching
def_namedobj $product;
def_int $max_count;
$product, $max_count = db_getfield($data, fletching_table:product, 0);
def_int $tip_count = min(inv_total(inv, $bolt_tips), $max_count);
def_int $bolt_count = min(inv_total(inv, bolt), $max_count);
def_int $bolts_count = min($tip_count, $bolt_count);

if(inv_itemspace(inv, $product, $bolts_count, inv_size(inv)) = false & (inv_total(inv, bolt) > $max_count | inv_total(inv, $bolt_tips) > $max_count)) {
    mes("You do not have enough inventory space for the tipped bolts."); // OSRS, this might be post july 2006?
    return;
}
// delete supplies
inv_del(inv, $bolt_tips, $bolts_count);
inv_del(inv, bolt, $bolts_count);
// osrs
if ($bolts_count > 1) {
    mes("You fletch <tostring($bolts_count)> bolts.");
} else {
    mes("You fletch a bolt.");
}

stat_advance(fletching, multiply($bolts_count, db_getfield($data, fletching_table:experience, 0)));
inv_add(inv, $product, $bolts_count);