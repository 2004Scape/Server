[proc,obj_gettotal](obj $obj)(int)
def_int $total = calc(inv_total(inv, $obj) + inv_total(bank, $obj) + inv_total(worn, $obj));
return($total);

[proc,obj_gettotalcat](category $obj_cat)(int)
def_int $total = calc(inv_totalcat(inv, $obj_cat) + inv_totalcat(bank, $obj_cat) + inv_totalcat(worn, $obj_cat));
return($total);

[proc,inv_slotspace](inv $from_inv, inv $to_inv, enum $slots)(boolean)
def_int $i = 0;
def_int $space_needed = 0;
while ($i < enum_getoutputcount($slots)) {
    def_int $slot = enum(int, int, $slots, $i);
    def_obj $obj = inv_getobj($from_inv, $slot);
    if ($obj ! null) {
        if (inv_itemspace($to_inv, $obj, inv_getnum($from_inv, $slot), inv_size($to_inv)) = true) {
            if (oc_stackable($obj) = false | (oc_stackable($obj) = true & inv_total($to_inv, $obj) < 1)) {
                $space_needed = add($space_needed, 1);
            }
        } else {
            return(false);
        }
        if (inv_freespace($to_inv) < $space_needed) {
            return(false);
        }
    }
    $i = add($i, 1);
}
return(true);

// reorganize all of the slots towards the left of the inv.
[proc,reorganize_inv](inv $inv)
def_int $size = inv_size($inv);
def_int $count = 0;

while ($count < $size) {
    def_int $slot_count = inv_getnum($inv, $count);

    if ($slot_count = 0) {
        def_int $peek_slot = ~inv_peek_next_available_obj_slot($inv, $count);

        if ($peek_slot = -1) {
            $count = add($size, 1);
        } else {
            inv_movetoslot($inv, $inv, $peek_slot, $count);
            $count = add($count, 1);
        }
    } else {
        $count = add($count, 1);
    }
}

// used to check for any obj slots that are ahead of the input slot.
// returns the slot or -1 if none available.
[proc,inv_peek_next_available_obj_slot](inv $inv, int $from_slot)(int)
def_int $size = inv_size($inv);
def_int $count = $from_slot;
def_int $slot = -1;

while ($count < $size) {
    def_int $slot_count = inv_getnum($inv, $count);
    if ($slot_count = 0) {
        $count = add($count, 1);
    } else {
        $slot = $count;
        $count = add($size, 1);
    }
}
return($slot);

[proc,del_all_if_exists](inv $inv, namedobj $obj)
def_int $total = inv_total($inv, $obj);
if ($total > 0) {
    inv_del($inv, $obj, $total);
}