[proc,add_article](string $word)(string)
def_string $first_letter = lowercase(substring($word, 0, 1));
if (compare($first_letter, "a") = 0 | compare($first_letter, "e") = 0 | compare($first_letter, "i") = 0 | compare($first_letter, "o") = 0 | compare($first_letter, "u") = 0) {
    return ("an <$word>");
} else {
    return ("a <$word>");
}

[proc,pluralise](int $amount, string $noun)(string)
if ($amount = 1) {
    return ("1 <$noun>");
} else {
    return ("<tostring($amount)> <$noun>s");
}

[proc,text_plural](int $amount, string $singular_noun, string $plural_noun)(string)
if ($amount = 1) {
    return ("1 <$singular_noun>");
} else {
    return ("<tostring($amount)> <$plural_noun>");
}

[proc,string_right](string $string, int $amount)(string)
return (substring($string, calc(string_length($string) - $amount), string_length($string)));

[proc,string_removeright](string $string, int $amount)(string)
return (substring($string, 0, calc(string_length($string) - $amount)));

[proc,string_left](string $string, int $amount)(string)
return (substring($string, 0, $amount));

[proc,string_removeleft](string $string, int $amount)(string)
return (substring($string, $amount, string_length($string)));

[proc,singularise](string $noun)(string)
if (compare(~string_right($noun, 3), "ces") = 0 | compare(~string_right($noun, 3), "ses") = 0 | compare(~string_right($noun, 3), "xes") = 0 | compare(~string_right($noun, 3), "zes") = 0) {
    return (~string_removeright($noun, 1));
} else if (compare(~string_right($noun, 3), "ies") = 0) {
    return ("<~string_removeright($noun, 3)>y");
} else if (compare(~string_right($noun, 2), "es") = 0) {
    return (~string_removeright($noun, 2));
} else if (compare(~string_right($noun, 1), "s") = 0) {
    return (~string_removeright($noun, 1));
} else {
    return ($noun);
}

[proc,string_isnullorempty](string $string)(boolean)
if (compare($string, "null") = 0 | compare($string, "") = 0) {
    return(true);
}
return(false);

[proc,tostring_boolean](boolean $boolean)(string)
if ($boolean = true) {
    return ("true");
} else {
    return ("false");
}
