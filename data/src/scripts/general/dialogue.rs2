[proc,p_choice2](string $string1, int $ret1, string $string2, int $ret2)(int)
if_settext(multi2:com_1, $string1);
if_settext(multi2:com_2, $string2);
if_openbottom(multi2);
if_setresumebuttons(multi2:com_1, multi2:com_2, null, null, null);
p_pausebutton;

switch_component (last_com) {
    case multi2:com_1 : return($ret1);
    case default : return($ret2);
}

[proc,p_choice2_header](string $string1, int $ret1, string $string2, int $ret2, string $headertext)(int)
if_settext(multi2:com_0, $headertext);
if_settext(multi2:com_1, $string1);
if_settext(multi2:com_2, $string2);
if_sethide(multi2:com_3, true);
if_sethide(multi2:com_6, false);

if_openbottom(multi2);
if_setresumebuttons(multi2:com_1, multi2:com_2, null, null, null);
p_pausebutton;

switch_component (last_com) {
    case multi2:com_1 : return($ret1);
    case default : return($ret2);
}

[proc,p_choice3](string $string1, int $ret1, string $string2, int $ret2, string $string3, int $ret3)(int)
if_settext(multi3:com_1, $string1);
if_settext(multi3:com_2, $string2);
if_settext(multi3:com_3, $string3);
if_openbottom(multi3);
if_setresumebuttons(multi3:com_1, multi3:com_2, multi3:com_3, null, null);
p_pausebutton;

switch_component (last_com) {
    case multi3:com_1 : return($ret1);
    case multi3:com_2 : return($ret2);
    case default : return($ret3);
}

[proc,p_choice4](string $string1, int $ret1, string $string2, int $ret2, string $string3, int $ret3, string $string4, int $ret4)(int)
if_settext(multi4:com_1, $string1);
if_settext(multi4:com_2, $string2);
if_settext(multi4:com_3, $string3);
if_settext(multi4:com_4, $string4);
if_openbottom(multi4);
if_setresumebuttons(multi4:com_1, multi4:com_2, multi4:com_3, multi4:com_4, null);
p_pausebutton;

switch_component (last_com) {
    case multi4:com_1 : return($ret1);
    case multi4:com_2 : return($ret2);
    case multi4:com_3 : return($ret3);
    case default : return($ret4);
}

[proc,p_choice5](string $string1, int $ret1, string $string2, int $ret2, string $string3, int $ret3, string $string4, int $ret4, string $string5, int $ret5)(int)
if_settext(multi5:com_1, $string1);
if_settext(multi5:com_2, $string2);
if_settext(multi5:com_3, $string3);
if_settext(multi5:com_4, $string4);
if_settext(multi5:com_5, $string5);
if_openbottom(multi5);
if_setresumebuttons(multi5:com_1, multi5:com_2, multi5:com_3, multi5:com_4, multi5:com_5);
p_pausebutton;

switch_component (last_com) {
    case multi5:com_1 : return($ret1);
    case multi5:com_2 : return($ret2);
    case multi5:com_3 : return($ret3);
    case multi5:com_4 : return($ret4);
    case default : return($ret5);
}

// ----

[label,multi2](string $string1, label $label1, string $string2, label $label2)
def_int $choice = ~p_choice2($string1, 1, $string2, 2);
if ($choice = 1) jump($label1);
jump($label2);

[label,multi3](string $string1, label $label1, string $string2, label $label2, string $string3, label $label3)
def_int $choice = ~p_choice3($string1, 1, $string2, 2, $string3, 3);
if ($choice = 1) jump($label1);
if ($choice = 2) jump($label2);
jump($label3);

[label,multi4](string $string1, label $label1, string $string2, label $label2, string $string3, label $label3, string $string4, label $label4)
def_int $choice = ~p_choice4($string1, 1, $string2, 2, $string3, 3, $string4, 4);
if ($choice = 1) jump($label1);
if ($choice = 2) jump($label2);
if ($choice = 3) jump($label3);
jump($label4);

[label,multi5](string $string1, label $label1, string $string2, label $label2, string $string3, label $label3, string $string4, label $label4, string $string5, label $label5)
def_int $choice = ~p_choice5($string1, 1, $string2, 2, $string3, 3, $string4, 4, $string5, 5);
if ($choice = 1) jump($label1);
if ($choice = 2) jump($label2);
if ($choice = 3) jump($label3);
if ($choice = 4) jump($label4);
jump($label5);

// ----

[proc,chatplayer_page](int $page)
def_int $lines = split_linecount($page);

def_interface $interface = null;
if ($lines = 0) {
    // sanity check
    return;
} else if ($lines = 1) {
    $interface = player_line1;
    if_setplayerhead(player_line1:com_0);
    if_setanim(player_line1:com_0, split_getanim($page));
    if_settext(player_line1:com_1, displayname);
    if_settext(player_line1:com_2, split_get($page, 0));
} else if ($lines = 2) {
    $interface = player_line2;
    if_setplayerhead(player_line2:com_0);
    if_setanim(player_line2:com_0, split_getanim($page));
    if_settext(player_line2:com_1, displayname);
    if_settext(player_line2:com_2, split_get($page, 0));
    if_settext(player_line2:com_3, split_get($page, 1));
} else if ($lines = 3) {
    $interface = player_line3;
    if_setplayerhead(player_line3:com_0);
    if_setanim(player_line3:com_0, split_getanim($page));
    if_settext(player_line3:com_1, displayname);
    if_settext(player_line3:com_2, split_get($page, 0));
    if_settext(player_line3:com_3, split_get($page, 1));
    if_settext(player_line3:com_4, split_get($page, 2));
} else if ($lines = 4) {
    $interface = player_line4;
    if_setplayerhead(player_line4:com_0);
    if_setanim(player_line4:com_0, split_getanim($page));
    if_settext(player_line4:com_1, displayname);
    if_settext(player_line4:com_2, split_get($page, 0));
    if_settext(player_line4:com_3, split_get($page, 1));
    if_settext(player_line4:com_4, split_get($page, 2));
    if_settext(player_line4:com_5, split_get($page, 3));
}

if_openbottom($interface);

[proc,chatplayer](mesanim $chathead, string $string)
split_init($string, 380, 4, q8, $chathead);
def_int $page = 0;
def_int $pagetotal = split_pagecount;
while ($page < $pagetotal) {
    ~chatplayer_page($page);
    p_pausebutton;
    $page = calc($page + 1);
}

// ----

[proc,chatnpc_page](int $page)
def_int $lines = split_linecount($page);

def_interface $interface = null;
if ($lines = 0) {
    // sanity check
    return;
} else if ($lines = 1) {
    $interface = npc_line1;
    if_setnpchead(npc_line1:com_0, npc_type);
    if_setanim(npc_line1:com_0, split_getanim($page));
    if_settext(npc_line1:com_1, npc_name);
    if_settext(npc_line1:com_2, split_get($page, 0));
} else if ($lines = 2) {
    $interface = npc_line2;
    if_setnpchead(npc_line2:com_0, npc_type);
    if_setanim(npc_line2:com_0, split_getanim($page));
    if_settext(npc_line2:com_1, npc_name);
    if_settext(npc_line2:com_2, split_get($page, 0));
    if_settext(npc_line2:com_3, split_get($page, 1));
} else if ($lines = 3) {
    $interface = npc_line3;
    if_setnpchead(npc_line3:com_0, npc_type);
    if_setanim(npc_line3:com_0, split_getanim($page));
    if_settext(npc_line3:com_1, npc_name);
    if_settext(npc_line3:com_2, split_get($page, 0));
    if_settext(npc_line3:com_3, split_get($page, 1));
    if_settext(npc_line3:com_4, split_get($page, 2));
} else if ($lines = 4) {
    $interface = npc_line4;
    if_setnpchead(npc_line4:com_0, npc_type);
    if_setanim(npc_line4:com_0, split_getanim($page));
    if_settext(npc_line4:com_1, npc_name);
    if_settext(npc_line4:com_2, split_get($page, 0));
    if_settext(npc_line4:com_3, split_get($page, 1));
    if_settext(npc_line4:com_4, split_get($page, 2));
    if_settext(npc_line4:com_5, split_get($page, 3));
}

if_openbottom($interface);

[proc,chatnpc_specific_page](string $npc_name, npc $npc, int $page)
def_int $lines = split_linecount($page);

def_interface $interface = null;
if ($lines = 0) {
    // sanity check
    return;
} else if ($lines = 1) {
    $interface = npc_line1;
    if_setnpchead(npc_line1:com_0, $npc);
    if_setanim(npc_line1:com_0, split_getanim($page));
    if_settext(npc_line1:com_1, $npc_name);
    if_settext(npc_line1:com_2, split_get($page, 0));
} else if ($lines = 2) {
    $interface = npc_line2;
    if_setnpchead(npc_line2:com_0, $npc);
    if_setanim(npc_line2:com_0, split_getanim($page));
    if_settext(npc_line2:com_1, $npc_name);
    if_settext(npc_line2:com_2, split_get($page, 0));
    if_settext(npc_line2:com_3, split_get($page, 1));
} else if ($lines = 3) {
    $interface = npc_line3;
    if_setnpchead(npc_line3:com_0, $npc);
    if_setanim(npc_line3:com_0, split_getanim($page));
    if_settext(npc_line3:com_1, $npc_name);
    if_settext(npc_line3:com_2, split_get($page, 0));
    if_settext(npc_line3:com_3, split_get($page, 1));
    if_settext(npc_line3:com_4, split_get($page, 2));
} else if ($lines = 4) {
    $interface = npc_line4;
    if_setnpchead(npc_line4:com_0, $npc);
    if_setanim(npc_line4:com_0, split_getanim($page));
    if_settext(npc_line4:com_1, $npc_name);
    if_settext(npc_line4:com_2, split_get($page, 0));
    if_settext(npc_line4:com_3, split_get($page, 1));
    if_settext(npc_line4:com_4, split_get($page, 2));
    if_settext(npc_line4:com_5, split_get($page, 3));
}

if_openbottom($interface);

[proc,chatnpc](mesanim $chathead, string $string)
split_init($string, 380, 4, q8, $chathead);
def_int $page = 0;
def_int $pagetotal = split_pagecount;
while ($page < $pagetotal) {
    ~chatnpc_page($page);
    p_pausebutton;
    $page = calc($page + 1);
}

[proc,chatnpc_specific](string $npc_name, npc $npc, mesanim $chathead, string $string)
split_init($string, 380, 4, q8, $chathead);
def_int $page = 0;
def_int $pagetotal = split_pagecount;
while ($page < $pagetotal) {
    ~chatnpc_specific_page($npc_name, $npc, $page);
    p_pausebutton;
    $page = calc($page + 1);
}
// ----

[proc,mesbox_page](int $page)
def_int $lines = split_linecount($page);

def_interface $interface = null;
if ($lines = 0) {
    // sanity check
    return;
} else if ($lines = 1) {
    $interface = message_line1;
    if_settext(message_line1:com_0, split_get($page, 0));
} else if ($lines = 2) {
    $interface = message_line2;
    if_settext(message_line2:com_0, split_get($page, 0));
    if_settext(message_line2:com_1, split_get($page, 1));
} else if ($lines = 3) {
    $interface = message_line3;
    if_settext(message_line3:com_0, split_get($page, 0));
    if_settext(message_line3:com_1, split_get($page, 1));
    if_settext(message_line3:com_2, split_get($page, 2));
} else if ($lines = 4) {
    $interface = message_line4;
    if_settext(message_line4:com_0, split_get($page, 0));
    if_settext(message_line4:com_1, split_get($page, 1));
    if_settext(message_line4:com_2, split_get($page, 2));
    if_settext(message_line4:com_3, split_get($page, 3));
} else if ($lines = 5) {
    $interface = message_line5;
    if_settext(message_line5:com_0, split_get($page, 0));
    if_settext(message_line5:com_1, split_get($page, 1));
    if_settext(message_line5:com_2, split_get($page, 2));
    if_settext(message_line5:com_3, split_get($page, 3));
    if_settext(message_line5:com_4, split_get($page, 4));
}
if_openbottom($interface);

[proc,mesbox](string $string)
split_init($string, 470, 5, q8, null);
def_int $page = 0;
def_int $pagetotal = split_pagecount;
while ($page < $pagetotal) {
    ~mesbox_page($page);
    p_pausebutton;
    $page = calc($page + 1);
}

// ----

[proc,objbox_page](namedobj $obj, int $scale, int $page)
def_int $lines = split_linecount($page);

def_interface $interface = null;
if ($lines = 0) {
    // sanity check
    return;
} else if ($lines = 1) {
    $interface = objbox_line1;
    if_setobject(objbox_line1:com_0, $obj, $scale);
    if_settext(objbox_line1:com_1, split_get($page, 0));
} else if ($lines = 2) {
    $interface = objbox_line2;
    if_setobject(objbox_line2:com_0, $obj, $scale);
    if_settext(objbox_line2:com_2, split_get($page, 0));
    if_settext(objbox_line2:com_1, split_get($page, 1));
} else if ($lines = 3) {
    $interface = objbox_line3;
    if_setobject(objbox_line3:com_0, $obj, $scale);
    if_settext(objbox_line3:com_2, split_get($page, 0));
    if_settext(objbox_line3:com_1, split_get($page, 1));
    if_settext(objbox_line3:com_4, split_get($page, 2));
} else if ($lines = 4) {
    $interface = objbox_line4;
    if_setobject(objbox_line4:com_0, $obj, $scale);
    if_settext(objbox_line4:com_2, split_get($page, 0));
    if_settext(objbox_line4:com_1, split_get($page, 1));
    if_settext(objbox_line4:com_4, split_get($page, 2));
    if_settext(objbox_line4:com_5, split_get($page, 3));
}
if_openbottom($interface);

[proc,objbox](namedobj $obj, int $scale, string $string)
split_init($string, 400, 4, q8, null);
def_int $page = 0;
def_int $pagetotal = split_pagecount;
while ($page < $pagetotal) {
    ~objbox_page($obj, $scale, $page);
    p_pausebutton;
    $page = calc($page + 1);
}
