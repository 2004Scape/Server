[logout,_]()(boolean)
if (busy = true) {
    return(false);
}

if (~in_duel_arena(coord) = true) {
    mes("You can't log out during a duel."); //https://youtu.be/xLE8au7T50Q?t=227
    return(false);
}

if (~inzone_coord_pair_table(gnomeball_zones, coord) = true) {
    mes("You can't log out on a gnomeball pitch."); // osrs
    return(false);
}

if (map_clock > 17 & add(%lastcombat, 17) > map_clock) {
    mes("You can't logout until 10 seconds after the end of combat."); // TODO: confirm this message for 2004
    return(false);
}

// logging out mid-trade, restore inv and cancel trade
if (%tradepartner ! null) {
    ~moveallinv(tempinv, inv);
    if (.finduid(%tradepartner) = true) {
        .if_close;
    }
    %tradepartner = null;
}
~reset_stall_trade_timers;
~set_pk_skull_logout;
~set_antifire_logout;
~set_antipoison_logout;

return(true);

[if_button,logout:try_logout]
if_close;
if (p_finduid(uid) = true) {
    p_logout;
}
