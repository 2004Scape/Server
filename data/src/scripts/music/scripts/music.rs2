[debugproc,lockalltracks]
if (p_finduid(uid) = true) {
    %music1 = 0;
    %music2 = 0;
    %music3 = 0;
    %music4 = 0;
    %music5 = 0;
    %music6 = 0;
} else {
    @please_finish;
}

[debugproc,unlockalltracks]
if (p_finduid(uid) = true) {
    %music1 = 0xFFFFFFFF;
    %music2 = 0xFFFFFFFF;
    %music3 = 0xFFFFFFFF;
    %music4 = 0xFFFFFFFF;
    %music5 = 0xFFFFFFFF;
    %music6 = 0xFFFFFFFF;
} else {
    @please_finish;
}

[proc,music_getvar](int $var)(int)
if ($var = 1) {
    return(%music1);
} else if ($var = 2) {
    return(%music2);
} else if ($var = 3) {
    return(%music3);
} else if ($var = 4) {
    return(%music4);
} else if ($var = 5) {
    return(%music5);
} else if ($var = 6) {
    return(%music6);
}
return(null);

[proc,music_setvar](int $var, int $bit)
if ($var = 1) {
    %music1 = setbit(%music1, $bit);
} else if ($var = 2) {
    %music2 = setbit(%music2, $bit);
} else if ($var = 3) {
    %music3 = setbit(%music3, $bit);
} else if ($var = 4) {
    %music4 = setbit(%music4, $bit);
} else if ($var = 5) {
    %music5 = setbit(%music5, $bit);
} else if ($var = 6) {
    %music6 = setbit(%music6, $bit);
}

[proc,music_playbybutton](component $playbutton)
db_find(music:playbutton, $playbutton);
def_dbrow $song = db_findnext;
if ($song = null) {
    return;
}

def_int $unlock;
def_int $bit;
$unlock, $bit = db_getfield($song, music:unlock, 0);

if ($unlock ! null) {
    if (testbit(~music_getvar($unlock), $bit) = false) {
        mes("You have not unlocked this piece of music yet!");
        return;
    }
}

%music_mode = 0; // switch to manual mode

def_string $name = db_getfield($song, music:name, 0);
if_settext(music:com_201, $name);
midi_song($name);

[label,music_playbyregion](coord $coord)
// first, search for the song linked to this mapsquare
def_int $map_x = calc(coordx($coord) / 64 * 64);
def_int $map_z = calc(coordz($coord) / 64 * 64);

db_find(musicregion:mapsquare, movecoord(0_0_0_0_0, $map_x, 0, $map_z));
def_dbrow $region = db_findnext;
if ($region = null) {
    return;
}

// now that we have the region, we can grab info on the song
db_find(music:name, db_getfield($region, musicregion:name, 0));
def_dbrow $song = db_findnext;
if ($song = null) {
    return;
}

def_int $unlock;
def_int $bit;
$unlock, $bit = db_getfield($song, music:unlock, 0);

if ($unlock ! null) {
    if (testbit(~music_getvar($unlock), $bit) = false) {
        ~music_setvar($unlock, $bit);
    }
}

if (%music_mode ! 0) {
    def_string $name = db_getfield($song, music:name, 0);
    if_settext(music:com_201, $name);
    midi_song($name);
}
