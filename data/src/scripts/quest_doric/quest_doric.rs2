// %doric_progress for quest stage
// Sources:
// This update made changes to Doric's quest. I'm assuming this updated the dialogue from the RSC version of the quest.
// https://oldschool.runescape.wiki/w/Update:Agility_Pyramid
// https://www.youtube.com/watch?v=SDSF7PAwP5E
// https://www.youtube.com/watch?v=umCTXiw4hZ0
// https://github.com/Open-RSC/Core-Framework/blob/develop/server/plugins/com/openrsc/server/plugins/authentic/quests/free/Dorics.java

[opnpc1,doric]
if (%doric_progress = 0) {
    @doric_not_started;
} else if (%doric_progress = 1) {
    @doric_materials;
} else {
    // REMOVE THIS
    %doric_progress = 0;
    @doric_post_complete;
}

[label,doric_not_started]
~chatnpc(happy, "Hello traveller, what brings you to my humble smithy?");
def_int $option = ~p_choice4("I wanted to use your anvils.", 1, "Mind your own business, shortstuff!", 2,  "I was just checking out the landscape.", 3, "What do you make here?", 4);

if ($option = 1) {
    ~chatplayer(quiz, "I wanted to use your anvils.");
    ~chatnpc(neutral, "My anvils get enough work with my own use. I make amulets, it takes a lot of work. If you could get me some more materials, I could let you use them.");
    def_int $option = ~p_choice2("Yes, I will get you materials.", 1 , "No, hitting rocks is for the boring people, sorry.", 2);

    if ($option = 1) {
        ~chatplayer(neutral, "Yes, I will get you materials.");
        ~chatnpc(neutral, "Well, clay is what I use more than anything. I make casts. Could you get me 6 clay, and 4 copper ore and 2 iron ore please? I could pay a little, and let you use my anvils.");
        ~chatplayer(happy, "Certainly, I will get them for you. Goodbye.");
        %doric_progress = calc(%doric_progress + 1);
    } else if ($option = 2) {
         ~chatplayer(down, "No, hitting rocks is for the boring people, sorry.");
         ~chatnpc(confused, "That is your choice. Nice to meet you anyway.");
    }
} else if ($option = 2) {
    ~chatplayer(angry, "Mind your own business, shortstuff!");
    ~chatnpc(confused, "How nice to meet someone with such pleasant manners. Do come again when you need to shout at someone smaller than you!");
} else if ($option = 3) {
    ~chatplayer(quiz, "I was just checking out the landscape.");
    ~chatnpc(neutral, "We have a fine town here, it suits us very well. Please enjoy your travels, and do visit my friends in their mine.");
} else if ($option = 4) {
    ~chatplayer(happy, "What do you make here?");
    ~chatnpc(neutral, "I make amulets. I am the best maker of them in Runescape!");
    ~chatplayer(quiz, "Do you have any to sell?");
    ~chatnpc(neutral, "Not at the moment, sorry. Try again later.");
}

[label,doric_materials]
~chatnpc(happy, "Have you got my materials yet, traveller?");
if (inv_total(inv, clay) > 5 & inv_total(inv, copper_ore) > 3 & inv_total(inv, iron_ore) > 1) {
    ~chatplayer(long, "I have everything you need!");
    ~chatnpc(neutral, "Many thanks, pass them here please.");
    // Remove ores here
    inv_del(inv, clay, 6);
    inv_del(inv, copper_ore, 4);
    inv_del(inv, iron_ore, 2);
    mes("You hand the Clay, Copper and Iron to Doric.");
    
    ~chatnpc(happy, "I can spare you some coins for your trouble.");
    // Add coins here
    inv_add(inv, coins, 180);
    mes("Doric hands you 180 coins");

    ~chatnpc(happy, "Please use my anvils any time you want.");
    //@update_doric_progress;
    %questpoints = calc(%questpoints + 1);
    if_settext(quest_complete:com_3, "You have completed\\nDoric's Quest");
    if_settext(quest_complete:com_9, "1");
    if_setobject(quest_complete:com_4, steel_pickaxe, 250);
    if_opentop(quest_complete);
    mes("Congratulations! Quest complete!");
    givexp(mining, 13000);
} else {
    ~chatplayer(sad, "Sorry, I don't have them all yet.");
    ~chatnpc(quiz, "Not to worry stick at it. Remember, I need 6 Clay, 4 Copper ore and 2 Iron ore.");
}

[label,doric_post_complete]
~chatnpc(quiz, "Hello traveller, how is your Metalworking coming along?");
~chatplayer(happy, "No too bad, thanks Doric.");
~chatnpc(happy, "Good, the love of metal is a thing close to my heart.");

// Reward Scroll
// According to a beta image the following are regular chat messages
// You hand the  Clay, Copper, and Iron to Doric.
// Doric hands you 180 coins.
// Congratulations! Quest Complete!

[label,update_doric_progress]
%doric_progress = calc(%doric_progress + 1);
