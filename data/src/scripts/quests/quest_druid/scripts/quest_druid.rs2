[label,druidic_ritual_start_1]
~chatplayer(neutral, "I'm in search of a quest.");
~chatnpc(neutral, "Hmm. I think I may have a worthwhile quest for you|actually. I don't know if you are familiar with the stone|circle south of Varrock or not, but...");
~chatnpc("default", "That used to be OUR stone circle. Unfortunately,|many many years ago, dark wizards cast a wicked spell|upon it so that they could corrupt its power for their|own evil ends.");
~chatnpc("default", "When they cursed the rocks for their rituals they made|them uselss for us and our magics. We require a brave|adventurer to go on a quest for us to help purify the|circle of Varrock.");
def_int $option = ~p_choice3("Ok, I will try and help.", 1, "No, that doesn't sound very interesting.", 2, "So... is there anything in this for me?", 3);

if($option = 1) {
    @druid_agree_to_help;
} else if($option = 2) {
    @druid_not_interested;
} else if($option = 3) {
    ~chatplayer("default", "So... is there anything in this for me?");
    ~chatnpc("default", "We druids value wisdom over wealth, so if you expect material gain, you will be disappointed.");
    ~chatnpc("default", "We are, however, very skilled in the art of Herblore, which we will share with you if you can assist us with this task. You may find such wisdom a greater reward than mere money.");
}

[label,druidic_ritual_start_2]
~chatplayer(quiz, "What about the stone circle full of dark wizards?");
~chatnpc("default", "That used to be OUR stone circle. Unfortunately,|many many years ago, dark wizards cast a wicked spell|upon it so that they could corrupt its power for their|own evil ends.");
~chatnpc("default", "When they cursed the rocks for their rituals they made|them uselss for us and our magics. We require a brave|adventurer to go on a quest for us to help purify the|circle of Varrock.");
@multi2("Ok, I will try and help.", druid_agree_to_help, "No, that doesn't sound very interesting.", druid_not_interested);

[label,druid_agree_to_help]
~chatplayer(neutral, "Ok, I will try to help.");
%druid_progress = 1;
~chatnpc("default", "Excellent. Go to the village south of this place and speak|to my fellow druid, Sanfew, who is working on the|purification ritual. He knows what is required to|complete it.");
~chatplayer(neutral, "Will do.");
~send_quest_progress(quest_journal:druid, %druid_progress, ^druid_complete);

[label,druid_not_interested]
~chatplayer(confused, "No, that doesn't sound very interesting.");
~chatnpc(neutral, "I will not try and change your mind adventurer. Some|day when you have matured you may reconsider your|position. We will wait until then");

[label,druid_sanfew_information]
~chatplayer(neutral, "I've been sent to assist you with the ritual to puriy the|Varrockian stone circle.");
~chatnpc("default", "Well, what I'm struggling with right now is the meats|needed for the potion to honor Guthix. I need the raw|meat of four different animals for it, but not just any|old meats will do.");
~chatnpc(neutral, "Each meat has to be dipped individually into the|Cauldron of thunder for it to work correctly.");
@druid_sanfew_questions;

[label,druid_sanfew_questions]
def_int $option = ~p_choice2("Where can I find this cauldron?", 1, "Ok, I'll do that then.", 2);

if($option = 1) {
    ~chatplayer(quiz, "Where can I find this cauldron?");
    ~chatnpc("default", "It is located somewhere in the mysterious underground|halls which are located somewhere in the woods just|South of here. They are too dangerous for me to go|myself however.");
    @druid_sanfew_questions;
} else if($option = 2) {
    %druid_progress = 2;
    ~chatplayer("default", "Ok, I'll do that then.");
    ~chatnpc("default", "Well thank you very much!");
}

[label,druid_sanfew_give_ingredients]
~chatnpc(quiz, "Did you bring me the required ingredients for the|potion?");

if(inv_total(inv, druid_enchanted_beef) > 0 & inv_total(inv, druid_enchanted_rat) > 0 & inv_total(inv, druid_enchanted_bear) > 0& inv_total(inv, druid_enchanted_chicken) > 0) {
    ~chatplayer(happy, "Yes, I have all four now!");
    def_string $line = text_gender("Well hand'em over then lad!", "Well hand'em over then lass!");
    ~chatnpc(happy, $line);
    inv_del(inv, druid_enchanted_beef, 1);
    inv_del(inv, druid_enchanted_rat, 1);
    inv_del(inv, druid_enchanted_bear, 1);
    inv_del(inv, druid_enchanted_chicken, 1);
    %druid_progress = 3;
    ~chatnpc(happy, "Thank you so much adventurer! These meats will allow|our potion to honour Guthix to be completed, and bring|one step closer to reclaiming our stone circle!");
    ~chatnpc(neutral, "Now go and talk to Kaqemeex and he will introduce|you to the wonderful world of herblor and potion|making!");
} else {
    ~chatplayer(sad, "No, not yet...");
    ~chatnpc(neutral, "Well let me know when you do young 'un.");
    def_int $option = ~p_choice2("What was I meant to be doing again?", 1, "I'll get on with it.", 2);

    if($option = 1) {
        ~chatplayer(neutral, "What was I meant to be doing again?");
        ~chatnpc(neutral, "Trouble with your memory eh young'un? I need the raw meats of four different animals that have been dipped into the Cauldron of Thunder so I can make my potion to honour Guthix.");
        ~chatplayer(neutral, "Ooooh yeah, I remember.");
        @druid_sanfew_questions;
    } else if($option = 2) {
        ~chatplayer(happy, "I'll get on with it.");
    }
}

[label,druid_completion]
~chatnpc("default", "I have word from Sanfew that you have been very|helpful in assisting him with his preparations for the|purification ritual. As promised I will now teach you the|ancient arts of Herblore.");
~chatnpc(neutral, "Herblore is the skill of working with herbs and other|ingredients, to make useful potions and poison.");
~chatnpc(neutral, "First you will need a vial, which can be found or made|with the crafting skill.");
~chatnpc(neutral, "Then you must gather the herbs needed to make the|potion you want.");
~chatnpc(neutral, "You must fill your vial with water and add the|ingredients you need. There are normally 2 ingredients|to each type of potion.");
~chatnpc(neutral, "Bear in mind, you must first clean each herb before|you can use it.");
~chatnpc(neutral, "You may also have to grind some ingredients before|you can use them. You will need a pestle and mortar|in order to do this.");
~chatnpc(neutral, "Herbs can be found on the ground, and are also|dropped by some monsters when you kill them.");
~chatnpc(neutral, "Let's try an example Attack potion: The first ingredient|is Guam leaf; the next is Eye of Newt.");
~chatnpc(neutral, "Mix these in your water-filled vial, and you will produce|an Attack potion. Drink this potion to increase your|Attack level.");
~chatnpc(neutral, "Different potions also require different Herblore levels|before you can make them.");
queue(druid_quest_complete, 0);

[queue,druid_quest_complete]
%druid_progress = ^druid_complete;
givexp(herblore, 2500);
~send_quest_complete(quest_journal:druid, marentill, 220, ^druid_questpoints, "You have completed the\\nDruidic Ritual Quest!");

[oplocu,druid_cauldron]
p_arrivedelay;
if(last_useitem = raw_rat_meat) {
    mes("You dip the raw rat meat in the cauldron.");
    inv_del(inv, raw_rat_meat, 1);
    inv_add(inv, druid_enchanted_rat, 1);
} else if(last_useitem = raw_beef) {
    mes("You dip the raw beef in the cauldron.");
    inv_del(inv, raw_beef, 1);
    inv_add(inv, druid_enchanted_beef, 1);
} else if(last_useitem = raw_bear_meat) {
    mes("You dip the raw bear meat in the cauldron.");
    inv_del(inv, raw_bear_meat, 1);
    inv_add(inv, druid_enchanted_bear, 1);
} else if(last_useitem = raw_chicken) {
    mes("You dip the raw chicken in the cauldron.");
    inv_del(inv, raw_chicken, 1);
    inv_add(inv, druid_enchanted_chicken, 1);
} else {
    @nothing_interesting_happens;
}
