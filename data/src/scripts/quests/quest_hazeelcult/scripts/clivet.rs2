[opnpc1,clivet]
switch_int(%hazeelcult_progress) {
    case ^hazeelcult_not_started:
        ~chatplayer("<p,neutral>Hello there.");
        ~chatnpc("<p,shifty>What do you want adventurer?");
        ~chatplayer("<p,neutral>Just passing by.");
        ~chatnpc("<p,neutral>You have no business here. Leave.... now.");
    case ^hazeelcult_started:
        ~chatplayer("<p,quiz>Do you know the Carnilleans?");
        ~chatnpc("<p,angry>You mind your business, I'll mind mine.");
        ~chatplayer("<p,angry>Look, I KNOW you're hiding something. I've heard theres a cult hideout down here.");
        ~chatnpc("<p,angry>If you want to stay healthy you'll leave now.");
        ~chatplayer("<p,angry>I have my orders.");
        ~chatnpc("<p,neutral>So... that two faced cold hearted snob has made you fall for his propaganda eh?");
        ~chatplayer("<p,angry>Sir Ceril Carnillean is a man of honour!");
        ~chatnpc("<p,neutral>Is he now? Is he REALLY? There is a lot more to the Carnilleans than meets the eye you fool...");
        ~chatnpc("<p,angry>And none of it is honourable.");
        %hazeelcult_progress = ^hazeelcult_spoken_clivet;
        @multi2("What do you mean?", clivet_what_do_you_mean, "I've heard enough of your rubbish.", clivet_rubbish);
    case ^hazeelcult_spoken_clivet:
        ~chatplayer("<p,neutral>Hello.");
        ~chatnpc("<p,neutral>So. You've returned. Now do you want to know the truth about the 'honourable' Carnilleans?");
        @multi2("What do you mean?", clivet_what_do_you_mean, "I've heard enough of your rubbish.", clivet_rubbish);  
    case ^hazeelcult_clivet_decision:
        ~chatplayer("<p,neutral>Hello there.");
        if(%hazeelcult_side = ^hazeelcult_goodside) {
            // guessing mesanims here
            ~chatnpc("<p,angry>Oh no... not you again.");
            ~chatplayer("<p,quiz>WHERE is the cult hideout?");
            ~chatnpc("<p,angry>You're more of a fool than you look if you think you will ever find it. When Lord Hazeel is revived you will be first to grovel for his mercy!");
        } else {
            ~chatnpc("<p,neutral>You have a mission for us adventurer. Go to the Carnilliean household and poison their meal to prove your loyalty.");
            if(inv_total(inv, poison) = 0) { // no bank check
                ~chatplayer("<p,sad>I lost the poison you gave me...");
                inv_add(inv, poison, 1);
                ~chatnpc("<p,angry>Useless. Take this and don't mess up this time. Lord Hazeel is depending on you.");
            }
        }
    case ^hazeelcult_poured_poison:
        ~chatplayer("<p,neutral>Hello.");
        ~chatplayer("<p,neutral>I poured the poison into the Carnillean's meal as requested. It didn't quite go to plan however.");
        ~chatnpc("<p,neutral>Yes, we heard all about it from one of our sources. Hazeel has eyes everywhere! You have now proved your loyalty and will be rewarded when Hazeel is revived!");
        ~chatplayer("<p,neutral>Ok. So what's next?");
        ~chatnpc("<p,neutral>Here. Wear this amulet; it is called The Sign Of Hazeel. Not only will it identify you as one of our brotherhood to other followers but it is also key to finding our hideout.");
        if(inv_total(inv, mark_of_hazeel) > 0) {
            ~chatnpc("<p,neutral>But of course, you already knew that, as you already have The Sign Of Hazeel.");
            return;
        }
        inv_add(inv, mark_of_hazeel, 1);
        ~chatplayer("<p,quiz>How does this amulet help do that then?");
        ~chatnpc("<p,neutral>Hazeel in his wisdom when he built this lair did so by making it inaccessible unless the flow of the sewers is controlled exactly by the sewer valves above ground.");
        ~chatnpc("<p,neutral>Each sewer valve must be correctly set before the water level is such that you may enter. The secret of the correct settings is contained in these amulets which honour him.");
        ~chatnpc("<p,neutral>Starting from left to right, follow the design of the amulet to locate each sewer valve, and then turn each so as to follow the amulet design.");
        ~chatnpc("<p,neutral>When you solve the sequence you may enter our hideout where our leader, Alomone, awaits you.");
    case ^hazeelcult_finished_side_task:
        if(%hazeelcult_side = ^hazeelcult_goodside) {
            ~chatplayer("<p,neutral>Hello again.");
            ~chatnpc("<p,angry>You again! You meddle in our affairs too much! When Lord Hazeel returns to this realm he will lay a mighty vengeance upon you!");
        } else {
            ~chatplayer("<p,neutral>Hello.");
            ~chatnpc("<p,neutral>Hello once more adventurer. All we need is Hazeels' magical scroll to bring him forth once more to this world.");
        }
    case ^hazeelcult_given_armour_or_scroll:
        if(%hazeelcult_side = ^hazeelcult_goodside) {
            ~chatplayer("<p,neutral>Hello again.");
            ~chatnpc("<p,angry>You again. Mark my words meddler; when Lord Hazeel returns you will be punished most severely for your interference in our schemes.");
        } else {
            ~chatplayer("<p,neutral>Hello again.");
            ~chatnpc("<p,quiz>Have you acquired the enchanted scroll required to resurrect Hazeel yet?");
            if(inv_total(inv, scroll_of_hazeel) = 0) {
                ~chatplayer("<p,confused>Uh... yeah... about that... I seem to have, errm, misplayed it somewhere...");
                ~chatnpc("<p,angry>Incompetent cretin! Return to the Carnillean household and do not return again without it!");
                return;
            }
            ~chatplayer("<p,neutral>Yes, it was in the house all along. Jones can't have looked that hard for it.");
            ~chatnpc("<p,happy>At last! The pieces are all in place for the glorious resurrection of Lord Hazeel! Now is the starting point for a new page in history, and our chance for vengeance!");
            ~chatnpc("<p,happy>Quickly now! Take the scroll to Alomone so that we may begin the ritual... and when Lord Hazeel returns I promise you that you shall be well rewarded!");
        }
    case ^hazeelcult_complete:
        ~chatplayer("<p,neutral>Hello.");
        if(%hazeelcult_side = ^hazeelcult_goodside) {
            ~chatnpc("<p,angry>You may have won this battle meddler, but the war rages on. Go bother some goblins or something for when Hazeel returns your destruction is assured.");
        } else {
            ~chatnpc("<p,happy>It is good to see you once more. Did I not tell you how glorious Lord Hazeel is? With each day his power grows, and soon, very soon, we shall rule these lands!");
            ~chatnpc("<p,happy> In the meantime I await his return here patiently until he calls upon me.");
        }
}

[label,clivet_what_do_you_mean]
~chatplayer("<p,quiz>What do you mean?");
~chatnpc("<p,neutral>The Carnillean home does not belong to them. The builder of the house was Lord Hazeel, one of the Mahjarrat followers of Zamorak. Many years ago there was a civil war");
~chatnpc("<p,neutral>in this land, and the hateful Saradominists declared war upon all Zamorakians who lived here. Lord Hazeel nobly would not repent his beliefs, and the Carnilleans harrassed");
~chatnpc("<p,neutral>Hazeel and his followers for many decades. One fateful night, under cover of darkness, they stormed his home in an angry mob torturing and butchering all loyal");
~chatnpc("<p,neutral>Zamorakians they encountered inside. The following morning, the Carnillean forefathers moved into the empty household and claimed it as their own.");
~chatnpc("<p,neutral>They have grown fat on the hard work of Lord Hazeel ever since. Unluckily for them Lord Hazeel as a Mahjarrat had access to powers and enchantments they knew nothing");
~chatnpc("<p,neutral>about, and made preparations for his return when first they began to storm his home. Soon the day will come when he will return to wreak his vengeance upon the thieves!");
~chatplayer("<p,neutral>The politics and histories of Ardougne do not concern me. I have been given a job and intend to see it through to the end.");
~chatnpc("<p,neutral>Well then friend, perhaps I can offer you a different job then? Sooner or later Our master WILL return to this land; those faithful to him will be well rewarded.");
~chatnpc("<p,neutral>Join our cult and assist us in his restoration, and you will be well rewarded; try and prevent his return and suffer the wrath of Zamorak and the Mahjarrat.");
switch_int(~p_choice2("You're crazy, I'd never help you.", 1, "So what would I have to do?", 2)) {
    case 1:
        ~chatplayer("<p,angry>You're crazy, I'd never help you.");
        @clivet_fool;
    case 2:
        ~chatplayer("<p,quiz>So what would I have to do?");
        ~chatnpc("<p,neutral>You must prove your loyalty to our cause. Killing one of the Carnillean household should be sufficient proof of your dedication to resurrecting our master Lord Hazeel.");
        ~chatnpc("<p,quiz>So what say you adventurer? Join our side against the Carnillean thieves?");
        switch_int(~p_choice2("No. I won't do it.", 1, "Ok, count me in.", 2)) {
            case 1:
                ~chatplayer("<p,angry>No. I won't do it.");
                @clivet_fool;
            case 2:
                ~chatplayer("<p,neutral>Ok, count me in.");
                ~chatnpc("<p,neutral>Excellent. It takes a rare character to see through the cursed Carnillean lies. I can tell you are a <text_gender("man", "lady")> of exactly the right character to join the followers of Hazeel.");
                ~chatnpc("<p,neutral>Here. Take this poison. Pour it into their food, and once the deed is done, return here and speak to me once more.");
                inv_add(inv, poison, 1);
                %hazeelcult_side = ^hazeelcult_evilside;
                %hazeelcult_progress = ^hazeelcult_clivet_decision;
        }
}

[label,clivet_rubbish]
// https://youtu.be/R5MdMtqvxaM?si=J64ldVsZ-1de4-26&t=117
~chatplayer("<p,angry>I've heard enough of your rubbish!");
~chatnpc("<p,angry>Then leave, fool!");

[label,clivet_fool]
~chatnpc("<p,neutral>Then you are a fool. Go back to your small minded mundane little life; you will never know the glories you could have tasted as one of us!");
~mesbox("The man jumps onto the raft and pushes off down into the sewer system.");
%hazeelcult_side = ^hazeelcult_goodside;
%hazeelcult_progress = ^hazeelcult_clivet_decision;
npc_del;
mes("Clivet: You'll never find us...");
