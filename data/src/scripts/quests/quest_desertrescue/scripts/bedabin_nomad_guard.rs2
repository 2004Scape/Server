[opnpc1,bedabin_nomad_guard]
if(%desertrescue_progress >= ^desertrescue_given_pineapple) {
    ~chatnpc("<p,happy>Sorry, but you can't use the tent without permission.|But thanks for all your help with the Bedabin people.");
} else if(testbit(%desertrescue_map_mechanisms, ^desertrescue_gained_tent_access) = true & %desertrescue_progress < ^desertrescue_learned_darts) {
    ~chatnpc("<p,happy>Oh, I remember you; yeah go on in. Hope the weapon construction is going okay.");
    if_close;
    if(coord ! 0_49_47_33_37) {
        def_int $dist = distance(coord, 0_49_47_33_37);
        ~forcewalk(0_49_47_33_37);
        p_delay(max(0, calc(3 - $dist)));
    }
    if(loc_find(0_49_47_33_38, loc_2700) = true) loc_change(loc_2701, 2);
    p_teleport(0_49_47_33_38);
    p_delay(3);
    return;
} else {
    ~chatnpc("<p,angry>Sorry, but you can't use the tent without permission. Orders of Al Shabim.");
    if(%desertrescue_progress = ^desertrescue_shown_plans_shabim) {
        if(inv_total(inv, technical_plans) = 0) {
            ~chatnpc("<p,neutral>Sorry, no one is allowed to enter.");
            ~chatplayer("<p,neutral>But Al Shabim said I could enter!");
            ~chatnpc("<p,neutral>I can only let someone in who has plans for the secret weapon.");
            if(inv_total(bank, technical_plans) > 0)  ~chatplayer("<p,neutral>Oh drat, I've gone and left those in my bank. I'll have to go and get them.");
            return;
        }
        ~chatnpc("<p,neutral>Okay, you can go in, Al Shabim has told me about you.");
        if_close;
        %desertrescue_map_mechanisms = setbit(%desertrescue_map_mechanisms, ^desertrescue_gained_tent_access);
        if(coord ! 0_49_47_33_37) {
            def_int $dist = distance(coord, 0_49_47_33_37);
            ~forcewalk(0_49_47_33_37);
            p_delay(max(0, calc(3 - $dist)));
        }
        if(loc_find(0_49_47_33_38, loc_2700) = true) loc_change(loc_2701, 2);
        p_teleport(0_49_47_33_38);
        p_delay(3);
    }
}

[oploc1,loc_2700]
if(testbit(%desertrescue_map_mechanisms, ^desertrescue_gained_tent_access) = true & %desertrescue_progress < ^desertrescue_learned_darts) {
    loc_change(loc_2701, 2);
    if(coordz(coord) >= coordz(loc_coord)) {
        mes("You walk out of the tent.");
        p_teleport(0_49_47_33_37);
        p_delay(3);
    } else {
        mes("You walk into the tent.");
        p_teleport(0_49_47_33_38); // no delay
    }
    return;
} else if(npc_find(coord, bedabin_nomad_guard, 5, 0) = true) { // you can kill these in 225, changed in Oct 2004 so need to confirm original behaviour
    if(%desertrescue_progress >= ^desertrescue_given_pineapple) {
        ~chatnpc("<p,happy>Sorry, but you can't use the tent without permission.|But thanks for all your help with the Bedabin people.");
    } else if(%desertrescue_progress = ^desertrescue_shown_plans_shabim) {
        if(inv_total(inv, technical_plans) > 0) {
            ~chatplayer("<p,neutral>Al Shabim said I could enter, here are the plans!");
            ~chatnpc("<p,happy>Okay go ahead.");
            if_close;
            %desertrescue_map_mechanisms = setbit(%desertrescue_map_mechanisms, ^desertrescue_gained_tent_access);
            p_teleport(movecoord(coord, 0, 0, 1));
            loc_change(loc_2701, 2); // curtain_open
            p_delay(3);
        } else { // todo - confirm mesanims
            ~chatnpc("<p,neutral>Sorry, no one is allowed to enter.");
            ~chatplayer("<p,neutral>But Al Shabim said I could enter!");
            ~chatnpc("<p,neutral>I can only let someone in who has plans for the secret weapon.");
            if(inv_total(bank, technical_plans) > 0)  ~chatplayer("<p,neutral>Oh drat, I've gone and left those in my bank. I'll have to go and get them.");
        }
    } else {
        ~chatnpc("<p,confused>Sorry, this is a private tent, no one is allowed in. Orders of Al Shabim...");
    }
}

[opnpcu,bedabin_nomad_guard]
if(last_useitem = technical_plans) {
    switch_int(%desertrescue_progress) {
        case default :
            inv_del(inv, technical_plans, 1);
            ~chatnpc("<p,neutral>Sorry, but you can't use the tent without permission.|But thanks for all your help with the Bedabin people.|And we'll take those plans off your hands as well!");
        case ^desertrescue_shown_plans_shabim, ^desertrescue_made_dart_tip, ^desertrescue_finished_dart : 
            ~chatnpc("<p,neutral>Okay, you can go in, Al Shabim has told me about you.");
            if_close;
            %desertrescue_map_mechanisms = setbit(%desertrescue_map_mechanisms, ^desertrescue_gained_tent_access);
            p_teleport(movecoord(coord, 0, 0, 1));
            if(loc_find(0_49_47_33_38, loc_2700) = true) loc_change(loc_2701, 2);
        case ^desertrescue_retrieved_plans :
            ~chatnpc("<p,neutral>Hmm, those plans look interesting. Go and show them to Al Shabim... I'm sure he'll be pleased to see them.");
    }
    return;
}
~chatnpc("<p,neutral>Sorry, but I have no use for that!");