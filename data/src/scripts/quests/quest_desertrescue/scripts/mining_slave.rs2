[opnpc1,female_slave] @standard_camp_slave;

[label,standard_camp_slave]
npc_say("Can't you see I'm busy?");
mes("This person doesn't want to talk to you.");
if((npc_find(coord, mercenary_1, 5, 0) = true | npc_find(coord, mercenary_2, 5, 0) = true) & ~wearing_slave_robes = false) {
    p_delay(1);
    @mining_camp_caught;
}

[opnpc1,male_slave]
if(inzone(0_51_147_0_0, 0_51_147_63_63, coord) = true) {
    @standard_camp_slave;
}

if(%desertrescue_progress >= ^desertrescue_traded_clothes) {
    // this only checks worn
    if(inv_total(worn, slave_shirt) = 0 | inv_total(worn, slave_robe) = 0 | inv_total(worn, slave_boots) = 0) {
        ~chatnpc("<p,neutral>Oh bother, I was caught by the guards again... Listen, if you can get me some Desert Clothes, I'll trade you for my slaves clothes again.. Do you want to trade?");
        @multi2("Yes, I'll trade.", male_slave_trade, "No thanks...", male_slave_notrade);
    } else {
        ~chatnpc("<p,neutral>Not much to do here but mine all day long.");
    }
} else if(%desertrescue_progress = ^desertrescue_freed_slave) {
    ~chatnpc("<p,neutral>Do you have the Desert Clothes yet?");
    @multi2("Yes, I'll trade.", male_slave_trade, "No thanks...", male_slave_notrade);
} else if(%desertrescue_progress = ^desertrescue_spoken_slave) {
    ~chatnpc("<p,quiz>Hello again, do you want to try and unlock my chains? I'd really appreciate it!");
    @multi2_header("Yeah, okay, let's give it a go.", male_slave_unlock, "I need to do some other things first.", male_slave_otherthing, "Unlock Prisoner's Chains?");
} else {
    ~chatnpc("<p,neutral>You look like a new 'recruit'. How long have you been here?");
    @multi2("I've just arrived.", male_slave_arrived, "Oh, I've been here ages.", male_slave_ages);
}

[label,male_slave_ages]
~chatplayer("<p,neutral>Oh, I've been here ages.");
~chatnpc("<p,quiz>That's funny, I haven't seen you around here before. You're clothes look too clean for you to have been here ages.");
@multi2("Okay, you caught me out.", male_slave_caught, "The guards allow me to clean my clothes.", male_slave_guards);

[label,male_slave_caught]
~chatplayer("<p,neutral>Okay, you caught me out.");
~chatnpc("<p,happy>Ah ha! I knew it! A new recruit then? It's a shame that I won't be around long enough to get to know you. I'm making a break for it today.");
~chatnpc("<p,happy>I have a plan to get out of here! It's amazing in its sophistication.");
@multi2("What are those big wooden doors in the corner of the compound?", male_slave_wooddoor, "Oh yes, that sounds interesting.", male_slave_interesting);

[label,male_slave_arrived]
~chatplayer("<p,neutral>I've just arrived.");
~chatnpc("<p,happy>Yeah, it looks like it as well. It's a shame that I won't be around long enough to get to know you. I'm making a break for it today. I have a plan to get out of here! It's amazing in its sophistication.");
@multi2("What are those big wooden doors in the corner of the compound?", male_slave_wooddoor, "Oh yes, that sounds interesting.", male_slave_interesting);

[label,male_slave_wooddoor]
~chatplayer("<p,quiz>What are those big wooden doors in the corner of the compound?");
~chatnpc("<p,confused>They lead to an underground mine, but you really don't want to go down there.");
~chatnpc("<p,confused>I've only seen slaves and guards go down there, I never see the slaves come back up. At least up here you have a nice view and a bit of sun. @dbl@-- The slave @dbl@smiles at you and goes back to his work. --");

[label,male_slave_guards]
~chatplayer("<p,neutral>The guards allow me to clean my clothes.");
~chatnpc("<p,angry>Oh, a special relationship with the guards heh? How very nice of them. Maybe you could persuade them to let me out of here?");

[label,male_slave_interesting]
~chatplayer("<p,neutral>Oh yes, that sounds interesting.");
~chatnpc("<p,happy>Yes, it is actually. I have all the details figured out except for one.");
@multi2("What's that then?", male_slave_what, "Oh, that's a shame...", male_slave_shame);

[label,male_slave_what]
~chatplayer("<p,neutral>What's that then?");
~chatnpc("<p,neutral>@dbl@-- The slave rattles the chains on his arms loudly. -- These bracelets, I can't seem to get them off. If I could get them off, I'd be able to climb my way out of here.");
@multi2("I can try to undo them for you.", male_slave_undo, "That's ridiculous, you're talking rubbish.", male_slave_rubbish);

[label,male_slave_rubbish]
~chatplayer("<p,neutral>That's ridiculous, you're talking rubbish.");
~chatnpc("<p,happy>No, it's true, I can make a break for it If I can just get these bracelets off.");
@multi2("Good luck!", male_slave_luck, "I can try to undo them for you.", male_slave_undo);

[label,male_slave_luck]
~chatplayer("<p,neutral>Good luck!");
~chatnpc("<p,neutral>Thanks... same to you.");

[label,male_slave_undo]
~chatplayer("<p,neutral>I can try to undo them for you.");
%desertrescue_progress = ^desertrescue_spoken_slave;
~chatnpc("<p,confused>Really, that would be great... @dbl@-- The slave looks at you @dbl@strangely. -- @bla@Hang on a minute... I suppose you want something for doing this?");
~chatnpc("<p,confused>The last time I did a trade in this place, I nearly lost the shirt from my back!");
switch_int(~p_choice2("It's funny you should say that...", 1, "That sounds awful.", 2)) {
    case 1 :
        ~chatplayer("<p,neutral>It's funny you should say that actually.");
        ~chatnpc("<p,silent>@dbl@-- The slave looks at you blankly. --");
        ~chatnpc("<p,neutral>Yeah, go on!");
        ~chatplayer("<p,quiz>If I can get the chains off, you have to give me something, okay?");
        ~chatnpc("<p,quiz>Sure, what do you want?");
        ~chatplayer("<p,happy>I want your clothes!");
        ~chatnpc("<p,shock>Blimey!");
        ~chatplayer("<p,quiz>I can dress like a slave and gain access to the mine area to scout it out.");
        ~chatnpc("<p,neutral>You're either incredibly brave or incredibly stupid. But what would I wear if you take my clothes? Get me some nice desert clothes and I'll think about it?");
        ~chatnpc("<p,quiz>Do you still want to try and undo the locks for me?");
        @multi2("Yeah, okay, let's give it a go.", male_slave_unlock, "I need to do some other things first.", male_slave_otherthing);
    case 2 : // todo - recheck mesanims here
        ~chatplayer("<p,neutral>That sounds awful.");
        ~chatnpc("<p,neutral>Yeah, bunch of no hopers, tried to rob me blind. But I guess that's what you get when you deal with convicts.");
}

[label,male_slave_unlock]
// https://youtu.be/Q1sqb_d2HGc?si=fy8Ow9JhA5rIiDhS&t=114
~chatplayer("<p,neutral>Yeah, okay, let's give it a go.");
~chatnpc("<p,neutral>Great!");
@male_slave_picklock;

[label,male_slave_picklock]
~mesbox("You use some nearby bits of wood and wire to try and pick the lock.");
def_int $fail = random(3);
def_int $attempts = getbit_range(%desertrescue_map_mechanisms, ^desertrescue_lock_attempts1, ^desertrescue_lock_attempts2); // OSRS has this mechanic stored as varb in varp 907, might be rework exclusive?
if($attempts = 2) $fail = add($fail, 2);
// you can get caught by guards first time in osrs, in rsc (at least openrsc) its always on 2nd attempt
if($fail = 0) {
    %desertrescue_map_mechanisms = setbit_range_toint(%desertrescue_map_mechanisms, add($attempts, 1), ^desertrescue_lock_attempts1, ^desertrescue_lock_attempts2);
    mes("You fail!");
    ~mesbox("You didn't manage to pick the lock this time, would you like another go?");
    switch_int(~p_choice2("Yeah I'll give it another go.", 1, "I'll try something different instead.", 2)) {
        case 1 : @male_slave_picklock;
        case 2 :
            mes("You decide to try something else.");
            ~chatnpc("<p,confused>Are you givin' in already?");
            ~chatplayer("<p,quiz>I just want to try something else.");
            ~chatnpc("<p,bored>Okay, if you want to try again, let me know.");
    }
} else if ($fail = 1) {
    %desertrescue_map_mechanisms = setbit_range_toint(%desertrescue_map_mechanisms, add($attempts, 1), ^desertrescue_lock_attempts1, ^desertrescue_lock_attempts2);
    mes("You fail!");
    if(.npc_find(coord, mercenary_1, 10, 0) = true | .npc_find(coord, mercenary_2, 10, 0) = true) {
        if_close;
        mes("A nearby guard spots you!");
        p_delay(3);
        npc_say("Oh oh!");
        .npc_say("Oi, what are you two doing?");
        mes("Slave: Oh oh!");
        mes("Guard: Oi, what are you two doing?");
        ~desertcamp_guard_search;
    }
} else {
    %desertrescue_progress = ^desertrescue_freed_slave;
    ~mesbox("You hear a satisfying 'click' as you tumble the lock mechanism.");
    ~chatnpc("<p,neutral>Great! You did it! Do you want to trade clothes now?");
    @multi2("Yes, I'll trade.", male_slave_trade, "No thanks...", male_slave_notrade);
}

[label,male_slave_trade]
~chatplayer("<p,neutral>Yes, I'll trade.");
if((inv_total(inv, desert_shirt) = 0 & inv_total(worn, desert_shirt) = 0) & (inv_total(inv, desert_robe) = 0 & inv_total(worn, desert_robe) = 0) & (inv_total(inv, desert_boots) = 0 & inv_total(worn, desert_boots) = 0)) {
    ~chatnpc("<p,neutral>I need a desert robe, a desert shirt and desert boots if you want these clothes off me.");
    return;
} else if((inv_total(inv, desert_shirt) = 0 & inv_total(worn, desert_shirt) = 0) & (inv_total(inv, desert_robe) = 0 & inv_total(worn, desert_robe) = 0)) {
    ~chatnpc("<p,neutral>I need a desert robe and a desert shirt if you want these clothes off me.");
    return;
} else if((inv_total(inv, desert_shirt) = 0 & inv_total(worn, desert_shirt) = 0) & (inv_total(inv, desert_boots) = 0 & inv_total(worn, desert_boots) = 0)) {
    ~chatnpc("<p,neutral>I need a desert shirt and desert boots if you want these clothes off me.");
    return;
} else if((inv_total(inv, desert_robe) = 0 & inv_total(worn, desert_robe) = 0) & (inv_total(inv, desert_boots) = 0 & inv_total(worn, desert_boots) = 0)) {
    ~chatnpc("<p,neutral>I need a desert robe and desert boots if you want these clothes off me.");
    return;
} else if(inv_total(inv, desert_shirt) = 0 & inv_total(worn, desert_shirt) = 0) {
    ~chatnpc("<p,neutral>I need a desert shirt if you want these clothes off me.");
    return;
} else if(inv_total(inv, desert_robe) = 0 & inv_total(worn, desert_robe) = 0) {
    ~chatnpc("<p,neutral>I need a desert robe if you want these clothes off me.");
    return;
} else if(inv_total(inv, desert_boots) = 0 & inv_total(worn, desert_boots) = 0) {
    ~chatnpc("<p,neutral>I need desert boots if you want these clothes off me.");
    return;
}

// based off this the gear swapping did exist originally https://web.archive.org/web/20060908030728/http://www.runeweb.net/index.php?page=rs2-quest-touristtrap
// "But he can't get the chains off. Offer to undo them for him and trade your clothes with his. You will now be wearing the Slave outfit!"
if(%desertrescue_progress = ^desertrescue_freed_slave) %desertrescue_progress = ^desertrescue_traded_clothes;
~chatnpc("<p,happy>Great! You have the Desert Clothes! @dbl@-- The slave starts @dbl@undressing right in front of you --@bla@ Okay, here's the @bla@clothes, I won't need them anymore.");
mes("The slave takes your desert robe.");
mes("The slave takes your desert shirt.");
mes("The slave takes your desert boots.");
def_boolean $traded_robe = false;
def_boolean $traded_shirt = false;
def_boolean $traded_boots = false;
if(inv_total(inv, desert_robe) > 0) {
    inv_del(inv, desert_robe, 1);
    inv_add(inv, slave_robe, 1);
    $traded_robe = true;
}
if(inv_total(inv, desert_shirt) > 0) {
    inv_del(inv, desert_shirt, 1);
    inv_add(inv, slave_shirt, 1);
    $traded_shirt = true;
}
if(inv_total(inv, desert_boots) > 0) {
    inv_del(inv, desert_boots, 1);
    inv_add(inv, slave_boots, 1);
    $traded_boots = true;
}
~objbox(slave_robe, "The slave gives you his dirty, flea infested robe.", 250, 0, 0);
~objbox(slave_shirt, "The slave gives you his muddy, sweat soaked shirt.", 250, 0, 0);
~objbox(slave_boots, "The slave gives you a smelly pair of decomposing boots.", 250, 0, divide(^objbox_height, 2));

if(inv_total(worn, desert_robe) > 0 & $traded_robe = false) {
    inv_setslot(worn, ^wearpos_legs, slave_robe, 1);
}
if(inv_total(worn, desert_shirt) > 0 & $traded_shirt = false) {
    inv_setslot(worn, ^wearpos_torso, slave_shirt, 1);
}
if(inv_total(worn, desert_boots) > 0 & $traded_boots = false) {
    inv_setslot(worn, ^wearpos_feet, slave_boots, 1);
}
~update_bas;
npc_changetype(escaping_slave, ^max_32bit_int);
npc_settimer(200); // 200t osrs
//npc_delay(9); // can't interact for 10t?
~chatnpc_specific(nc_name(escaping_slave), escaping_slave, "<p,neutral>Right, I'm off! Good luck!");
~chatplayer("<p,neutral>Yeah, good luck to you too!");

[label,male_slave_notrade]
~chatplayer("<p,neutral>No thanks...");
~chatnpc("<p,neutral>Okay, fair enough, let me know if you change your mind though.");

[label,male_slave_otherthing]
~chatplayer("<p,neutral>I need to do some other things first.");
~chatnpc("<p,neutral>Okay, fair enough. Let me know when you want to give it another go.");

[label,male_slave_shame]
~chatplayer("<p,neutral>Oh, that's a shame...");
~chatplayer("<p,neutral>Still, 'worse things happen at sea right?'");
~chatnpc("<p,confused>You've obviously never worked as a slave");
~chatnpc("<p,angry>...in a mining camp...");
~chatnpc("<p,angry>...in the middle of the desert.");
~chatplayer("<p,neutral>Well I suppose I'd better be getting on my way now...");
~chatnpc("<p,silent>@dbl@-- The slave nods in agreement and goes back to@dbl@work.");

[ai_timer,escaping_slave]
npc_changetype(male_slave, ^max_32bit_int);

[opnpc1,escaping_slave]
if(%desertrescue_progress >= ^desertrescue_traded_clothes) {
    // this only checks worn
    if(inv_total(worn, slave_shirt) = 0 | inv_total(worn, slave_robe) = 0 | inv_total(worn, slave_boots) = 0) {
        npc_changetype(male_slave, ^max_32bit_int);
        ~chatnpc("<p,neutral>Oh bother, I was caught by the guards again... Listen, if you can get me some Desert Clothes, I'll trade you for my slaves clothes again.. Do you want to trade?");
        @multi2("Yes, I'll trade.", male_slave_trade, "No thanks...", male_slave_notrade);
    } else {
        ~chatnpc("<p,neutral>Not much to do here but mine all day long.");
    }
} else {
    ~chatnpc("<p,neutral>Not much to do here but mine all day long."); // need to confirm idk
}

[opnpc1,rowdy_slave]
switch_int(random(5)) {
    case 0 : npc_say("Hey, you're in for a good beating!");
    case 1 : npc_say("I'm gonna teach you some respect!");
    case 2 : npc_say("Oi! Are you looking at me!");
    case 3 : npc_say("Oi! I'm gonna give you some trouble!");
    case 4 : npc_say("You wanna piece of me?");
}
~npc_retaliate(0);

[ai_queue3,rowdy_slave]
if (finduid(%npc_aggressive_player) = true) { 
    if(inv_total(inv, slave_shirt) = 0) {
        obj_add(npc_coord, slave_shirt, 1, ^lootdrop_duration);
        mes("The slave drops his shirt.");
    }
    if(inv_total(inv, slave_robe) = 0) {
        obj_add(npc_coord, slave_robe, 1, ^lootdrop_duration);
        mes("The slave drops his robe.");
    }
    if(inv_total(inv, slave_boots) = 0) {
        obj_add(npc_coord, slave_boots, 1, ^lootdrop_duration);
        mes("The slave drops his boots.");
    }
}
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
