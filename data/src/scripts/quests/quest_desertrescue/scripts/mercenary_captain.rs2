[opnpc1,npc_830]
if(%desertrescue_progress <= ^desertrescue_not_started) {
    ~chatnpc("<p,angry>Move along now... we've had enough of your sort!");
} else {
    ~mesbox("You approach the Mercenary Captain.");
    if(%desertrescue_progress = ^desertrescue_started) %desertrescue_progress = ^desertrescue_approached_captain;
    @multi3("Wow! A real captain!", mercenary_capt_realcapt, "You there!", mercenary_capt_you, "Hey ugly!", mercenary_capt_ugly);
}

[label,mercenary_capt_realcapt]

[label,mercenary_capt_you]

[label,mercenary_capt_ugly]
~chatplayer("<p,happy>Hey ugly!");
~chatnpc("<p,angry>I will not tolerate such insults.. Guards, kill <text_gender("him", "her")>!");
~mesbox("The captain marches away in disgust leaving his guards to tackle you.");
@mercenary_capt_randompunish;

[label,mercenary_capt_randompunish]
def_int $cur_stage = getbit_range(%desertrescue_map_mechanisms, 0, 2);
def_int $next_stage = calc($cur_stage + 1);
if($next_stage > 3) $next_stage = 0;
%desertrescue_map_mechanisms = setbit_range_toint(%desertrescue_map_mechanisms, $next_stage, 0, 2);
if(npc_find(coord, npc_828, 7, 0) = true) {
    switch_int($cur_stage) {
        case 0 :
            ~mesbox("A guard approaches you and pretends to start hitting you.");
            if_close;
            p_delay(2);
            ~chatnpc("<p,neutral>Take that you infidel!");
            ~mesbox("The guard leans closer to you and says in a low voice.");
            ~chatnpc("<p,neutral>We're sick of having to kill every lunatic that comes along and insults the captain, it makes such a mess. Thankfully, he's a bit decrepit so he doesn't notice so please, buzz off and don't come here again.");
        case 1 :
            ~mesbox("The guard approaches you again and gives you a sharp kick.");
            ~damage_self(1);
            say("Ow!");
            ~chatnpc("<p,neutral>Take that you mad child of a dog!"); // can't get past this in osrs cause damage closes if, RSC dialogue
            ~mesbox("The guard leans closer to you and says in a low voice.");
            ~chatnpc("<p,neutral>What are you doing here again?|Didn't I tell you to get out of here!|Now get lost, properly this time!|Or we may be forced to see his orders through properly.");
        case 2 : 
            ~chatnpc("<p,neutral>Prepare to die effendi!");
            ~mesbox("The guard leans close and whispers");
            ~chatnpc("<p,neutral>Are you mad effendi? This is your last chance! Leave now and never come back or I'll introduce you to my friend.");
            ~objbox(bronze_scimitar, "The guard half draws his fearsome looking scimitar.", 250, 0, 0);
            ~chatnpc("<p,happy>And we'll be pleased to clean the mess up after you've been dispatched.");
        case 3 :
            if_close;
            mes("An angry guard approaches you and whips out his sword.");
            p_delay(2);
            npc_say("Okay, that does it!");
            p_delay(2);
            npc_say("You're in serious trouble now!");
            p_delay(2);
            npc_say("Okay men, we need to teach this person a thing or two");
            p_delay(2);
            npc_say("about desert survival techniques.");
            p_delay(2);
            mes("The guards grab you and rough you up a bit.");
            p_delay(2);
            mes("You're grabbed and manhandled onto a cart.");
            p_delay(2);
            mes("Sometime later you're dumped in the middle of the desert.");
            switch_int(random(2)) {
                case 0 : p_teleport(0_50_48_38_19);
                case 1 : p_teleport(0_50_48_9_47);
            }
            p_delay(0);
            // seq doesn't exist (probably rework)
            if(inv_total(inv, water_skin1) > 0 | inv_total(inv, water_skin2) > 0 | inv_total(inv, water_skin3) > 0 | inv_total(inv, water_skin4) > 0) {
                mes("Guard: You won't be needing that water anymore!");
                mes("The guards throw your water away.");
                inv_del(inv, water_skin1, ^max_32bit_int);
                inv_del(inv, water_skin2, ^max_32bit_int);
                inv_del(inv, water_skin3, ^max_32bit_int);
                inv_del(inv, water_skin4, ^max_32bit_int);
                if(inv_total(inv, water_skin0) = 0) inv_add(inv, water_skin0, 1);
            }
            mes("The guards move off leaving you stranded in the desert.");
            ~mesbox("The guards move off leaving you stranded in the desert.");
    }
}

[opnpc3,npc_830]
~mesbox("You watch the Mercenary Captain for some time.|He has a large metal key attached to his belt.|You notice that he usually gets his men to do his dirty work.");

[proc,mercenary_capt_attack](boolean $npct)(boolean)
if($npct = true) { 
    p_delay(0); 
}
~mesbox("This mercenary Captain looks very fierce.|Are you sure you want to attack him?");
def_int $op = ~p_choice2("Yes, I can take him on!", 1, "Er, no thanks, I've had second thoughts about it.", 2);
if($op = 2) {
    ~mesbox("You decide not to fight the Mercenary Captain.");
    return (false);
}
npc_say("Kill that intruder guards!");
npc_setmode(playerescape);
if(npc_find(coord, npc_828, 7, 0) = true) ~mercenary_attack;
return (false);