[oploc1,tunnelstairstop] @dwarven_tunnel;
[oploc1,tunnelstairstop2] @dwarven_tunnel;

[oploc1,tunnelstairs] p_telejump(0_44_54_4_30);
[oploc1,tunnelstairs2] p_telejump(0_44_54_61_26);

[label,dwarven_tunnel]
if(%fishingcompo_progress = ^fishingcompo_complete) {
    if(loc_type = tunnelstairstop) {
        p_telejump(0_44_154_4_26);
    } else if(loc_type = tunnelstairstop2) {
        p_telejump(0_44_154_60_22);
    }
    return;
} else if(npc_find(coord, tunnel_dwarf, 5, 0) = true) {
    switch_int (%fishingcompo_progress) {
        case ^fishingcompo_not_started : @tunnel_dwarf_stairs;
        case default : @tunnel_dwarf_won;
    }
}

[oploc1,_hemenster_gate]
if(coordx(coord) <= coordx(loc_coord)) {
    if(%fishingcompo_progress >= ^fishingcompo_in_comp & %fishingcompo_progress < ^fishingcompo_won_comp | loc_type = fishinggateclosedr & %fishingcompo_progress < ^fishingcompo_won_comp) {
        // On OSRS this dialogue always happens (during the quest) when leaving from the south side of the gate, and 
        // on both sides during an active comp. Not sure if this is a rework thing since Bonzo is sitting on a table by the south side
        // post-rework, but we'll use this behaviour for now
        if(npc_find(coord, bonzo, 12, 0) = true) {
            @bonzo_quits;
        }
    }
    p_teleport(movecoord(loc_coord, 1, 0, 0));
    ~open_fishingcompo_gate;
    return;
}
~chatnpc_specific("Morris", morris, "<p,neutral>Competition pass please.");
if(inv_total(inv, fishing_competition_pass) > 0) {
    ~objbox(fishing_competition_pass, "You show Morris your pass.", 250, 0, 0);
    ~chatnpc_specific("Morris", morris, "<p,happy>Move on through.");
    if_close;
    p_teleport(loc_coord);
    ~open_fishingcompo_gate;
    return;
}
def_int $option = ~p_choice2("I don't have one of them.", 1, "What do I need that for?", 2);
if($option = 1) {
    ~chatplayer("<p,sad>I don't have one of them.");
} else if($option = 2) {
    ~chatplayer("<p,confused>What do I need that for?");
    ~chatnpc_specific("Morris", morris, "<p,neutral>This is the entrance to the Hemenster fishing|competition. It's a high class competition.|Invitation only.");
}

[proc,open_fishingcompo_gate]
loc_findallzone(coord);
while(loc_findnext = true) {
     if(loc_category = hemenster_gate) {
            def_coord $central_coord = loc_coord;
            def_int $orig_angle = loc_angle;
            def_loc $type = loc_type;
            // Temp note: dur updated
            loc_change(inviswall, 3);
            if($type = fishinggateclosedl) {
                // Temp note: dur updated
                loc_add(movecoord($central_coord, 1, 0, 0), inacfishinggateclosedl, 1, loc_shape, 3);
                loc_add(movecoord($central_coord, 2, 0, 0), inacfishinggateclosedr, 1, loc_shape, 3); 
            }
     }
}
sound_synth(door_open, 0, 0);
p_delay(0);

// The pipe has some odd interactions in OSRS, but I think this probably related
// to the rework (https://oldschool.runescape.wiki/w/Wall_Pipe), based off this image
// I think it probably works very similarly to RSC: https://web.archive.org/web/20130324085821im_/http://www.runeweb.net/fireball/Fishing%20Contest%20Images/FishingContest4.JPG
[oplocu,garlicpipe]
switch_obj (last_useitem) {
    case garlic : @stash_garlic;
    case default : ~displaymessage(^dm_default);
}

// option doesn't exist in RSC, but based off the 2005 image, I think it probably always sends nothing interesting happens
// when searching it (this matches the behaviour of OSRS when searching it from 3-8 tiles away)
[oploc1,garlicpipe] ~displaymessage(^dm_default);

[label,stash_garlic]
mes("You stash the garlic in the pipe.");
p_delay(2);
inv_del(inv, garlic, 1);
%hemenster_pipe_stashed = true;
if(%hemenster_comp_stage = ^hemenster_comp_paidfee) {
    // This uses a new stage on OSRS (probably for quest journal?)
    %fishingcompo_progress = ^fishingcompo_garlic_comp;
    ~move_hemenster_pipe;
}

[proc,move_hemenster_pipe]
if(npc_find(coord, sinister_stranger, 8, 0) = true) {
    ~chatnpc("<p,angry>Arrgh! WHAT is that GHASTLY smell???|I think I will move over here instead...");
    npc_tele(0_41_53_7_43); // telejumps in classic
    npc_facesquare(movecoord(npc_coord, 1, 0, 0));
    if (npc_find(coord, bonzo, 12, 0) = true) {
        %fishingcompo_progress = ^fishingcompo_garlic_comp;
        ~chatnpc("<p,confused>Hmm. You'd better go and take the area by the pipes then.");
        ~mesbox("Your fishing competition spot is now beside the pipes.");
    }
}

[queue,fishingcompo_quest_complete]
%fishingcompo_progress = ^fishingcompo_complete;
session_log(^log_adventure, "Quest complete: Fishing Contest");
~send_quest_complete(questlist:fishingcompo, hemenster_fishing_trophy, 180, ^fishingcompo_questpoints, "You have completed the\\nFishing Competition Quest!");
stat_advance(fishing, 24370);