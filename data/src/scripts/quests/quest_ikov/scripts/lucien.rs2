[opnpc1,lucien_ardougne]
switch_int (%ikov_progress) {
    case ^ikov_started:
        ~chatnpc("<p,angry>I told you not to meet me here again!");
        if(inv_total(inv, obj_86) = 0) {
            ~chatplayer("<p,sad>I've lost the pendant you gave me.");
            ~chatnpc("<p,angry>Imbecile!");
            inv_add(inv, obj_86, 1);
            ~objbox(obj_86, "Lucien has given you another pendant!");
            return;
        }
        ~chatplayer("<p,confused>Sorry! Can you remind me of my mission?");
        ~chatnpc("<p,angry>My patience grows thin hero!");
        ~chatnpc("<p,neutral>I need the Staff of Armadyl. It's in the Temple of Ikov, near Hemenster, north east of here.");
        ~chatplayer("<p,happy>I'm up for it!");
    case ^ikov_not_started :
        // https://youtu.be/sCl0GprumNE?si=pzg_lmiunV4RJNxn&t=27
        ~chatnpc("<p,neutral>I seek a hero to go on an important mission!");
        @multi2("I'm a mighty hero!", lucien_mightyhero, "Yep, lots of heros about these days.", lucien_heros);
}

[label,lucien_mightyhero]
~chatplayer("<p,angry>I am a mighty hero!");
~chatnpc("<p,neutral>I require the Staff of Armadyl. It is in the deserted Temple of Ikov, near Hemenster, north east of here.");
~chatnpc("<p,angry>Take care hero! There is a dangerous monster somewhere in the temple!");
@multi4("Why can't you get it yourself?", lucien_why_yourself, "That sounds like a laugh!", lucien_laugh, "Oh no! Sounds far too dangerous!", lucien_no, "What's the reward?!", lucien_reward);

[label,lucien_why_yourself]
~chatplayer("<p,confused>Why can't you get it yourself?");
~chatnpc("<p,angry>The guardians of the Staff of Armadyl fear me!");
~chatnpc("<p,sad>They have set up a magical barrier which even my power cannot overcome!");
@multi3("That sounds like a laugh!", lucien_laugh, "Oh no! Sounds far too dangerous!", lucien_no, "What's the reward?", lucien_reward);

[label,lucien_laugh]
// https://youtu.be/cePHhIOqsqg?si=lJX8ygtjkYXnXWag&t=18
~chatplayer("<p,happy>That sounds like a laugh!");
~chatnpc("<p,sad>It's not as easy as it sounds.|The monster can only be killed with a weapon of ice.|There are many other dangers.");
~chatplayer("<p,happy>I'm up for it!");
~chatnpc("<p,neutral>Take this pendant. Without it you will not be able to enter the Chamber of Fear.");
%ikov_progress = ^ikov_started;
inv_add(inv, obj_86, 1);
~objbox(obj_86, "Lucien has given you a pendant!");
~chatnpc("<p,sad>I cannot stay here much longer.");
~chatnpc("<p,neutral>I will be in the forest north of Varrock.|I have a small holding up there.");

[label,lucien_no]
~chatplayer("<p,sad>Oh no! Sounds far too dangerous!");
~chatnpc("<p,angry>Wimp! Call yourself a hero?! My daughter is more of a hero than you!");

[label,lucien_reward]
~chatplayer("<p,happy>What's the reward?");
~chatnpc("<p,neutral>I see you are the mercenary type.");
~chatplayer("<p,neutral>It's a living.");
~chatnpc("<p,happy>I will reward you well if you bring me the staff.");
@multi2("That sounds like a laugh!", lucien_laugh, "I'll pass this time.", lucien_pass);

[label,lucien_pass]
~chatplayer("<p,neutral>I'll pass this time.");
~chatnpc("<p,sad>Hmph.");

[label,lucien_heros]
~chatplayer("<p,neutral>Yep, lots of heros about these days.");
~chatnpc("<p,neutral>Well if you see any be sure to point them in my direction.");

