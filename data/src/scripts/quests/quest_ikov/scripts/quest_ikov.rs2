[oploc1,loc_102]
if(%ikov_progress >= ^ikov_started) {
    ~open_and_close_door2(loc_1532, ~check_axis_locactive(coord), door_open);
    return;
}
mes("The door won't open.");

[proc,randomize_ice_arrow_chest]
switch_int(random(6)) {
    case 0: %ikov_icearrowchest_coord = 0_42_153_22_58;
    case 1: %ikov_icearrowchest_coord = 0_42_153_31_46;
    case 2: %ikov_icearrowchest_coord = 0_42_153_41_58;
    case 3: %ikov_icearrowchest_coord = 0_42_153_59_56;
    case 4: %ikov_icearrowchest_coord = 0_42_153_50_43;
    case 5: %ikov_icearrowchest_coord = 0_42_153_57_29;
}

[proc,lucien_attack](boolean)
~chatnpc("<p,neutral>You don't want to attack me. I am your friend.");
if_close;
p_delay(0);
npc_anim(seq_805, 0);
p_delay(1);
~mesbox("You decide not to attack Lucien. He is your friend.");
return (false);

[opobj3,staff_of_armadyl]
if(%ikov_progress >= ^ikov_completed_armadyl) {
    mes("You have completed the quest of the Temple of Ikov.");
    return;
}
if(%ikov_progress = ^ikov_helping_armadyl) {
    mes("You decide not to steal the staff as you have agreed to help the Guardians.");
    return;  
}
if(~obj_gettotal(staff_of_armadyl) > 0) {
    mes("I already have the staff.");
    return;
}
npc_huntall(coord, 5, 1); // requires los
while (npc_huntnext = true) {
    if (npc_category = guardian_of_armadyl) {
        npc_say("That is not thine to take!");
        npc_setmode(opplayer2);
        return;
    }
}
@pickup_obj(obj_coord, obj_type, obj_count);