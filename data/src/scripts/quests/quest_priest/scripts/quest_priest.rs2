// Quest was reworked 22 May 2006
// https://oldschool.runescape.wiki/w/Father_Aereck
// Sources:
// (2005) https://www.youtube.com/watch?v=QX-ieoyDXh4
// https://runescapeclassic.fandom.com/wiki/The_Restless_Ghost
[oploc1,loc_2145]
p_arrivedelay;
mes("You open the coffin.");
anim(human_openchest, 0);
sound_synth(coffin_open, 0, 0);
~check_restlessghost_spawn; // probably before delay like closing
p_delay(0);
loc_change(loc_2146, 300);

[oploc1,loc_2146]
mes("You close the coffin.");
anim(human_closechest, 0);
sound_synth(coffin_close, 0, 0);
~check_restlessghost_spawn; // https://youtu.be/QX-ieoyDXh4?si=mJjuZG052xLGiVWq&t=950
p_delay(0);
loc_change(loc_2145, 300);

[proc,check_restlessghost_spawn]
if(%priest_progress = ^priest_complete) { // Don't send ghost if the quest is complete
    return;
}
if (npc_find(loc_coord, restless_ghost, 4, 0) = false) {
    npc_add(0_50_49_49_58, restless_ghost, 100);
}

[oploc2,loc_2146]
if (%priest_progress = ^priest_complete) {
    ~mesbox("Theres a nice and complete skeleton in here!");
} else if (%priest_progress >= ^priest_started) {
    ~mesbox("There's a skeleton without a skull in here.");
} else {
    mes("You search the coffin and find some human remains.");
}
~check_restlessghost_spawn;

[oplocu,loc_2145]
if (last_useitem = ghost_skull & %priest_progress = ^priest_obtained_skull) {
    ~mesbox("Maybe I should open it first.");
    return;
}
~displaymessage(^dm_default);

[oplocu,loc_2146]
if (last_useitem = ghost_skull & %priest_progress = ^priest_obtained_skull) {
    ~mesbox("You put the skull in the coffin.");
    inv_del(inv, ghost_skull, 1);
    if(npc_find(coord, restless_ghost, 4, 0) = true) {
        npc_del;
    }
    queue(priest_quest_complete, 0);
    ~mesbox("The ghost vanishes.");
    ~mesbox("You think you hear a faint voice on the wind...Thank... you...");
    return;
}
~displaymessage(^dm_default);

[opobj3,ghost_skull]
if(inv_total(inv, ghost_skull) > 0 | inv_total(bank, ghost_skull) > 0) {
    ~mesbox("You already have the Ghost's skull.");
    return;
}
if (%priest_progress = ^priest_spoken_ghost | %priest_progress = ^priest_obtained_skull) {
    def_coord $skeleton_spawnpoint = 0_48_149_48_29;
    %priest_progress = ^priest_obtained_skull;
    npc_add($skeleton_spawnpoint, priest_skeleton, 500);
    ~npc_retaliate(0);
    mes("Out of nowhere a skeleton appears!");
    @pickup_obj(obj_coord, obj_type, obj_count);
} else {
    // https://youtu.be/QX-ieoyDXh4?t=370
    ~mesbox("That skull looks scary. I've got no reason to take it, I think I'll leave|it alone.");
}

[opheld5,ghost_skull]
// objbox in osrs, dont think so here considering the objbox's in this quest are mesbox pre-rework 
~mesbox("Dropping this skull here will destroy it!");
def_int $choice = ~p_choice2_header("Yes.", 1, "No.", 2, "Are you sure you want to drop the skull?");
if ($choice = 1) {
    inv_del(inv, ghost_skull, 1);
}
if_close;

[queue,priest_quest_complete]
%priest_progress = ^priest_complete;
~send_quest_complete(questlist:priest, ghost_skull, 220, ^priest_questpoints, "You have completed the\\nRestless Ghost Quest!");
givexp(prayer, 11250);

