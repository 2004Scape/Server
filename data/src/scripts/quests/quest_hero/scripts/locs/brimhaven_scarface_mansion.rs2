[oploc1,brimhaven_mansion_door]
if (%hero_progress >= ^hero_blackarm_mansion_unlocked & %blackarmgang_progress >= ^blackarmgang_complete) {
  // todo - check if wearing black armor?
  ~open_and_close_door(loc_type, ~check_axis(coord, loc_coord, loc_angle), false);
  return;
}
@attempt_open_brimhaven_mansion_door;

[oploc1,grips_cupboard_closed]
// Temp note: dur does not need updated
loc_change(grips_cupboard_opened, 500); // todo - how long to keep open?
@summon_grip;

[oploc1,grips_cupboard_opened]
@summon_grip;

[label,summon_grip]
if (npc_find(coord, pirate_guard, 4, 0) = true) {
  facesquare(npc_coord);
  ~chatnpc("<p,confused>I don't think Mr Grip will like you opening that. That's his private drinking cabinet.");
  switch_int(~p_choice2("He won't notice me having a quick look.", 1, "Ok, I'll leave it.", 2)) {
    case 1 :
      ~chatplayer("<p,neutral>He won't notice me having a quick look.");
      if (npc_find(coord, grip, 12, 0) = true) {
        npc_setmode(none);
        npc_walk(0_43_49_25_62);
        npc_say("Stay out of my drinks cabinet!");
        if_close;
        p_delay(6);
        npc_setmode(null);
      }
    case 2 :
      ~chatplayer("<p,neutral>Ok, I'll leave it.");
  }
}

[oploc1,brimhaven_mansion_backdoor]
if (coordz(coord) = coordz(loc_coord)) {
  ~open_and_close_door(loc_type, ~check_axis(coord, loc_coord, loc_angle), false);
  return;
}
~mesbox("This door is locked.");
~chatplayer("<p,neutral>This room isn't a lot of use on its own... Maybe I can|get extra help from the inside somehow... I wonder if|any of the other players have found a way in.");

[oplocu,brimhaven_mansion_backdoor]
if (last_useitem = misc_key) {
  ~open_and_close_door(loc_type, ~check_axis(coord, loc_coord, loc_angle), false);
  return;
}
~displaymessage(^dm_default);

[oploc1,scarface_pete_trophy_room_door]
// opening chest room https://youtu.be/KRhwwgo8YHM?t=524 , https://youtu.be/jo9mc5vtrQ4?t=576 
if (coord = loc_coord) {
  mes("The door locks shut behind you.");
  ~open_and_close_door(loc_type, ~check_axis(coord, loc_coord, loc_angle), true);
  return;
}
mes("The door is locked.");

[oplocu,scarface_pete_trophy_room_door]
if (last_useitem = grip_keys) {
  if (%hero_progress < ^hero_blackarm_id_papers_given) {
    // todo - need dialogue if you try opening door without progressing correctly
    mes("The key doesn't fit.");
  } else {
    mes("Grip's key unlocks the door.");
    ~open_and_close_door(loc_type, ~check_axis(coord, loc_coord, loc_angle), false);
  }
  return;
}
~displaymessage(^dm_default);

[oploc1,scarface_pete_candlestick_chest]
mes("You open the chest.");
// Temp note: dur does not need updated
loc_change(scarface_pete_candlestick_chest_open, 500); // todo - how long to keep open?

[oploc1,scarface_pete_candlestick_chest_open]
if (~obj_gettotal(petecandlestick) > 0) {
  ~mesbox("The chest is empty.");
  if(inv_total(bank, petecandlestick) > 0) {
    ~chatplayer("<p,happy>I'm sure I've got a candlestick from this chest somewhere in my bank.");
  }
} else {
  ~mesbox("You find two candlesticks in the chest. So that will be one for you, and one for the person who killed Grip for you.");
  inv_add(inv, petecandlestick, 2);
  if (%hero_progress < ^hero_blackarm_looted_chest) {
    %hero_progress = ^hero_blackarm_looted_chest;
  }
}
// Temp note: dur does not need updated
loc_change(scarface_pete_candlestick_chest, 500);
