[opnpc1,garv]
if (~unlock_brimhaven_mansion = true) {
  return;
}
~chatnpc("<p,neutral>Hello. What do you want?");
switch_int(~p_choice2("Can I go in there?", 1, "I want for nothing!", 2)) {
  case 1 :
    ~chatplayer("<p,quiz>Can I go in there?");
    if (%hero_progress >= ^hero_blackarm_mansion_unlocked & %blackarmgang_progress >= ^blackarmgang_complete) {
      ~chatnpc("<p,neutral>Sure."); // todo need dialogue if you talk to Garv after unlocking the door
    } else {
      ~chatnpc("<p,neutral>No. In there is private");
    }
  case 2 :
    ~chatplayer("<p,neutral>I want for nothing!");
    ~chatnpc("<p,neutral>You're one of a very lucky few then.");
}

[label,attempt_open_brimhaven_mansion_door]
if (npc_find(coord, garv, 7, 0) = true) {
  facesquare(npc_coord);
  ~chatnpc("<p,angry>Oi! Where do you think you're going pal?");
  if (~unlock_brimhaven_mansion = true) {
    return;
  }
}
~displaymessage(^dm_default);

[proc,unlock_brimhaven_mansion]()(boolean)
if (%hero_progress = ^hero_blackarm_id_papers_obtained) { 
  ~chatplayer("<p,neutral>Hi. I'm Hertigen. I've come to work here.");
  if(~wearing_black_armour = false) { // https://youtu.be/rEzuCix51aM?t=1503 
    ~chatnpc("<p,neutral>Hartigen the Black Knight? I don't think so. He doesn't dress like that"); 
    return(false);
  }
  ~chatnpc("<p,neutral>I assume you have your I.D. papers then?");
  if (inv_total(inv, id_papers) > 0) {
    ~chatnpc("<p,neutral>You'd better come in then. Grip will want to talk to you.");
    %hero_progress = ^hero_blackarm_mansion_unlocked;
    return(true);
  } else {
    ~chatplayer("<p,neutral>Uh... Yeah. About that...I must have left them in my other suit of armour.");
    return(false);
  }
} else {
  return(false);
}
