[opnpc1,grip]
// https://youtu.be/rEzuCix51aM?t=1582 - showing speaking again after giving ID papers
if (%hero_progress < ^hero_blackarm_id_papers_given) {
  ~chatplayer("<p,neutral>Hi there. I am Hertigen. reporting for duty as your new deputy sir!");
  ~chatnpc("<p,neutral>Ah good, at last. You took your timee getting here! Now let me see...");
  ~chatnpc("<p,neutral>I'll get your hours and duty roster sorted out in a while. Oh, and do you have your I.D. papers with you? Internal security is almost as important as external security for a guard.");
  if(inv_total(inv, id_papers) < 1) {
    // https://youtu.be/KRhwwgo8YHM?t=424 - OSRS talking to grip without ID paper
    ~chatplayer("<p,neutral>Oh dear... I don't have that with me anymore..");
    ~chatnpc("<p,neutral>Well that's no good! Go get them immediately, then report back for duty.");
    return;
  }
  ~chatplayer("<p,neutral>Right here sir!");
  ~mesbox("You hand the ID papers over to Grip.");
  inv_del(inv, id_papers, 1);
  %hero_progress = ^hero_blackarm_id_papers_given;
}
@grip_chat_options(^grip_stage_first_talk);

[label,grip_chat_options](int $stage)
def_int $option;
switch_int($stage) {
  case ^grip_stage_first_talk :
    $option = ~p_choice3("So can I guard the treasure room please?", 1, "So what do my duties involve?", 2, "Well, I'd better sort my new room out.", 3);
  case ^grip_stage_asked_duties :
    $option = ~p_choice3("So can I guard the treasure room please?", 1, "Well, I'd better sort my new room out.", 3, "Anything I can do now?", 4);
  case ^grip_stage_asked_treasure :
    $option = ~p_choice2("So what do my duties involve?", 2, "Well, I'd better sort my new room out.", 3);
}
switch_int($option) {
  case 1 :
    // https://youtu.be/1lx2X_PvEfY?t=735
    ~chatplayer("<p,quiz>So can I guard the treasure room please?");
    ~chatnpc("<p,neutral>Well, I might post you outside it sometimes. I prefer to be the only one inside however.");
    ~chatnpc("<p,neutral>There's some pretty valuable artefacts in there! Those keys stay ONLY with the head guard and Scarface Pete.");
    @grip_chat_options(^grip_stage_asked_treasure);
  case 2 :
    ~chatplayer("<p,quiz>So what do my duties involve?");
    ~chatnpc("<p,neutral>You'll have various guard related duties on various shifts. I'll assign specific duties as the are required as and when they become necessary. Just so you know, if anything happens to me");
    ~chatnpc("<p,neutral>you'll need to take over as head guard here. You'll find important keys to the treasure room and Pete's quarters inside my jacket - although I doubt anything bad's going to happen to");
    ~chatnpc("<p,neutral>me anytime soon!");
    ~mesbox("Grip laughs to himself at the thought.");
    @grip_chat_options(^grip_stage_asked_duties);
  case 3 :
    // https://youtu.be/1lx2X_PvEfY?t=731
    ~chatplayer("<p,neutral>Well, I'd better sort my new room out.");
    ~chatnpc("<p,neutral>Yeah, I'll give you time to settle in. Better get a good nights sleep, I expect you to report for duty at oh five hundred hours tomorrow on the dot!");
  case 4 :
    ~chatplayer("<p,quiz>Anything I can do now?");
    if (~obj_gettotal(miscellaneous_key) < 1) {
      ~chatnpc("<p,neutral>Hmm. Well, you could find out what this key opens for me. Apparently it's for something in this building, but for the life of me I can't find what.");
      inv_add(inv, miscellaneous_key, 1);
      ~mesbox("Grip hands you a key.");
    } else {
      ~chatnpc("<p,neutral>Can't think of anything right now.");
    }
}

[proc,grip_attack]()(boolean)
if (%hero_progress >= ^hero_phoenix_talked_charlie & %phoenixgang_progress >= ^phoenixgang_complete) {
  return(true);
}
~chatplayer("<p,neutral>I can't attack the head guard here! There are too many witnesses around to see me do it! I'd have the whole of Brimhaven after me! Besides, if he dies I want the promotion!");
// todo - I think theres a ~mesbox("") after the chatplayer, but I can't find the source anymore, something about find another player to help
return(false);
