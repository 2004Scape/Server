[opnpc1,doctor_orbon]
switch_int(%sheepherder_progress) {
    case ^sheepherder_not_started: @doctor_orbon_before_sheep_herder;
    case ^sheepherder_tasked_with_talking_to_dr_orbon: @doctor_orbon_looking_for_protective_clothing;
    case ^sheepherder_tasked_with_disposing_of_sheep: @doctor_orbon_post_clothing_purchase;
    case ^sheepherder_complete: @doctor_orbon_after_sheep_herder;
}

[label,doctor_orbon_before_sheep_herder]
~chatplayer("<p,neutral>Hello.");
~chatnpc("<p,quiz>How do you feel?|No heavy flu or the shivers?");
~chatplayer("<p,quiz>No, I'm fine.");
~chatnpc("<p,quiz>How about nightmares?|Have you had any problems with really scary nightmares?");
~chatplayer("<p,quiz>No, not since I was young.");
~chatnpc("<p,happy>Good, good.|Have to be careful nowadays,|the plague spreads faster than a common cold.");
@multi2(
    "The plague? Tell me more.", doctor_orbon_more_about_plague,
    "Ok, I'll be careful.", doctor_orbon_ill_be_more_careful
);

[label,doctor_orbon_more_about_plague]
~chatplayer("<p,neutral>The plague? Tell me more.");
~chatnpc("<p,angry>The virus came from the west and is deadly.");
~chatplayer("<p,neutral>What are the symptoms?");
~chatnpc("<p,confused>Watch out for abnormal nightmares and strong| flu symptoms. When you find a thick black liquid dripping from your nose and eyes,|then no one can save you.");

[label,doctor_orbon_ill_be_more_careful]
~chatplayer("<p,neutral>Ok, I'll be careful.");
~chatnpc("<p,happy>You do that traveller.");

[label,doctor_orbon_looking_for_protective_clothing]
~chatplayer("<p,neutral>Hi Doctor.");
~chatplayer("<p,neutral>I need to acquire some protective clothing|so I can recapture some escaped sheep who have the plague.");
~chatnpc("<p,neutral>I'm afraid I only have one suit|which I made to keep myself safe from infected patients.");
~chatnpc("<p,neutral>I could sell it to you|then I could make myself another.|Hmmm.... I'll need at least 100 gold coins.");
@multi2(
    "Ok, I'll take it.", doctor_orbon_ill_take_it,
    "Sorry doc, that's too much.", doctor_orbon_sorry_doc_thats_too_much
);

[label,doctor_orbon_ill_take_it]
~chatplayer("<p,happy>Ok, I'll take it.");
@doctor_orbon_purchase_plague_outfit;

[label,doctor_orbon_purchase_plague_outfit]
if (inv_total(inv, coins) < 100) {
    ~chatplayer("<p,neutral>Oops, I don't have enough money.");
    ~chatnpc("<p,happy>That's ok, but don't go near those sheep.|If you can find the money I'll be waiting here.");
} else {
    inv_del(inv, coins, 100);
    inv_add(inv, plague_jacket, 1);
    inv_add(inv, plague_trousers, 1);
    mes("You give Doctor Orbon 100 coins.");
    mes("Doctor Orbon gives you a protective suit.");
    if (%sheepherder_progress = ^sheepherder_tasked_with_talking_to_dr_orbon) {
        %sheepherder_progress = ^sheepherder_tasked_with_disposing_of_sheep;
    }
    ~chatnpc("<p,happy>These will keep you safe from the plague");
}

[label,doctor_orbon_sorry_doc_thats_too_much]
~chatplayer("<p,sad>Sorry doc, that's too much.");

[proc,has_plague_outfit]()(boolean)
if ( 
    ((inv_total(inv, plague_trousers) > 0) | (inv_total(worn, plague_trousers) > 0))
    &  ((inv_total(inv, plague_jacket) > 0) | (inv_total(worn, plague_jacket) > 0))) {
        return(true);
    }
    return(false);


[label,doctor_orbon_post_clothing_purchase]
~chatnpc("<p,quiz>Have you managed to get rid of those sheep?");
~chatplayer("<p,neutral>Not yet.");
~chatnpc("<p,neutral>You must hurry.|They could have the whole town infected in days.");

def_boolean $has_plague_outfit = ~has_plague_outfit;
    
if ($has_plague_outfit = false) {
    ~chatnpc("<p,neutral>I see you don't have your protective clothing with you.|Would you like to buy some more?|Same price as before.");
    @multi2(
        "Ok, I'll take it.", doctor_orbon_ill_take_it,
        "No, I don't need anymore.", doctor_orbon_no_i_dont_need_any_more
    );
}

[label,doctor_orbon_no_i_dont_need_any_more]
~chatplayer("<p,neutral>No, I don't need anymore.");

[label,doctor_orbon_after_sheep_herder]
~chatnpc("<p,happy>Well hello again.|I was so relieved when I heard you disposed of the plagued sheep.|Now the town is safe again.");

