[opnpc1,student3]
// https://youtu.be/TrSlu4__Rt4?t=508
def_int $itexam_progress = ~itexam_progress;
switch_int ($itexam_progress) {
    case ^itexam_first_exam : @itexam_student3_first_exam_tips;
    case ^itexam_second_exam : @itexam_student3_second_exam_tips;
    case ^itexam_third_exam : @itexam_student3_third_exam_tips;
    case default : @student3_pre_quest;
}

[opnpcu,student3]
def_obj $item = last_useitem;

if ($item = cracked_rock_sample) {
    ~chatnpc("<p,angry>Uh? That's not mine!");
} else if ($item = rock_sample_3) {
    @itexam_student3_deliver_sample;
} else {
    ~displaymessage(^dm_default);
}

[label,student3_pre_quest]
~chatplayer("<p,happy>Hello there.");
~chatnpc("<p,happy>Hi there. I'm studying for the Earth Sciences exam.");
~chatplayer("<p,happy>Interesting... This exam seems to be a popular one!");

[label,itexam_student3_first_exam_tips]
def_int $errand_state = ~itexam_errand_progress(^itexam_errand_student3);

if ($errand_state = 0) {
    ~chatplayer("<p,neutral>Can you help me with the Earth Sciences exams at all?");
    ~chatnpc("<p,neutral>I can if you help me...");
    ~chatplayer("<p,quiz>How can I do that?");
    ~chatnpc("<p,sad>I have lost my rock sample.");
    ~chatplayer("<p,neutral>Do you know where you dropped it?");
    ~chatnpc("<p,quiz>Well, I was doing a lot of walking that day... Oh yes, that's right - we were studying ceramics in fact, near the edge of the digsite.");
    ~chatnpc("<p,neutral>I found some pottery that seemed to match the design on those large urns.");
    ~chatnpc("<p,sad>I was in the process of checking this out, and when we got back to the centre my rock sample had gone!");
    ~chatplayer("<p,neutral>Leave it to me, I'll find it.");
    ~chatnpc("<p,happy>Oh great! Thanks!");
    ~itexam_set_errand_progress(^itexam_errand_student3, 1);
} else if ($errand_state = 1) {
    if (inv_total(inv, rock_sample_3) > 0) {
        @itexam_student3_deliver_sample;
    } else {
        ~chatplayer("<p,happy>How's the study going?");
        ~chatnpc("<p,neutral>Very well thanks. Have you found my rock sample yet?");
        ~chatplayer("<p,sad>No sorry, not yet.");
        ~chatnpc("<p,confused>I'm sure it's just outside the digsite somewhere...");
    }
} else {
    ~chatnpc("<p,happy>How's it going?");
    ~chatplayer("<p,confused>I am stuck on some more exam questions.");
    ~chatnpc("<p,happy>Okay, I'll tell you my latest notes...");
    // https://web.archive.org/web/20060909012420im_/http://www.runeweb.net/fireball/Digsite%20Images/digsite6.png
    ~chatnpc("<p,neutral>The proper health and safety points are:|Leather gloves and boots to be worn at all times,|proper tools must be used.");
    ~chatplayer("<p,happy>Great, thanks for your advice.");
}

[label,itexam_student3_deliver_sample]
~chatplayer("<p,happy>Guess what I found.");
inv_del(inv, rock_sample_3, ^max_32bit_int);
~itexam_set_errand_progress(^itexam_errand_student3, 2);
~chatnpc("<p,happy>Hey! My sample! Thanks ever so much. Let me help you with those questions now.");
~chatnpc("<p,neutral>The proper health and safety points are:|Leather gloves and boots to be worn at all times,|proper tools must be used.");
~chatplayer("<p,happy>Great, thanks for your advice.");

[label,itexam_student3_second_exam_tips]
~chatnpc("<p,happy>How's it going?");
~chatplayer("<p,confused>I am stuck on some more exam questions.");
~chatnpc("<p,happy>Okay, I'll tell you my latest notes...");
~itexam_set_errand_progress(^itexam_errand_student3, 2);
// https://web.archive.org/web/20060909012420im_/http://www.runeweb.net/fireball/Digsite%20Images/digsite10.png
~chatnpc("<p,neutral>Finds handling:|Finds must be carefully handled, and gloves worn.");
~chatplayer("<p,happy>Great, thanks for your advice.");

[label,itexam_student3_third_exam_tips]
def_int $errand_state = ~itexam_errand_progress(^itexam_errand_student3);

if ($errand_state = 0) {
    ~chatplayer("<p,happy>Hello there.");
    ~chatnpc("<p,confused>What, you want more help?");
    ~chatplayer("<p,neutral>Err... Yes please!");
    ~chatnpc("<p,shifty>Well... It's going to cost you...");
    ~chatplayer("<p,bored>Oh, well how much?");
    ~chatnpc("<p,happy>I'll tell you what I would like: a precious stone. I don't find many of them. My favourites are opals; they are beautiful.");
    ~chatnpc("<p,happy>Just like me! Tee hee hee!");
    ~chatplayer("<p,confused>Err... OK I'll see what I can do, but I'm not sure where I'd get one.");
    ~chatnpc("<p,happy>Well, I have seen people get them from panning occasionally.");
    ~chatplayer("<p,happy>OK, I'll see what I can turn up for you.");
    ~itexam_set_errand_progress(^itexam_errand_student3, 1);
} else if ($errand_state = 1) {
    ~chatplayer("<p,happy>Hello there.");
    ~chatnpc("<p,happy>Oh, hi again. Did you bring me the opal?");

    if (inv_total(inv, opal) > 0) {
        ~chatplayer("<p,happy>Would an opal look like this by any chance?");
        ~chatnpc("<p,happy>Wow, great, you've found one. This will look beautiful set in my necklace. Thanks for that; now I'll tell you what I know...");
        inv_del(inv, opal, 1);
        ~itexam_set_errand_progress(^itexam_errand_student3, 2);
        // https://web.archive.org/web/20060909012420im_/http://www.runeweb.net/fireball/Digsite%20Images/digsite11.png
        ~chatnpc("<p,neutral>Sample preparation:|Samples cleaned and carried only in specimen jars.");
        ~chatplayer("<p,happy>Great, thanks for your advice.");
    } else {
        ~chatplayer("<p,sad>I haven't found one yet.");
        ~chatnpc("<p,neutral>Oh well, tell me when you do. Remember that they can be found around the digsite; perhaps try panning the river.");
    }
} else {
    ~chatnpc("<p,happy>How's it going?");
    ~chatplayer("<p,confused>I am stuck on some more exam questions.");
    ~chatnpc("<p,happy>Okay, I'll tell you my latest notes...");
    ~chatnpc("<p,neutral>Sample preparation:|Samples cleaned and carried only in specimen jars.");
    ~chatplayer("<p,happy>Great, thanks for your advice.");
}


