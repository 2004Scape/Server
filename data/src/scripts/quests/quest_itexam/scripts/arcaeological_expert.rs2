[opnpc1,archaeological_expert]
def_int $progress = ~itexam_progress;

if ($progress = ^itexam_complete) {
    @archaeological_expert_quest_complete;
} else if ($progress < ^itexam_mineshaft_permit) {
    ~chatplayer("<p,happy>Hello. Who are you?");
    ~chatnpc("<p,happy>Good day to you. My name is Terry Balando; I am an expert on digsite finds.");
    ~chatnpc("<p,happy>I am employed by the museum in Varrock to oversee all finds at this digsite. Anything you find must be reported to me.");
    ~chatplayer("<p,happy>Oh, ok. If I find anything of interest I will bring it here.");
    ~chatnpc("<p,happy>Can I help you at all?");
} else {
    ~chatnpc("<p,happy>Hello there, <displayname>!");
    ~chatnpc("<p,happy>Can I help you at all?");
}

if (~itexam_testbit(^itexam_bit_shown_ancient_symbol) = ^true & inv_total(inv, digexpertscroll) = 0) {
    // It is intentional that, even after using the invitation letter, you can get another from terry (confirmed on OSRS)
    @multi5("I have something I need checking out.", archaeological_expert_checkout, "No thanks.", archaeological_expert_no_thanks, "Can you tell me anything about the digsite?", archaeological_expert_digsite_info, "I lost the letter you gave me.", archaeological_expert_lost_letter, "Can you tell me more about the tools an archaeologist uses?", archaeological_expert_tool_help);
} else {
    @multi4("I have something I need checking out.", archaeological_expert_checkout, "No thanks.", archaeological_expert_no_thanks, "Can you tell me anything about the digsite?", archaeological_expert_digsite_info, "Can you tell me more about the tools an archaeologist uses?", archaeological_expert_tool_help);
}

[label,archaeological_expert_quest_complete]
~chatnpc("<p,happy>Hello again. How goes the archaeology?");
~chatplayer("<p,neutral>Pretty good, thanks.");
~chatnpc("<p,happy>Glad to hear it!");

[label,archaeological_expert_checkout]
~chatplayer("<p,neutral>I have something I need checking out.");
~chatnpc("<p,happy>Okay, give it to me and I'll have a look for you.");

[label,archaeological_expert_no_thanks]
~chatplayer("<p,happy>No thanks.");
~chatnpc("<p,neutral>Good, let me know if you find anything unusual.");

[label,archaeological_expert_digsite_info]
~chatplayer("<p,neutral>Can you tell me anything about the digsite?");
~chatnpc("<p,neutral>Yes, indeed; I am studying the lives of the settlers. During the end of the third age there used to be a great city at the site.");
~chatnpc("<p,neutral>Its inhabitants were humans, supporters of the god Saradomin. It's not recorded what happened to the community here.");
~chatnpc("<p,neutral>I suspect nobody has lived here for over a millennium!");

def_int $choice = ~p_choice2("Can you tell me more about the tools an archaeologist uses?", 1, "Thank you!", 2);

if ($choice = 1) {
    @archaeological_expert_tool_help;
} else if ($choice = 2) {
    ~chatplayer("<p,neutral>Thank you!");
}

[label,archaeological_expert_lost_letter]
~chatplayer("<p,sad>I lost the letter you gave me.");

if (inv_total(inv, digexpertscroll) > 0) {
    ~chatnpc("<p,angry>No you haven't! You have one in your pack!");
} else if (inv_total(bank, digexpertscroll) > 0) {
    ~chatnpc("<p,angry>No you haven't! You have one in your bank!");
} else {
    inv_add(inv, digexpertscroll, 1);
    ~chatnpc("<p,neutral>I can't believe it! I go to all the effort of writing you a letter of recommendation and you lose it! Here, I'll write another... Don't lose it again!");
}


[opnpcu,archaeological_expert]
def_obj $item = last_useitem;

// Not all the mesanims in here are correct
if ($item = digtalisman) {
    if (~itexam_testbit(^itexam_bit_shown_ancient_symbol) = ^true) {
        ~mesbox("The expert has already seen the talisman.");
        return;
    }
    ~chatplayer("<p,neutral>Take a look at this talisman.");
    ~chatnpc("<p,confused>Unusual... This object doesn't appear right...");
    ~chatnpc("<p,confused>Hmmm...");
    ~chatnpc("<p,neutral>I wonder... Let me check my guide... Could it be? Surely not!");
    ~chatnpc("<p,neutral>From the markings on it, it seems to be a ceremonial ornament to a god named...");
    ~chatnpc("<p,happy>...Zaros? I haven't heard much about him before. This is a great discovery; we know very little of the pagan gods that people worshipped.");
    ~chatnpc("<p,neutral>There is some strange writing embossed upon it - it says, 'Zaros will return and wreak his vengeance upon Zamorak the pretender.'");
    ~chatnpc("<p,neutral>I wonder what it means by that? Some silly superstition, probably.");
    ~chatnpc("<p,happy>Still, I wonder what this is doing around here. I'll tell you what; as you have found this, I will allow you to use the private dig shafts.");
    ~chatnpc("<p,happy>You obviously have a keen eye. Take this letter and give it to one of the workmen, and they will allow you to use them.");
    // varbit 2550 osrs 0 to 1
    ~itexam_setbit(^itexam_bit_shown_ancient_symbol);
    inv_del(inv, digtalisman, ^max_32bit_int);
    inv_add(inv, digexpertscroll, 1);
    mes("The expert hands you a letter.");
} else if ($item = zarosstonetablet) {
    if(%itexam_progress = ^itexam_complete) {
        ~chatnpc("<p,confused>I don't need another tablet!|One is enough, thank you.");
        return;
    }
    ~chatplayer("<p,happy>I found this in a hidden cavern beneath the digsite.");
    ~chatnpc("<p,neutral>Incredible!");
    ~chatplayer("<p,neutral>There is an altar down there.|The place is crawling with skeletons!");
    ~chatnpc("<p,neutral>Yuck!");
    ~chatnpc("<p,happy>This is an amazing discovery!|All this while we were convinced|that no other race had lived here.");
    ~chatnpc("<p,happy>It seems the followers of Saradomin|have tried to cover up the evidence of the Zaros altar.|This whole city must have been built over it!");
    ~chatnpc("<p,happy>Thanks for your help;|your sharp eyes have spotted what many have missed.|Here, take this gold as your reward.");
    ~objbox(gold_bar, "The expert gives you 2 gold bars as payment.", 250, 0, divide(^objbox_height, 2));
    inv_del(inv, zarosstonetablet, 1);
    queue(itexam_complete, 0);
} else if ($item = unidentified_liquid) {
    ~chatplayer("<p,happy>Do you know what this is?");
    ~chatnpc("<p,shock>Where did you get this?");
    ~chatplayer("<p,happy>From one of the barrels at the digsite.");
    ~chatnpc("<p,shock>This is a VERY dangerous liquid called nitroglycerin. Be careful how you handle it. Don't drop it or it will explode!");
    inv_del(inv, unidentified_liquid, 1);
    inv_add(inv, nitroglycerin, 1);
} else if ($item = unidentified_powder) {
    ~chatplayer("<p,confused>Do you know what this powder is?");
    ~chatnpc("<p,neutral>Really, you do find the most unusual items. I know what this is, it's a strong chemical called ammonium nitrate. Why you want this I'll never know...");
    inv_del(inv, unidentified_powder, 1);
    inv_add(inv, ammonium_nitrate, 1);
} else if ($item = nitroglycerin) {
    ~chatplayer("<p,neutral>Can you tell me any more about this?");
    ~chatnpc("<p,neutral>Nitroglycerin! This is a dangerous substance. This is normally mixed with other chemicals to produce a potent compound.");
    ~chatnpc("<p,neutral>Be sure not to drop it! That stuff is highly volatile...");
} else if ($item = precharcoalmixture) {
    ~chatplayer("<p,happy>Hey, look at this.");
    ~chatnpc("<p,neutral>Hmm, that looks dangerous. Handle it carefully and don't drop it!");
} else if ($item = postcharcoalmixture) {
    ~chatplayer("<p,happy>See what I have done with the compound now.");
    ~chatnpc("<p,bored>Seriously, I think you have a death wish! What on earth are you going to do with that stuff?");
    ~chatplayer("<p,happy>I'll find a use for it!");
} else if ($item = digcompound) {
    ~chatplayer("<p,neutral>What do you think about this?");
    ~chatnpc("<p,confused>What have you concocted now? Just be careful when playing with chemicals!");
} else if ($item = nuggets) {
    ~chatplayer("<p,neutral>I have these gold nuggets...");
    if (inv_total(inv, nuggets) < 3) {
        ~chatnpc("<p,neutral>I can't do much with these nuggets yet. Come back when you have 3 and I will exchange them for you.");
    } else {
        inv_del(inv, nuggets, 3);
        inv_add(inv, gold_ore, 1);
        ~chatnpc("<p,neutral>Good – that's three; I can exchange them for normal gold now. You can get this refined and make a profit!");
        ~chatplayer("<p,happy>Excellent!");
    }

} else if ($item = tray_empty) {
    ~chatnpc("<p,confused>I have no need for panning trays!");
} else if ($item = tray_mud) {
    ~chatnpc("<p,confused>Have you searched this tray yet?");
    ~chatplayer("<p,confused>Not that I remember...");
    ~chatnpc("<p,neutral>It may contain something; I don't want to get my hands dirty.");
    mes("The expert hands the tray back to you.");
} else if ($item = digexpertscroll) {
    ~chatnpc("<p,confused>There's no point in giving me this back!");
} else if ($item = digsitesword) {
    ~chatplayer("<p,happy>I found an old sword.");
    ~chatnpc("<p,neutral>Oh, it's very rusty isn't it? I'm not sure this sword belongs here, it looks very out of place.");
} else if ($item = digsitestaff) {
    ~chatplayer("<p,happy>Have a look at this staff.");
    ~chatnpc("<p,neutral>Look at this... Interesting... This appears to belong to a cleric of some kind; certainly not a follower of Saradomin, however.");
    ~chatnpc("<p,neutral>I wonder if there was another civilization here before the Saradominists?");
} else if ($item = digsitearmour2) {
    ~chatplayer("<p,happy>I found some armour.");
    ~chatnpc("<p,neutral>It looks like the wearer of this fought a mighty battle.");
} else if ($item = digsitearmour1) {
    ~chatplayer("<p,happy>I found some old armour.");
    ~chatnpc("<p,neutral>How unusual. This armour doesn't seem to match with the other finds. Keep looking.");
} else if ($item = needle) {
    ~chatplayer("<p,happy>I found a needle.");
    ~chatnpc("<p,neutral>Hmm, yes; I wondered why this race were so well dressed! It looks like they had mastery of needlework.");
} else if ($item = digsiteglass) {
    ~chatplayer("<p,happy>Have a look at this glass.");
    ~chatnpc("<p,neutral>Hey you should be careful of that. It might cut your fingers, throw it away!");
} else if ($item = digsitearrow) {
    ~chatplayer("<p,happy>Have a look at this arrow.");
    ~chatnpc("<p,neutral>No doubt this arrow was shot by a strong warrior – it's split in half! It is not a valuable object though.");
} else if ($item = bones) {
    ~chatplayer("<p,happy>Have a look at these bones.");
    ~chatnpc("<p,neutral>Ah, yes – a fine bone example... no noticeable fractures... and in good condition. These are common cow bones, however; they have no archaeological value.");
} else if ($item = digsitebuttons) {
    ~chatplayer("<p,happy>I found these buttons.");
    ~chatnpc("<p,neutral>Let's have a look. Ah, I think these are from the nobility, perhaps a royal servant?");
} else if ($item = cracked_sample) {
    ~chatplayer("<p,happy>I found this rock...");
    ~chatnpc("<p,neutral>What a shame it's cracked; this looks like it would have been a good sample.");
} else if ($item = old_tooth) {
    ~chatplayer("<p,happy>Hey look at this.");
    ~chatnpc("<p,neutral>Oh, an old tooth. It looks like it has come from a mighty being. Pity there are no tooth fairies around here!");
} else if ($item = digsitebuckle) {
    ~chatplayer("<p,happy>Have a look at this unusual item...");
    ~chatnpc("<p,neutral>Let me see. This is a belt buckle. Not so unusual - I should imagine it came from a guard.");
} else {
    ~displaymessage(^dm_default);
}

[label,archaeological_expert_tool_help]
~chatplayer("<p,quiz>Can you tell me more about the tools an archaeologist uses?");
~chatnpc("<p,happy>Of course! Let's see now... Rock picks are for splitting rocks or scraping away soil; you can get one from a cupboard in the Education Centre.");
~chatplayer("<p,quiz>What about sample jars?");
~chatnpc("<p,happy>I think you'll find them scattered about pretty much everywhere, but I know you can get one from a cupboard somewhere in the Education Centre, just like the rock pick!");
~chatplayer("<p,quiz>Okay, what about a specimen brush?");
~chatnpc("<p,sad>We have a bit of a shortage of those at the moment. You could try borrowing one from a workman on the site... but I don't think they'd give it willingly.");
~chatplayer("<p,shifty>Sounds like I'll need to be sneaky to get one of those, then... Okay - trowel?");
~chatnpc("<p,happy>Ahh... that you must earn by passing your exams! The examiner holds those.");
~chatplayer("<p,neutral>Ahh, ok thanks.");

[opheld2,digexpertscroll]
~chatplayer("<p,neutral>It says, 'I give permission for the bearer... to use the mine shafts on site. - signed Terrance Balando, Archaeological Expert, City of Varrock.'");