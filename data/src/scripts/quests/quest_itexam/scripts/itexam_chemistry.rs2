[opheldu,ammonium_nitrate]
switch_obj (last_useitem) {
    case nitroglycerin : @itexam_mixed_chemicals1;
    case default : ~displaymessage(^dm_default);
}

[opheldu,charcoal]
switch_obj (last_useitem) {
    case pestle_and_mortar : @grind_charcoal;
    case default : ~displaymessage(^dm_default);
}

[opheldu,precharcoalmixture]
switch_obj (last_useitem) {
    case ground_charcoal : @itexam_mixed_chemicals2;
    case default : ~displaymessage(^dm_default);
}

[opheldu,postcharcoalmixture]

if (stat(herblore) < 10) {
    // TODO: Unconfirmed message
    mes("You need a Herblore level of 10 to mix this potion.");
    return;
}
switch_obj (last_useitem) {
    case arcenia_root : @itexam_explosive;
    case default : ~displaymessage(^dm_default);
}

[label,itexam_mixed_chemicals1]
inv_del(inv, ammonium_nitrate, 1);
inv_del(inv, nitroglycerin, 1);
inv_add(inv, precharcoalmixture, 1);
stat_advance(herblore, 25);
mes("You mix the nitrate powder into the liquid.");
mes("It has produced a foul mixture.");

[label,itexam_mixed_chemicals2]

if (stat(herblore) < 10) {
    // TODO: Unconfirmed message
    mes("You need a Herblore level of 10 to mix this potion.");
    return;
}
inv_del(inv, ground_charcoal, 1);
inv_del(inv, precharcoalmixture, 1);
inv_add(inv, postcharcoalmixture, 1);
stat_advance(herblore, 25);
mes("You mix the charcoal into the liquid.");
mes("It has produced an even fouler mixture.");

[label,itexam_explosive]

if (stat(herblore) < 10) {
    // TODO: Unconfirmed message
    mes("You need a Herblore level of 10 to mix this potion.");
    return;
}
mes("You mix the root into the mixture.");
mes("You produce a potentially explosive compound.");
anim(human_herbing_vial, 0);
sound_synth(grind, 0, 0);
~chatplayer("<p,happy>Excellent! This looks just right!");
inv_del(inv, arcenia_root, 1);
inv_del(inv, postcharcoalmixture, 1);
inv_add(inv, digcompound, 1);
stat_advance(herblore, 25);

[label,grind_charcoal]
inv_del(inv, charcoal, 1);
inv_add(inv, ground_charcoal, 1);
mes("You grind the charcoal to a powder.");