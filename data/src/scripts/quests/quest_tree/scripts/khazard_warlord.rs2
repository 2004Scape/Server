[opnpc1,khazard_warlord]
switch_int(%tree_progress) {
    case ^tree_retrieved_orb:
        ~chatplayer(happy, "Hello there.");
        ~chatnpc(angry, "You think you're so clever.|You know nothing!");
        ~chatplayer(quiz, "What?");
        ~chatnpc(angry, "I'll crush you and those pesky little green men!");
    case ^tree_returned_first_orb:
        ~chatplayer(angry, "You there, stop!");
        ~chatnpc(angry, "Go back to your pesky little green friends.");
        ~chatplayer(angry, "I've come for the orbs.");
        ~chatnpc(angry, "You're out of your depth traveller.|These orbs are part of a much larger picture.");
        ~chatplayer(angry, "They're stolen goods,|now give them here!");
        ~chatnpc(angry, "Ha, you really think you stand a chance?|I'll crush you.");
    case ^tree_defeated_warlord, ^tree_complete:
        ~chatplayer(shock, "I thought I killed you?");
        ~chatnpc(angry, "Fool, warriors blessed by Khazard don't die.|You can't kill that which is already dead.|However I can kill you!");
    case default:
        ~chatplayer(happy, "Hello, how are you?");
        ~chatnpc(angry, "Don't speak to me you insignificant wretch!|Die in the name of Khazard!");
}
~npc_retaliate(0);

[ai_queue3,khazard_warlord](int $arg)
gosub(npc_death);
if(npc_findhero = true) {
    if(%tree_progress = ^tree_returned_first_orb | %tree_progress = ^tree_defeated_warlord) {
        if(p_finduid(uid) = true) { // get protected access, if not then queue
            @defeat_warlord;
        }
        if_close;
        queue(queue_defeat_warlord, 0, npc_uid); //strongqueue probably
        return;
    }
    // Not sure if he actually drops bones here, he drops nothing during the quest in old videos, in OSRS he drops the bones + orbs during the quest and just bones after
    obj_add(npc_coord, bones, 1, 200);
}

[queue,queue_defeat_warlord](npc_uid $npc)
if (npc_finduid($npc) = true) {
    @defeat_warlord;
}

[label,defeat_warlord]
if(inv_total(inv, orbs_of_protection) > 0 | inv_total(bank, orbs_of_protection) > 0) {
    ~mesbox("As the warlord falls to the ground, a ghostly vapour|floats upwards from his battle-worn armour. Out of sight|you hear a shrill scream in the still air of the valley.");
} else {
    %tree_progress = ^tree_defeated_warlord;
    inv_add(inv, orbs_of_protection, 1);
    ~mesbox("As the warlord falls to the ground, a ghostly vapour|floats upwards from his battle worn armour. Out of sight|you hear a shrill scream in the still air of the valley.|You search his satchel and find the orbs of protection.");
} 

[opobj3,orbs_of_protection]
if(inv_total(inv, orbs_of_protection) > 0 | inv_total(bank, orbs_of_protection) > 0) {
    mes("You already have the orbs.");
    obj_del;
    return;
}
if(%tree_progress = ^tree_complete) {
    mes("King Bolren doesn't need anymore orbs from you."); // OSRS wiki, didn't confirm this in-game
    obj_del;
    return;
}
@pickup_obj(obj_coord, obj_type, obj_count);