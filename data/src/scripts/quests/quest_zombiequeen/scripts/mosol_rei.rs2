[opnpc1,mosol_rei]
if(%junglepotion_progress < ^junglepotion_complete) {
    ~chatnpc("<p,neutral>Sorry Bwana, I cannot help you at this time. Go and talk to Trufitus at Tai Bwo Wannai, I believe he needs some help."); // rs3
    return;
}
switch_int(%zombiequeen_progress) {
    case ^zombiequeen_complete, ^zombiequeen_complete_spoken_trufitus1, ^zombiequeen_complete_spoken_trufitus2 :
        ~chatplayer("<p,neutral>Greetings!");
        ~chatnpc("<p,happy>Greetings Bwana! We've removed the threat of Rashiliyia and though there are still some random outbreaks of undead activity we are more than able to deal with it.");
        // https://web.archive.org/web/20050204071450im_/http://www.runehq.com//images/cityguides/shilo3.jpg
        ~chatnpc("<p,happy>You're welcome to enter the village now Bwana,|shall I show you the way?");
        def_int $op = ~p_choice2_header("Yes, I'll give it a go!", 1, "I think I'll see it some other time.", 2, "You can now enter Shilo Village.");
        if($op = 2) {
            mes("You decide not to visit the village.");
            return;
        }
        ~mesbox("Mosol leads you into the village.");
        p_telejump(0_44_46_50_8);
        mes("Mosol leaves you by the gate and walks back out into the jungle.");
    case default : @mosol_belt;
    case ^zombiequeen_not_started :
        if(~obj_gettotal(mosol_wampum_belt) > 0) {
            @mosol_belt;
        }
        ~mesbox("Mosol seems to be looking around very cautiously. He jumps a little when you approach and talk to him.");
        ~chatnpc("<p,neutral>Run! Run for your life! Save yourself! I'll keep them back as long as I can...");
        @multi4("Why do I need to run?", mosol_whyrun, "Yeah... Ok, I'm running!", mosol_yeahok, "Who are you?", mosol_who, "Ok. Thanks for your help.", mosol_ok_thanks);
}

[label,mosol_belt]
~chatnpc("<p,neutral>Hey there Bwana, have you delivered that Wampum Belt to Trufitus yet?");
switch_int(~p_choice2("Yes, of course!", 1, "Not yet, I've been busy.", 2)) {
    case 1 :
        ~chatplayer("<p,neutral>Yes, of course!");
        ~chatnpc("<p,neutral>Well hopefully Trufitus will come up with some good ideas on how to resolve this problem with Rashiliyia. You should talk to him if you really want to help.");
    case 2 :
        ~chatplayer("<p,neutral>Not yet, I've been busy.");
        ~chatnpc("<p,neutral>If you can take it to him quickly, it would help me out a lot. If not, please can you give it back to me so that I can send a scout to deliver it.");
}
@multi2("Ok. Thanks for your help.", mosol_ok_thanks, "Why are the undead here?", mosol_whyundead);

[label,mosol_whyrun]
~chatplayer("<p,happy>Why do I need to run?");
~chatnpc("<p,neutral>Your very life is in danger. Rashiliyia has returned and we are all doomed.");
@multi3("Rashiliyia? Who is she?", mosol_whoisshe, "What danger is there around here?", mosol_danger, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_whoisshe]
~chatplayer("<p,happy>Rashiliyia? Who is she?");
~chatnpc("<p,neutral>Rashiliyia is the Queen of the dead. She has returned and has brought a plague of undead with her. They now occupy our village and we have them  trapped. I warn people like yourself to stay away.");
@multi3("What can we do?", mosol_whatcanwe, "Uh, it sounds nasty, just the kind of thing I want to avoid.", mosol_avoid, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_whatcanwe]
~chatplayer("<p,happy>What can we do?");
~chatnpc("<p,neutral>We're doing all we can to keep the undead at bay. The village is covered in a deadly green mist. If you go into the village, a terrible sickness will befall you.");
~chatnpc("<p,neutral>And the undead are even stronger beyond the gates. My guess is that it has something to do with the Legend of Rashiliyia.");
~chatnpc("<p,neutral>But you would need to talk to the Shaman in Tai Bwo Wannai village to get more details about that. I really have to go now and fight these undead.");
@multi5("Why are the undead here?", mosol_whyundead, "What can we do?", mosol_whatcanwe, "Can I help in any way?", mosol_canhelp, "I'll go to see the Shaman.", mosol_shaman, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_whyundead]
~chatplayer("<p,neutral>Why are the undead here?");
~chatnpc("<p,neutral>Rashiliyia! The Queen of the undead has risen! She is the mother of the undead creatures that roam this land. But alas I know nothing of the legend that surrounds her.");
@multi3("Legend you say?", mosol_legend, "I don't think this is something I can help with at the moment.", mosol_dontthink, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_legend]
~chatplayer("<p,neutral>Legend you say?");
~chatnpc("<p,angry>Yes.... I said it is a legend that I know nothing about.");
@multi4("Oh, ok, sorry for bothering you.", mosol_sorrybother, "Oh come on, you must know something.", mosol_knowsomething, "Maybe you know someone who does know something?", mosol_whoknow, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_knowsomething]
~chatplayer("<p,neutral>Oh come on, you must know something?");
~mesbox("Mosol lowers his head in deep concentration.");
~chatnpc("<p,neutral>Well, let me think now...");
~mesbox("He scratches his head...");
~chatnpc("<p,neutral>Hmmm, there was something I think that might help...");
~mesbox("Mosol strains to remember something...");
~chatnpc("<p,neutral>Nope, sorry. It's gone.");
@multi3("Maybe you know someone who does know something?", mosol_knowsomething, "Oh, Ok, sorry for bothering you.", mosol_sorrybother, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_whoknow]
~chatplayer("<p,neutral>Maybe you know someone who does know something?");
~chatnpc("<p,neutral>My guess is that this has something to do with the legend of Rashiliyia. But you need to speak to the Shaman in 'Tai Bwo Wannai' village to get more details about that.");
~chatnpc("<p,neutral>I really have to fight these undead now Bwana, before they take over the world!");
@multi3("Oh, ok, sorry for bothering you.", mosol_sorrybother, "I'll go to see the Shaman.", mosol_shaman, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_sorrybother]
~chatplayer("<p,neutral>Oh, Ok, sorry for bothering you.");
~chatnpc("<p,neutral>Ok, perhaps you'd like to be on your way now?");

[label,mosol_shaman]
~chatplayer("<p,neutral>I'll go to see the Shaman.");
if(~obj_gettotal(mosol_wampum_belt) > 0 | %zombiequeen_progress > ^zombiequeen_started) {
    ~chatnpc("<p,neutral>Well, if you go to see the Shaman, please give him that Wampum belt I gave to you, it will give him more details about our situation down here.");
} else {
    ~chatnpc("<p,neutral>Well, that would be helpful Bwana. If you're sure you want to go, you can take a Wampum belt to him for me. It will give the Shaman, Trufitus the story of our problems down here. Are you sure you want to go?");
}
@multi3("Errr, I'm having second thoughts now.", mosol_secondthoughts, "Yes, I'm sure and I'll take the Wampum belt to Trufitus.", mosol_wampumbelt, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_wampumbelt]
~chatplayer("<p,neutral>Yes, I'm sure and I'll take the Wampum belt to Trufitus.");
if(~obj_gettotal(mosol_wampum_belt) > 0 | %zombiequeen_progress > ^zombiequeen_started) {
    ~chatnpc("<p,neutral>That's great Bwana, if you give Trufitus the Wampum belt I gave you, it will give him more details of our current situation.");
    return;
}
~chatnpc("<p,neutral>I would be very grateful if you did. Here take this...");
inv_add(inv, mosol_wampum_belt, 1);
~objbox(mosol_wampum_belt, "Mosol gives you a Wampum belt.", 250, 0, divide(^objbox_height, 2));
~chatnpc("<p,neutral>Please can you give it to Trufitus. He may be able to give you extra details on the legend surrounding Rashiliyia. Good luck!");

[label,mosol_secondthoughts]
~chatplayer("<p,neutral>Errr, I'm having second thoughts now.");
~chatnpc("<p,neutral>That's Ok Bwana, it's a big responsibility. Come back and see me if you change your mind.");

[label,mosol_dontthink]
~chatplayer("<p,neutral>Sorry, but I don't think this is something that I can help with at the moment!");
~chatnpc("<p,neutral>Ok, I understand Bwana! You may as well be on your way then.");

[label,mosol_canhelp]
~chatplayer("<p,happy>Can I help in any way?");
~chatnpc("<p,neutral>I don't think so Bwana, but you may want to consult with Trufitus the Shaman in Tai Bwo Wannai village, he may have some information which could help.");
@multi4("Why are the undead here?", mosol_whyundead, "What can we do?", mosol_whatcanwe, "I'll go to see the Shaman.", mosol_shaman, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_who]
~chatplayer("<p,happy>Who are you?");
~chatnpc("<p,neutral>I am Mosol Rei, I am a Jungle Warrior. I used to live in this village, but it is too dangerous for you to stay around here.");
@multi3("Mosol Rei, that's a nice name.", mosol_nicename, "What danger is there around here?", mosol_danger, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_avoid]
~chatplayer("<p,happy>Uh, it sounds nasty, just the kind of thing I want to avoid!");
~chatnpc("<p,sad>Quite right, Bwana, please make all haste! Before your spine turns to water as we speak.");

[label,mosol_nicename]
~chatplayer("<p,happy>Mosol Rei, that's a nice name.");
~mesbox("Mosol looks at you and shakes his head in bewilderment.");
~chatnpc("<p,quiz>Thanks, but you really should leave.");

[label,mosol_danger]
~chatplayer("<p,happy>What danger is there around here?");
~chatnpc("<p,neutral>Can you not see Bwana? This whole area is infested with the living dead.");
if_close;
npc_say("Arrggghhh, here are some now!");
mes("Mosol Rei: Arrggghhh, here are some now!");
def_int $i = 0;
def_int $count = ~random_range(1, 3);
while($i < $count) {
    def_coord $npc_coord = map_findsquare(coord, 1, 4, ^map_findsquare_lineofwalk);
    npc_add($npc_coord, ~random_undead_one(), 200);
    sound_synth(smokepuff, 0, 0);
    spotanim_map(smokepuff, $npc_coord, 5, 0);
    npc_setmode(opplayer2);
    def_int $rand = random(5);
    if($rand = 0) npc_say("You cannot escape me, prepare to die.");
    else if($rand = 1) npc_say("Prepare to go to the other side!");
    else if ($rand = 2) npc_say("Soon you will be undead!");
    else if($rand = 3) npc_say("Rahhhhh! OOoohhhhhh!");
    $i = add($i,1);
}

[label,mosol_yeahok]
~chatplayer("<p,happy>Yeah... Ok, I'm running!");
~chatnpc("<p,neutral>God speed to you my friend.");

[label,mosol_ok_thanks]
~chatplayer("<p,neutral>Ok. Thanks for your help.");
~chatnpc("<p,neutral>You're welcome Bwana, good luck.");