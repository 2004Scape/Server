[opnpc1,npc_500]
if(%junglepotion_progress < ^junglepotion_complete) {
    ~chatnpc("<p,neutral>Sorry Bwana, I cannot help you at this time. Go and talk to Trufitus at Tai Bwo Wannai, I believe he needs some help."); // rs3
    return;
}
switch_int(%zombiequeen_progress) {
    case ^zombiequeen_not_started:
        ~mesbox("Mosol seems to be looking around very cautiously. He jumps a little when you approach and talk to him.");
        ~chatnpc("<p,neutral>Run! Run for your life! Save yourself! I'll keep them back as long as I can...");
        @multi4("Why do I need to run?", mosol_whyrun, "Yeah... Ok, I'm running!", mosol_yeahok, "Who are you?", mosol_who, "Ok. Thanks for your help.", mosol_ok_thanks);
}

[label,mosol_whyrun]
~chatplayer("<p,happy>Why do I need to run?");
~chatnpc("<p,neutral>Your very life is in danger. Rashiliyia has returned and we are all doomed.");

[label,mosol_who]
~chatplayer("<p,happy>Who are you?");
~chatnpc("<p,neutral>I am Mosol Rei, I am a Jungle Warrior. I used to live in this village, but it is too dangerous for you to stay around here.");
@multi3("Mosol Rei, that's a nice name.", mosol_nicename, "What danger is there around here?", mosol_danger, "Ok. Thanks for your help.", mosol_ok_thanks);

[label,mosol_nicename]
~chatplayer("<p,happy>Mosol Rei, that's a nice name.");
~mesbox("Mosol looks at you and shakes his head in bewilderment.");
~chatnpc("<p,quiz>Thanks, but you really should leave.");

[label,mosol_danger]
~chatplayer("<p,happy>What danger is there around here?");
~chatnpc("<p,neutral>Can you not see Bwana? This whole area is infested with the living dead.");
if_close;
say("Arrggghhh, here are some now!");
def_int $i = 0;
def_int $count = ~random_range(1, 3);
while($i < $count) {
    def_coord $npc_coord = map_findsquare(coord, 1, 4, ^map_findsquare_lineofwalk);
    npc_add($npc_coord, ~random_undead_one(), 200);
    sound_synth(smokepuff, 0, 0);
    spotanim_map(small_smokepuff, $npc_coord, 5, 0);
    npc_setmode(opplayer2);
    def_int $rand = random(5);
    if($rand = 0) npc_say("You cannot escape me, prepare to die.");
    else if($rand = 1) npc_say("Prepare to go to the other side!");
    else if ($rand = 2) npc_say("Soon you will be undead!");
    else if($rand = 3) npc_say("Rahhhhh! OOoohhhhhh!");
    $i = add($i,1);
}

[label,mosol_yeahok]
~chatplayer("<p,happy>Yeah... Ok, I'm running!");
~chatnpc("<p,neutral>God speed to you my friend.");

[label,mosol_ok_thanks]
~chatplayer("<p,neutral>Ok. Thanks for your help.");
~chatnpc("<p,neutral>You're welcome Bwana, good luck.");