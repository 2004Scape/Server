[apnpc1,hengrad] 
if (npc_range(coord) > 2) {
    p_aprange(2);
    return;
}
// I don't think you can be in the jail area at this quest-stage?
if(%arena_progress = ^arena_sent_jail) {
    ~chatplayer("<p,quiz>Are you ok stranger?");
    ~chatnpc("<p,neutral>I'm fine thanks.");
    ~chatnpc("<p,quiz>So Khazard got his hands on you too?");
    ~chatplayer("<p,sad>I'm afraid so.");
    ~chatnpc("<p,neutral>If you're lucky you may last as long as me.");
    ~chatplayer("<p,quiz>How long have you been here?");
    ~chatnpc("<p,sad>I've been in Khazard's prisons ever since I can remember. I was a child when his men kidnapped me. My whole life has been spent killing and fighting, all in the hope that one day I might escape.");
    ~chatplayer("<p,sad>Don't give up.");
    ~chatnpc("<p,happy>Thanks friend.");
    ~chatnpc("<p,shifty>Wait.. sshh, the guard is coming. Looks like you'll be going into the arena. Good luck friend.");
    if_close;
    // https://youtu.be/NHsQfeSPUb0?si=c_S0jTnjDBF0xilE&t=189
    npc_add(0_40_49_26_4, khazard_guard_cutscene, 100);
    npc_setmode(none);
    p_delay(0);
    // https://youtu.be/Y6eWjQnV4Mo?si=7aaHIhL6qoxCuntZ&t=170 the guard can get stuck at any point, doesnt interrupt anything
    npc_walk(0_40_49_39_5);
    p_delay(9);
    npc_say("Right you, move it.");
    def_int $delay_diff = distance(coord, 0_40_49_40_6);
    ~forcemove(0_40_49_40_6);
    p_delay(calc(4 - $delay_diff));
    npc_anim(human_closedoorr, 0);
    p_delay(0);
    if(loc_find(0_40_49_40_6, arena_prisondoor) = true) {
        def_int $x;
        def_int $z;
        def_coord $loc_coord = loc_coord;
        def_int $angle = loc_angle;
        def_locshape $shape = loc_shape;
        $x, $z = ~door_open($angle, loc_shape);
        // Temp note: dur updated
        loc_change(inviswall, 3);
        loc_add(movecoord($loc_coord, $x, 0, $z), ctratgatea, modulo(add($angle, 1), 4), $shape, 3);
        sound_synth(grate_open, 0, 0);
    }
    p_teleport(movecoord(coord, 0, 0, -1));
    p_delay(1);
    npc_walk(movecoord(0_40_49_47_6, -1, 0, 0));
    ~forcemove(0_40_49_42_5);
    ~forcemove(0_40_49_47_6);
    npc_walk(movecoord(0_40_49_47_15, 1, 0, -1));
    ~forcemove(0_40_49_47_15);
    p_delay(1);
    npc_say("Get out there!");
    npc_setmode(null);
    if(loc_find(0_40_49_46_16, fightarena_door2) = true) {
        def_int $x;
        def_int $z;
        def_coord $loc_coord = loc_coord;
        def_int $angle = loc_angle;
        def_locshape $shape = loc_shape;
        $x, $z = ~door_open($angle, loc_shape);
        // Temp note: dur updated
        loc_change(inviswall, 3);
        loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 3);
        sound_synth(door_open, 0, 0);
    }
    ~forcemove(movecoord(coord, -4, 0, 4));
    npc_del;
    ~mesbox("From above you hear a voice..... 'Ladies and gentlemen! Let today's first fight between the outsider and everyone's favourite scorpion commence.'");
    @arena_release_scorp;
} else if(%arena_progress >= ^arena_defeated_scorpion) { // you can't access the cells between being sent there and defeating bouncer
    ~chatplayer("<p,neutral>Hello again.");
    ~chatnpc("<p,shifty>I don't know how you managed to escape the arena, but you may not be as lucky next time. Get out of here while you can.");
} else {
    ~chatplayer("<p,quiz>Do you know a Justin or Jeremy Servil?");
    if(~wearing_khazard_armour = true) {
        ~chatnpc("<p,shifty>You're not a real guard. I've been in here long enough to know that. What's your business?");
        ~chatplayer("<p,neutral>I've been sent to rescue Sir Servil and his son.");
    }
    ~chatnpc("<p,neutral>I've seen them in here, although they don't look up to much. My advice to you is to leave before you end up in a cell too.");
}