[oploc1,loc_75]
mes("You open the chest.");
~open_chest(loc_76);

[oploc1,loc_76]
// doesnt check bank in RS3, assuming this will probably work the same way
def_int $armour_count = calc(inv_total(inv, khazard_armour) + inv_total(worn, khazard_armour));
def_int $helmet_count = calc(inv_total(inv, khazard_helmet) + inv_total(worn, khazard_helmet)); 
if(%arena_progress = 0 | ($armour_count > 0 & $helmet_count > 0)) {
    mes("You search the chest, but find nothing.");
    return;
}
if($armour_count > 0 & $helmet_count = 0) {
    inv_add(inv, khazard_helmet, 1);
    // assuming these 2 have linebreaks at the same point as below image
    ~mesbox("You search the chest, and find the|helmet of a Khazard guard.");
    return;
}
if($helmet_count > 0 & $armour_count = 0) {
    inv_add(inv, khazard_armour, 1);
    ~mesbox("You search the chest, and find the|armour of a Khazard guard.");
    return;
}
if(%arena_progress = ^arena_started) {
    %arena_progress = ^arena_obtained_armour;
}
inv_add(inv, khazard_armour, 1);
inv_add(inv, khazard_helmet, 1);
// https://web.archive.org/web/20050305053139im_/http://runevillage.com/images/rsfight2.gif
~mesbox("You search the chest, and find the|helmet and armour of a Khazard guard.");

[oploc1,loc_81]
if(~check_axis(coord, loc_coord, loc_angle) = true) {
    ~open_and_close_door(loc_1532, true, false);
    return;
}
// no "locked" sound... (osrs has it): https://youtu.be/Oltfw1gvWjQ?si=pAK0N9CJmLUyWOBk&t=106
~chatplayer("<p,neutral>This door appears to be locked.");
if (npc_find(coord, khazard_guard_cells, 5, 0) = false) {
    return;
}
switch_int(%arena_progress) {
    case ^arena_freed_servils, ^arena_defeated_genkhazard, ^arena_complete, ^arena_complete_defeated_genkhazard : @khazard_guard_generals_pet;
    case ^arena_sent_jail, ^arena_defeated_scorpion, ^arena_defeated_bouncer :
        ~chatnpc("<p,shifty>So you're back for more are you? General Khazard will be pleased.");
        @arena_enter;
    case default :
        if(%arena_progress < ^arena_obtained_armour | (inv_total(worn, khazard_helmet) = 0 & inv_total(worn, khazard_armour) = 0)) {
            ~chatnpc("<p,angry>Yeah, it's to prevent people like you bothering us. Get out of here.");
            return;
        }
        ~chatnpc("<p,bored>Nice observation guard. You could have just asked to be let in like a normal person.");
}
~open_and_close_door(loc_1532, false, false);

[proc,wearing_khazard_armour]()(boolean)
if(inv_total(worn, khazard_helmet) > 0 & inv_total(worn, khazard_armour) > 0) {
    return (true);
}
return (false);

[oploc1,loc_79]
// https://youtu.be/IOFokveRlxo?si=ub-OWnA_SJAqjFbp&t=458
mes("The gate appears to be locked.");

[oploc1,loc_80]
mes("The cell gate is securely locked."); 

[oplocu,loc_79]
// use keys
if(last_useitem = khazard_cell_keys) {
    ~chatplayer("<p,shifty>The gate is locked, but Jeremy's not in there anyway."); // chisel
    return;
}
~displaymessage(^dm_default);

[oplocu,loc_80]
// use keys
if(last_useitem = khazard_cell_keys) {
    if(%arena_progress = ^arena_given_khali_brew) {
        if (npc_find(coord, jeremy_servil, 4, 0) = true & npc_getmode ! null) {
            @free_jeremy_servil;
        }
    } else if(%arena_progress < ^arena_defeated_bouncer & %arena_progress >= ^arena_entered_ogre_fight) { // rs3 cage works differently so using pre-rework messages from OSRS
        mes("Jeremy is in the arena with his father now.");
    } else {
        mes("You've already rescued Jeremy from the fight arena.");
    }
    return;
}
~displaymessage(^dm_default);

[oploc1,loc_82]
if(coordx(coord) > coordx(loc_coord) | coordz(coord) < coordz(loc_coord)) {
    switch_int(%arena_progress) { 
        case ^arena_freed_servils, ^arena_defeated_genkhazard, ^arena_complete, ^arena_complete_defeated_genkhazard :
            if (npc_find(coord, khazard_guard_standard, 7, 0) = false) {
                mes("This door appears to be locked."); // probably (this from RS3)? not sure
                return;
            }
            @khazard_guard_killed_bouncer;
        case ^arena_entered_ogre_fight, ^arena_defeated_ogre : @open_arena_door;
        case default :
            if (npc_find(coord, khazard_guard_standard, 7, 0) = false) {
                mes("This door appears to be locked.");
                return;
            }
        ~chatnpc("<p,angry>And where do you think you're going? Only General Khazard decides who fights in the arena. Get out of here.");
    }
} else {
    switch_int(%arena_progress) {
        case ^arena_sent_jail, ^arena_defeated_scorpion :
            ~chatnpc_specific(nc_name(khazard_guard_cells), khazard_guard_cells, "<p,shifty>It would be impolite to leave now. The General insists you stay a little longer.");
            ~forcemove(movecoord(coord, -2, 0, 2));
            if (%arena_progress = ^arena_sent_jail) @arena_release_scorp;
            else if (%arena_progress = ^arena_defeated_scorpion) @arena_release_bouncer;
        case ^arena_defeated_ogre : @khazard_welldone;
        case ^arena_given_khali_brew, ^arena_entered_ogre_fight :
            // chatnpc_specific on RS3
            ~chatnpc_specific(nc_name(jeremy_servil), jeremy_servil, "<p,sad>Please don't go, you must help us. We'll never survive in here alone.");
            ~chatplayer("<p,quiz>Where's your father Jeremy?");
            ~chatnpc_specific(nc_name(jeremy_servil), jeremy_servil, "<p,sad>Quick, help him! That beast will kill him. He's too old to fight.");
            @ogre_attack_justin;
        case default :
            sound_synth(door_open, 0, 0);
            ~forcemove(0_40_49_45_17);
            def_int $x;
            def_int $z;
            def_coord $loc_coord = loc_coord;
            def_int $angle = loc_angle;
            def_locshape $shape = loc_shape;
            $x, $z = ~door_open($angle, loc_shape);
            // Temp note: dur updated
            loc_change(loc_83, 3);
            loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 3);
            ~forcemove(movecoord(coord, 2, 0, -2));
    }
}

[label,arena_enter]
p_teleport(0_40_49_47_16);
// these numbers are all eyeballed (cam packets) since OSRS has changed this part entirely
cam_moveto(0_40_49_45_24, 270, 0, 100);
cam_lookat(0_40_49_43_17, 200, 0, 100);
p_delay(1);
if(loc_find(0_40_49_46_16, loc_82) = true) {
    def_int $x;
    def_int $z;
    def_coord $loc_coord = loc_coord;
    def_int $angle = loc_angle;
    def_locshape $shape = loc_shape;
    $x, $z = ~door_open($angle, loc_shape);
    // Temp note: dur updated
    loc_change(loc_83, 3);
    loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 3);
    sound_synth(door_open, 0, 0);
}
cam_moveto(0_40_49_43_26, 270, 1, 1);
cam_lookat(0_40_49_43_15, 270, 1, 1);
~forcemove(movecoord(coord, -2, 0, 0));
cam_reset;
cam_moveto(0_40_49_43_26, 270, 2, 1);
cam_lookat(0_40_49_43_15, 270, 2, 1);
~forcemove(movecoord(coord, -2, 0, 2));
~forcemove(movecoord(coord, 0, 0, 1));
p_delay(1);
cam_reset;
if (%arena_progress = ^arena_entered_ogre_fight & npc_find(coord, jeremy_servil_arena, 8, 0) = true) {
    ~chatnpc("<p,shock>Quick, help him! That beast will kill him. He's too old to fight.");
    @ogre_attack_justin;
} else if (%arena_progress = ^arena_sent_jail) {
    ~mesbox("From above you hear a voice..... 'Ladies and gentlemen! Let today's first fight between the outsider and everyone's favourite scorpion commence.'");
    @arena_release_scorp;
}

[label,open_arena_door]
sound_synth(door_open, 0, 0);
~forcemove(0_40_49_47_15);
def_int $x;
def_int $z;
def_coord $loc_coord = loc_coord;
def_int $angle = loc_angle;
def_locshape $shape = loc_shape;
$x, $z = ~door_open($angle, loc_shape);
// Temp note: dur updated
loc_change(loc_83, 3);
loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 3);
~forcemove(movecoord(coord, -2, 0, 2));
if (%arena_progress = ^arena_defeated_ogre & npc_find(coord, general_khazard, 8, 0) = true) {
    @khazard_welldone;
} 

[label,arena_release_scorp]
// again, guessed numbers
if_close;
def_coord $cam_coord = 0_40_49_39_23;
def_coord $scorp_spawn = 0_40_49_47_23;
if (npc_find($scorp_spawn, khazard_scorpion, 14, 0) = true) {
    if(npc_getmode = none) { // npc in cutscene already, do nothing
        return;
    } else if(inzone($scorp_spawn, 0_40_49_49_24, npc_coord) = false) {
        if(add(%npc_lastcombat, 8) <= map_clock) {
            // out of combat and out of cage so attack player, otherwise we'll just set ptr and start cutscene
            ~npc_retaliate(0);
            return;
        }
        npc_add($scorp_spawn, khazard_scorpion, 500);
    }
} else {
    npc_add($scorp_spawn, khazard_scorpion, 500);
}
npc_setmode(none);
cam_moveto(0_40_49_35_23, 700, 0, 100);
cam_lookat($scorp_spawn, 100, 0, 100);
cam_moveto(0_40_49_41_23, 450, 4, 2);
npc_walk($scorp_spawn);
p_delay(2);
if(loc_find(0_40_49_46_23, loc_78) = true & .loc_find(0_40_49_46_24, loc_77) = true) {
    // Temp note: dur updated
    loc_del(6);
    loc_add(movecoord(loc_coord, 1, 0, 0), loc_type, 3, loc_shape, 6);
    .loc_del(6);
    .loc_add(movecoord(.loc_coord, 1, 0, 0), .loc_type, 1, .loc_shape, 6);
}
npc_tele(movecoord(npc_coord, -1, 0, 0));
~npc_retaliate(0);
p_delay(1);
cam_reset;

[label,arena_release_bouncer]
// again, guessed numbers
if_close;
def_coord $cam_coord = 0_40_49_39_23;
def_coord $bouncer_spawn = 0_40_49_47_26;
if (npc_find($bouncer_spawn, bouncer, 14, 0) = true) {
    if(npc_getmode = none) { // npc in cutscene already, do nothing
        return;
    } else if(inzone($bouncer_spawn, 0_40_49_49_27, npc_coord) = false) {
        if(add(%npc_lastcombat, 8) <= map_clock) {
            // out of combat and out of cage so attack player, otherwise we'll just set ptr and start cutscene
            ~npc_retaliate(0);
            return;
        }
        npc_add($bouncer_spawn, bouncer, 500);
    }
} else {
    npc_add($bouncer_spawn, bouncer, 500);
}
npc_setmode(none);
cam_moveto(0_40_49_35_26, 700, 0, 100);
cam_lookat($bouncer_spawn, 100, 0, 100);
cam_moveto(0_40_49_41_26, 450, 4, 2);
npc_walk($bouncer_spawn);
p_delay(2);
if(loc_find(0_40_49_46_26, loc_78) = true & .loc_find(0_40_49_46_27, loc_77) = true) {
    // Temp note: dur updated
    loc_del(6);
    loc_add(movecoord(loc_coord, 1, 0, 0), loc_type, 3, loc_shape, 6);
    .loc_del(6);
    .loc_add(movecoord(.loc_coord, 1, 0, 0), .loc_type, 1, .loc_shape, 6);
}
npc_tele(movecoord(npc_coord, -1, 0, 0));
~npc_retaliate(0);
p_delay(2);
cam_reset;

[debugproc,c3a]
if(p_finduid(uid) = false) {
    return;
}
@arena_release_scorp;

[debugproc,c4a]
if(p_finduid(uid) = false) {
    return;
}
@arena_release_bouncer;

[queue,arena_quest_complete]
%arena_progress = calc(%arena_progress + 2); // 14 if you're on 12 or 15 on if you're on 13 
inv_add(inv, coins, 1000);
stat_advance(attack, 121750);
stat_advance(thieving, 21750);
session_log(^log_adventure, "Quest complete: Fight Arena");
// todo: can't find an image of this so might need to confirm obj zoom + text breaking
~send_quest_complete(questlist:arena, coins, 250, ^arena_questpoints, "You have completed the\\nFight Arena Quest!");
