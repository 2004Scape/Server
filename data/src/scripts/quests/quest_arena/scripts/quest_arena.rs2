[oploc1,loc_75]
mes("You open the chest.");
~open_chest(loc_76);

[oploc1,loc_76]
// doesnt check bank in RS3, assuming this will probably work the same way
def_int $armour_count = calc(inv_total(inv, khazard_armour) + inv_total(worn, khazard_armour));
def_int $helmet_count = calc(inv_total(inv, khazard_helmet) + inv_total(worn, khazard_helmet)); 
if(%arena_progress = 0 | ($armour_count > 0 & $helmet_count > 0)) {
    mes("You search the chest, but find nothing.");
    return;
}
if($armour_count > 0 & $helmet_count = 0) {
    inv_add(inv, khazard_helmet, 1);
    // assuming these 2 have linebreaks at the same point as below image
    ~mesbox("You search the chest, and find the|helmet of a Khazard guard.");
    return;
}
if($helmet_count > 0 & $armour_count = 0) {
    inv_add(inv, khazard_armour, 1);
    ~mesbox("You search the chest, and find the|armour of a Khazard guard.");
    return;
}
if(%arena_progress = ^arena_started) {
    %arena_progress = ^arena_obtained_armour;
}
inv_add(inv, khazard_armour, 1);
inv_add(inv, khazard_helmet, 1);
// https://web.archive.org/web/20050305053139im_/http://runevillage.com/images/rsfight2.gif
~mesbox("You search the chest, and find the|helmet and armour of a Khazard guard.");

[oploc1,loc_81]
if(~check_axis(coord, loc_coord, loc_angle) = true) {
    ~open_and_close_door(loc_1532, true, false);
    return;
}
// no "locked" sound... (osrs has it): https://youtu.be/Oltfw1gvWjQ?si=pAK0N9CJmLUyWOBk&t=106
~chatplayer(neutral, "This door appears to be locked.");
if (npc_finduid(~npc_within_distance(coord, khazard_guard_cells, 5)) = false) {
    return;
}
switch_int(%arena_progress) {
    case default:
        if(%arena_progress < ^arena_obtained_armour | (inv_total(worn, khazard_helmet) = 0 & inv_total(worn, khazard_armour) = 0)) {
            ~chatnpc(angry, "Yeah, it's to prevent people like you bothering us. Get out of here.");
            return;
        }
        ~chatnpc(bored, "Nice observation guard. You could have just asked to be let in like a normal person.");
}
~open_and_close_door(loc_1532, false, false);

[proc,wearing_khazard_armour](boolean)
if(inv_total(worn, khazard_helmet) > 0 & inv_total(worn, khazard_armour) > 0) {
    return (true);
}
return (false);

[oploc1,loc_79]
// https://youtu.be/IOFokveRlxo?si=ub-OWnA_SJAqjFbp&t=458
mes("The gate appears to be locked.");

[oploc1,loc_80]
mes("The cell gate is securely locked."); 

[oplocu,loc_79]
// use keys
if(last_useitem = khazard_cell_keys) {
    ~chatplayer(shifty, "The gate is locked, but Jeremy's not in there anyway."); // chisel
    return;
}
~displaymessage(^dm_default);

[oplocu,loc_80]
// use keys
if(last_useitem = khazard_cell_keys & %arena_progress = ^arena_given_khali_brew) {
    if (npc_finduid(~npc_within_distance(coord, jeremy_servil, 4)) = true & npc_getmode ! null) {
        @free_jeremy_servil;
    }
}
~displaymessage(^dm_default);

[oploc1,loc_82]
if(coordz(coord) < 3152) {
    switch_int(%arena_progress) { 
        case ^arena_entered_ogre_fight: ~open_arena_door;
        case default:
            if (npc_finduid(~npc_within_distance(coord, khazard_guard_standard, 5)) = false) {
                mes("This door appears to be locked.");
                return;
            }
        ~chatnpc(angry, "And where do you think you're going? Only General Khazard decides who fights in the arena. Get out of here.");
    }
} else {
    switch_int(%arena_progress) { 
        case ^arena_given_khali_brew, ^arena_entered_ogre_fight: 
            ~chatnpc_specific(nc_name(jeremy_servil), jeremy_servil, sad, "Please don't go, you must help us. We'll never survive in here alone.");
            ~chatplayer(quiz, "Where's your father Jeremy?");
            ~chatnpc_specific(nc_name(jeremy_servil), jeremy_servil, sad, "Quick, help him! That beast will kill him. He's too old to fight.");
            @ogre_attack_justin;
    }
}

[proc,open_arena_door]
~forcewalk(0_40_49_47_15);
def_int $x;
def_int $z;
def_coord $loc_coord = loc_coord;
def_int $angle = loc_angle;
def_locshape $shape = loc_shape;
$x, $z = ~door_open($angle, loc_shape);
loc_change(loc_83, 2);
loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 2);
sound_synth(door_open, 0, 0);
~forcemove(movecoord(coord, -2, 0, 2));