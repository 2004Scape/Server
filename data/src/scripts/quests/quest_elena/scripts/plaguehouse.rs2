[oploc1,loc_2530]
switch_int(%elena_progress) {
    case ^quest_elena_spoke_cured_bravek: {
        if (inv_total(inv, elena_key) > 0) {
            mes("The barrel is empty.");
        } else {
            inv_add(inv, elena_key, 1);
            ~objboxt(elena_key, "You find a small key in the barrel.");
        }
    }
    case default: mes("The barrel is empty.");
}

[oploc1,loc_2522]
mes("You walk down the stairs...");
p_telejump(0_39_151_41_6);

[oploc1,loc_2523]
mes("You walk up the stairs...");
p_telejump(0_39_51_40_7);

[oplocu,loc_2526]
switch_obj(last_useitem) {
    case elena_key: ~open_and_close_door(loc_param(next_loc_stage), loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);
}

[oploc1,loc_2526] // elena door
if (~check_axis(coord, loc_coord, loc_angle) = false) {
    ~open_and_close_door(loc_param(next_loc_stage), loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);
    return;
}
switch_int(%elena_progress) {
    case ^quest_elena_freed_elena, ^elena_complete: ~open_and_close_door(loc_param(next_loc_stage), loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);
    case default: {
        if (inv_total(inv, elena_key) > 0) {
            ~mesbox("You unlock the door.");
            if_close; // if you see this well just know this is kinda retarded but kk -jordan
            ~open_and_close_door(loc_param(next_loc_stage), loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);
        } else {
            ~mesbox("The door is locked.");
            if (npc_finduid(~npc_within_distance(coord, elena_imprisoned, 5)) = true) {
                ~chatnpc(shock, "Hey get me out of here please!");
                ~chatplayer(sad, "I would do but I don't have a key.");
                ~chatnpc(confused, "I think there may be one around somewhere. I'm sure I heard them stashing it somewhere.");
                @multi2("Have you caught the plague?", elena_improsoned_have_you_caught_the_plague, "Okay, I'll look for it.", elena_improsioned_ok_ill_look_for_it);
            }
        }
    }
}

[label,elena_improsoned_have_you_caught_the_plague]
~chatplayer(quiz, "Have you caught the plague?");
~chatnpc(neutral, "No, I have none of the symptoms.");
~chatplayer(confused, "Strange, I was told this house was plague infected.");
~chatnpc(neutral, "I suppose that was a cover up by the kidnappers.");

[label,elena_improsioned_ok_ill_look_for_it]
~chatplayer(neutral, "Okay, I'll look for it.");
