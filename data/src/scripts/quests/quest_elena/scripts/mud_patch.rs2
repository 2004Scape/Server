[oplocu,elena_mud_patch1]
switch_obj(last_useitem) {
    case spade: @elena_dig_mud_patch;
    case bucket_water: @elena_bucket_mud_patch(last_useslot);
}

[oplocu,elena_mud_patch2]
switch_obj(last_useitem) {
    case spade: @elena_dig_mud_patch;
    case bucket_water: @elena_bucket_mud_patch(last_useslot);
}

[label,elena_dig_mud_patch]
anim(human_dig, 0);
if (%elena_progress < ^quest_elena_mud_patch4) {
    ~objboxt(spade, "You dig the soil...|The ground is rather hard.");
    return;
}
p_delay(1);
p_telejump(^quest_elena_sewer_mud_pile_coord);
mes("You climb down into the hole.");
if (%elena_progress < ^quest_elena_opened_tunnel) {
    ~mesbox("You dig deep into the soft soil... Suddenly it crumbles away! You fall through into the sewer. <nc_name(edmond)> follows you down the hole.");
    ~quest_elena_set_progress(^quest_elena_opened_tunnel);
}

[label,elena_bucket_mud_patch](int $slot)
// this does not return a nothing interesting happens.
if (%elena_progress < ^quest_elena_gasmask) {
    ~mesbox("You see no reason to do that at the moment.");
    return;
}
if (%elena_progress < ^quest_elena_opened_tunnel) {
    ~mesbox("You don't need to pour any more water. The soil is soft enough already.");
    return;
}
switch_int(%elena_progress) {
    case ^quest_elena_gasmask: ~quest_elena_set_progress(^quest_elena_mud_patch1);
    case ^quest_elena_mud_patch1: ~quest_elena_set_progress(^quest_elena_mud_patch2);
    case ^quest_elena_mud_patch2: ~quest_elena_set_progress(^quest_elena_mud_patch3);
    case ^quest_elena_mud_patch3: ~quest_elena_set_progress(^quest_elena_mud_patch4);
}
inv_delslot(inv, $slot);
inv_add(inv, bucket_empty, 1);
if (%elena_progress = ^quest_elena_mud_patch4) {
    ~mesbox("You pour water onto the soil.|The soil is now soft enough to dig into.");
} else {
    ~mesbox("You pour the water onto the soil.|The soil softens slightly.");
}
