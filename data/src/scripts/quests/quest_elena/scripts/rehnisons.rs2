[oploc1,loc_2539]
mes("You walk up the stairs.");
p_telejump(1_39_52_31_3);

[oploc1,loc_2540]
mes("You walk down the stairs.");
p_telejump(0_39_52_31_3);

[opnpc1,billy_rehnison] mes("Billy isn't interested in talking.");
[opnpc1,ted_rehnison] @rehnisons_i_hear_a_woman_is_staying_here;
[opnpc1,martha_rehnison] @rehnisons_i_hear_a_woman_is_staying_here;
[opnpc1,milli_rehnison] @rehnisons_ask_milli_what_happened;

[label,rehnisons_i_hear_a_woman_is_staying_here]
switch_int(%elena_progress) {
    case ^quest_elena_returned_book: {
        ~chatplayer("<p,happy>Hi, I hear a woman called <nc_name(elena)> is staying here.");
        ~chatnpc("<p,sad>Yes she was staying here,|but slightly over a week ago|she was getting ready to go back.");
        ~chatnpc("<p,sad>However she never managed to leave.|My daughter Milli was playing near the west wall|when she saw some shadowy figures jump out and grab her.|Milli is upstairs if you wish to speak to her."); // TODO this chathead should be null or something idk - jordan
        ~chatnpc("<p,sad>Milli is upstairs if you wish to speak to her.");
    }
    case ^quest_elena_spoke_to_milli, ^quest_elena_spoke_to_plague_house, ^quest_elena_spoke_to_clerk, ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek: {
        ~chatnpc("<p,quiz>Any luck with finding <nc_name(elena)> yet?");
        ~chatplayer("<p,sad>Not yet...");
        ~chatnpc("<p,sad>I wish you luck, she did a lot for us");
    }
    case ^quest_elena_freed_elena, ^elena_complete: {
        ~chatnpc("<p,quiz>Any luck finding <nc_name(elena)> yet?");
        ~chatplayer("<p,happy>Yes, she is safe at home now.");
        ~chatnpc("<p,happy>That's good to hear, she helped us a lot.");
    }
}

[label,rehnisons_ask_milli_what_happened]
switch_int(%elena_progress) {
    case ^quest_elena_returned_book: {
        ~chatplayer("<p,neutral>Hello.|Your parents say you saw what happened to <nc_name(elena)>...");
        ~chatnpc("<p,sad>*sniff*|Yes I was near the south east corner|when I saw <nc_name(elena)> walking by.");
        ~chatnpc("<p,sad>I was about to run to greet her|when some men jumped out.|Shoved a sack over her head|and dragged her into a building.");
        ~chatplayer("<p,quiz>Which building?");
        ~chatnpc("<p,sad>It was the mossy windowless building|in that south east corner of West Ardougne.");
        ~quest_elena_set_progress(^quest_elena_spoke_to_milli);
    }
    case ^quest_elena_spoke_to_milli, ^quest_elena_spoke_to_plague_house, ^quest_elena_spoke_to_clerk, ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek: {
        ~chatnpc("<p,quiz>Have you found <nc_name(elena)> yet?");
        ~chatplayer("<p,sad>No... I am still looking");
        ~chatnpc("<p,sad>I hope you find her...|She was nice.");
    }
    case ^elena_complete: {
        ~chatnpc("<p,quiz>Have you found <nc_name(elena)> yet?");
        ~chatplayer("<p,happy>Yes, she's safe at home now.");
        ~chatnpc("<p,neutral>I hope she comes and visits sometime.");
        ~chatplayer("<p,neutral>Maybe.");
    }
}
