[oploc1,loc_2537] @rehnissons_enter_house;
[oploc1,loc_2534] @west_ardougne_plague_house_doors;
[oploc1,loc_2535] mes("This door is locked."); // TODO probs not right but yolo in 225 seems only 1 door is the correct one.

[label,west_ardougne_plague_house_doors]
if (~check_axis(coord, loc_coord, loc_angle) = false) {
    ~open_and_close_door(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), false);
    return;
}
switch_int(%elena_progress) {
    case ^quest_elena_spoke_to_milli: {
        ~mesbox("The door won't open.|You notice a black cross on the door.");
        if (npc_find(coord, npc_719, 14, 0) = true) {
            ~chatnpcrange("<p,neutral>I'd stand away from there. That black cross means that house has been touched by the plague.");
            @multi3("But I think a kidnap victim is in here.", plague_house_but_i_think_a_kidnap_victim_is_in_here, "I fear not a mere plague.", plague_house_i_fear_not_a_mere_plague, "Thanks for the warning.", plague_house_thanks_for_the_warning);
        }
    }
    case ^quest_elena_spoke_cured_bravek: {
        if (inv_total(inv, elena_warrant) > 0 & npc_find(coord, npc_719, 14, 0) = true) {
            ~chatnpcrange("<p,neutral>I'd stand away from there. That black cross means that house has been touched by the plague.");
            ~chatplayer("<p,neutral>I have a warrant from <nc_name(bravek)> to enter here.");
            if_close;
            ~open_and_close_door(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), false);
        } else if (npc_find(coord, npc_719, 14, 0) = true) {
            ~mesbox("The door won't open.|You notice a black cross on the door.");
            ~chatnpcrange("<p,neutral>I'd stand away from there. That black cross means that house has been touched by the plague.");
        }
    }
    case ^quest_elena_freed_elena, ^elena_complete: ~open_and_close_door(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), false);
    case default: {
        ~mesbox("The door won't open.|You notice a black cross on the door.");
        if (npc_find(coord, npc_719, 14, 0) = true) {
            ~chatnpcrange("<p,neutral>I'd stand away from there. That black cross means that house has been touched by the plague.");
        }
    }
}

[label,plague_house_but_i_think_a_kidnap_victim_is_in_here]
~chatplayer("<p,neutral>But I think a kidnap victim is in here.");
~chatnpcrange("<p,neutral>Sounds unlikely, even kidnappers wouldn't go in there. Even if someone is in there, they're probably dead by now.");
@multi2("Good point.", plague_house_good_point, "I want to check anyway.", plague_house_i_want_to_check_anyway);

[label,plague_house_good_point]
// TODO unconfirmed details of this dialogue -jordan
~chatplayer("<p,neutral>Good point.");

[label,plague_house_i_want_to_check_anyway]
~chatplayer("<p,neutral>I want to check anyway.");
~chatnpcrange("<p,neutral>You don't have clearance to go in there.");
@plague_house_how_do_i_get_clearance;

[label,plague_house_i_fear_not_a_mere_plague]
~chatplayer("<p,neutral>I fear not a mere plague.");
~chatnpc("<p,neutral>That's irrelevant. You don't have clearance to go in there.");
@plague_house_how_do_i_get_clearance;

[label,plague_house_how_do_i_get_clearance]
~chatplayer("<p,quiz>How do I get clearance?");
~chatnpcrange("<p,neutral>Well you'd need to apply to the <lowercase(nc_name(head_mourner))>, or I suppose <nc_name(bravek)> the city warder.");
~quest_elena_set_progress(^quest_elena_spoke_to_plague_house);
~chatnpcrange("<p,neutral>I wouldn't get your hopes up though.");

[label,plague_house_thanks_for_the_warning]
~chatplayer("<p,neutral>Thanks for the warning.");

[label,rehnissons_enter_house]
if (%elena_progress >= ^quest_elena_returned_book) {
    ~open_and_close_door(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), false);
    return;
}
if (npc_find(coord, ted_rehnison, 7, 0) = false) {
    return;
}
~chatnpc("<p,angry>Go away. We don't want any.");
switch_int(%elena_progress) {
    case ^quest_elena_opened_pipe: {
        if (inv_total(inv, elena_book) > 0) {
            ~chatplayer("<p,neutral>I'm a friend of <nc_name(jethick)>'s, I have come to return a book he borrowed.");
            ~chatnpc("<p,neutral>Oh... Why didn't you say, come in then.");
            if_close; // very regarded
            ~quest_elena_set_progress(^quest_elena_returned_book);
            ~open_and_close_door(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), false);
            inv_del(inv, elena_book, 1);
            ~objbox(elena_book, "You hand the book to Ted as you enter.");
            ~chatnpc("<p,happy>Thanks, I've been missing that.");
        }
    }
}

