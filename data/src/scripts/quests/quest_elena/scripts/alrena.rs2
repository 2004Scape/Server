[opnpc1,alrena]
switch_int(%elena_progress) {
    case ^quest_elena_not_started: {
        ~chatplayer("<p,neutral>Hello Madam.");
        ~chatnpc("<p,neutral>Oh, hello there.");
        ~chatplayer("<p,quiz>Are you ok?");
        ~chatnpc("<p,sad>Not too bad... I've just got some|troubles on my mind...");
    }
    case ^quest_elena_started: {
        ~chatplayer("<p,neutral>Hello, <nc_name(edmond)> has asked me to help find your daughter.");
        ~chatnpc("<p,neutral>Yes he told me. I've begun making your special gas mask, but I need some <lowercase(oc_name(dwellberry))> to finish it.");
        if (inv_total(inv, dwellberry) = 0) {
            ~chatplayer("<p,neutral>I'll try to get some.");
            ~chatnpc("<p,neutral>The best place to look is in McGrubor's Wood to the north.");
        } else {
            ~chatplayer("<p,happy>Yes I've got some here.");
            ~objbox(dwellberry, "You give the <lowercase(oc_name(dwellberry))> to <nc_name(alrena)>.");
            if_close;
            mes("<nc_name(alrena)> crushes the berries into a smooth paste.");
            npc_anim(human_herbing_grind, 0);
            p_delay(2); // 3t rsc
            mes("She then smears the paste over a strange mask.");
            p_delay(2);
            ~quest_elena_set_progress(^quest_elena_gasmask);
            inv_del(inv, dwellberry, 1);
            inv_add(inv, gasmask, 1);
            ~chatnpc("<p,neutral>There we go, all done. While in West Ardougne you must wear this at all times, or you'll never make it back.");
            ~objbox(gasmask, "<nc_name(alrena)> has given you a gas mask.");
            ~chatnpc("<p,neutral>While you two are digging, I'll make a spare mask.|I'll hide it in the cupboard incase the mourners come in.");
        }
    }
    case ^quest_elena_started_mud_patch, ^quest_elena_mud_patch1, ^quest_elena_mud_patch2, ^quest_elena_mud_patch3: {
        ~chatplayer("<p,happy>Hello <nc_name(alrena)>.");
        ~chatnpc("<p,happy>Hello darling,|how's that tunnel coming along?");
        ~chatplayer("<p,neutral>We're getting there...");
        ~chatnpc("<p,neutral>Well I'm sure you're quicker than <nc_name(edmond)>.");
        ~chatplayer("<p,neutral>I just need to soften the soil and then we'll start digging.");
        ~chatnpc("<p,neutral>If you lose your protective clothing, I've made a spare set. They're hidden in the cupboard incase the mourners come in.");
    }
    case ^quest_elena_mud_patch4: {
        ~chatplayer("<p,happy>Hello again <nc_name(alrena)>.");
        ~chatnpc("<p,neutral>How's the tunnel going?");
        ~chatplayer("<p,neutral>I'm getting there.");
        ~chatnpc("<p,shifty>One of the mourners has been sniffling around asking questions about you and <nc_name(edmond)>, you should keep an eye out for him.");
        ~chatplayer("<p,neutral>Ok, thanks <nc_name(alrena)>");
    }
    case ^quest_elena_opened_tunnel: {
        ~chatplayer("<p,happy>Hello <nc_name(alrena)>.");
        ~chatnpc("<p,neutral>Hi, have you managed to get through to West Ardougne?");
        ~chatplayer("<p,sad>Not yet, but I should be going soon.");
        ~chatnpc("<p,sad>Make sure you wear your mask while you are over there! I can't think of a worse way to die.");
    }
    case ^quest_elena_tied_rope, ^quest_elena_opened_pipe, ^quest_elena_returned_book, ^quest_elena_spoke_to_milli, ^quest_elena_spoke_to_plague_house, ^quest_elena_spoke_to_clerk, ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek: {
        ~chatplayer("<p,neutral>Hello <nc_name(alrena)>.");
        ~chatnpc("<p,confused>Hello, any word on <nc_name(elena)>?");
        ~chatplayer("<p,sad>Not yet I'm afraid.");
    }
    case ^quest_elena_freed_elena, ^elena_complete, ^elena_complete_read_scroll: ~chatnpc("<p,happy>Thank you for rescuing my daughter! <nc_name(elena)> has told me of your bravery entering a house that could have been plague infected. I can't thank you enough!");
}
