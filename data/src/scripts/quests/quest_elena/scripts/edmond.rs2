[opnpc1,edmond]
switch_int(%elena_progress) {
    case ^quest_elena_not_started: {
        ~chatplayer(happy, "Hello old man.");
        ~mesbox("The man looks upset.");
        ~chatplayer(quiz, "What's wrong?");
        ~chatnpc(sad, "I've got to find my daughter. I pray that she is still alive.");
        @multi2("What's happened to her?", edmond_what_happened_to_elena, "Well, good luck finding her.", edmond_goodluck_finding_elena);
    }
    case ^quest_elena_started: {
        ~chatplayer(happy, "Hello Edmond.");
        ~chatnpc(quiz, "Have you got the <lowercase(oc_name(dwellberry))>?");
        if (inv_total(inv, dwellberry) = 0) {
            ~chatplayer(sad, "Sorry, I'm afraid not.");
            ~chatnpc(neutral, "You'll probably find them in McGrubor's Wood to the north.");
        } else {
            ~chatplayer(happy, "Yes, I have some here.");
            ~chatnpc(neutral, "Take them to my wife <nc_name(alrena)>.");
        }
    }
    case ^quest_elena_gasmask, ^quest_elena_mud_patch1, ^quest_elena_mud_patch2, ^quest_elena_mud_patch3, ^quest_elena_mud_patch4: {
        ~chatplayer(happy, "Hi <nc_name(edmond)>, I've got the gas mask now.");
        ~chatnpc(neutral, "Good stuff, now for the digging. Beneath are the Ardougne sewers. There you'll find access to West Ardougne.");
        ~chatnpc(default, "The problem is the soil is rock hard. You'll need to pour on some buckets of water to soften it up. I'll keep an eye out for the mourners.");
    }
    case ^quest_elena_opened_tunnel: {
        ~chatplayer(confused, "<nc_name(edmond)>, I can't get through to West Ardougne! There's an iron grill blocking my way. I can't pull it off alone.");
        ~chatnpc(neutral, "If you get some rope you could tie to the grill, then we could both pull it from here.");
    }
    case ^quest_elena_tied_rope: {
        ~chatplayer(neutral, "I've tied the other end of this rope to the grill.");
        ~mesbox("<nc_name(edmond)> gets a good grip on the ripe. Together you tug the rope. You hear a clunk as you both fly backwards...");
        ~chatnpc(neutral, "That's done the job.");
        ~chatnpc(neutral, "Remember to always wear the gasmask,|otherwise you'll die over there for certain...|Please bring my Elena back safe and sound.");
        ~quest_elena_set_progress(^quest_elena_opened_pipe);
    }
    case ^quest_elena_opened_pipe, ^quest_elena_returned_book, ^quest_elena_spoke_to_milli, ^quest_elena_spoke_to_plague_house, ^quest_elena_spoke_to_clerk, ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek: {
        ~chatplayer(neutral, "Hello.");
        ~chatnpc(quiz, "Have you found <nc_name(elena)> yet?");
        ~chatplayer(sad, "Not yet, it's a big city over there.");
        ~chatnpc(sad, "I hope it's not too late.");
    }
    case ^quest_elena_freed_elena: {
        ~chatnpc(happy, "Thank you, thank you! <nc_name(elena)> beat you back by minutes. Now I said I'd give you a reward. What can I give you as a reward I wonder? Here take this magic scroll, I have little use for it but it may help you.");
        queue(quest_elena_complete, 0);
        ~chatnpc(happy, "Now I'd recommend you go and see Elena. She'll want to thank you herself. She lives in the house opposite ours.");
    }
    case ^elena_complete: {
        ~chatplayer(happy, "Hello there.");
        ~chatnpc(happy, "Ah hello. Thank you again for rescuing my daughter.");
        ~chatplayer(happy, "No problem.");
    }
}

[label,edmond_what_happened_to_elena]
~chatplayer(quiz, "What's happened to her?");
~chatnpc(sad, "<nc_name(elena)>'s a missionary and a healer.|Three weeks ago she managed to cross the Ardougne wall.");
~chatnpc(sad, "No one's allowed to cross the wall in case they spread the plague, but after hearing the screams of suffering, she felt she had to help.");
~chatnpc(sad, "She said she'd be gone for a few days, but we've heard nothing since");
@multi3("Tell me more about the plague.", edmond_tell_me_more, "Can I help find her?", edmond_can_i_help_find_elena, "I'm sorry, I have to go.", edmond_sorry_i_have_to_go);

[label,edmond_tell_me_more]
~chatplayer(neutral, "Tell me more about the plague.");
~chatnpc(sad, "The mourners can tell you more than me, they're the only ones allowed to cross the border. I do know the plague is a horrible way to go... That's why <nc_name(elena)> felt she had to go help.");
@multi2("Can I help find her?", edmond_can_i_help_find_elena, "I'm sorry, I have to go.", edmond_sorry_i_have_to_go);

[label,edmond_can_i_help_find_elena]
~chatplayer(quiz, "Can I help find her?");
~chatnpc(confused, "Really, would you? I've been working on a plan to get over the wall, but I'm too old and tired to carry it through.");
~chatnpc(neutral, "If you're going over, the first thing you'll need is protection. My wife made a special gasmask for <nc_name(elena)> with <lowercase(oc_name(dwellberry))> rubbed into it.");
~chatnpc(neutral, "<oc_name(dwellberry)> help repel the virus.|We need some more though...");
~chatplayer(quiz, "Where can I find these <oc_name(dwellberry)>?");
~quest_elena_set_progress(^quest_elena_started);
~chatnpc(neutral, "The only place I know is McGrubor's Wood to the north.");
~chatplayer(neutral, "Okay, I'll go get some.");

[label,edmond_sorry_i_have_to_go]
~chatplayer(neutral, "I'm sorry, I have to go.");
~chatnpc(sad, "Okay, then, goodbye.");

[label,edmond_goodluck_finding_elena]
~chatplayer(neutral, "Well, good luck finding her.");
