[opobj3,white_cog]
if (~can_pickup_cog = true) {
    @pickup_obj(obj_coord, obj_type, obj_count);
}

[opobj3,blue_cog]
if (~can_pickup_cog = true) {
    @pickup_obj(obj_coord, obj_type, obj_count);
}

[opobj3,red_cog]
if (~can_pickup_cog = true) {
    @pickup_obj(obj_coord, obj_type, obj_count);
}

[opobj3,black_cog]
if (~can_pickup_cog = true) {
    if(inv_total(inv, bucket_water) > 0 | inv_total(worn, ice_gloves) > 0) {
        @take_black_cog;
    } else {
        ~mesbox("The cog is red hot from flames.|You cannot pick it up.");
    }
}

[opobju,black_cog]
if(last_useitem = bucket_water) {
    if (~can_pickup_cog = true) {
        @take_black_cog;
    }
} else {
    ~displaymessage(^dm_default);
}

[label,take_black_cog]
~mesbox("You pour water over the cog.|It quickly cools down enough to take.");
inv_del(inv, bucket_water, 1);
inv_add(inv, bucket_empty, 1);
if(inv_freespace(inv) > 0) {
    obj_takeitem(inv);
}

[proc,can_pickup_cog]()(boolean)
if (~get_cog_progress = ^quest_cog_not_started) {
    mes("You must speak to Brother Kojo to begin this quest.");
    return(false);
}

if (~get_cog_progress = ^quest_cog_complete) {
    mes("You have already completed this quest.");
    return(false);
}

if (inv_totalcat(inv, cog) > 0) {
    mes("The cogs are too heavy to carry more than one at a time.");
    return(false);
}
return(true);