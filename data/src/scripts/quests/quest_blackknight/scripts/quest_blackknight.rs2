// Locations:
// 3016 3513 0 - Black Knight's Fortress
// 2962 3338 2 - Sir Amik Varze
// 3031 3508 1 - Hole
// 3025 3508 0 - Grate

[oploc1,blackknight_double_door_right]
mes("The doors are locked.");

[oploc1,blackknight_double_door_left]
mes("The doors are locked.");

[oploc1,blackknight_front_door]
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
if ($is_outside = true & ((inv_getobj(worn, ^wearpos_hat) ! bronze_med_helm) | (inv_getobj(worn, ^wearpos_torso) ! iron_chainbody))) {
    @blackknight_entrance_dialogue;
} else {
    ~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);
}

[oploc1,blackknight_door_to_grill]
def_boolean $is_outside = ~check_axis(coord, loc_coord, loc_angle);
if ($is_outside = false) {
    def_boolean $has_spoken = false;
    $has_spoken = ~set_black_knight_aggro(1_47_54_17_55, $has_spoken);
    ~set_black_knight_aggro(1_47_54_15_55, $has_spoken);
}
~open_and_close_door(loc_param(next_loc_stage), $is_outside, false);

[label,blackknight_entrance_dialogue]
npc_findallzone(0_47_54_8_59);
while (npc_findnext = true) {
    if (npc_type = fortress_guard) {
        @blackknight_entrance_fortress_guard;
    }
}

[label,blackknight_entrance_fortress_guard]
~chatnpc("<p,angry>Hey, you can't come in here!|This is a high security military installation.");
def_int $option = ~p_choice3("Yes, but I work here!", 1, "Oh sorry.", 2, "So who does it belong to?", 3);
if ($option = 1) {
    ~chatplayer("<p,angry>Yes, but I work here!");
    ~chatnpc("<p,angry>Well this the guards entrance. I might be new here but I can tell you're not a guard.|You're not even wearing proper guards uniform!");
    def_int $beg_or_more_info = ~p_choice2("Pleaassee let me in!", 1, "So what is this uniform?", 2);

    if ($beg_or_more_info = 1) {
        ~chatplayer("<p,sad>Pleaassee let me in!");
        ~chatnpc("<p,angry>Go away, you're getting annoying.");
    } else if ($beg_or_more_info = 2) {
        ~chatplayer("<p,neutral>So what is this uniform?");
        ~chatnpc("<p,confused>Well you can see me wearing it.|It's iron chain mail and a medium bronze helmet.");
    }
} else if ($option = 2) {
   ~chatplayer("<p,neutral>Oh sorry.");
   ~chatnpc("<p,angry>Don't let it happen again.");
   // TODO: Set all black knights in the banquet hall to attack player and yell "Die intruder!"
} else if ($option = 3) {
    ~chatplayer("<p,neutral>So who does it belong to?");
    ~chatnpc("<p,angry>This fortress belongs to the order of Black Knights known as the Kinshra.");
}

[oploc1,blackknight_banquet_hall_door]
def_boolean $is_inside = ~check_axis(coord, loc_coord, loc_angle);
def_coord $blackknight_near_banquet_hall = 0_47_54_11_59;
def_coord $blackknight_outside = 0_47_54_8_57;
if ($is_inside = false) {
    npc_findallzone($blackknight_near_banquet_hall);
    while (npc_findnext = true) {
        if (npc_type = fortress_guard) {
           @blackknight_banquet_hall_fortress_guard($is_inside);
        }
    }

    npc_findallzone($blackknight_outside);
    while (npc_findnext = true) {
        if (npc_type = fortress_guard) {
           @blackknight_banquet_hall_fortress_guard($is_inside);
        }
    }
} else {
    ~open_and_close_door(loc_param(next_loc_stage), $is_inside, false);
}

[label,blackknight_banquet_hall_fortress_guard](boolean $is_inside)
~chatnpc("<p,neutral>I wouldn't go in there if I woz you.|Those Black Knights are in an important meeting;|They said they'd kill anyone who went in there!");
def_int $option = ~p_choice2("Ok I won't.", 1, "I don't care. I'm going in anyway.", 2);
if ($option = 1) {
    ~chatplayer("<p,neutral>Ok I won't.");
} else {
   ~chatplayer("<p,neutral>I don't care. I'm going in anyway.");
   ~open_and_close_door(loc_param(next_loc_stage), $is_inside, false);
   // TODO: Set all black knights in the banquet hall to attack player and yell "Die intruder!"
   def_boolean $has_spoken = false;
   $has_spoken =  ~set_black_knight_aggro(0_47_54_13_59, $has_spoken);
   ~set_black_knight_aggro(0_47_54_21_59, $has_spoken);
   ~set_black_knight_aggro(0_47_54_22_55, $has_spoken);
}
[oploc1,blackknight_potion_room_door]
mes("It's locked.");

// Using Cabbage on hole
[oplocu,blackknight_hole]
switch_obj (last_useitem)
{
    case cabbage : @black_knights_fortress_correct_cabbage;
    case cabbage_draynor : @black_knights_fortress_incorrect_cabbage;
    case default : ~displaymessage(^dm_default);
};

[label,black_knights_fortress_correct_cabbage]
if (%blackknight_progress = 2) {
    inv_del(inv, cabbage, 1);
    mes("You drop a cabbage down the hole.");
    p_delay(2);
    mes("The cabbage lands in the cauldron below.");
    p_delay(2);
    mes("The mixture in the cauldron starts to froth and bubble.");
    p_delay(2);
    mes("You hear the witch groan in dismay.");
    p_delay(2);
    ~chatplayer("<p,quiz>Right I think that's successfully sabotaged the secret weapon.");
    ~update_blackknight_progress;
} else {
    ~chatplayer("<p,confused>Why would I want to do that?");
}

[label,black_knights_fortress_incorrect_cabbage]
if (%blackknight_progress = 2) {
    mes("This is the wrong sort of cabbage!");
    p_delay(2);
    mes("You are meant to be hindering the witch.");
    p_delay(2);
    mes("Not helping her.");
    p_delay(2);
} else {
    ~chatplayer("<p,confused>Why would I want to do that?");
}

// Listening at grate
[oploc1,blackknight_grate]
if (%blackknight_progress = 1) {
    ~chatnpc_specific("Black Knight", black_knight_knight, "<p,quiz>So, how's the secret weapon coming along?");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,happy>The invincibility potion is almost ready...");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,happy>It's taken me five years but it's almost ready.");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,neutral>Greldo the Goblin here|is just going to fetch the last ingredient for me.");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,neutral>It's a specially grown cabbage.|Grown by my cousin Helda who lives in Draynor Manor.");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,neutral>The soil there is slightly magical.|And it gives the cabbages slight magic properties.");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,shock>Not to mention the trees!");
    ~chatnpc_specific("Witch", black_knight_witch, "<p,neutral>Now remember Greldo only a Draynor Manor cabbage will do. Don't get lazy and bring any old cabbage.|That would entirely wreck the potion.");
    ~chatnpc_specific("Greldo", greldo, "<p,goblinchat>Yes mithreth."); // neutral in osrs
    ~update_blackknight_progress;
} else {
    mes("I can't hear much right now.");
}

[proc,set_black_knight_aggro](coord $coord, boolean $has_spoken)(boolean)
npc_findallzone($coord);
while (npc_findnext = true) {
    if (npc_type = npc_179) {
        if ($has_spoken = false) {
            npc_say("Die intruder!");
            $has_spoken = true;
        }
        npc_setmode(opplayer2);
    }
}
return($has_spoken);

[proc,update_blackknight_progress]
%blackknight_progress = calc(%blackknight_progress + 1);
~send_quest_progress(questlist:fortress, %blackknight_progress, ^blackknight_complete);

[queue,black_knights_fortress_quest_complete]
%blackknight_progress = ^blackknight_complete;
inv_add(inv, coins, 2500);
mes("Sir Amik hands you 2500 coins.");
~send_quest_complete(questlist:fortress, coins, 250, ^blackknight_questpoints, "You have completed the\\nBlack Knights' Fortress Quest!");