[opheld3,ogre_bow]
if (%chompybird_progress < ^chompybird_complete) {
    mes("You've scratched up no kills yet!");
    mes("You've got to complete the quest first!");
    return;
}
def_int $kills = %chompybird_kills;
def_string $rank = ~add_article(~get_chompy_rank($kills));
def_string $rank_mes = "~ You're <$rank>! ~";
if($kills = 0) {
    $rank_mes = "You've not even started.";
}
mes("You've scratched up a total of <tostring($kills)> chompy bird kills so far!");
mes($rank_mes);
if($kills >= 4000) {
    // https://youtu.be/LtoYNXgM5Wg?si=MeXmjKYkbEKliZ9F&t=16
    ~objbox(chompy_bird_obj, "@blu@**** Congratulations! ****|@dre@~ You're an Expert Dragon Archer! ~|This is the highest honour that can be bestowed on any|chompy bird hunter.", 250, 0, ^objbox_height); 
    return;
}
~objbox(chompy_bird_obj, "You've killed a total of @blu@<tostring($kills)> @bla@chompy birds so far!||@blu@<$rank_mes>", 250, 0, ^objbox_height); // imgur.com/N4FttpP

[proc,get_chompy_rank](int $kills)(string)
if ($kills < 5) {
    return("Ogre Novice");
}
if ($kills < 15) {
    return("Beginner");
}
if ($kills < 20) {
    return("Ogre Learner");
}
if ($kills < 30) {
    return("Learner");
}
if ($kills < 40) {
    return("Ogre Bowman");
}
if ($kills < 50) {
    return("Bowman");
}
if ($kills < 70) {
    return("Ogre Yeoman");
}
if ($kills < 95) {
    return("Yeoman");
}
if ($kills < 125) {
    return("Ogre Markman");
}
if ($kills < 170) {
    return("Marksman");
}
if ($kills < 225) {
    return("Ogre Woodsman");
}
if ($kills < 300) {
    return("Woodsman");
}
if ($kills < 400) {
    return("Ogre Forester");
}
if ($kills < 550) {
    return("Forester");
}
if ($kills < 700) {
    return("Ogre Bowmaster");
}
if ($kills < 1000) {
    return("Bowmaster");
}
if ($kills < 1300) {
    return("Ogre Expert");
}
if ($kills < 1700) {
    return("Expert");
}
if ($kills < 2250) {
    return("Ogre Dragon Archer");
}
if ($kills < 3000) {
    return("Dragon Archer");
}
if ($kills < 4000) {
    return("Expert Ogre Dragon Archer");
}
return ("Expert Dragon Archer");