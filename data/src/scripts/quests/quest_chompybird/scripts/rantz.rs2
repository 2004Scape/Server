// text and mesanims are taken from OSRS
[opnpc1,chompybird_rantz]
if(%chompybird_progress >= ^chompybird_rantz_gave_player_bow & ~obj_gettotal(ogre_bow) = 0) {
    ~chatplayer("<p,neutral>Hey, Rantz, I've lost the 'stabbie chucker', do you have another one please?");
    def_int $cost = ~random_range(500, 550);
    ~chatnpc("<p,neutral>Mee's has, but is not for freeness, dis one cost <tostring($cost)> of da gold coinses. Does you wants it?");
    switch_int(~p_choice2("Yes, I'll buy the bow.", 1, "No thanks...", 2)) {
        case 1 :
            ~chatplayer("<p,neutral>Yes, I'll buy the bow.");
            if(inv_total(inv, coins) < $cost) {
                ~chatnpc("<p,neutral>Hey you don't got da coins... come back when you have.");
            } else {
                inv_del(inv, coins, $cost);
                inv_add(inv, ogre_bow, 1);
                ~doubleobjbox(ogre_bow, coins, "You hand over the coins, and Rantz hands over the bow.", 200);
                ~chatplayer("<p,neutral>Thanks.");
            }
        case 2 :
            ~chatplayer("<p,neutral>No thanks...");
    }
}
@rantz_main_dialogue;

[label,rantz_main_dialogue]
switch_int(%chompybird_progress) {
    case ^chompybird_not_started :
        ~chatnpc("<p,neutral>Hey you creature! Make me some stabbers! I wanna hunt da chompy?");

        def_int $op = ~p_choice4("What are 'stabbers'?", 1, "What's a 'chompy'?", 2, "Ok, I'll make you some 'stabbers'.", 3, "Er, make you're own 'stabbers'!", 4);
        while($op ! null) {
            if($op = 1) {
                ~chatplayer("<p,neutral>What are stabbers?");
                ~chatnpc("<p,neutral>For da stabbie chucker, I's wanna hunt da chompy! Creature knows what Rantz wants... ...flyin' to stabbie da chompy!");
                ~objbox(ogre_bow,"The ogre shows you a huge but crude bow and then starts to nod energetically in an effort to help you understand.", 250, 0, divide(^objbox_height, 2));
                ~chatplayer("<p,neutral>I think I understand. You want me to make some arrows for you?");
                ~chatnpc("<p,neutral>Yeah, is what Rantz sayed, make da stabbers for da stabby chucker!");
                $op = ~p_choice4("How do I make the 'stabbers'?", 5, "What's a 'chompy'?", 2, "Ok, I'll make you some 'stabbers'.", 3, "Er, make you're own 'stabbers'!", 4);
            } else if($op = 2) { 
                ~chatplayer("<p,neutral>What's a 'chompy'?");
                ~chatnpc("<p,neutral>Da chompy is der bestest yummies for Rantz, Fycie and Bugs! We's looking for da yummies all da time. Da chompy is a big flapper, Rantz want's stabbers to sneaky, sneaky, stick da chompy.");
                ~chatplayer("<p,quiz>Ah, so 'da chompy' is some kind of bird?");
                ~chatnpc("<p,neutral>Yeah, is what Rantz sayed, Da chompy is da big flapper and is bestest yummies. But Rantz needs stabbers to stick da chompy... Will creatures make dem stabbers for us?");
                $op = ~p_choice4("What are 'stabbers'?", 1, "What's a 'chompy'?", 2, "Ok, I'll make you some 'stabbers'.", 3, "Er, make you're own 'stabbers'!", 4);
            } else if($op = 3) {
                $op = null;
                ~chatplayer("<p,neutral>OK, I'll make you some 'stabbers'.");
                ~chatnpc("<p,neutral>Good you creature, you need sticksies from achey tree and stabbies from dog bones.");
                %chompybird_progress = ^chompybird_started;
                ~send_quest_progress(questlist:chompybird, %chompybird_progress, ^chompybird_complete);
            } else if($op = 4) {
                $op = null;
                ~chatplayer("<p,neutral>Er, make you're own 'stabbers'!");
                ~chatnpc("<p,angry>When I make 'stabbers', I pretend you chompy and practice on you!");
            } else if($op = 5) {
                ~chatplayer("<p,neutral>How do I make the 'stabbers'?");
                ~chatnpc("<p,neutral>Ahhh, da creature wants to know Rantz secret for stabbers? Rantz not say till da creature says will make da stabbers for us else you Creature steals da chompy from us!");
                $op = ~p_choice3("What's a 'chompy'?", 2, "OK, I'll make you some 'stabbers'.", 3, "Er, make you're own 'stabbers'!", 4);
            }
        }
    case ^chompybird_started :
        @return_to_rantz;
    case ^chompybird_given_arrows, ^chompybird_kids_play_with_toad, ^chompybird_removed_rock_from_chest :
        ~chatnpc("<p,quiz>Hey you creature, you still here?");
        ~chatnpc("<p,quiz>Da chompy still not coming! We need da fatsy toady to get da chompy, do you got it? Do you got da fatsy toady? Then we can sneaky, sneaky stick da chompy.");
        
        if (inv_total(inv, bloated_toad) = 0) {
            ~chatplayer("<p,angry>No I haven't got the 'fatsy toady' yet!");
            ~chatnpc("<p,sad>Dat's a pidy... but maybe Rantz can help da creature?");
            @toadies_questions;
        } else {
            %chompybird_progress = ^chompybird_shown_toad;
            ~chatplayer("<p,neutral>Yes, I have a 'fatsy toady' for you, here look!");
            @show_toad;
        }
    case ^chompybird_shown_toad :
        @where_to_put_toadies;
    // Maybe chompy_ate_toad has a separate set of dialogue, but in normal circumstances the stage
    // advances to rantz_tried_to_shoot_chompy too quickly to tell.
    case ^chompybird_dropped_toad, ^chompybird_chompy_ate_toad :
        // todo mesanim TBC
        ~chatplayer("<p,neutral>There you go, I've placed the bait.");
        ~chatnpc("<p,neutral>Goodz, me now waits for da chompy! It shouldn't be long now. Sneaky... sneaky...");
        ~chatplayer("<p,neutral>Yes, I know... stick da chompy!");
        ~chatnpc("<p,neutral>Hey, you's creature, is da fatsy toady still dere? Go get more fatsy toadies if dey all gone!");
        ~chatplayer("<p,neutral>What? I have to get more bait if there's none there? Does this Chompy Bird even exist I wonder?");
    case ^chompybird_rantz_tried_to_shoot_chompy :
        ~chatplayer("<p,neutral>Hey there, you keep missing the chompy bird.");
        ~chatnpc("<p,angry>I knows, I keeps missing... because your stabbers are|worserer at flying than a dead dog.");

        switch_int(~p_choice2("Oh, keep trying then... you might hit one through pure luck.", 1, "Come on, let me have a go...", 2)) {
            case 1 :
                ~chatplayer("<p,angry>Oh, keep trying then... you might hit one through pure luck.");
                ~chatnpc("<p,angry>Grrrr... You lookin' like a chompy!");
            case 2 :
                ~chatplayer("<p,angry>Come on, let me have a go...");
                ~chatnpc("<p,angry>No, is Rantz stabby thrower... you too weedy...");

                switch_int(~p_choice2("I'm actually quite strong... please let me try.", 1, "Oh suit yourself, you'll just have to go hungry.", 2)) {
                    case 1 :
                        ~chatplayer("<p,sad>I'm actually quite strong... please let me try.");
                        ~chatnpc("<p,angry>Oh, ok...I lend you other stabby thrower... but creature don't better cry when it hurts itself.");

                        inv_add(inv, ogre_bow, 1);
                        %chompybird_progress = ^chompybird_rantz_gave_player_bow;
                        ~objbox(ogre_bow,"Rantz hands over an ogre bow. It's huge! You can|barely drawn back the string!", 250, 0, divide(^objbox_height, 2));
                    case 2 :
                        ~chatplayer("<p,angry>Oh suit yourself, you'll just have to go hungry.");
                        ~chatnpc("<p,happy>Or I eat you instead!");
                }
        }
    case ^chompybird_rantz_gave_player_bow, ^chompybird_player_killed_chompy :
        ~chatnpc("<p,angry>Hey You! Got da chompy yet?");
        if (inv_total(inv, raw_chompy) > 0) {
            ~chatplayer("<p,neutral>Yep, here's your chompy Bird!");
            @rantz_show_chompy;
        }
        ~chatplayer("<p,neutral>Not yet!");
        ~chatnpc("<p,angry>Well hurry up and get some, we is hungry!");
    case ^chompybird_told_to_cook_chompy :
        @rantz_tell_chompy_flavour;
    case ^chompybird_chompy_cooked :
        @hand_chompy_to_rantz;
    case ^chompybird_complete :
        ~chatnpc("<p,neutral>Hey deyr, t'anks for da chompy, it was scrumbly!");
        // No chompy bird hats yet
}

[label,rantz_show_chompy]
~objbox(raw_chompy,"You show Rantz the freshly plucked chompy carcass.", 250, 0, divide(^objbox_height, 2));

if(%chompybird_progress < ^chompybird_player_killed_chompy) {
    ~chatnpc("<p,angry>Dat's a rotten chompy, did you shoot that creature? Is stinking bad, I's thinks it's too old. Go shoot your own chompy bird.");
    return;
}
~chatnpc("<p,angry>Dat's a great chompy, you musta got a lucky shot wiv|da stabbie chucker.");
%chompybird_progress = ^chompybird_told_to_cook_chompy;
if (random(1) = 0) {
    %chompybird_kills = setbit(%chompybird_kills, ^chompybird_varbit_rantz_flavour);
} else {
    %chompybird_kills = clearbit(%chompybird_kills, ^chompybird_varbit_rantz_flavour);
}

@rantz_tell_chompy_flavour;

[label,rantz_tell_chompy_flavour]
~chatnpc("<p,angry>Okay's now you's needs to cook da chompy! Slurp!|You's can cook it's over der!|@blu@~ Rantz points to a nearby spit roast. ~");

def_string $chompy_flavour = "Potato";
if (testbit(%chompybird_kills, ^chompybird_varbit_rantz_flavour) = true) {
    // full stop (sic) in OSRS
    $chompy_flavour = "Onion.";
}

~chatnpc("<p,angry>But's we's particular about our chompy yumms. Me's|wants <$chompy_flavour> wiv mine! Fycie and Bugs want|something wiv der's as well, go and ask 'em wat dey|want.");
~chatplayer("<p,angry>What! Now I've got the chompy bird, you expect me to|cook it as well?");
~chatnpc("<p,angry>Yep, da spit's over der! Last time Rantz did yummies,|got very bad, did bad things to food... and belly.");
~chatplayer("<p,angry>HUH!");

[opnpcu,chompybird_rantz]
switch_obj(last_useitem) {
    case raw_chompy :
        if (%chompybird_progress < ^chompybird_player_killed_chompy) {
            ~chatnpc("<p,angry>Erk! Da's a yukky chompy, I's wants a yummie chompy!");
            @return_to_rantz;
            return;
        }

        if (%chompybird_progress = ^chompybird_player_killed_chompy) {
            @rantz_show_chompy;
            return;
        }

        if (%chompybird_progress = ^chompybird_told_to_cook_chompy) {
            ~objbox(raw_chompy,"You show Rantz the freshly plucked chompy carcass.", 250, 0, divide(^objbox_height, 2));
            @rantz_tell_chompy_flavour;
            return;
        }

        ~chatnpc("<p,neutral>Erk! You's creature can cook da chompy on da spit! Rantz not do it for you!");

    case cooked_s_chompy :
        ~objbox(cooked_s_chompy, "You show the seasoned chompy to Rantz.", 250, 0, divide(^objbox_height, 2));
        if (%chompybird_progress = ^chompybird_chompy_cooked) {
            @hand_chompy_to_rantz;
            return;
        }
        ~chatnpc("<p,neutral>Erhhhh! Dat doesn't look like somfin I'd wanna eat. Wher'dya find it?");

    case cooked_chompy :
        ~objbox(cooked_chompy,"You offer Rantz the cooked chompy Bird.", 250, 0, divide(^objbox_height, 2));
        ~chatnpc("<p,happy>Erm.. Da's very kindly of you creature!");
        inv_del(inv, cooked_chompy, 1);

    case ogre_arrow :
        // singular "Arrow" is correct on OSRS
        ~objbox(ogre_arrow_5,"You show Rantz the ogre arrow.", 250, 0, divide(^objbox_height, 2));
        if (%chompybird_progress = ^chompybird_not_started) {
            ~chatnpc("<p,angry>Don't creature threaten Rantz with pointy stabbers!");
            return;
        }

        if (%chompybird_progress = ^chompybird_given_arrows) {
            ~chatnpc("<p,neutral>Creature already given Rantz stabbers, Dey's worser at flying dan a dead dog.");
            return;
        }

        ~chatnpc("<p,neutral>Hey you creature..you made da stabbers|Dat's good creature!");
        
        if (%chompybird_progress = ^chompybird_started & testbit(%chompybird_kills, ^chompybird_varbit_made_arrows) = false) { 
            ~chatnpc("<p,angry>Hey, deese stabbers no good... did you make dem you's self? You go make dem stabbers for me creature! Don't get old ones or from other creatures.");
            @no_arrows_choice;
            return;
        }

        @attempt_give_rantz_arrows;

    case bloated_toad :
        if (%chompybird_progress >= ^chompybird_shown_toad) {
            ~objbox(bloated_toad,"You show the bloated toad to Rantz.", 250, 0, divide(^objbox_height, 2));
            ~chatnpc("<p,neutral>Hey creature got da fatsy toady Is you's gonna stick da chompy?");
            return;
        }

        @show_toad;

    case ogre_bow :
        ~chatnpc("<p,neutral>Yeah, it's a goodly stabbie chucker, but you's is too weedy to use it like Rantz.");

    case default :
        mes("Nothing interesting happens.");
}

[label,return_to_rantz]
~chatnpc("<p,neutral>Hey you creature... Have you made me da stabbers? I wanna stick da chompy?");
if (inv_total(inv, ogre_arrow) > 0) {
    @attempt_give_rantz_arrows;
} else {
    @no_arrows;
}

[label,no_arrows]
~chatplayer("<p,neutral>Er not exactly?");
~chatnpc("<p,angry>You do stabbers quick quick! Or Rantz make stabbers for Rantz and then practice for chompy sticking on creature!");
@no_arrows_choice;

[label,no_arrows_choice]
switch_int(~p_choice2("How do I make the 'stabbers' again?", 1, "Ok, I'll make the 'stabbers' for you.", 2)) {
    case 1 :
        ~chatplayer("<p,neutral>How do I make the 'stabbers' again?");
        ~chatnpc("<p,neutral>Grrr creature... you's no good stabber maker! You's make da stabbie bit from da dog bones, and get da sticksies from da Achey tree... simple see? Oh and da flufsies from da flappers as well!");
        ~chatplayer("<p,quiz>Oh, so I need logs from the achey tree, bones from a canine... and feathers?");
        ~chatnpc("<p,happy>Is just what Rantz sayed!|@blu@~ The hulking ogre nods excitedly. ~");
    case 2 :
        ~chatplayer("<p,neutral>Ok, I'll make the 'stabbers' for you.");
        ~chatnpc("<p,neutral>Good creature, quickly, hurry bring da stabbers!");
}

[label,attempt_give_rantz_arrows]
~chatplayer("<p,angry>Well, yes actually, as you asked so nicely. Here you go! Here's your 'stabbers'.");

// This check runs first, then quantity
if (testbit(%chompybird_kills, ^chompybird_varbit_made_arrows) = false) {
    ~chatnpc("<p,angry>Hey, 'dee'se stabbers no good! Did you make dem you's self? YOU go make dem arrows for ME creature!");
}
else if (inv_total(inv, ogre_arrow) < 6) {
    ~chatnpc("<p,neutral>Dat's not enough, me's an good shot, but need some for practice. Bring more than fingers on hand...");
}
else {
    @give_rantz_arrows;
}

[label,give_rantz_arrows]
inv_del(inv, ogre_arrow, 6);
%chompybird_progress = ^chompybird_given_arrows;
~objbox(ogre_arrow_5,"Rantz takes six ogre arrows off you.", 250, 0, divide(^objbox_height, 2));

~chatnpc("<p,happy>Ahh, der creature has dem... goodly, goodly. Now us can stick der chompy bird...");
~chatnpc("<p,sad>But da chompy not coming without da fatsy toadies... Godda get der fatsy toadies to get da chompys. Den we put it over de're and sneaky, sneaky stick da chompy.");

@toadies_questions;

[label,toadies_questions]
switch_int(~p_choice5("How do we make the chompys come?", 1, "What are 'fatsy toadies'?", 2, "Where do we put the 'fatsy toadies'?", 3, "What do you mean 'sneaky..sneaky, stick da chompy?'", 4, "Ok, thanks.", 5)) {
    case 1 :
        ~chatplayer("<p,neutral>How do we make the chompys come?");
        ~chatnpc("<p,neutral>Chompys love da fatsy toadies. Toadies get big on der swamp gas and der chompys are licking der lips for em as me is licking lips for da chompy. Da chompys don't like da smaller toadies from nearby swampy.");
        if (%chompybird_progress < ^chompybird_kids_play_with_toad) {
            %chompybird_progress = ^chompybird_kids_play_with_toad;
        }
        ~chatnpc("<p,happy>Dey's fussie eaters just like Rantz. Fycie an' Bugs play with toadies and blower dey's all times making fatsy toadies.");
        @toadies_questions;
    case 2 :
        ~chatplayer("<p,neutral>What are 'fatsy toadies'?");
        ~chatnpc("<p,neutral>Fatsy toadies are da chompy burds bestest yumms. But da toadies here are too small for da chompy. You've godda make da toadies big and round!");
        @toadies_questions;
    case 3 :
        ~chatplayer("<p,neutral>Where do we put the 'fatsy toadies'?");
        ~chatnpc("<p,angry>Over der!");

        ~mesbox("The ogre points to a small clearing to da south.");

        ~chatnpc("<p,angry>Ok creature? You got dat? Over dere by der no tree's place.");
        @toadies_questions;
    case 4 :
        ~chatplayer("<p,neutral>What do you mean 'sneaky..sneaky, stick da chompy?'");
        ~chatnpc("<p,quiz>Duh! You creature is a bit stoopid yes? Us needs to sneaky, sneaky and stick da chompy! Den we can eat da chompy!");
        @toadies_questions;
    case 5 :
        ~chatplayer("<p,neutral>Ok, thanks.");
}

[label,where_to_put_toadies]
~chatplayer("<p,neutral>Where should I put the 'fatsy toadies'?");
cam_lookat(0_41_46_12_22, 25, 7, 7);
cam_moveto(0_41_46_15_12, 900, 2, 2);
hint_stop();
hint_coord(^hint_center, 0_41_46_12_22, 0);
queue(cr_queue, 0);
~chatnpc("<p,neutral>Over 'dere creature, put da toadies over der!|@blu@~ The ogre points to a clearing to the south. ~");

[label,show_toad]
~objbox(bloated_toad,"You show the bloated toad to Rantz. He nods with approval.", 250, 0, divide(^objbox_height, 2));
~chatnpc("<p,neutral>Dat's a good fatsy toady, now we's need to put it for da chompy to come.");
@where_to_put_toadies;

[label,hand_chompy_to_rantz]
~chatnpc("<p,angry>Hey creature, did you's get da cooked chompy yet?|I smelled something cooking and it mades me 'ungry.|Hand over da chompy if ya know what's good for ya.");
if (inv_total(inv, cooked_s_chompy) < 1) {
    ~chatplayer("<p,neutral>Well, erm, I don't have one at the moment.");
    ~chatnpc("<p,angry>Well, 'urry up... else you's creature is looking tasty to me soon!");
    return;
}
~chatplayer("<p,angry>Yes, here you go, here's your cooked chompy bird.");
~objbox(cooked_s_chompy, "You hand over the cooked chompy bird to Rantz.", 250, 0, divide(^objbox_height, 2));
~chatnpc("<p,happy>Hey hey! We got da delicious chompy bird - yay!|This looks really tasty as well!");
~chatnpc("<p,happy>Tank's very much for da chompy...|Fycie an Bugs like very much da chompy yumms!|@blu@~ The family of ogres sit down together ~|@blu@~ and enjoy your well cooked chompy bird ~");
~chatplayer("<p,laugh>It's my pleasure!");
inv_del(inv, cooked_s_chompy, 1);
queue(quest_chompybird_complete, 0);

// look for any chompy birds to shoot
[ai_timer,chompybird_rantz]
if (.npc_find(npc_coord, chompy_bird, 17, ^vis_lineofsight) = true) {
    if (p_finduid(.%quest_chompybird_baiter) = true) {
        // we only run this logic for players in the right quest state
        if (%chompybird_progress >= ^chompybird_chompy_ate_toad & %chompybird_progress < ^chompybird_rantz_gave_player_bow) {
            %rantz_attacking_chompy = .npc_uid;
            %quest_chompybird_baiter = uid;
            mes("Rantz: Hey, dere's da chompy, I's gonna shoot it.");
            npc_say("Hey, dere's da chompy, I's gonna shoot it.");
            npc_facesquare(.npc_coord);
            npc_setmode(none);
            npc_queue(11, 0, 2);
            npc_settimer(calc(10 + (random(6) * 2)));
            return;
        }
    }
}

[ai_queue11,chompybird_rantz]
if(.npc_finduid(%rantz_attacking_chompy) = true) {
    npc_anim(ogre_longbow, 0);
    spotanim_npc(ogre_arrow_launch, 50, 0);
    def_int $duration = ~npc_projectile(npc_coord, .npc_uid, ogre_arrow_travel, 40, 36, 41, 15, 5, 11, 5);
    def_int $delay = add(sub(divide($duration, 30), 1), 2);
    npc_queue(12, 0, calc($delay + 1));
}

[ai_queue12,chompybird_rantz]
npc_setmode(null);
if(.npc_finduid(%rantz_attacking_chompy) = true) {
    if (p_finduid(%quest_chompybird_baiter) = true) {
        %chompybird_progress = ^chompybird_rantz_tried_to_shoot_chompy;
        mes("Rantz keeps missing the chompy bird...");
        mes("Rantz: Grrr...de'ese arrows are rubbish.");
        npc_say("Grrr...de'ese arrows are rubbish.");
    }
}