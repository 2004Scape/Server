[oploc1,loc_3377]
if (%chompybird_progress < ^chompybird_kids_play_with_toad) {
    mes("Perhaps you'd better ask permission before opening this.");
    return;
}

say("Humph!");
anim(human_pickuptable, 0);

p_delay(0);

if (p_finduid(uid) = true) {
    // TODO confirm chance
    if (random(3) = 0) {
        mes("You strain to lift the huge rock off the chest...");

        p_delay(1);
        
        if (p_finduid(uid) = true) {
            // TODO confirm message/reduction in 2004
            mes("... but it's just too heavy for you.");
            mes("The experience has left you feeling temporarily weakened.");
            say("Arrgghhhhh!");
            stat_sub(strength, 1, 0);
            return;
        }

        return;
    }

    mes("You manage to lift the huge rock off the chest.");
    say("I guess this is what an ogre would call a locked chest.");

    if (%chompybird_progress = ^chompybird_kids_play_with_toad) {
        %chompybird_progress = ^chompybird_removed_rock_from_chest;
    }

    // duration from OSRS
    loc_change(loc_3378, 48);
}

[oploc1,loc_3378]
if (inv_freespace(inv) = 0) {
    ~objbox(ogre_bellows_0,"Rantz stores ogre bellows in here, but you don't have space to hold any.");
    return;
}

inv_add(inv, ogre_bellows_0, 1);
~objbox(ogre_bellows_0,"You take a pair of bellows.");
