[proc,reduce_ogre_bellows](obj $used_bellow)
{
    if ($used_bellow = filled_ogre_bellow3) {
        inv_del(inv, filled_ogre_bellow3, 1);
        inv_add(inv, filled_ogre_bellow2, 1);
    } else if ($used_bellow = filled_ogre_bellow2) {
        inv_del(inv, filled_ogre_bellow2, 1);
        inv_add(inv, filled_ogre_bellow1, 1);
    } else if ($used_bellow = filled_ogre_bellow1) {
        inv_del(inv, filled_ogre_bellow1, 1);
        inv_add(inv, empty_ogre_bellows, 1);
    }
}

[proc,come_here_toady]()
{
    say("Come here toady!");
    npc_setmode(playerfaceclose);
    anim(human_chompybird_ogrebellows, 30);
    sound_synth(ogre_bellows, 0, 30);
    spotanim_pl(ogre_bellows_use, 64, 30);
    npc_anim(chompy_toad_inflate, 30);
}

[opnpcu,swamp_toad]
switch_obj(last_useitem) {
    case empty_ogre_bellows :

        ~come_here_toady();

        p_delay(1);

        if (p_finduid(uid) = true) {
            npc_say("Hissss....");
            
            p_delay(1);

            if (p_finduid(uid) = true) {
                mes("The air seems too thin to stay in the toad.");
                mes("Perhaps you need something thicker than air.");
            }
        }        
    case filled_ogre_bellow3, filled_ogre_bellow2, filled_ogre_bellow1 :
        if (inv_freespace(inv) < 1) {
            mes("You don't have space to carry that.");
            return;
        }

        def_obj $used_bellow = last_useitem;

        if (inv_total(inv, bloated_toad) >= 3) {
            mes("One of your bloated toads manages to escape.");

            npc_add(map_findsquare(coord, 1, 1, ^map_findsquare_lineofwalk), swamp_toad, 100);
            npc_say("Ribbitt!");
            inv_del(inv, bloated_toad, 1);

            p_delay(1);

            if (p_finduid(uid) = true) {
                mes("You manage to catch the toad and inflate it with the swamp gas.");
                ~come_here_toady();

                p_delay(3);

                if (p_finduid(uid) = true) {
                    mes("You add the bloated toad to your inventory.");
                    inv_add(inv, bloated_toad, 1);

                    ~reduce_ogre_bellows($used_bellow);

                    npc_del;
                }
            }

            return;
        }
        
        mes("You manage to catch the toad and inflate it with the swamp gas.");
        ~come_here_toady();

        p_delay(3);
        ~reduce_ogre_bellows($used_bellow);
        mes("You add the bloated toad to your inventory.");
        inv_add(inv, bloated_toad, 1);

        npc_del;

    case default :
        mes("Nothing interesting happens.");
}
