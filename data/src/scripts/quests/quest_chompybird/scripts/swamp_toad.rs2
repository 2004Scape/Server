[proc,reduce_ogre_bellows](obj $used_bellow)
{
    if ($used_bellow = ogre_bellows_3) {
        inv_del(inv, ogre_bellows_3, 1);
        inv_add(inv, ogre_bellows_2, 1);
    } else if ($used_bellow = ogre_bellows_2) {
        inv_del(inv, ogre_bellows_2, 1);
        inv_add(inv, ogre_bellows_1, 1);
    } else if ($used_bellow = ogre_bellows_1) {
        inv_del(inv, ogre_bellows_1, 1);
        inv_add(inv, ogre_bellows_0, 1);
    }
}

[proc,come_here_toady]()
{
    // TODO face toad towards player and face player towards toad?
    // TODO sound? in OSRS it's sound 1455
    say("Come here toady!");
    anim(human_ogre_bellows_use, 30);
    spotanim_pl(ogre_bellows_use, 64, 30);
    npc_anim(chompy_toad_death, 30);
}

[opnpcu,swamp_toad]
switch_obj(last_useitem) {
    case ogre_bellows_0:
        // TODO tbc - wasn't tested on OSRS for if you can attempt with full inv but empty bellows
        if (inv_freespace(inv) < 1) {
            mes("You don't have space to carry that.");
            return;
        }

        ~come_here_toady();

        p_delay(1);

        if (p_finduid(uid) = true) {
            npc_say("Hissss....");
            
            p_delay(1);

            if (p_finduid(uid) = true) {
                mes("The air seems too thin to stay in the toad.");
                mes("Perhaps you need something thicker than air.");
            }
        }        
    case ogre_bellows_3, ogre_bellows_2, ogre_bellows_1:
        if (inv_freespace(inv) < 1) {
            mes("You don't have space to carry that.");
            return;
        }

        def_obj $used_bellow = last_useitem;

        if (inv_total(inv, bloated_toad) >= 3) {
            mes("One of your bloated toads manages to escape.");

            // TODO does this spawn at players feet?
            // TODO is duration correct?
            npc_add(coord, swamp_toad, 100);
            npc_say("Ribbitt!");
            inv_del(inv, bloated_toad, 1);

            p_delay(1);

            if (p_finduid(uid) = true) {
                mes("You manage to catch the toad and inflate it with the swamp gas.");
                ~come_here_toady();

                p_delay(3);

                if (p_finduid(uid) = true) {
                    mes("You add the bloated toad to your inventory.");
                    inv_add(inv, bloated_toad, 1);

                    ~reduce_ogre_bellows($used_bellow);

                    npc_del;
                }
            }

            return;
        }
        
        mes("You manage to catch the toad and inflate it with the swamp gas.");
        ~come_here_toady();

        p_delay(3);

        if (p_finduid(uid) = true) {
            ~reduce_ogre_bellows($used_bellow);
            mes("You add the bloated toad to your inventory.");
            inv_add(inv, bloated_toad, 1);

            npc_del;
        }

    case default:
        mes("Nothing interesting happens.");
}
