[opnpc1,king_narnode]
switch_int(%grandtree_progress) {
    case ^grandtree_not_started:
        if(coordz(npc_coord) > 6400) {
            @king_narnode_foundations;
        }
        ~chatnpc("<p,happy>Welcome Traveller. I am King Narnode.|It's nice to see an outsider.");
        ~chatplayer("<p,happy>Hi! It seems to be a busy settlement.");
        ~chatnpc("<p,worried>For now.");
        switch_int(~p_choice2("You seem worried, what's up?", 1, "I'll be off now.", 2)) {
            case 1:
                ~chatplayer("<p,quiz>You seem worried, what's up?");
                ~chatnpc("<p,sad>Traveller, Can I speak to you in strictest confidence?");
                ~chatplayer("<p,neutral>Of course sire.");
                npc_setmode(none);
                ~chatnpcnoturn("<p,shifty>Not here, follow me.");
                if_close;
                def_int $npc_dist = distance(npc_coord, 0_38_54_32_42);
                npc_walk(0_38_54_32_42);
                if($npc_dist > 0) {
                    p_delay(calc($npc_dist + 1)); // TODO: + 1 until delay change is fix
                }
                npc_facesquare(movecoord(npc_coord, -1, 0, -1));
                if(loc_find(movecoord(coord, -1, 0, 0), loc_2446) = true) {
                    loc_change(loc_2445, 10);
                }
                npc_say("Down here.");
                ~forcemove2(0_38_54_32_41);
                facesquare(movecoord(coord, -1, 0, 0));
                p_delay(0);
                anim(human_pickupfloor, 0);
                p_delay(0);
                npc_setmode(null);
                p_telejump(movecoord(coord, -1, 0, 6399));
                p_delay(2);
                if(npc_find(coord, king_narnode, 6, 0) = true) {
                    $npc_dist = distance(npc_coord, movecoord(coord, 1, 0, 0));
                    npc_walk(movecoord(coord, 1, 0, 0));
                    if($npc_dist > 0) {
                        p_delay(calc($npc_dist + 1));
                    }
                    npc_setmode(playerface);
                    @king_narnode_foundations;
                }
            case 2:
                ~chatplayer("<p,neutral>I'll be off now.");
                ~chatnpc("<p,neutral>Enjoy your stay with us.|There are many things to see in my kingdom.");
        }
}

[label,king_narnode_back_up]
~chatnpc("<p,neutral>I'll show you the way back up.");
if_close;
npc_setmode(none);
def_int $npc_dist = distance(npc_coord, 0_38_154_32_43);
npc_walk(0_38_154_32_43);
if($npc_dist > 0) {
    p_delay(calc($npc_dist + 1)); // TODO: + 1 until delay change is fix
}
npc_facesquare(movecoord(npc_coord, -1, 0, -1));
p_delay(0);
npc_say("Up here.");
~forcemove2(0_38_54_32_41);
facesquare(movecoord(coord, -1, 0, 0));
p_delay(0);
anim(human_reachforladdertop, 0);
p_delay(0);
npc_setmode(null);
p_telejump(movecoord(coord, -1, 0, 6399));

[label,king_narnode_foundations]
~chatplayer("<p,quiz>So what is this place?");
~chatnpc("<p,neutral>These, my friend, are the foundations of the stronghold.");
~chatplayer("<p,neutral>They look like roots to me.");
~chatnpc("<p,neutral>Not just any roots Traveller!|These were created by gnome mages eons ago,|since then they have grown to become a mighty stronghold!");
~chatplayer("<p,neutral>Impressive. What exactly is the problem?");
~chatnpc("<p,worried>In the last two months our tree guardians have reported continuing deterioration of the Grand Tree's health. I've never seen this before! It could be the end for us all!");
~chatplayer("<p,quiz>You mean the tree is ill?");
~chatnpc("<p,neutral>In effect yes. Would you be willing to help us discover what is happening to the tree?");
switch_int(~p_choice2("I'm sorry, I don't want to get involved.", 1, "I'd be happy to help!", 2)) {
    case 1:
        ~chatplayer("<p,sad>I'm sorry, I don't want to get involved.");
        ~chatnpc("<p,sad>I understand Traveller.|Please keep this to yourself.");
        ~chatplayer("<p,neutral>Of course.");
        @king_narnode_back_up;
    case 2:
        ~chatplayer("<p,happy>'d be happy to help!");
        ~chatnpc("<p,happy>Thank Guthix for your arrival!");
        ~chatnpc("<p,neutral>The first task is to find out what's killing the tree.");
        ~chatplayer("<p,quiz>Do you have an idea?");
        ~chatnpc("<p,neutral>My top tree guardian, Glough, believes it's human sabotage. I'm not so sure! The only way to know for sure is to talk to Hazelmere.");
        ~chatplayer("<p,quiz>Who's Hazelmere?");
        ~chatnpc("<p,neutral>Hazelmere is one of the mages that created the Grand Tree! He is the only one that has survived from that time. Take this bark sample to him, he will be able to help!");
        ~objboxt(bark_sample, "The king has given you a sample of bark.");
        ~chatnpc("<p,neutral>The mage only talks in the old tongue, you'll need this.");
        ~objboxt(translation_book, "The king has given you a translation book.");
        ~chatplayer("<p,quiz>What is it?");
        ~chatnpc("<p,neutral>It's a translation book, you'll need it to translate what Hazelmere says. Do that carefully! His words are our only hope! You'll find his dwellings high upon a towering hill, on an island east of Yanille.");
        %grandtree_progress = ^grandtree_started;
        inv_add(inv, bark_sample, 1);
        inv_add(inv, translation_book, 1);
        @king_narnode_back_up;
}
