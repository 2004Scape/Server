[apnpc1,grandtree_charlie]
// no range 
switch_int(%grandtree_progress) {
    case ^grandtree_found_invasion_plans :
        ~chatplayer("<p,angry>You were right! I searched Glough's chest and found some invasion plans!");
        ~chatnpc("<p,angry>Go to the King and tell him! Finally you have proof. Surely King Narnode will belive you now and set me free!"); // typo is authentic // https://chisel.weirdgloop.org/dialogue/content/95838
        ~chatplayer("<p,happy>Hold tight Charlie!");
        ~chatnpc("<p,neutral>I'm not going anywhere!");
    case ^grandtree_obtained_lumber_order :
        ~chatplayer("<p,quiz>How are you doing Charlie?");
        ~chatnpc("<p,neutral>I've been better.");
        ~chatplayer("<p,angry>Glough has some plan to rule RuneScape!");
        ~chatnpc("<p,sad>I wouldn't put it past him, the Gnome's crazy!");
        ~chatplayer("<p,neutral>I need some proof to convince the King.");
        // https://youtu.be/LFvLBRCxFwk?si=jZsV07Dsc1uTR1WH&t=997
        ~chatnpc("<p,neutral>Hmm...you could be in luck! Before Glough had me|locked up I heard him mention that he'd left his|chest keys at his girlfriend's.");
        ~chatplayer("<p,quiz>Where does she live?");
        ~chatnpc("<p,neutral>Just west of the toad swamp.");
        ~chatplayer("<p,neutral>OK, I'll see what I can find.");
        %grandtree_progress = ^grandtree_clue_charlie; // OSRS increments this earlier
    case ^grandtree_released_prison :
        ~chatplayer("<p,sad>I can't figure this out Charlie!");
        ~chatnpc("<p,neutral>Go and see the foreman in the Karamja jungle, there's a shipyard there, you might find some clues. Don't forget the password is Ka-Lu-Min; if they realise that you're not working for Glough there'll be trouble!");
    case ^grandtree_found_journal :
        if(coord = ^grandtree_jail_coord) {
            p_delay(3);
            ~chatnpc("<p,sad>So they got you as well?");
            ~chatplayer("<p,angry>It's Glough! He's trying to cover something up.");
        } else {
            ~chatplayer("<p,angry>I think Glough is trying to cover something up!");
        }
        @charlie_shouldnt_tell;
    case ^grandtree_spoken_prisoner :
        ~chatplayer("<p,neutral>Hello Charlie.");
        ~chatnpc("<p,neutral>Hello adventurer. Have you figured out what's going on?");
        ~chatplayer("<p,neutral>No idea.");
        ~chatnpc("<p,neutral>To get to the bottom of this you'll need to search Glough's home.");
    case ^grandtree_found_prisoner :
        // https://youtu.be/LFvLBRCxFwk?si=2LxhRmfBnwuNBFf_&t=543
        ~chatplayer("<p,neutral>Tell me. Why would you want to kill the Grand Tree?");
        ~chatnpc("<p,shock>What do you mean?!");
        // https://web.archive.org/web/20041114190435/http://img52.exs.cx/img52/1067/wittyremark.jpg
        ~chatplayer("<p,laugh>Don't tell me, you just happened|to be caught carrying Daconia rocks!");
        ~chatnpc("<p,sad>All I know is that I did what I was asked.");
        ~chatplayer("<p,confused>I don't understand.");
        // https://web.archive.org/web/20060911005925im_/http://www.runeweb.net/fireball/Grand%20Tree%20Images/GrandTree6.PNG
        ~chatnpc("<p,neutral>Glough paid me to go to this gnome on a hill.|I gave the gnome a seal and he gave me|some rocks to give to Glough.");
        ~chatnpc("<p,sad>I've been doing it for weeks, this time though Glough locked me up here! I just don't understand it.");
        ~chatplayer("<p,angry>Sounds like Glough is hiding something!");
        %grandtree_progress = ^grandtree_spoken_prisoner;
        ~chatnpc("<p,neutral>I don't know what he's up to. If you want to find out you'd better search his home.");
        ~chatplayer("<p,neutral>OK. Thanks Charlie.");
        ~chatnpc("<p,neutral>Good luck!");
    case default :
        mes("The prisoner is in no mood to talk.");
}

[label,charlie_shouldnt_tell]
~chatnpc("<p,shifty>I shouldn't tell you this adventurer.");
~chatnpc("<p,neutral>But if you want to get to the bottom of this|you should go and talk to the Karamja Shipyard foreman.");
~chatplayer("<p,quiz>Why?");
~chatnpc("<p,neutral>Glough sent me to Karamja to meet him. I delivered a large amount of gold. For what? I don't know. He may be able to tell you what Glough's up to. That's if you can get out of here. You'll find him in the");
~chatnpc("<p,neutral>Karamja Shipyard, east of Shilo village. Be careful! If he discovers you're not working for Glough there'll be trouble! The sea men use the password Ka-Lu-Min.");
~chatplayer("<p,happy>Thanks Charlie!");
if(coord ! ^grandtree_jail_coord) {
    return;
}
if_close;
npc_add(movecoord(^grandtree_jail_coord, 3, 0, 0), king_narnode, 200);
p_delay(0); 
npc_walk(movecoord(coord, 1, 0, 0));
p_delay(2); // 3t
~chatnpc("<p,sad>Traveller please accept my apologies!|Glough had no right to arrest you!|I just think he's scared of humans.|Let me get you out of there.");
if_close;
// can't use door proc here since it opens incorrectly (notice the lock side)
if(loc_find(movecoord(coord, 1, 0, 0), loc_3367) = true) {
    loc_del(6);
    loc_add(loc_coord, loc_1546, 1, loc_shape, 6);
    sound_synth(locked, 0, 0);
} 
p_teleport(movecoord(coord, 1, 0, 0));
p_delay(2); // 3t
@narnode_trust_glough;
