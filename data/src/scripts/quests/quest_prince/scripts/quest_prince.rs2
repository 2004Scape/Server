[opheldu,wig_grey]
if(last_useitem = yellow_dye) {
    @make_blonde_wig;
}
~displaymessage(^dm_default);

[label,make_blonde_wig]
inv_del(inv, yellow_dye, 1);
inv_del(inv, wig_grey, 1);
// add the dye
inv_add(inv, wig_blonde, 1);
mes("You dye the wig blonde.");

[opnpcu,lady_keli]
if (last_useitem ! rope | %prince_progress <= 3) {
    ~displaymessage(^dm_default);
    return;
}
if(%prince_progress >= ^prince_saved) {
    mes("You have rescued the prince already, you cannot use the same plan again!");
    return;
}
if(%prince_progress < ^prince_guard_drunk) {
    mes("You cannot tie Keli up until you have all equipment and disabled the guard!");
    return;
}
~mesbox("You overpower Keli, tie her up, and put her in a cupboard.");
if(inv_total(inv, rope) > 0) {
    inv_del(inv, rope, 1);
    npc_del;
    %prince_progress = ^prince_tied_keli;
}

[oplocu,loc_2881]
if (last_useitem ! bronze_key | coordz(coord) <= coordz(loc_coord)) {
    ~displaymessage(^dm_default);
    return;
}
if(%prince_progress <= ^prince_guard_drunk | npc_find(coord, lady_keli, 10, 0) = true) {
    mes("You'd better get rid of Lady Keli before trying to go through there.");
    return;
}
mes("You unlock the door.");
~open_and_close_metal_gate2(loc_1541, false, false);

[oploc1,loc_2881]
if(coordz(coord) > coordz(loc_coord)) {
    mes("The door is locked.");
    sound_synth(locked, 0, 0);
    return;
}
~open_and_close_metal_gate2(loc_1541, true, false);

[ai_timer,prince_ali_disguised]
npc_changetype(prince_ali, ^max_32bit_int);

[queue,prince_complete]
%prince_progress = ^prince_complete;
session_log(^log_adventure, "Quest complete: Prince Ali Rescue");
~send_quest_complete(questlist:prince, coins, 250, ^prince_questpoints, "You have completed the\\nPrince Ali Rescue Quest!");
inv_add(inv, coins, 700);