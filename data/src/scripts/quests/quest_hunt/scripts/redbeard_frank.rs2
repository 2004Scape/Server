[opnpc1,redbeard_frank]
~chatnpc(laugh, "Arr, Matey!");

if (%hunt_progress = 1) {
    @redboard_progress;
} else if (%hunt_progress = 2 & inv_total(inv, hunt_chest_key) = 0) {
    ~chatplayer(sad, "I seem to have lost my chest key...");
    ~chatnpc(neutral, "Arr, silly you. Fortunately I took the precaution to have another one made.");

    ~objbox(hunt_chest_key, "Frank hands you a chest key");
    inv_add(inv, hunt_chest_key, 1);

    @redbeard_options;
} else {
    @redbeard_options;
}
[label,redbeard_options]
if (%hunt_progress = 0) {
    @multi3("I'm in search of treasure.", redbeard_treasure, "Arr!", redbeard_arr, "Do you have anything for trade?", redbeard_trade);
} else {
    @multi2("Arr!", redbeard_arr, "Do you have anything for trade?", redbeard_trade);
}

[label,redboard_progress]
~chatnpc(quiz, "Have ye brought some rum for yer ol' mate Frank?");

if (inv_total(inv, karamja_rum) >= 1) {
    ~chatplayer(happy, "Yes, I've got some.");
    @redbeard_hand_rum;
    return;
}

~chatplayer(sad, "No, not yet.");

[label,redbeard_hand_rum]
~chatnpc(shifty, "Now a deal's a deal, I'll tell ye about the treasure. I used to serve under a pirate captain called One-Eyed Hector.");
~chatnpc(shifty, "Hector were very successful and became very rich. But about a year ago we were boarded by the Customs and Excise Agents.");
~chatnpc(sad, "Hector were killed along with many of the crew, I were one of the few to escape and I escaped with this.");

%hunt_progress = 2;

inv_del(inv, karamja_rum, 1);
inv_add(inv, hunt_chest_key, 1);

~objbox(hunt_chest_key, "Frank happily takes the rum... ... and hands you a key.");
~chatnpc(shifty, "This be Hector's key. I believe it opens his chest in his old room in the Blue Moon Inn in Varrock.");
~chatnpc(neutral, "With any luck his treasure will be in there.");

def_int $option = ~p_choice2("Ok thanks, I'll go and get it.", 1, "So why didn't you ever get it?", 2);
if ($option = 1) {
    ~chatplayer(happy, "Ok thanks, I'll go and get it.");
} else if ($option = 2) {
    ~chatplayer(quiz, "So why didn't you ever get it?");
    ~chatnpc(sad, "I'm not allowed in the Blue Moon Inn. Apparently I'm a drunken trouble maker.");
}


[label,redbeard_treasure]
~chatplayer(shifty, "I'm in search of treasure.");
~chatnpc(shifty, "Arr, treasure you be after eh? Well I might be able to tell you where to find some... For a price...");
~chatplayer(quiz, "What sort of price?");
~chatnpc(neutral, "Well for example if you can get me a bottle of rum... Not just any rum mind...");
~chatnpc(happy, "I'd like some rum made on Karamja Island. There's no rum like Karamja Rum!");

%hunt_progress = 1;
~send_quest_progress(questlist:hunt, %hunt_progress, ^hunt_complete);

[label,redbeard_arr]
~chatplayer(laugh, "Arr!");
~chatnpc(laugh, "Arr!");
@redbeard_options;

[label,redbeard_trade]
~chatplayer(quiz, "Do you have anything for trade?");
~chatnpc(neutral, "Nothin' at the moment, but then again the Customs Agents are on the warpath right now.");

[opnpcu,redbeard_frank]
if (last_useitem ! karamja_rum) {
    @nothing_interesting_happens;
}

// I'm unsure what happens if you attempt this when progress = 0,
// but it shouldn't be possible anyway.
if (%hunt_progress = 1) {
    ~chatplayer(happy, "Frank, I have some Karamja Rum.");
    @redbeard_hand_rum;
} else {
    ~chatnpc(happy, "Thanks fer the thought, but I still have to finish this bottle.");
}
