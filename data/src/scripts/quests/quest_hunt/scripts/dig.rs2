[label,hunt_dig]
if (%hunt_progress ! 3) {
    mes("It seems a shame to dig up these nice flowers for no reason.");
    return;
}

// Seems like there wasn't a gardener to kill. There's no "Gardener" NPC and the 
// global-rs guide makes no mention of it.

anim(human_dig, 0);
mes("You dig a hole in the ground...");

p_delay(4);

mes("and find a little chest of treasure.");
inv_add(inv, coins, 450);
inv_add(inv, gold_ring, 1);
inv_add(inv, emerald, 1);

%hunt_progress = 4;

// https://web.archive.org/web/20080325101204/http://www.global-rs.com/quests/piratestreasure/
~send_quest_complete(quest_journal:hunt, emerald, 250, 2, "You have completed the Pirate's Treasure Quest!");
