[opnpc1,luthas]
if(map_members = ^true & inv_total(inv, trail_clue_medium_anagram012) > 0) {
    ~chatnpc("<p,neutral>Congratulations.");
    ~progress_clue_medium(trail_clue_medium_anagram012, "Luthas has given you another clue scroll!");
    return;
}

if (testbit(%hunt_store_employed, ^plantation) = ^false) {
    ~chatnpc("<p,happy>Hello I'm Luthas, I run the banana plantation here.");
    @multi2("Could you offer me employment on your plantation?", luthas_employment, "That customs officer is annoying isn't she?", luthas_annoying_customs);
}

if (%crate_bananas = 10) {
    ~chatplayer("<p,happy>I've filled a crate with bananas.");
    ~chatnpc("<p,happy>Well done, here's your payment.");

    if_close;
    mes("Luthas hands you 30 coins.");
    inv_add(inv, coins, 30);
    %hunt_store_employed = clearbit(%hunt_store_employed, ^plantation);
    %crate_bananas = 0;
    if (%crate_rum = 1) {
        %crate_rum = 2;
    }

    p_delay(3);
    @multi4("Will you pay me for another crate full?", luthas_another, "Thank you, I'll be on my way", luthas_thanks, "So where are these bananas going to be delivered to?", luthas_where_delivered, "That customs officer is annoying isn't she?", luthas_annoying_customs);
}

~chatnpc("<p,quiz>Have you completed your task yet?");
@multi4("What did I have to do again?", luthas_what_to_do, "No, the crate isn't full yet.", luthas_crate_not_full, "So where are these bananas going to be delivered to?", luthas_where_delivered, "That customs officer is annoying isn't she?", luthas_annoying_customs);

[label,luthas_employment]
~chatplayer("<p,quiz>Could you offer me employment on your plantation?");
~chatnpc("<p,happy>Yes, I can sort something out. There's a crate ready to be loaded onto the ship.");
~chatnpc("<p,happy>If you could fill it up with bananas, I'll pay you 30 gold.");
%hunt_store_employed = setbit(%hunt_store_employed, ^plantation);

[label,luthas_annoying_customs]
~chatplayer("<p,shifty>That customs officer is annoying isn't she?");
~chatnpc("<p,neutral>Well I know her pretty well. She doesn't cause me any trouble any more.");
~chatnpc("<p,neutral>She doesn't even search my export crates any more. She knows they only contain bananas.");

[label,luthas_what_to_do]
~chatplayer("<p,quiz>What did I have to do again?");
~chatnpc("<p,happy>There's a crate ready to be loaded onto the ship. If you could fill it up with bananas, I'll pay you 30 gold.");

[label,luthas_crate_not_full]
~chatplayer("<p,sad>No, the crate isn't full yet...");
~chatnpc("<p,angry>Well come back when it is.");

[label,luthas_where_delivered]
~chatplayer("<p,quiz>So where are these bananas going to be delivered to?");
~chatnpc("<p,neutral>I sell them to Wydin who runs a grocery store in Port Sarim.");

[label,luthas_another]
~chatplayer("<p,quiz>Will you pay me for another crate full?");
~chatnpc("<p,happy>Yes certainly.");
~chatnpc("<p,neutral>If you go outside you should see the old crate has been loaded on to the ship, and there is another empty crate in its place.");
%hunt_store_employed = setbit(%hunt_store_employed, ^plantation);

[label,luthas_thanks]
~chatplayer("<p,happy>Thank you, I'll be on my way.");
