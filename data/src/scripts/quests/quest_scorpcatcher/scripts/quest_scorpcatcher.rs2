[label,scorpion_caught]
mes("You've already caught this scorpion");

[label,scorpion_sting]
mes("The scorpion stings you!");
~damage_self(3);

[label,catch_scorp](namedobj $next_cage)
inv_del(inv, last_useitem, 1);
npc_del;
inv_add(inv, $next_cage, 1);
mes("You catch a scorpion!");

[opnpcu,kharid_scorpion_first]
switch_obj (last_useitem) {
    case scorpion_cage_first, scorpion_cage_first_second, scorpion_cage_first_second, scorpion_cage_all: @scorpion_caught;
    case scorpion_cage_empty: @catch_scorp(scorpion_cage_first);
    case scorpion_cage_second: @catch_scorp(scorpion_cage_first_second);
    case scorpion_cage_third: @catch_scorp(scorpion_cage_first_third);
    case scorpion_cage_second_third: @catch_scorp(scorpion_cage_all);
}

[opnpcu,kharid_scorpion_second]
switch_obj (last_useitem) {
    case scorpion_cage_second, scorpion_cage_first_second, scorpion_cage_second_third, scorpion_cage_all: @scorpion_caught;
    case scorpion_cage_empty: @catch_scorp(scorpion_cage_second);
    case scorpion_cage_first: @catch_scorp(scorpion_cage_first_second);
    case scorpion_cage_third: @catch_scorp(scorpion_cage_second_third);
    case scorpion_cage_first_third: @catch_scorp(scorpion_cage_all);
}

[opnpcu,kharid_scorpion_third]
switch_obj (last_useitem) {
    case scorpion_cage_third, scorpion_cage_first_third, scorpion_cage_second_third, scorpion_cage_all: @scorpion_caught;
    case scorpion_cage_empty: @catch_scorp(scorpion_cage_third);
    case scorpion_cage_first: @catch_scorp(scorpion_cage_first_third);
    case scorpion_cage_second: @catch_scorp(scorpion_cage_second_third);
    case scorpion_cage_first_second: @catch_scorp(scorpion_cage_all);
}

[opnpc1,kharid_scorpion_first] @scorpion_sting;
[opnpc1,kharid_scorpion_second] @scorpion_sting;
[opnpc1,kharid_scorpion_third] @scorpion_sting;

// Taverly wall
[oploc1,loc_2117]
if(%scorpcatcher_progress < ^scorpcatcher_first_hint) {
    mes("Looks like a wall...");
    return;
}
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
mes("You've found a secret door");
~open_and_close_door2(oldwall, ~check_axis(coord, loc_coord, loc_angle), false);

[queue,scorpcatcher_quest_complete]
inv_del(inv, scorpion_cage_all, 1);
%scorpcatcher_progress = ^scorpcatcher_complete;
~send_quest_complete(questlist:scorpcatcher, scorpion_cage_all, 250, ^scorpcatcher_questpoints, "You have completed the\\nScorpion Catcher Quest!");
givexp(strength, 66250);