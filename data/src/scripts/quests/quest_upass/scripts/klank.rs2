[opnpc1,klank]
switch_int(%upass_progress) {
    case ^upass_confronted_iban, ^upass_defeated_iban, ^upass_complete :
        ~chatplayer("<p,happy>Hello Klank.");
        ~chatnpc("<p,happy>Hello again adventurer, so you're still around?");
        ~chatplayer("<p,happy>Still here!");
        switch_int(~p_choice2("Have you anymore gauntlets?", 1, "Take care Klank.", 2)) {
            case 1 :
                ~chatplayer("<p,neutral>Have you any more gauntlets?");
                ~chatnpc("<p,neutral>Well..yes, but they're not cheap to make. I'll have to sell you a pair...");
                ~chatplayer("<p,neutral>How much?");
                ~chatnpc("<p,neutral>5000 coins.");
                @multi2("5000, you must be joking!", klank_nogauntlets, "Okay then, I'll take a pair.", klank_buygauntlets);
            case 2 :
                ~chatplayer("<p,happy>Take care Klank.");
                ~chatnpc("<p,happy>You too adventurer...");
        }
    case ^upass_found_doll :
        ~chatplayer("<p,happy>Hi Klank.");
        ~chatnpc("<p,shock>Traveller, I hear you plan to destroy Iban?");
        ~chatplayer("<p,happy>That's right.");
        ~chatnpc("<p,happy>I have a gift for you, they may help. I crafted these long ago to protect myself from the teeth of the Soulless, their bite is vicious.");
        ~chatnpc("<p,sad>I haven't seen another pair which can withstand their jaws...");
        if(inv_total(inv, klanks_gauntlets) > 0 | inv_total(worn, klanks_gauntlets) > 0) {
            ~chatnpc("<p,happy>Oh... you have a pair of my gauntlets, look after them.");
        } else if(inv_total(bank, klanks_gauntlets) > 0) {
            ~chatnpc("<p,happy>Oh... you have a pair of my gauntlets. I'd get them from your bank if I were you.");
        } else {
            inv_add(inv, klanks_gauntlets, 1);
        }
        ~chatplayer("<p,happy>Thanks Klank.");
        ~chatnpc("<p,happy>Good luck traveller, give Iban a slap for me!");
    case ^upass_entered_main_area, ^upass_spoken_nilhoof :
        ~chatplayer("<p,happy>Hello my good man.");
        ~chatnpc("<p,happy>Good day to you outsider. I'm Klank, I'm the only blacksmith still alive down here. In fact we're the only ones that haven't yet turned.");
        ~chatnpc("<p,shock>If you're not careful you'll become one of them too!");
        ~chatplayer("<p,confused>Who?.. Iban's followers?");
        ~chatnpc("<p,neutral>They're not followers, they're slaves, they're the Soulless...");
        switch_int(~p_choice2("What happened to them?", 1, "No wonder their breath was so bad!", 2)) {
            case 1 :
                ~chatplayer("<p,neutral>What happened to them?");
                ~chatnpc("<p,neutral>They were normal once, adventurers, treasure hunters. But men are weak, they couldn't ignore the voices.");
                ~chatnpc("<p,sad>Now they all seem to think with one conscience... As if they're being controlled by one being...");
                ~chatplayer("<p,confused>Iban?");
                ~chatnpc("<p,neutral>Maybe... maybe Zamorak himself. Those who try and fight it Iban locks in cages, until their minds are too weak to resist.");
                ~chatnpc("<p,sad>Eventually they all fall to his control...");
            case 2 :
                ~chatplayer("<p,shock>No wonder their breath was so bad!");
                ~chatnpc("<p,angry>You think this is funny... Eh?");
                ~chatplayer("<p,bored>Not really, just trying to lighten up the conversation.");
                ~chatnpc("<p,neutral>Hmph!");
        }
}

[label,klank_nogauntlets]
~chatplayer("<p,angry>5000, you must be joking!");
~chatnpc("<p,angry>We don't joke down here friend...");

[label,klank_buygauntlets]
~chatplayer("<p,neutral>Okay then, I'll take a pair.");
if(inv_total(inv, coins) < 5000) {
    ~chatplayer("<p,sad>Oh dear, I haven't enough money...");
    ~chatnpc("<p,neutral>Sorry, I can't sell them any cheaper than that.");
    return;
}
if_close;
inv_del(inv, coins, 5000);
inv_add(inv, klanks_gauntlets, 1);
mes("You give Klank 5000 coins...");
p_delay(3); // 4t
~chatnpc("<p,happy>There you go... I hope they help.");
~chatplayer("<p,happy>I'll see you around Klank.");