[opnpc1,ogre_guard_east]
switch_int(%itwatchtower_gold) {
    case ^itwatchtower_found_gold :
        ~chatnpc("<p,neutral>I know you, creature; you may pass.");
        if(loc_find(0_39_47_53_20, loc_2787) = true) p_oploc(1);
    case ^itwatchtower_looking_gold :
        ~chatnpc("<p,quiz>Creature, did you bring me the gold?");
        if(inv_total(inv, gold_bar) > 0) {
            ~chatplayer("<p,happy>Here it is!");
            inv_del(inv, gold_bar, 1);
            %itwatchtower_gold = ^itwatchtower_found_gold;
            ~chatnpc("<p,happy>It's brought it! On your way.");
            if(loc_find(0_39_47_53_20, loc_2787) = true) p_oploc(1);
            return;
        }
        ~chatplayer("<p,sad>No, I don't have it.");
        ~chatnpc("<p,angry>No gold, no passage.|Get out of this city!");
        mes("The guard pushes you back down the hill.");
        p_teleport(^gutanoth_hill);
    case default :
        ~chatnpc("<p,angry>Halt! You cannot pass here.");
        ~chatplayer("<p,happy>I am a friend to ogres.");
        ~chatnpc("<p,angry>You will be my friend only with gold.|Bring me a bar of pure gold and I will let you pass.");
        %itwatchtower_gold = ^itwatchtower_looking_gold;
        ~chatnpc("<p,angry>For now - begone!");
        mes("The guard pushes you back down the hill.");
        p_teleport(^gutanoth_hill);
}

[opnpcu,ogre_guard_east]
if(%itwatchtower_gold >= ^itwatchtower_found_gold) {
    ~chatnpc("<p,happy>No thank you, creature.|You are already my friend.");
    return;
} else if(%itwatchtower_gold = ^itwatchtower_looking_gold) {
    if(last_useitem = gold_bar) p_opnpc(1); 
    else ~chatnpc("<p,angry>That is not what I want, creature! Bring me a bar of pure gold and I will let you pass.");
} else {
    mes("The ogre has not asked you for anything.");
}

[opnpc1,ogre_guard_west]
~chatnpc("<p,angry>Stop, creature! Only ogres and their friends allowed in this city. Show me a sign of companionship, like a lost relic or somefing, and you may pass");
~chatnpc("<p,happy>Until then, back to whence you came!");
%itwatchtower_bits = setbit(%itwatchtower_bits, ^itwatchtower_looking_relic);
if(inv_total(inv, ogre_relic) > 0) {
    ~chatplayer("<p,happy>I've got a proof of friendship right here!");
    ~chatnpc("<p,quiz>Well, what proof of friendship did you bring?");
    ~chatplayer("<p,happy>I have a relic!");
    %itwatchtower_progress = ^itwatchtower_given_relic;
    ~chatnpc("<p,happy>It's got the statue of Dalgroth.|Welcome to Gu'Tanoth,|friend of the ogres.");
    if(loc_find(0_39_47_8_54, loc_2788) = true) p_oploc(1);
    return;
}
mes("The guard pushes you back down the hill.");
p_teleport(^gutanoth_hill);

[opnpcu,ogre_guard_west]
if(%itwatchtower_progress >= 4) {
    ~chatnpc("<p,happy>No thank you, creature.|You have already proven your companionship.");
    return;
} else if(testbit(%itwatchtower_bits, ^itwatchtower_looking_relic) = true) {
    if(last_useitem = ogre_relic) p_opnpc(1); 
    else ~chatnpc("<p,angry>That is of no use to me, creature!");
} else {
    mes("The ogre has not asked you for anything.");
}