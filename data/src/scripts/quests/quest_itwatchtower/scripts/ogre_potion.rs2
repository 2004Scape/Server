[opheldu,ground_bat_bones]
if(last_useitem = vial_water | last_useitem = guamvial | last_useitem = jangervial) {
  if(%itwatchtower_progress < ^itwatchtower_learned_potion) { // this check is also one way
    ~chatplayer("<p,confused>Hmmm. Perhaps I shouldn't try and mix these items together. It might have unpredictable results...");
    return;
  }
  @ogrepotion_add_bat_bones(last_useitem);
}
~displaymessage(^dm_default);

[opheldu,jangerberries]
if(last_useitem = vial_water | last_useitem = guamvial) {
    if(%itwatchtower_progress < ^itwatchtower_learned_potion) {
        ~chatplayer("<p,confused>Hmm, perhaps I shouldn't mix these together. It might have unpredictable results...");
        return;
    }
    @ogrepotion_add_jangerberries(last_useitem);
}
~displaymessage(^dm_default);

[opheldu,jangervial]
if(last_useitem ! guam_leaf & last_useitem ! ground_bat_bones) {
  ~displaymessage(^dm_default);
  return;
}
if(stat(herblore) < 14) {
  mes("You need a Herblore level of 14 or above to make this potion.");
  return;
}
switch_obj(last_useitem) {
  case guam_leaf : ~potion_explosion(guam_leaf, jangervial, "You mix the guam leaf into the liquid.");
  case guamvial : ~potion_explosion(ground_bat_bones, jangervial, "You mix the ground bones into the liquid.");
}

[opheldu,guamjangervial]
if(last_useitem = bat_bones) {
    mes("The bat bones are too bulky to fit into the vial.");
    return;
}
if(last_useitem ! ground_bat_bones) {
  ~displaymessage(^dm_default);
  return;
}
if(stat(herblore) < 14) {
  mes("You need a Herblore level of 14 or above to make this potion.");
  return;
}
inv_del(inv, ground_bat_bones, 1);
inv_del(inv, guamjangervial, 1);
inv_add(inv, ogre_potion, 1);
stat_advance(herblore, 100);
mes("You mix the ground bones into the liquid.");
mes("You produce a strong potion.");

[label,ogrepotion_add_bat_bones](obj $secondary)
if(stat(herblore) < 14) {
  mes("You need a Herblore level of 14 or above to make this potion.");
  return;
}
switch_obj($secondary) {
  case vial_water :
    inv_del(inv, ground_bat_bones, 1);
    inv_del(inv, vial_water, 1);
    inv_add(inv, vial_empty, 1);
    ~chatplayer("<p,sad>Oh dear, the mixture has evaporated.");
  case guamvial : ~potion_explosion(ground_bat_bones, guamvial, "You mix the ground bones into the liquid.");

}

[label,ogrepotion_add_jangerberries](obj $secondary)
if(stat(herblore) < 14) {
  mes("You need a Herblore level of 14 or above to make this potion.");
  return;
}
switch_obj($secondary) {
  case vial_water :
    inv_del(inv, jangerberries, 1);
    inv_del(inv, vial_water, 1);
    inv_add(inv, jangervial, 1);
    stat_advance(herblore, 100);
    mes("You mix the jangerberries into the water.");
  case guamvial :
    inv_del(inv, jangerberries, 1);
    inv_del(inv, guamvial, 1);
    inv_add(inv, guamjangervial, 1);
    stat_advance(herblore, 100);
    mes("You mix the jangerberries into your potion.");
}

/** Potion explosion */
[proc,potion_explosion](namedobj $ingredient, namedobj $solvent, string $message)
mes($message);
if($solvent ! jangervial & $ingredient ! ground_bat_bones) mes("You mixed the ingredients incorrectly and the mixure exploded.");
inv_del(inv, $ingredient, 1);
inv_del(inv, $solvent, 1);
inv_add(inv, vial_empty, 1);
anim(human_explodingvial, 0);
spotanim_pl(explodingvial, 92, 0);
sound_synth(explosion, 0, 0);
~damage_self(5);