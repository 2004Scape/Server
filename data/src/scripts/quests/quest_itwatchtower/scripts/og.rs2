[opnpc1,og]
if(%itwatchtower_progress >= ^itwatchtower_made_relic | %itwatchtower_progress < ^itwatchtower_given_fingernails) {
    mes("He's busy; try him another time.");
} else if(testbit(%itwatchtower_bits, ^itwatchtower_helped_og) = ^true) {
    ~chatnpc("<p,bored>It's the little rat again.");
    switch_int(~p_choice2("Do you have any other tasks for me?", 1, "I have lost the relic part you gave me.", 2)) {
        case 1 : 
            ~chatplayer("<p,neutral>Do you have any other tasks for me?");
            ~chatnpc("<p,bored>No, I have no more tasks for you, now go away.");
        case 2 : 
            ~chatplayer("<p,sad>I have lost the relic part you gave me.");
            if(inv_total(inv, relicpart1) > 0) {
                ~chatnpc("<p,angry>Are you blind? I can see you got it even from here!");
                return;
            }
            inv_add(inv, relicpart1, 1);
            ~chatnpc("<p,angry>Grrr, why do I bother?|It's a good job I got another part.");
    }
} else if(testbit(%itwatchtower_bits, ^itwatchtower_spoken_og) = ^true) {
    @og_spoken;
} else if(%itwatchtower_progress >= ^itwatchtower_given_fingernails) {
    // https://web.archive.org/web/20051025143129im_/http://www.runeweb.net/fireball/Watchtower%20Images/Watchtower5.PNG
    ~chatnpc("<p,confused>Why you here little rat?");
    switch_int(~p_choice2("I seek entrance to the city of ogres.", 1, "I have come to kill you.", 2)) {
        case 1 :
            ~chatplayer("<p,neutral>I seek entrance to the city of ogres.");
            ~chatnpc("<p,angry>You got no business there!");
            ~chatnpc("<p,neutral>Just a minute... Maybe if you did something for me, I might help you get in.");
            ~chatplayer("<p,quiz>What can I do to help an ogre?");
            ~chatnpc("<p,neutral>South-east of here der is more ogres -|the name of the chieftain is Toban.|He stole gold from me|and I want it back!");
            %itwatchtower_bits = setbit(%itwatchtower_bits, ^itwatchtower_spoken_og);
            inv_add(inv, toban_key, 1);
            ~chatnpc("<p,neutral>Here is a key to the chest it's in.|If you bring it here,|I may reward you...");
        case 2 :
            ~chatplayer("<p,angry>I have come to kill you!");
            npc_huntall(coord, 5, 0); // seems to be 5 tiles in OSRS
            while(npc_huntnext = true) {
                if(npc_type = ogre2) ~npc_retaliate(0);
            }
            ~chatnpc("<p,angry>Kill me, eh?|You shall be crushed like the vermin you are!|Guards!");
    }
}

[label,og_spoken]
~chatnpc("<p,neutral>Where my gold from dat dirty Toban?");
if(inv_total(inv, stolen_gold) > 0) {
    ~chatplayer("<p,happy>I have your gold.");
    inv_del(inv, stolen_gold, 1);
    inv_add(inv, relicpart1, 1);
    %itwatchtower_bits = setbit(%itwatchtower_bits, ^itwatchtower_helped_og);
    // https://web.archive.org/web/20041201004103if_/http://img35.imageshack.us:80/img35/9167/relicpartone.jpg
    ~chatnpc("<p,happy>Well well, the little rat has got it!|Take this to show little rat is a friend to the ogres.|Hahahahaha!");
    ~objbox(relicpart1, "The ogre gives you part of a horrible statue.", 250, 0, divide(^objbox_height, 2));
    return;
}
switch_int(~p_choice3("I have your gold.", 1, "I haven't got it yet.", 2, "I have lost the key!", 3)) {
    case 1 :
        ~chatplayer("<p,happy>I have your gold.");
        ~chatnpc("<p,angry>No you don't!|If you want to impress me,|get da gold I asked for!");
    case 2 : 
        ~chatplayer("<p,sad>I haven't got it yet.");
        ~chatnpc("<p,angry>Don't come back until you have it,|unless you wanna be on tonight's menu!");
    case 3 :
        ~chatplayer("<p,sad>I have lost the key!");
        if(inv_total(inv, toban_key) = 0) {
            inv_add(inv, toban_key, 1);
            ~chatnpc("<p,angry>Stoopid! Take another and don't lose it!");
            return;
        }
        ~chatnpc("<p,angry>Oh yeah? What's dat den?");
        ~chatplayer("<p,confused>Oh yeah, I've got it right here.");
        ~chatnpc("<p,angry>Stoopid!");
}

[opnpcu,og]
if(last_useitem = stolen_gold & testbit(%itwatchtower_bits, ^itwatchtower_spoken_og) = ^true & testbit(%itwatchtower_bits, ^itwatchtower_helped_og) = ^false) {
    @og_spoken;
}
~displaymessage(^dm_default);