[opnpc1,watchtower_wizard]
switch_int(%itwatchtower_progress) {
    case ^itwatchtower_given_relic :
        ~chatnpc("<p,neutral>How are you doing with the ogres?");
        ~chatplayer("<p,happy>I have gained entry to the city!");
        ~chatnpc("<p,happy>Already? Excellent!");
        ~chatplayer("<p,confused>I still can't navigate the skavid caves.");
        ~chatnpc("<p,neutral>You need a map of some kind...|I bet one of the ogres has one.");
        ~chatplayer("<p,neutral>Okay thanks, I'll go and find out.");
    case ^itwatchtower_made_relic :
        ~chatnpc("<p,happy>Ah, the warrior returns.|Have you found a way into Gu'Tanoth yet?");
        ~chatplayer("<p,sad>I can't get past the guards.");
        ~chatnpc("<p,neutral>Well, ogres dislike others apart from their own kind. What you need is some form of proof of friendship, something to trick them into believing you're their friend.");
        ~chatnpc("<p,happy>Which shouldn't be too hard, considering their intelligence.");
        switch_int(~p_choice2("I have lost the relic you gave me.", 1, "I will find my way in, no problem.", 2)) {
            case 1 :
                ~chatplayer("<p,sad>I have lost the relic you gave me.");
                if(inv_total(inv, ogre_relic) > 0) { // did bank and freesapce checks exist originally? classic doesn't have them
                    ~chatnpc("<p,angry>Have you gone mad? I can see you have it with you!");
                    return;
                }
                ~chatnpc("<p,neutral>What? Lost the relic? How careless!|It's a good job I copied its design, then!|You can take this copy instead - it's just as good.");
                inv_add(inv, ogre_relic, 1);
                ~objbox(ogre_relic, "The wizard gives you another statue.", 250, 0, 0);
            case 2 : 
                ~chatplayer("<p,happy>I will find my way in, no problem.");
                ~chatnpc("<p,happy>Yes, I'm sure you will... Good luck!");
        }
    case ^itwatchtower_given_fingernails :
        ~chatnpc("<p,happy>How's it going?");
        switch_int(~p_choice3("I am having difficulty with the tribes.", 1, "I have everything under control.", 2, "I have lost something the ogres gave to me.", 3)) {
            case 1 :
                ~chatplayer("<p,sad>I am having difficulty with the tribes.");
                ~chatnpc("<p,neutral>Talk to them face to face|and don't show any fear.|Make sure you are rested and well fed,|and fight the good fight!");
            case 2 :
                ~chatplayer("<p,happy>I have everything under control.");
                ~chatnpc("Good, good! I will expect the crystals back shortly then...");
            case 3 :
                ~chatplayer("<p,sad>I have lost something the ogres gave to me.");
                ~chatnpc("<p,neutral>Oh, deary me!|Well, there's nothing I can do about it.|You will have to go back to them, I'm afraid.");
        }
    case ^itwatchtower_started :
        ~chatnpc("<p,happy>Hello again.|Did you find anything of interest?");
        if(inv_total(inv, fingernails) > 0) {
            ~chatplayer("<p,happy>Have a look at these...");
            ~chatnpc("<p,neutral>Interesting, very interesting.");
            ~chatnpc("<p,confused>Long nails...grey in colour...well chewed...");
            ~chatnpc("<p,happy>Of course! They belong to a skavid!");
            ~chatplayer("<p,confused>A skavid?");
            ~chatnpc("<p,neutral>A servant race to the ogres:|grey, depressed-looking creatures,|always losing nails, teeth and hair!");
            ~chatnpc("<p,neutral>They inhabit the caves in the Feldip Hills.");
            ~chatnpc("<p,neutral>They normally keep to themselves, though.|It's unusual for them to venture from their caves.");
            def_int $op = ~p_choice2("What do you suggest I do?", 1, "Shall I search the caves?", 2);
            if($op = 1) {
                ~chatplayer("<p,neutral>What do you suggest I do?");
            } else {
                ~chatplayer("<p,neutral>Shall I search the caves?");
            }
            ~chatnpc("<p,neutral>It's no good searching the caves. Well, not yet, anyway.");
            ~chatplayer("<p,confused>Why not?");
            ~chatnpc("<p,neutral>They are deep and complex. The only way you will navigate the caves is to have a map or something.");
            ~chatnpc("<p,neutral>It may be that the ogres have one...");
            ~chatplayer("<p,confused>And how do you know that?");
            ~chatnpc("<p,happy>Well... I don't.");
            @multi2("So what do I do?", watchwiz_whatdo, "I won't bother then.", watchwiz_wontbother);
        }
        if(inv_total(inv, old_robe) > 0) {
            ~chatplayer("<p,happy>I have this robe...");
            @watchwiz_not_evidence;
        }
        if(inv_total(inv, old_robe) > 0) {
            ~chatplayer("<p,happy>I found this dagger...");
            @watchwiz_not_evidence;
        }
        if(inv_total(inv, old_robe) > 0) {
            ~chatplayer("<p,happy>I have this armour...");
            @watchwiz_not_evidence;
        }
        if(inv_total(inv, old_robe) > 0) {
            ~chatplayer("<p,happy>I have an eye patch...");
            @watchwiz_not_evidence;
        }
        ~chatplayer("<p,sad>No, sorry, nothing yet.");
        ~chatnpc("<p,sad>Oh dear, oh dear, there must be something,|somewhere...");
    case ^itwatchtower_not_started :
        ~chatnpc("<p,sad>Oh my, oh my!");
        switch_int(~p_choice2("What's the matter?", 1, "You wizards are always complaining.", 2)) {
            case 1 :
                ~chatnpc("<p,sad>Oh dear, oh dear.|Darn and drat!");
                ~chatnpc("<p,sad>We try hard to keep this town protected.");
                ~chatnpc("<p,sad>But how can we do that when the Watchtower isn't working?");
                ~chatplayer("<p,neutral>What do you mean it isn't working?");
                ~chatnpc("<p,neutral>The Watchtower here works by the power of magic.|An ancient spell designed to ward off ogres|that has been in place here for many moons.");
                ~chatnpc("<p,sad>The exact knowledge of the spell is lost to us now,|but the essence of the spell|has been infused into four powering crystals|that keep the tower protected from the hordes in the Feldips.");
                @multi2("So how come the spell doesn't work?", watchwiz_spell, "I'm not interested in your rantings.", watchwiz_rantings);
            case 2 :
                ~chatplayer("<p,happy>You wizards are always complaining.");
                ~chatnpc("<p,angry>Complaining? Complaining!");
                ~chatnpc("<p,neutral>What folks these days don't realise|is that if it weren't for us wizards,|this entire world would be overrun|with every creature you could possibly imagine.|And some you couldn't even conceive of!");
        }
}

[opnpcu,watchtower_wizard]
if(last_useitem = ogre_relic) {
    ~chatplayer("<p,confused>What is this?");
    ~chatnpc("<p,confused>It is the ogre statue I finished for you.");
} else if(last_useitem = watchtower_crystal1) {
    ~chatplayer("<p,happy>Wizard, look what I have found.");
    ~chatnpc("<p,happy>Well done, well done!|You are clever;|hold onto it until you have all four.");
    ~chatnpc("<p,neutral>Keep searching for the others.|If you've dropped any,|then you will need to go back to where you got it from.");
} else if(last_useitem = relic_part_1) {
    inv_del(inv, relic_part_1, 1);
    ~chatplayer("<p,neutral>I had this given to me...");
    if(testbit(%itwatchtower_bits, ^itwatchtower_relic1) = true) {
        ~chatnpc("<p,shifty>I already have that part..."); // guessed, rsc dialogue
        return;
    }
    @check_relic_parts;
} else if(last_useitem = relic_part_2) {
    inv_del(inv, relic_part_2, 1);
    ~chatplayer("<p,neutral>I got given this by an ogre...");
    if(testbit(%itwatchtower_bits, ^itwatchtower_relic1) = true) {
        ~chatnpc("<p,shifty>I already have that part..."); // guessed, rsc dialogue
        return;
    }
    @check_relic_parts;
} else if(last_useitem = relic_part_3) {
    inv_del(inv, relic_part_3, 1);
    ~chatplayer("<p,neutral>An ogre gave me this...");
    if(testbit(%itwatchtower_bits, ^itwatchtower_relic1) = true) {
        ~chatnpc("<p,shifty>I already have that part..."); // guessed, rsc dialogue
        return;
    }
    // https://web.archive.org/web/20051025143129im_/http://www.runeweb.net/fireball/Watchtower%20Images/Watchtower8.PNG
    ~chatnpc("<p,neutral>Ah, it's part of an old ogre statue.");
    @check_relic_parts;
} else if(last_useitem = fingernails) {
    if(%itwatchtower_progress ! ^itwatchtower_started) {
        inv_del(inv, last_useitem, 1);
        ~chatnpc("<p,shifty>Yes, I'll take that off you.");
        return;
    }
    p_opnpc(1);
} else if(last_useitem = old_robe | last_useitem = unusual_armour | last_useitem = tattered_eye_patch | last_useitem = damaged_dagger) {
    if(%itwatchtower_progress ! ^itwatchtower_started) {
        inv_del(inv, last_useitem, 1);
        ~chatnpc("<p,shifty>Yes, I'll take that off you.");
        return;
    }
    @watchwiz_not_evidence;
}
~displaymessage(^dm_default);


[label,check_relic_parts]
if(getbit_range(%itwatchtower_bits, ^itwatchtower_relic1, ^itwatchtower_relic3) < 7) {
    ~chatnpc("<p,neutral>There may be more parts to find...|I'll keep this for later.");
    return;
}
inv_add(inv, ogre_relic, 1);
%itwatchtower_progress = ^itwatchtower_made_relic;
~chatnpc("<p,happy>Excellent! That seems to be all the pieces.|Now I can assemble it...");
~chatnpc("<p,neutral>Hmm, yes it is as I thought...|A statue symbolising an ogre warrior of old.");
~chatnpc("<p,happy>Well, if you ever wanted to make friends with an ogre, then this is the item to have!");
~objbox(ogre_relic, "The wizard gives you a complete statue.", 250, 0, 0);

[label,watchwiz_not_evidence]
~chatnpc("<p,confused>Let me see...");
~chatnpc("<p,neutral>No, sorry, this is not evidence. You need to keep searching, I'm afraid.");

[label,watchwiz_rantings]
~chatplayer("<p,neutral>I'm not interested in your rantings.");
~chatnpc("<p,angry>Hmph! Suit yourself.");

[label,watchwiz_spell]
~chatplayer("<p,neutral>So how come the spell doesn't work?");
~chatnpc("<p,sad>The crystals! The crystals have been taken!");
~chatplayer("<p,quiz>Taken?");
~chatnpc("<p,angry>Stolen!");
~chatplayer("<p,confused>Stolen?");
~chatnpc("<p,angry>Yes, yes! Do I have to repeat myself?");
switch_int(~p_choice3("Can I be of help?", 1, "I'm not sure I can help.", 2, "I'm not interested.", 3)) {
    case 1 :
        ~chatplayer("<p,neutral>Can I be of help?");
        ~chatnpc("<p,happy>Help? Oh wonderful, dear traveller!");
        ~chatnpc("<p,happy>Yes I could do with an extra pair of eyes here.");
        ~chatplayer("<p,neutral>???");
        ~chatnpc("<p,neutral>There must be some evidence of what has happened here. Perhaps you could assist me in searching for clues?");
        ~chatplayer("<p,neutral>I would be happy to.");
        %itwatchtower_progress = ^itwatchtower_started;
        ~chatnpc("<p,neutral>Try searching the surrounding area.|If you find anything unusual, bring it here.");
    case 2 :
        ~chatplayer("<p,neutral>I'm not sure I can help.");
        ~chatnpc("<p,sad>Oh dear, what am I to do? The safety of this whole area is in jeopardy!");
    case 3 :
        ~chatplayer("<p,neutral>I'm not interested.");
        ~chatnpc("<p,angry>That's typical, nowadays.|It's left to us wizards to do all the work.");
}

[label,watchwiz_whatdo]
~chatplayer("<p,confused>So what do I do?");
~chatnpc("<p,neutral>You need to be fearless and gain entrance to|Gu'Tanoth, the city of the ogres, then find out how to|navigate the caves.");
~chatplayer("<p,neutral>That sounds scary...");
~chatnpc("<p,neutral>Ogres are nasty creatures, yes. Only a strong warrior,|and a clever one at that, can get the better of them.");
~chatplayer("<p,neutral>What do I need to do to get into the city?");
~chatnpc("<p,neutral>You'll need some way to make the guards let you in.");
%itwatchtower_progress = ^itwatchtower_given_fingernails;
inv_del(inv, fingernails, 1);
~chatnpc("<p,happy>You could start by checking out the ogre settlements|around here. Tribal ogres often dislike their|neighbours... In the meantime, I'll throw those|fingernails out for you.");

[label,watchwiz_wontbother]
~chatplayer("<p,neutral>I won't bother then.");
~chatnpc("<p,angry>Won't bother? Won't bother!");
~chatnpc("<p,confused>Perhaps this quest is too hard for you?");