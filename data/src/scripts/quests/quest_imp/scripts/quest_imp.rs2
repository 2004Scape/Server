[label,imp_catcher_start]
~chatplayer("default", "Give me a quest!");
~chatnpc("default", "Give me a quest what?");

def_int $option = ~p_choice3("Give me a quest please.", 1, "Give me a quest or else!", 2, "Just stop messing around and give me a quest!", 3);

if($option = 1) {
    ~chatplayer("default", "Give me a quest please.");
    ~chatnpc("default", "Well seeing as you asked nicely... I could do with some help.");
    ~chatnpc("default", "The wizard Grayzag next door decided he didn't like me so cast a spell of summoning. And summoned hundreds of little imps.");
    ~chatnpc("default", "The imps stole all sorts of my things. Most of these things I don't really care about. They're just eggs and balls of strings and things.");
    ~chatnpc("default", "But they stole my 4 magical beads. There was a red one, a yellow one, a black one and a white one.");
    ~chatnpc("default", "The imps have now spread out all over the kingdom. Could you get my beads back for me?");
    $option = ~p_choice2("I'll try.", 1, "No.", 2);
    if($option = 1) {
        ~chatplayer("default", "I'll try.");
        %imp_progress = 1;
    } else if($option = 2) {
        ~chatplayer("default", "I've better things to do than chase imps.");
        ~chatnpc("default", "Well if you're not interested in the quests I have to give you, don't waste my time by asking me for them.");
    }
    ~chatplayer("default", "I'll try.");
    %imp_progress = 1;
    ~send_quest_progress(quest_journal:imp, %imp_progress, ^imp_complete);
} else if($option = 2) {
    ~chatplayer("default", "Give me a quest or else!");
    ~chatnpc("default", "Or else what? You'll attack me?");
    ~chatnpc("default", "Hahaha!");
} else if($option = 3) {
     ~chatplayer("default", "Just stop messing around and give me a quest!");
     ~chatnpc("default", "Ah now you're assuming I have one to give.");
}

[label,imp_catcher_inprogress]
~chatnpc("default", "So how are you doing finding my beads?");

if(inv_total(inv, imp_black_bead) > 0 & inv_total(inv, imp_white_bead) > 0 & inv_total(inv, imp_yellow_bead) > 0 & inv_total(inv, imp_red_bead) > 0) {
    ~chatplayer("default", "I've got all four beads!");
    ~chatplayer("default", "It was hard work I can tell you!");
    ~chatnpc("default", "Give them here and I'll sort out a reward.");
    ~mesbox("You give four coloured beads to Wizard Mizgog.");
    inv_del(inv, imp_black_bead, 1);
    inv_del(inv, imp_white_bead, 1);
    inv_del(inv, imp_yellow_bead, 1);
    inv_del(inv, imp_red_bead, 1);
    ~chatnpc("default", "Here's your reward then, an amulet of accuracy.");
    mes("The wizard hands you an amulet.");
    inv_add(inv, amulet_accuracy, 1);
    queue(imp_quest_complete, 0);
    return;
}

if(inv_total(inv, imp_black_bead) = 0 & inv_total(inv, imp_white_bead) = 0 & inv_total(inv, imp_yellow_bead) = 0 & inv_total(inv, imp_red_bead) = 0) {
    ~chatplayer("default", "I have not found any yet.");
    ~chatnpc("default", "Well get on with it. I've lost a white bead, a red bead, a black bead, and a yellow bead. Go kill som imps!");
} else {
    ~chatplayer("default", "I have found some of your beads.");
    ~chatnpc("default", "Come back when you have them all. The colour of the four beads I need are red, yellow, black, and white. Go chase some imps!");
}

[queue,imp_quest_complete]
%imp_progress = ^imp_complete;
givexp(magic, 8750);
~send_quest_complete(quest_journal:imp, amulet_accuracy, 200, ^imp_questpoints, "You have completed the\\nImp Catcher Quest!");