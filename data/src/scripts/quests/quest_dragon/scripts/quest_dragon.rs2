// Dragon slayer was reworked in 2007, but some of the dialogue between rsc and 
// osrs are similar. A good chunk of dialogue choices are speculation based off
// of micro-sized pieces of evidence. The dialogue is essentially rsc dialogue with
// some slight adjustments that align with old guides/videos.
// some sources i used:
// https://web.archive.org/web/20060910111257/http://www.runeweb.net/index.php?page=rs2-quest-dragonslay
// https://web.archive.org/web/20060110090813/http://runescape.salmoneus.net/quests/DragonSlayer.html
// https://web.archive.org/web/20050301055103/http://runevillage.com/quest2DragonSlayer.php
// https://web.archive.org/web/20051104084237/http://www.nomadgaming.com/index.php?page=rs2_dragonslayer (has some partially transcribed dialogue)
// https://web.archive.org/web/20050208000721/http://rscb.sitesled.com/
// https://web.archive.org/web/20060828222012/http://www.trillionareguild.com/runescape/dragon.php
// https://web.archive.org/web/20051202151118/http://runehq.com/RHQQuestsView.php?id=00146
// https://web.archive.org/web/20051104235547/http://www.tip.it/runescape/index.php?rs2quest_id=21

// rsc sources:
// https://gitlab.com/open-runescape-classic/core/-/blob/develop/server/plugins/com/openrsc/server/plugins/authentic/npcs/varrock/Guildmaster.java
// https://gitlab.com/open-runescape-classic/core/-/blob/develop/server/plugins/com/openrsc/server/plugins/authentic/quests/free/DragonSlayer.java
// https://gitlab.com/open-runescape-classic/core/-/blob/develop/server/plugins/com/openrsc/server/plugins/authentic/npcs/barbarian/Oracle.java

// osrs dialogue:
// https://oldschool.runescape.wiki/w/Transcript:Dragon_Slayer_I

// youtube videos:
// https://youtube.com/playlist?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&si=OBw8ZF5_Oz5oKwKK

// magic door
[oploc1,loc_2586]
// not sure if this is true or not
if (~quest_dragon_getting_map_parts = false | %quest_dragon_oracle = ^false) {
    mes("The door is locked.");
    return;
}
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
if ($entering = false) {
    if (inv_total(inv, wizards_mind_bomb) > 0 & inv_total(inv, unfired_bowl) > 0 & inv_total(inv, lobster_pot) > 0 & inv_total(inv, silk) > 0) {
        // osrs
        inv_del(inv, wizards_mind_bomb, 1);
        inv_del(inv, unfired_bowl, 1);
        inv_del(inv, lobster_pot, 1);
        inv_del(inv, silk, 1);
        mes("The door opens...");
        ~open_and_close_door(loc_param(next_loc_stage), $entering, false);
    } else {
        // rsc
        mes("The door is locked.");
    }
} else {
    ~open_and_close_door(loc_param(next_loc_stage), $entering, false);
}


// magic chest
[oploc1,loc_2587]
p_arrivedelay;
mes("You open the chest.");
anim(human_openchest, 0);
sound_synth(chest_open, 0, 0);
p_delay(0);
loc_del(300);
loc_add(loc_coord, loc_2588, loc_angle, loc_shape, 300);

[oploc2,loc_2588]
p_arrivedelay;
mes("You close the chest.");
anim(human_closechest, 0);
sound_synth(chest_close, 0, 0);
p_delay(0);
loc_del(300);
loc_add(loc_coord, loc_2587, loc_angle, loc_shape, 300);

[oploc1,loc_2588]
if (~quest_dragon_getting_map_parts = false | ~obj_gettotal(crandor_map_part3) > 0 | ~obj_gettotal(crandor_map) > 0) {
    mes("You find nothing in the chest.");
} else {
    // objbox looks soooo bad for our version, no way they used it
    // edit: confirmed it uses mesbox! https://youtu.be/tmPf68hogzw?t=471
    // ~objbox(crandor_map_part3, "You find a map piece in the chest.");
    ~mesbox("You find a map piece in the chest.");
    inv_add(inv, crandor_map_part3, 1);
}
// auto closes, yes. https://youtu.be/tmPf68hogzw?t=471
loc_del(300);
loc_add(loc_coord, loc_2587, loc_angle, loc_shape, 300);

// map parts
// slightly different message than osrs, was prob mesbox before, and they changed to objbox which couldve messed with line breaks https://youtu.be/pyhwp2-wMus?t=1008
[opheld1,crandor_map_part2]
~mesbox("This is a piece of map that you got from the Wormbrain,|the goblin thief. You will need to join it to the other two|map pieces before you can see the route to Crandor.");

[opheld1,crandor_map_part1]
~mesbox("This is a piece of map that you found in Melzar's|Maze. You will need to join it to the other two map|pieces before you can see the route to Crandor.");

[opheld1,crandor_map_part3]
~mesbox("This is a piece of map that you found in the chest|in the Dwarven Mine. You will need to join it to the|other two map pieces before you can see the route to|Crandor.");

[opheldu,crandor_map_part1]
if (last_useitem = crandor_map_part2 | last_useitem = crandor_map_part3) {
    @combine_crandor_map_parts;
} else {
    @nothing_interesting_happens;
}

[opheldu,crandor_map_part2]
if (last_useitem = crandor_map_part1 | last_useitem = crandor_map_part3) {
    @combine_crandor_map_parts;
} else {
    @nothing_interesting_happens;
}

[opheldu,crandor_map_part3]
if (last_useitem = crandor_map_part1 | last_useitem = crandor_map_part2) {
    @combine_crandor_map_parts;
} else {
    @nothing_interesting_happens;
}


[label,combine_crandor_map_parts]
// cant use inv_totalcat since you can get more than one of each part.
if (inv_total(inv, crandor_map_part1) > 0 & inv_total(inv, crandor_map_part2) > 0 & inv_total(inv, crandor_map_part3) > 0) {
    inv_del(inv, crandor_map_part1, 1);
    inv_del(inv, crandor_map_part2, 1);
    inv_del(inv, crandor_map_part3, 1);
    inv_add(inv, crandor_map, 1);
    ~mesbox("You join the three pieces together and assemble a map|that shows the route through the reefs to Crandor.");
} else {
    mes("You still need one more piece of map.");
}

[opheld1,crandor_map]
// complete guess. Cant find any sources that say what the message is.
~mesbox("This is a map that shows the route through the reefs|to Crandor. You will need a ship to follow this route.");


[proc,quest_dragon_getting_map_parts]()(boolean)
if (%dragon_progress >= ^quest_dragon_spoken_to_oziach & %dragon_progress < ^quest_dragon_ned_given_map) {
    return(true);
}
return(false);