[debugproc,dragslay]
mes("Dragon Slayer progress: <tostring(%dragon_progress)>.");
mes("Dragon Slayer Planks: <tostring(%dragon_planks)>.");
mes("Ned Hired: <tostring(%quest_dragon_ned_hired)>.");

[opnpc1,dragonslayer_ned]
// just incase the player gets to crandor with this varp value
// since ned is also on crandor
if ((%dragon_progress = ^quest_dragon_ned_given_map | %dragon_progress = ^quest_dragon_sailed_to_crandor) & coordy(coord) = 3) {
    ~chatnpc(quiz, "Ah! There you are! Ready to go?");
    def_int $choice = ~p_choice2("Yep lets go!", 1, "No I'm not quite ready yet...", 2);
    if ($choice = 2) {
        ~chatplayer(neutral, "No I'm not quite ready yet...");
        ~chatnpc(neutral, "Well, you go do whatever you need to do. I'll wait here for you until you're ready.");
        return;
    }
    ~chatplayer(quiz, "Yep lets go!");
    mes("You feel the ship begin to move...");
    mes("The ship is sailing across the ocean...");
    %boat_takeoff = ^sail_port_sarim_to_crandor;

    if_settext(boat_travel:com_0, "You sail to Crandor Isle.");
    if_openmainmodal(boat_travel);
    midi_jingle(^sailing_journey_jingle, ^sailing_journey_jingle_millis);

    p_telejump(^crandor_tele_coord);
    %dragon_progress = ^quest_dragon_sailed_to_crandor;
    %quest_dragon_ned_hired = ^false;
    %dragon_planks = 0;
    p_delay(13);
    if_close;
    if_settext(boat_travel:com_0, "You Journey on the ship...."); // reset text
    // https://youtu.be/ucaYfz3ihWs?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=28
    ~mesbox("You hear a loud crack as the ship comes to a juddering halt.");
    ~chatnpc(neutral,"Um... I think we're there. I probably should have paid closer attention.");
    ~chatplayer(angry, "Gee.. you think?");
    return;
} else {
    // https://youtu.be/uETjjZFKWus?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=10
    // boat just crashed on crandor:
    def_int $choice = ~p_choice2("Is the ship ready to sail back?", 1, "So are you enjoying this exotic island vacation?", 2);
    // MISSING DIALOGUE:
    if ($choice = 1) {
        ~chatplayer(neutral, "Is the ship ready to sail back?");
        ~chatnpc(sad, "I'm afraid not. It looks like we're stranded."); // complete guess
        return;
    }
    if ($choice = 2) {
        ~chatplayer(neutral, "So are you enjoying this exotic island vacation?");
        ~chatnpc(sad, "Not really. I'm afraid we're stranded here."); // complete guess
        return;
    }
}