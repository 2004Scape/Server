[debugproc,dragslay]
mes("Progress: <tostring(%dragon_progress)>.");
mes("Planks: <tostring(%dragon_planks)>.");
mes("Ned Hired: <tostring(%dragon_ned_hired)>.");
mes("Oracle: <tostring(%dragon_oracle)>.");
mes("Goblin: <tostring(%dragon_goblin)>.");
mes("Shield: <tostring(%dragon_shield)>.");
mes("Wall: <tostring(%dragon_wall)>.");


[opnpc1,dragonslayer_ned]
// just incase the player gets to crandor with this varp value
// since ned is also on crandor
if ((%dragon_progress = ^quest_dragon_ned_given_map | %dragon_progress = ^quest_dragon_sailed_to_crandor) & coordy(coord) = 3) {
    ~chatnpc("<p,quiz>Ah! There you are! Ready to go?");
    def_int $choice = ~p_choice2("Yep lets go!", 1, "No I'm not quite ready yet...", 2);
    if ($choice = 2) {
        ~chatplayer("<p,neutral>No I'm not quite ready yet...");
        ~chatnpc("<p,neutral>Well, you go do whatever you need to do. I'll wait here for you until you're ready.");
        return;
    }
    ~chatplayer("<p,quiz>Yep lets go!");
    mes("You feel the ship begin to move...");
    mes("The ship is sailing across the ocean...");
    %boat_takeoff = ^sail_port_sarim_to_crandor;

    if_settext(ship_journey:com_0, "You sail to Crandor Isle.");
    if_openmain(ship_journey);
    ~midi_jingle(^sailing_journey_jingle, ^sailing_journey_jingle_millis);

    p_telejump(^crandor_tele_coord);
    %dragon_progress = ^quest_dragon_sailed_to_crandor;
    %dragon_ned_hired = ^false;
    %dragon_planks = 0;
    p_delay(11);
    if_close;
    if_settext(ship_journey:com_0, "You Journey on the ship...."); // reset text
    // https://youtu.be/ucaYfz3ihWs?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=28
    ~mesbox("You hear a loud crack as the ship comes to a juddering halt.");
    ~chatnpc("<p,neutral>Um... I think we're there. I probably should have paid closer attention.");
    ~chatplayer("<p,angry>Gee.. you think?");
    return;
} else {
    // https://youtu.be/uETjjZFKWus?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=10
    // boat just crashed on crandor:
    def_int $choice = ~p_choice2("Is the ship ready to sail back?", 1, "So are you enjoying this exotic island vacation?", 2);
    // https://storage.googleapis.com/tannerdino/images/crandorned2.png
    // same as rsc
    if ($choice = 1) {
        ~chatplayer("<p,quiz>Is the ship ready to sail back?");
        ~chatnpc("<p,neutral>Well when we arrived, the ship took a nasty jar from those rocks.|We may be stranded.");
        return;
    }
    if ($choice = 2) {
        ~chatplayer("<p,quiz>So are you enjoying this exotic island vacation?");
        ~chatnpc("<p,neutral>Well it would have been better if I'd brought my sun|lotion. Oh, and the skeletons which won't let me leave|the ship probably aren't helping either.");
        return;
    }
}