// magic door
[oploc1,dragon_slayer_magic_door]
// not sure if this is true or not
if (%dragon_oracle < ^quest_dragon_has_spoken_to_oracle) {
    mes("The door is locked.");
    return;
}
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
if ($entering = false & %dragon_oracle < ^quest_dragon_gone_through_oracle_door) {
    if (inv_total(inv, wizards_mind_bomb) > 0 & inv_total(inv, bowl_unfired) > 0 & inv_total(inv, lobster_pot) > 0 & inv_total(inv, silk) > 0) {
        // osrs
        inv_del(inv, wizards_mind_bomb, 1);
        inv_del(inv, bowl_unfired, 1);
        inv_del(inv, lobster_pot, 1);
        inv_del(inv, silk, 1);
        %dragon_oracle = ^quest_dragon_gone_through_oracle_door;
        mes("A strange glow appears around the silk, unfired bowl,"); // https://youtu.be/VPxaojkZOF4?t=33
        mes("lobster pot and wizard's mind bomb in your inventory.");
        ~open_and_close_door(loc_param(next_loc_stage), $entering, false);
    } else {
        // rsc
        mes("The door is locked.");
    }
} else {
    // once you enter in osrs you can freely enter from now on
    ~open_and_close_door(loc_param(next_loc_stage), $entering, false);
}


// magic chest
[oploc1,dragon_slayer_magic_chest_closed]
p_arrivedelay;
mes("You open the chest.");
anim(human_openchest, 0);
sound_synth(chest_open, 0, 0);
p_delay(0);
// Temp note: dur does not need updated
loc_change(dragon_slayer_magic_chest_opened, 300);

[oploc2,dragon_slayer_magic_chest_opened]
p_arrivedelay;
mes("You close the chest.");
anim(human_closechest, 0);
sound_synth(chest_close, 0, 0);
p_delay(0);
// Temp note: dur does not need updated
loc_change(dragon_slayer_magic_chest_closed, 300);

[oploc1,dragon_slayer_magic_chest_opened]
if (~quest_dragon_getting_map_parts = false | ~obj_gettotal(mappart3) > 0 | ~obj_gettotal(dragonmap) > 0) {
    mes("You find nothing in the chest.");
} else {
    // objbox looks soooo bad for our version, no way they used it
    // edit: confirmed it uses mesbox! https://youtu.be/tmPf68hogzw?t=471
    // ~objbox(crandor_map_part3, "You find a map piece in the chest.", 250, 0, divide(^objbox_height, 2));
    ~mesbox("You find a map piece in the chest.");
    inv_add(inv, mappart3, 1);
}
// auto closes, yes. https://youtu.be/tmPf68hogzw?t=471
// Temp note: dur does not need updated
loc_change(dragon_slayer_magic_chest_closed, 300);
