// entrance
[oploc1,melzar_maze_entrance] 
sound_synth(locked, 0, 0);
mes("The door is securely locked.");
// def_boolean $entering = ~check_axis_locactive(coord);
// if ($entering = true) {
//     sound_synth(locked, 0, 0);
//     mes("The door is securely locked.");
//     return;
// }
// ~open_and_close_door(loc_1535, $entering, false);

[oplocu,melzar_maze_entrance] // https://youtu.be/tmPf68hogzw?t=54
 if (last_useitem = maze_key) {
    ~open_and_close_door(loc_1535, ~check_axis_locactive(coord), false);
} else {
    ~displaymessage(^dm_default);
}

[oploc1,melzar_maze_exit_door]
if (~check_axis_locactive(coord) = false) {
    mes("The door is locked."); // osrs
    return;
}
~open_and_close_door(loc_1535, true, false);


// broken ladder
[oploc1,broken_ladder] 
mes("The ladder is broken, I can't climb it."); // osrs

// ladder
[oploc1,melzar_maze_ladder]
// No arrivedelay in osrs
@climb_ladder(movecoord(coord, 0, 0, 6400), true);

// red door
[oploc1,melzar_maze_red_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_red_door] @open_melzar_maze_door(maze_key_red);
// orange door
[oploc1,melzar_maze_orange_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_orange_door] @open_melzar_maze_door(maze_key_orange);
// yellow door
[oploc1,melzar_maze_yellow_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_yellow_door] @open_melzar_maze_door(maze_key_yellow);
// blue door
[oploc1,melzar_maze_blue_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_blue_door] @open_melzar_maze_door(maze_key_blue);
// magenta door
[oploc1,melzar_maze_magenta_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_magenta_door] @open_melzar_maze_door(maze_key_magenta);
// green door
[oploc1,melzar_maze_green_door] mes("This door is securely locked."); // osrs
[oplocu,melzar_maze_green_door] @open_melzar_maze_door(maze_key_green);


[label,open_melzar_maze_door](obj $key)
 if (last_useitem = $key) {
    mes("The key disintegrates as it unlocks the door."); // osrs
    inv_del(inv, $key, 1);
    ~open_and_close_door(loc_1535, ~check_axis_locactive(coord), false);
} else {
    ~displaymessage(^dm_default); // osrs
}

// rat
[ai_queue3,melzar_maze_giant_rat](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
// Drop red key.
obj_add(npc_coord, maze_key_red, 1, ^lootdrop_duration);


// ghost
[ai_queue3,melzar_maze_ghost](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Drop orange key.
obj_add(npc_coord, maze_key_orange, 1, ^lootdrop_duration);


// skeleton
[ai_queue3,melzar_maze_skeleton](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
// Drop yellow key.
obj_add(npc_coord, maze_key_yellow, 1, ^lootdrop_duration);


// zombie
[ai_queue3,melzar_maze_zombie](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
// Drop blue key.
obj_add(npc_coord, maze_key_blue, 1, ^lootdrop_duration);


// melzar
[ai_queue3,melzar](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
// Drop magenta key.
obj_add(npc_coord, maze_key_magenta, 1, ^lootdrop_duration);


// lesser demon
[ai_queue3,melzar_maze_lesser_demon](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, ashes, 1, ^lootdrop_duration);
// Drop green key..
obj_add(npc_coord, maze_key_green, 1, ^lootdrop_duration);


// chest
[oploc1,melzar_maze_chest_closed]
p_arrivedelay;
mes("You open the chest.");
anim(human_openchest, 0);
sound_synth(chest_open, 0, 0);
p_delay(0);
loc_change(melzar_maze_chest_opened, 300);


[oploc2,melzar_maze_chest_opened]
p_arrivedelay;
mes("You close the chest.");
anim(human_closechest, 0);
sound_synth(chest_close, 0, 0);
p_delay(0);
loc_change(melzar_maze_chest_closed, 300);

[oploc1,melzar_maze_chest_opened]
if (~quest_dragon_getting_map_parts = false | ~obj_gettotal(crandor_map_part1) > 0 | ~obj_gettotal(crandor_map) > 0) {
    mes("You find nothing in the chest.");
} else {
    // objbox looks soooo bad for our version, no way they used it
    // edit: confirmed it uses mesbox! https://youtu.be/tmPf68hogzw?t=471
    // ~objbox(crandor_map_part1, "You find a map piece in the chest.");
    ~mesbox("You find a map piece in the chest.");
    inv_add(inv, crandor_map_part1, 1);
}
loc_change(melzar_maze_chest_closed, 300);