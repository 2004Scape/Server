// death
[ai_queue3,elvarg](int $arg)
gosub(npc_death);
if (npc_findhero = false) {
    return;
}
// Default drop from config.
obj_add(npc_coord, npc_param(death_drop), 1, ^lootdrop_duration);
// finish dragon slayer quest
// this is currently 1t too slow for some reason:
// https://youtu.be/zmYEl97Smxs?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=222
if (%dragon_progress < ^dragon_complete) {
    queue(dragon_complete, 0);
}


//data is only from about 50 hits
[ai_queue1,elvarg](int $arg)
if (finduid(%npc_attacking_uid) = false) {
    return;
}
// flinch
// set npc to ap if its out of combat
if (~npc_out_of_combat = true) {
    %npc_action_delay = add(map_clock, divide(npc_param(attackrate), 2));
    npc_setmode(applayer2);
    return;
}
npc_setmode(opplayer2);

[ai_applayer2,elvarg]
if (p_finduid(uid) = false) { // get protected access
    return;
}
//mes("<tostring(map_clock)>: Ap");
if (%npc_action_delay > map_clock) {
    npc_setmode(applayer2);
    return;
}
// seems like 3/4 chance to dragon fire if ap.
def_int $random = random(4);
if ($random = 0) {
    ~npc_default_attack; // melee
    npc_setmode(opplayer2);
    return;
} else {
    ~elvarg_dragon_fire;
}
npc_setmode(applayer2);


[ai_opplayer2,elvarg]
if (p_finduid(uid) = false) { // get protected access
    return;
}
//mes("<tostring(map_clock)>: Op");
if (%npc_action_delay > map_clock) {
    npc_setmode(opplayer2);
    return;
}
// seems like 3/4 chance to melee if op.
def_int $random = random(4);
if ($random = 0) {
    ~elvarg_dragon_fire;
    npc_setmode(applayer2);
    return;
} else {
    ~npc_default_attack;
}
npc_setmode(opplayer2);


[proc,elvarg_dragon_fire]
if_close;
spotanim_npc(spotanim_1, 92, 0);
npc_anim(dragon_firebreath_middle_attack, 0);
sound_synth(sound_22, 0, 30);
%npc_action_delay = add(map_clock, 5);

// damage player
def_int $damage = randominc(~elvarg_max_hit);
queue(queue_damage_player, 0, $damage);


// proc is used when the player has dragon fire shiled, prot magic, or antifire potion
[proc,elvarg_max_hit]()(int)
def_int $maxhit = 70;
if (inv_total(worn, dragonfire_shield) > 0) {
    // https://youtu.be/zmYEl97Smxs?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=169
    mes("Your shield absorbs most of the dragon fire!");
    $maxhit = sub($maxhit, 60);
    if (%prayer_protectfrommagic = ^true) {
        $maxhit = sub($maxhit, 3);
        // https://youtu.be/lSV2whAirvY?t=41
        mes("Your prayers resist some of the dragon fire!");
    }
    if (%antifire = ^true) {
        $maxhit = sub($maxhit, 3);
        // guess based off this https://youtu.be/UXNFe3FeDgg?t=15
        mes("Your potion protects you from the heat of the dragon fire!");
    }
} else {
    if (%prayer_protectfrommagic = ^true) {
        $maxhit = sub($maxhit, 15);
    }
    if (%antifire = ^true) {
        $maxhit = sub($maxhit, 15);
    }
}
return($maxhit);

[debugproc,breath]
sound_synth(dragon_1, 0, 0);
p_delay(2);
sound_synth(dragon_2, 0, 0);
p_delay(2);
sound_synth(dragon_3, 0, 0);