// This update made changes to Doric's quest. I'm assuming this updated the dialogue from the RSC version of the quest.
// https://oldschool.runescape.wiki/w/Update:Agility_Pyramid
// Sources:
// https://www.youtube.com/watch?v=SDSF7PAwP5E
// https://www.youtube.com/watch?v=umCTXiw4hZ0
// https://github.com/Open-RSC/Core-Framework/blob/develop/server/plugins/com/openrsc/server/plugins/authentic/quests/free/Dorics.java

[opnpc1,doric]
// Treasure Trail
if (map_members = ^true & inv_total(inv, trail_clue_easy_simple022) = 1) {
    @trail_doric;
}

if (%doric_progress = 0) {
    @doric_not_started;
} else if (%doric_progress = 10) {
    @doric_materials;
} else {
    @doric_post_complete;
}

[oplocu,doric_anvil]

if (%doric_progress = 0) {
    mes("You must complete Doric's Quest to use this anvil.");
    if(npc_find(coord, doric, 5, 0) = true) @doric_not_started_anvil;
} else if (%doric_progress = 10) {
    mes("You must complete Doric's Quest to use this anvil.");
    if(npc_find(coord, doric, 5, 0) = true) @doric_materials;
} else {
    // Do regular anvil stuff
    if (last_useitem = gold_bar & %legends_progress >= ^legends_asked_gujuo_holy_water) @make_golden_bowl;
    else if (oc_category(last_useitem) = category_151) @smithing_anvil_interface(last_useitem);
    else if (last_useitem = dragonshield_a | last_useitem = dragonshield_b) @make_dragon_sq;
    else ~displaymessage(^dm_default);
}

[label,doric_not_started_anvil]
~chatnpc("<p,angry>Hey who said you could use that! My anvils get enough work with my own use. I make pickaxes, and it takes a lot of hard work.");
def_int $option = ~p_choice2("Sorry, would it be OK if I used your anvils?", 1,  "I didn't want to use your anvils anyway.", 2);
if ($option = 1) {
    ~chatplayer("<p,quiz>Sorry, would it be OK if I used your anvils?");
    ~chatnpc("<p,neutral>If you could get me some more materials then I could let you use them.");
    @start_quest_options;
} else if ($option = 2) {
    ~chatplayer("<p,bored>I didn't want to use your anvils anyway.");
    ~chatnpc("<p,confused>That is your choice.");
}

[label,doric_not_started]
~chatnpc("<p,quiz>Hello traveller, what brings you to my humble smithy?");
def_int $option = ~p_choice4("I wanted to use your anvils.", 1, "Mind your own business, shortstuff!", 2,  "I was just checking out the landscape.", 3, "What do you make here?", 4);

if ($option = 1) {
    ~chatplayer("<p,neutral>I wanted to use your anvils.");
    ~chatnpc("<p,neutral>My anvils get enough work with my own use. I make amulets, it takes a lot of work. If you could get me some more materials, I could let you use them.");
    @start_quest_options;
} else if ($option = 2) {
    ~chatplayer("<p,angry>Mind your own business, shortstuff!");
    ~chatnpc("<p,angry>How nice to meet someone with such pleasant manners. Do come again when you need to shout at someone smaller than you!");
} else if ($option = 3) {
    ~chatplayer("<p,neutral>I was just checking out the landscape.");
    ~chatnpc("<p,happy>We have a fine town here, it suits us very well. Please enjoy your travels, and do visit my friends in their mine.");
} else if ($option = 4) {
    ~chatplayer("<p,quiz>What do you make here?");
    ~chatnpc("<p,happy>I make amulets. I am the best maker of them in Runescape!");
    ~chatplayer("<p,quiz>Do you have any to sell?");
    ~chatnpc("<p,neutral>Not at the moment, sorry. Try again later.");
}
[label,start_quest_options]
 def_int $option = ~p_choice2("Yes, I will get you materials.", 1 , "No, hitting rocks is for the boring people, sorry.", 2);

if ($option = 1) {
    ~chatplayer("<p,happy>Yes, I will get you materials.");
    %doric_progress = 10;
    ~send_quest_progress(questlist:doric, %doric_progress, ^doric_complete);
    ~chatnpc("<p,neutral>Well, clay is what I use more than anything. I make casts. Could you get me 6 clay, and 4 copper ore and 2 iron ore please? I could pay a little, and let you use my anvils.");
    ~chatplayer("<p,happy>Certainly, I will get them for you. Goodbye.");
} else if ($option = 2) {
     ~chatplayer("<p,bored>No, hitting rocks is for the boring people, sorry.");
     ~chatnpc("<p,confused>That is your choice. Nice to meet you anyway.");
}

[label,doric_materials]
~chatnpc("<p,quiz>Have you got my materials yet, traveller?");
if (inv_total(inv, clay) > 5 & inv_total(inv, copper_ore) > 3 & inv_total(inv, iron_ore) > 1) {
    ~chatplayer("<p,happy>I have everything you need!");
    ~chatnpc("<p,happy>Many thanks, pass them here please.");
    // Remove ores here
    inv_del(inv, clay, 6);
    inv_del(inv, copper_ore, 4);
    inv_del(inv, iron_ore, 2);
    mes("You hand the Clay, Copper and Iron to Doric.");
    // Doing this makes it so you can miss out on getting coins... but does trigger the quest complete
    // Talked With Tanner about being able to miss coins and it feels like a correct thing to do.
    queue(doric_quest_complete, 0);
    ~chatnpc("<p,happy>I can spare you some coins for your trouble.");
    mes("Doric hands you 180 coins.");
    inv_add(inv, coins, 180);
    ~chatnpc("<p,happy>Please use my anvils any time you want.");

} else {
    ~chatplayer("<p,sad>Sorry, I don't have them all yet.");
    ~chatnpc("<p,neutral>Not to worry, stick at it. Remember, I need 6 clay, 4 copper ore and 2 iron ore.");
}

[label,doric_post_complete]
~chatnpc("<p,neutral>Hello traveller, how is your metalworking coming along?");
~chatplayer("<p,neutral>Not too bad, thanks Doric.");
~chatnpc("<p,happy>Good, the love of metal is a thing close to my heart.");

// Reward Scroll
[queue,doric_quest_complete]
%doric_progress = ^doric_complete;
stat_advance(mining, 13000);
session_log(^log_adventure, "Quest complete: Doric's Quest");
~send_quest_complete(questlist:doric, steel_pickaxe, 250, ^doric_questpoints, "You have completed\\nDoric's Quest!");

[label,trail_doric]
// https://www.youtube.com/watch?v=Smadil97tFc
~chatnpc("<p,neutral>So, you've come to the right place?");
~progress_clue_easy(trail_clue_easy_simple022, "Doric has given you another clue scroll!");