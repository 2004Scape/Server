[oploc1,loc_2706]
if(coordz(coord) > coordz(loc_coord)) {
    ~open_and_close_door(loc_1535, loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);
    return;
}
mes("This door is securely locked.");

[oploc2,loc_2707]
if(%totem_progress >= ^totem_started) {
    if(inv_total(inv, address_label) = 0) {
        ~mesbox("There is a label on this crate. It says; To Lord Handelmort, Handelmort Mansion, Ardougne.You carefully peel it off and take it.");
        inv_add(inv, address_label, 1);
    } else {
        ~mesbox("You can see the gluey outline from where you peeled the address label off.");
    }
}
mes("This crate is securely fastened shut.");

[oplocu,loc_2708]
switch_obj(last_item) {
    case address_label: 
        if(%totem_progress >= ^totem_crate_marked) {
            mes("You have already replaced the delivery address label.");
            return;
        }
        ~mesbox("You carefully place the delivery label over the existing label, covering it completely.");
        inv_del(inv, address_label, 1);
        %totem_progress = ^totem_crate_marked;
        ~chatplayer(happy, "Now I just need someone to deliver it for me.");
        return;
    case default: @nothing_interesting_happens;
}

[if_button,tribal_door2:com_57]
if_close;
if(%handelmort_traps_disabled = 0) {
    if(%tribal_door_lock1 = 10 & %tribal_door_lock2 = 20 & %tribal_door_lock3 = 17 & %tribal_door_lock4 = 19) {
        %handelmort_traps_disabled = 1;
        mes("The combination seems correct!");
        return;
    }
    mes("This combination is incorrect.");
    return;
} 

// OSRS seems to have a bug with this lock puzzle where the shown letter and the stored varp value 
// can desync somehow, TODO: figure out how to recreate that
[if_button,tribal_door2:com_47] @tribal_door_combo1(-1);
[if_button,tribal_door2:com_49] @tribal_door_combo2(-1);
[if_button,tribal_door2:com_51] @tribal_door_combo3(-1);
[if_button,tribal_door2:com_53] @tribal_door_combo4(-1);

[if_button,tribal_door2:com_48] @tribal_door_combo1(1);
[if_button,tribal_door2:com_50] @tribal_door_combo2(1);
[if_button,tribal_door2:com_52] @tribal_door_combo3(1);
[if_button,tribal_door2:com_54] @tribal_door_combo4(1);

[proc,clear_tribaldoor_inter]
%tribal_door_lock1 = 0;
%tribal_door_lock2 = 0;
%tribal_door_lock3 = 0;
%tribal_door_lock4 = 0;
if_settext(tribal_door2:com_43, enum(int, string, alphabet, 0));
if_settext(tribal_door2:com_44, enum(int, string, alphabet, 0));
if_settext(tribal_door2:com_45, enum(int, string, alphabet, 0));
if_settext(tribal_door2:com_46, enum(int, string, alphabet, 0));

[proc,calc_tribaldoor_value](int $current, int $change)(int)
def_int $stage = calc($current + $change);
if($stage > 25) {
    $stage = 0;
} else if ($stage < 0) {
    $stage = 25;
}
return ($stage);

[label,tribal_door_combo1](int $change)
def_int $stage = ~calc_tribaldoor_value(%tribal_door_lock1, $change);
%tribal_door_lock1 = $stage;
if_settext(tribal_door2:com_43, enum(int, string, alphabet, $stage));

[label,tribal_door_combo2](int $change)
def_int $stage = ~calc_tribaldoor_value(%tribal_door_lock2, $change);
%tribal_door_lock2 = $stage;
if_settext(tribal_door2:com_44, enum(int, string, alphabet, $stage));

[label,tribal_door_combo3](int $change)
def_int $stage = ~calc_tribaldoor_value(%tribal_door_lock3, $change);
%tribal_door_lock3= $stage;
if_settext(tribal_door2:com_45, enum(int, string, alphabet, $stage));

[label,tribal_door_combo4](int $change)
def_int $stage = ~calc_tribaldoor_value(%tribal_door_lock4, $change);
%tribal_door_lock4 = $stage;
if_settext(tribal_door2:com_46, enum(int, string, alphabet, $stage));

[oploc1,loc_2705]
if(%handelmort_traps_disabled = 0) {
    ~clear_tribaldoor_inter;
    if_openmainmodal(tribal_door2);
    return;
}
~open_and_close_door(loc_1535, loc_coord, loc_angle, loc_shape, ~check_axis(coord, loc_coord, loc_angle), false);

[oploc1,loc_2711]
// OSRS seems to only allow interaction from the top side of the ladder, so just going to assume
// this is probably how it worked in 2004
def_coord $start = movecoord(loc_coord, 0, 0, 3);
p_walk($start);
p_delay(calc(distance(coord, $start) + 1));
if(%handelmort_traps_disabled >= 2) {
    p_telejump(1_41_51_7_57);
    mes("You climb up the stairs.");
    return;
}
mes("As you climb the stairs you hear a click...");
sound_synth(locked, 0, 0);
// TODO: screen fade to blackout
p_delay(2);
mes("You have fallen through a trap!");
p_telejump(^totem_stairs_fail_coord);
// unfade
p_delay(2);
~damage_self(calc(((stat(hitpoints) * 20) / 100) + 1));

[oploc2,loc_2711]
def_coord $start = movecoord(loc_coord, 0, 0, 3);
p_walk($start);
p_delay(calc(distance(coord, $start) + 1));
if(stat(thieving) < 21) {
    mes("You don't find anything interesting");
    return;
}
// doesn't use the stage varp here either even though it could
%handelmort_traps_disabled = 2;
~mesbox("Your trained senses as a thief enable you to see that there is a trap in these stairs. You make a note of its location for future reference when using these stairs.");

[oploc1,loc_2709]
p_arrivedelay;
anim(human_openchest, 0);
sound_synth(chest_open, 0, 0);
p_delay(0);
loc_del(300);
loc_add(loc_coord, loc_2710, loc_angle, loc_shape, 300);
mes("You open the chest.");

[oploc1,loc_2710]
if(inv_total(inv, totem) = 0 & inv_total(bank, totem) = 0) {
    ~mesbox("Inside the chest you find the tribal totem.");
    inv_add(inv, totem, 1);
} else {
    mes("The chest is empty.");
}
loc_del(300);
loc_add(loc_coord, loc_2709, loc_angle, loc_shape, 300);

[oploc2,loc_2710]
p_arrivedelay;
anim(human_closechest, 0);
sound_synth(chest_close, 0, 0);
p_delay(0);
loc_del(300);
loc_add(loc_coord, loc_2709, loc_angle, loc_shape, 300);
mes("You close the chest.");

[oploc2,loc_2708]
~mesbox("There is a label on this crate. It says;");
if(%totem_progress = ^totem_crate_marked) {
    ~mesbox("To Lord Handelmort, Handelmort Mansion, Ardougne.");
}
~mesbox("Senior Patents Clerk, Chamber of Invention, The Wizards' Tower, Misthalin. The crate is securely fastened shut and ready for delivery.");

[queue,totem_quest_complete]
%totem_progress = ^totem_complete;
~send_quest_complete(quest_journal:totem, totem, 250, 1, "You have completed the\\nTribal Totem Quest!");
givexp(thieving, 17750);
inv_add(inv, swordfish, 5);