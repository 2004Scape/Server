// https://web.archive.org/web/20041115013147/http://runescape.salmoneus.net/quests/DemonSlayer.html

[queue,demon_slayer_complete]
%demon_progress = ^demon_complete;
session_log(^log_adventure, "Quest complete: Demon Slayer");
~send_quest_complete(questlist:demon, silverlight, 250, ^demon_questpoints, "You have completed the\\nDemon Slayer Quest!");

// Search Drain
[oploc1,varrock_palace_sewer_drain]
~mesbox("This is the drainpipe running from the kitchen sink to the sewer.");
if (%demon_progress < ^demon_key_hunt) {
    ~mesbox("I can see a key but I can't quite reach it...");
} else {
    ~mesbox("I can see a key just inside the drain.|That must be the one Sir Prysin dropped.");
}
~mesbox("I don't seem to be able to reach it. I wonder if I can dislodge it somehow and knock it down into the sewer.");

// For using bucket on drain
[oplocu,varrock_palace_sewer_drain]
if (last_useitem ! bucket_water) {
    ~displaymessage(^dm_default);
    return;
}
mes("You pour the liquid down the drain.");
p_delay(2);
mes("Ok, I think I've washed the key down to the sewer.");
p_delay(2);
mes("I'd better go down and get it before someone else finds it.");
p_delay(2);
inv_del(inv, bucket_water, 1);
inv_add(inv, bucket_empty, 1);
// Tele to 3227 9898 0
// todo: this should be instanced per player
obj_add(0_50_154_25_41, demon_prysin_key, 1, 300); // Guessing Duration is ticks?


[proc,demon_has_prysin_key]()(boolean)
if (%demon_progress >= ^demon_silverlight | inv_total(inv, demon_prysin_key) > 0 | inv_total(bank, demon_prysin_key) > 0){
    return(true);
}
else{
    return(false);
}