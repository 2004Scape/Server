// Can kill delrith with magic as long as Silverlight is equipped, otherwise no message is sent when trying to attack
// https://runescape.salmoneus.net/forums/topic/366965-cant-get-past-demon-slayer/

[ai_queue3,delrith]
[ai_queue4,delrith] gosub(npc_death);

[ai_queue2,delrith]
~npc_default_damage(last_int);
if (npc_stat(hitpoints) = 0) {
    if (finduid(%npc_attacking_uid) = true) {
        queue(delrith_death, 1);
    }
}

[apnpc2,delrith]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@attack_delrith;

[opnpc2,delrith]
@attack_delrith;

[label,attack_delrith]
if (%demon_progress = ^demon_silverlight) {
    if (inv_getobj(worn, 3) ! silverlight) {
        if (inv_total(inv, silverlight) > 0) {
            mes("Maybe I'd better wield silverlight first.");
            return;
        }

        if (inv_total(inv, silverlight) < 1) {
            mes("I'd rather not. He looks scary.");
            return;
        }
    }
    @player_combat_start;
} else if (%demon_progress = ^demon_complete) {
    mes("You've already done that quest.");
}
else{
    mes("I'd rather not. He looks scary");
}

[queue,delrith_death]
if_close;
if (npc_find(coord, delrith, 8, 0) = true) {
    npc_changetype(weakened_delrith);
    npc_setmode(none); // stop fighting
}

// https://www.youtube.com/watch?v=rjQprrDDUfE

p_stopaction;
p_delay(1);
~chatplayer("<p,angry>Now what was that incantation again?");
def_int $option = ~p_choice4("Carlem Gabindo Purchai Zaree Camerinthum", 1, "Purchai Zaree Gabindo Carlem Camerinthum", 2, "Purchai Camerinthum Aber Gabindo Carlem", 3, "Carlem Aber Camerinthum Purchai Gabindo", 4);

if ($option = 1) {
    ~chatplayer("<p,happy>Carlem Gabindo Purchai Zaree Camerinthum");
    @demon_slayer_wrong_incantation;
} else if ($option = 2) {
    ~chatplayer("<p,happy>Purchai Zaree Gabindo Carlem Camerinthum");
    @demon_slayer_wrong_incantation;
} else if ($option = 3) {
    ~chatplayer("<p,happy>Purchai Camerinthum Aber Gabindo Carlem");
    @demon_slayer_wrong_incantation;
} else if ($option = 4) {
    ~chatplayer("<p,happy>Carlem Aber Camerinthum Purchai Gabindo");
    @demon_slayer_correct_incantation;
}

[label,demon_slayer_wrong_incantation]
if_close;
~mesbox("As you chant, Delrith is sucked towards the vortex....");
if (npc_find(coord, weakened_delrith, 8, 0) = true) {
    // On restore Delrith does not keep attacking you in 2004
    // https://www.youtube.com/watch?v=rjQprrDDUfE
    npc_changetype(delrith);
    npc_statheal(hitpoints, npc_basestat(hitpoints), 0);
    ~mesbox("Suddenly the vortex collapses. That was the wrong incantation");
    return;
}

[label,demon_slayer_correct_incantation]
// Between 20 feb and 6 march 2006 this becomes 2 separate message boxes
// https://www.youtube.com/watch?v=w7uLKC_rYfc
~mesbox("As you chant, Delrith is sucked towards the vortex...|Back to the dark dimension from which he came...");
if_close;
if (npc_find(coord, weakened_delrith, 8, 0) = true) {
    gosub(npc_death);
    queue(demon_slayer_complete, 1);
    return;
}

// Everything below here is a guess. This is set up so if a player doesn't kill weakened delrith he becomes
// Regular delrith again after a minute.
[ai_timer,weakened_delrith]
npc_changetype(delrith);
npc_statheal(hitpoints, npc_basestat(hitpoints), 0);
npc_setmode(none);
