[opnpc1,shamus]
~chatnpc(angry, "Ay yer big elephant! Yer've caught me, to be sure! What would an elephant like yer be wanting wid ol' Shamus then?");
switch_int(%zanaris_progress) {
    case ^zanaris_not_started:
        ~chatplayer(confused, "I'm not sure.");
        ~chatnpc(neutral, "Well you'll have to be catchin' me again when yer are, elephant!");
        @remove_shamus;
        return;
    case ^zanaris_started:
        ~chatplayer(neutral, "I want to find Zanaris.");
        ~chatnpc(neutral, "Zanaris is it now? Well well well... Yer'll be needing to be going to that funny little shed out there in the swamp, so you will.");
        ~chatplayer(confused, "...but... I thought... Zanaris was a city...?");
        ~chatnpc(neutral, "Aye that it is!");
        def_int $option = ~p_choice2("How does it fit in a shed then?", 1, "I've been in that shed, I didn't see a city.", 2);
        if($option = 1) {
            ~chatplayer(confused, "..How does it fit in a shed then?");
            ~chatnpc(angry, "Ah yer stupid elephant! The city isn't IN the shed! The doorway to the shed is being a portal to Zanaris, so it is.");
            ~chatplayer(confused, "So I just walk into the shed and end up in Zanaris then?");
            @shamus_dramenwood_staff;
        } else if($option = 2) {
            ~chatplayer(angry, "I've been in that shed. I didn't see a city.");
            @shamus_dramenwood_staff;
        }
        return;
    case default:
        def_int $option = ~p_choice2("I'm not sure.", 1, "How do I get to Zanaris again?", 2);
        if($option = 1) {
            ~chatplayer(confused, "I'm not sure.");
            ~chatnpc(angry, "Ha! Look at yer! Look at the stupid elephant who tries to go catching a leprechaun when he don't even be knowing what he wants!");
            @remove_shamus;
        } else if($option = 2) {
            ~chatplayer(quiz, "How do I get to Zanaris again?");
            ~chatnpc(angry, "Yer stupid elephant! I'll tell yer again! Yer need to be entering the shed in the middle of the swamp while holding a dramenwood staff! Yer can make the Dramen staff");
            ~chatnpc(angry, "from a dramen tree branch, and there's a Dramen tree on Entrana! Now leave me alone yer great elephant!");
            @remove_shamus;
        }
        return;
}

[label,shamus_dramenwood_staff]
~chatnpc(neutral, "Oh, was I fergetting to say?|Yer need to be carrying a Dramenwood|staff to be getting there! Otherwise|Yer'll just be ending up in the shed.");
~chatplayer(quiz, "So where would I get a staff?");
~chatnpc(neutral, "Dramenwood staffs are crafted from branches of the Dramen tree, so they are. I hear there's a Dramen tree over on the island of Entrana in a cave");
~chatnpc(neutral, "or some such. There would probably be a good place for an elephant like yer to be starting looking I reckon.");
~chatnpc(neutral, "The monks are running a ship from Port Sarim to Entrana, I hear too. Now leave me alone yer elephant!");
%zanaris_progress = ^zanaris_spoken_shamus;
@remove_shamus;

[label,remove_shamus]
~mesbox("The leprechaun magically disappears.");
npc_del;