[label,hetty_start]
~chatplayer("neutral", "I'm in search of a quest.");
~chatnpc("neutral", "Hmmmm... Maybe I can think of something for you.");
~chatnpc("neutral", "Would you like to become more proficient in the dark arts?");

def_int $option = ~p_choice3("Yes help me become one with my darker side.", 1, "No I have my principles and honour.", 2, "What, you mean improve my magic?", 3);
if($option = 1) {
    ~chatplayer("happy", "Yes help me become one with my darker side.");
    @hetty_step_1;
} else if($option = 2) {
    ~chatplayer("neutral", "No I have my principles and honour.");
    ~chatnpc("sad", "Suit yourself, but you're missing out.");
} else if($option = 3) {
    ~chatplayer("default", "What, you mean improve my magic?");
    ~chatnpc("default", "Yes improve your magic.");
    ~chatnpc("default", "Do you have no sense of drama?");
    $option = ~p_choice3("Yes I'd like to improve my magic.", 1, "No, I'm not interested.", 2, "Show me the mysteries of the dark arts.", 3);
    if($option = 1) {
        ~chatplayer("default", "Yes I'd like to improve my magic.");
        @hetty_step_1;
    } else if($option = 2) {
        ~chatplayer("default", "No, I'm not interested.");
        ~chatnpc("default", "Many aren't to start off with. But I think you'll be drawn back to this place.");
    } else if($option = 3) {
        ~chatplayer("default", "Show me the mysteries of the dark arts.");
        @hetty_step_1;
    }
}

[label,hetty_step_1]
%hetty_progress = ^hetty_started;
~send_quest_progress(quest_journal:hetty, %hetty_progress, ^hetty_complete);

~chatnpc("happy", "Okay, I'm going to make a potion to help bring out|your darker self.");
~chatnpc("neutral", "You will need certain ingredients.");
~chatplayer("neutral", "What do I need?");
~chatnpc("neutral", "You need an eye of newt, a rat's tail, an onion... Oh|and a piece of burnt meat.");
~chatplayer("happy", "Great, I'll go and get them.");

[label,hetty_step_2]
~chatplayer("happy", "I've been looking for those ingredients.");
~chatnpc("happy", "So what have you found so far?");

if(inv_total(inv, burnt_meat) > 0 & inv_total(inv, rats_tail) > 0 & inv_total(inv, eye_of_newt) > 0 & inv_total(inv, onion) > 0) {
    ~chatplayer("happy", "In fact I have everything!");
    ~chatnpc("happy", "Excellent, can I have them then?");
    ~mesbox("You pass the ingredients to Hetty and she puts them all into her|cauldron. Hetty closes her eyes and begins to chant. The cauldron|bubbles mysteriously.");
    inv_del(inv, burnt_meat, 1);
    inv_del(inv, rats_tail, 1);
    inv_del(inv, eye_of_newt, 1);
    inv_del(inv, onion, 1);
    %hetty_progress = ^hetty_objects_given;
    ~chatplayer("neutral", "Well, is it ready?");
    ~chatnpc("happy", "Ok, now drink from the cauldron.");
} else if(inv_total(inv, burnt_meat) = 0 & inv_total(inv, rats_tail) = 0 & inv_total(inv, eye_of_newt) = 0 & inv_total(inv, onion) = 0){
    ~chatplayer("sad", "I'm afraid I don't have any of them yet.");
    ~chatnpc("sad", "Well I can't make the potion without them! Remember...|You need an eye of newt, a rat's tail, an onion, and a|piece of burnt meat. Off you go dear!");
} else {
    def_string $message = "";
    if(inv_total(inv, rats_tail) = 0) {
        $message = append($message, "I don't have a rat's tail, ");
    } else {
        $message = append($message, "I have the rat's tail (ewww), ");
    }

    if(inv_total(inv, burnt_meat) = 0) {
        $message = append($message, "I don't have any burnt meat, ");
    } else {
        $message = append($message, "I have the burnt meat, ");
    }

    if(inv_total(inv, onion) = 0) {
        $message = append($message, "I|don't have an onion, ");
    } else {
        $message = append($message, "I|have an onion, ");
    }

    if(inv_total(inv, eye_of_newt) = 0) {
        $message = append($message, "and I don't have an eye of newt.");
    } else {
        $message = append($message, "and I have the eye of newt, yum!");
    }
    ~chatplayer("neutral", $message);
    ~chatnpc("neutral", "Great, but I'll need the other ingredients as well.");
}

[oploc1,loc_2024]
if(%hetty_progress = ^hetty_objects_given) {
    ~mesbox("You drink from the cauldron, it tastes horrible! You feel yourself imbued with power.");
    queue(hetty_quest_complete, 0);
} else {
    ~chatplayer("neutral", "As nice as that looks I think I'll give it a miss for now.");
}

[queue,hetty_quest_complete]
%hetty_progress = ^hetty_complete;
givexp(magic, 3250);
~send_quest_complete(quest_journal:hetty, eye_of_newt, 250, ^hetty_questpoints, "You have completed the\\nWitches Potion Quest!");