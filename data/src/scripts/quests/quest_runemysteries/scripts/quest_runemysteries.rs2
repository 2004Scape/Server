[opnpc1,sedridor]
// we don't support teleporting until the quest is complete
if(%runemysteries_progress = 0) {
    ~chatnpc("default", "Welcome adventurer, to the world reknowned Wizards' Tower. How may I help you?");
    def_int $option = ~p_choice2("Nothing thanks, I'm just looking around.", 1, "What are you doing down here?", 2);
    if ($option = 1) {
        ~chatplayer("default", "Nothing thanks. Im just looking around.");
    }
    if ($option = 2) {
        @sedridor_2;
    }
} else if(%runemysteries_progress = 1) {
    ~chatnpc("default", "Welcome adventurer, to the world reknowned Wizards' Tower. How may I help you?");
    @rune_mysteries;
} else if(%runemysteries_progress = 2) {
    if(~obj_gettotal(research_package) = 0) {
        ~chatnpc("default", "Welcome adventurer, to the world reknowned Wizards' Tower. How may I help you?");
        ~chatplayer("default", "...I lost the package you gave me.");
        ~chatnpc("default", "You WHAT?");
        ~chatnpc("default", "Tch, that was really very careless of you. Luckily as head wizard I have great powers, and will be able to teleport it back here without too much effort.");
        ~chatnpc("default", "Ok, I have retrieved it. Luckily it doesn't appear to have been damaged. Now please take it to Aubury, and try not to lose it again.");
        if (inv_freespace(inv) = 0) {
            ~objbox(research_package, 250, "Sedridor tries to hand you the Research Package, but you don't have enough room to take them.");
            return;
        }
        inv_add(inv, research_package, 1);
    } else {
        ~chatnpc("default", "How goes your quest? Have you delivered the research notes to my friend Aubury yet?");
        ~chatplayer("default", "Not yet...");
        ~chatnpc("default", "Well, please do so as soon as possible. Remember: to get to Varrock, head due North, through Draynor Village, around Draynor Manor, and then head East when");
        ~chatnpc("default", "you get to the Barbarian village. The man you seek is named Aubury, and he owns the rune shop there. It is vital he receives this package.");
    }
} else if (%runemysteries_progress = 4) {
    ~chatnpc("default", "Welcome adventurer, to the world reknowned Wizards' Tower. How may I help you?");
    ~chatnpc("default", "Ah, <displayname()>. How goes our quest? Have you delivered the research notes to my friend Aubury yet?");
    ~chatplayer("default", "Yes, I have. He gave me some research notes to pass on to you.");
    ~chatnpc("default", "May I have his notes then?");
    if(~obj_gettotal(notes) = 0) {
        ~chatplayer("default", "Uh... I kind of... lost them...");
        ~chatnpc("default", "You did? You are extremely careless aren't you? I suggest you go and speak to Aubury once more, with luck he will have made copies of his research.");
        return;
    }
    if(inv_total(inv, notes) = 0) {
        ~chatplayer("default", "Sure, I'll go and get them from the bank.");
        return;
    }
    ~chatplayer("default", "Sure. I have them here.");
    ~chatnpc("default", "Well, before you hand them over to me, as you have been nothing but truthful with me to this point, and I admire that in an adventurer, I will let you into the secret of our research.");
    ~chatnpc("default", "Now as you may or may not know, many centuries ago, the wizards at this Tower learnt the secret of creating Rune Stones, which allowed us to cast Magic very easily.");
    ~chatnpc("default", "When this Tower was burnt down the secret of creating runes was lost to us for all time... except it wasn't. Some months ago, while searching these ruins for information from old days,");
    ~chatnpc("default", "I came upon a scroll, almost destroyed that detailed a magical rock deep in the icefields of the North, closed off from access by anything other than magical means.");
    ~chatnpc("default", "This rock was called the 'Rune Essence' by the magicians who studied its powers. Apparently, by simply breaking a chunk from it, a Rune Stone could be fashioned very quickly and easily at certain");
    ~chatnpc("default", "elemental altars that were scattered across the land back then. Now, this is an intersting little piece of history, but not much use to us as modern wizards without access to the Rune Essence,");
    ~chatnpc("default", "or these elemental altars. This is where you and Aubury come into this story. A few weeks back, Aubury discovered in a standard delivery of runes to his store, a patchment detailing a");
    ~chatnpc("default", "teleportation spell that he had never come across before. To his shock, when cast it took him to a strange rock he had never encountered before... yet that felt strabgeky familiar...");
    ~chatnpc("default", "As I'm sure you have now guessed, he had discovered a portal leading to the mythical Rune Essence. As soon as he told me of this spell I saw the importance of his find,");
    ~chatnpc("default", "for if we could but find the elemental altars spoken of in the ancient texts, we would once more be able to create runes as our ancestors had done! It would be the saviour of the wizards' art!");
    ~chatplayer("default", "I'm still not sure how I fit into this little story of yours...");
    ~chatnpc("default", "You haven't guessed? This talisman you brought me... it is the key to the elemental altar of air! When you hold it next, it will direct you towards");
    ~chatnpc("default", "the entrance to the long forgotten Air Altar! By brnging pieces of the Rune Essence to the Air Temple you will be able to fashion your own Air Runes!");
    ~chatnpc("default", "And this is not all! By finding other talismans similar to this one, you will eventually be able to craft every rune that is available on this world! Just");
    ~chatnpc("default", "as our ancestors did! I cannot stress enough what a find this is! Now, due to the risks involved of letting this mighty power fall into the wrong hands");
    ~chatnpc("default", "I will keep the teleport skill to the Rune Essence a closely guarded secret, shared only by myself and those Magic users around the world wom I trust enough to keep it.");
    ~chatnpc("default", "This means that if any evil power should discover the talismans required to enter the elemental temples, we will be able to prevent their access to the Rune Essence and prevent");
    ~chatnpc("default", "tragedy befalling this world. I know not where the templates are located, nor do I know where the talismans have been scattered to in this land, but I now");
    ~chatnpc("default", "return your Air Talisman to you. Find the Air Template and you wil be able to charge your Rune Essences to become Air Runes at will. Any time");
    ~chatnpc("default", "you wish to visit the Rune Essence, speak to me or Aubury and we will open a portal to that mystical place for you to visit.");
    ~chatplayer("default", "So only you and Aubury know the teleport spell to the Rune Essence?");
    ~chatnpc("default", "No... there are others... whom I will tell of your authorisation to visit that place. When you speak to them, they will know you, and grant you access to that place when asked.");
    ~chatnpc("default", "Use the Air Talisman to locate the air template, and use any further talismans you find to locate the other missing elemental templates. Now... my research notes please?");

    inv_del(inv, notes, 1);
    inv_add(inv, air_talisman, 1);
    ~mesbox("You hand the head wizard the research notes.|He hands you back the Air Talisman.");

    queue(rune_mysteries_complete, 0);

} else if(%runemysteries_progress = 5) {
    ~chatnpc("default", "Welcome adventurer, to the world reknowned Wizards' Tower. How may I help you?");
    def_int $option = ~p_choice2("Nothing thanks, I'm just looking around.", 1, "Can you teleport me to the Rune Essence?", 2);
    if ($option = 1) {
        ~chatplayer("default", "Nothing thanks. Im just looking around.");
    }
    if ($option = 2) {
        ~chatplayer("default", "Can you teleport me to the Rune Essence?");
        npc_say("Seventior disthine molenko!");
        sound_synth(curse_all, 1, 0);
        strongqueue(runeess_teleport, 2);
    }
}

[queue,rune_mysteries_complete]
%runemysteries_progress = 5;
~send_quest_complete(quest_journal:runemysteries, air_talisman, 250, 1, "You have completed the\\nMysteries Of The Runes Quest!");

[label,rune_mysteries]
def_int $option = ~p_choice3("Nothing thanks. Im just looking around.", 1, "What are you doing down here?", 2, "I'm looking for the head wizard.", 3);
if ($option = 1) {
    ~chatplayer("default", "Nothing thanks. Im just looking around.");
} else if ($option = 2) {
    @sedridor_2;
} else if ($option = 3) {
    ~chatplayer("default", "I'm looking for the head wizard.");
    ~chatnpc("default", "Oh you are, are you?|And just why would you be doing that?");
    ~chatplayer("default", "The Duke of Lumbridge sent me to find him. I have this weird talisman he found. He said the head wizard would be interested in it.");
    ~chatnpc("default", "Did he now? HmmmMMMMMmmmmm. Well that IS interesting. Hand it over then adventurer. let me see what all the hubbub about it is. Just some amulet I'll wager.");
    $option = ~p_choice2("Ok, here you are.", 1, "No, I'll only give it to the head wizard.", 2);
    if ($option = 1) {
        @seridor_3;
    } else if ($option = 2) {
        ~chatplayer("default", "No, I'll only give it to the head wizard.");
        ~chatnpc("default", "Hmmm. Well, I admire your caution adventurer, perhaps I can prove myself? I will use my mental powers to discover...");
        ~chatnpc("default", "Your name is... <displayname()>!");
        ~chatplayer("default", "You're right!");
        ~chatnpc("default", "Well I am head wizard you know! You don't get to my position without learning a few tricks along the way!");
        ~chatnpc("default", "So now I have proved myself to you why don't you hand over that talisman, hmm?");
        @seridor_3;
    }
}

[label,sedridor_2]
~chatnpc("default", "That is indeed a good question. Here in the cellar of the Wizards' Tower you find the remains of the old Wizards' Tower, destroyed by fire");
~chatnpc("default", "many years past by the treachery of the Zamorakians. Many mysteries were lost, which we try to find once more. By building this Tower on the remains of the old,");
~chatnpc("default", "we sought to show the world of our dedication to learning the mysteries of Magic. I am here searching through these fragments for knowledge from the artefacts from our past");
~chatplayer("default", "And have you found anything useful?");
~chatnpc("default", "Aaaah... that would be telling adventurer. Anything I have found I cannot speak freely of, for fear the treachery of the past might be repeated.");
def_int $option = ~p_choice2("Ok, well I'll leave you to it.", 1, "What do you mean treachery?", 2);
if ($option = 1) {
    ~chatplayer("default", "Ok, well I'll leave you to it.");
} else if ($option = 2) {
    ~chatnpc("default", "Well, it is a long story from the past... Many years ago, this Wizards' Tower was a focus of great learning, as we mages studied together to try and learn the secrets behind");
    ~chatnpc("default", "the Rune Stones that allow us to use Magic. Who makes them? Where do they come from? How many types are there? What spells can they produce? All these questions and more are still unknown to us,");
    ~chatnpc("default", "but were once known to our ancestors. Legends tell us that in the past the mages who lived here could fashion Rune Stones almost at will, and as many as they desired.");
    ~chatplayer("default", "But they cannot anymore?");
    ~chatnpc("default", "No, unfortunately not. Many years past, the Wizards who follow Zamorak, the god of chaos, burned this Tower to the ground, and all who were inside.");
    ~chatnpc("default", "To this day we do not fully know why they did this terrible act, but all our research, all of our greatest magical minds were destroyed in one fell swoop.");
    ~chatnpc("default", "This is why I spend my time searching through these few remains we have left from the glorious old Tower. I hope someday to find something that will tell us once more of the mysteries of");
    ~chatnpc("default", "the runes that we use daily, which dwindle in supply with each use. Someday I hope we may once more create our own runes, and the Wizards' Tower will once more be a place of glory!");
    ~chatplayer("default", "Ok, well I'll leave you to it.");
}

[label,seridor_3]
~chatplayer("default", "Ok here you are.");
if (inv_total(inv, air_talisman) = 0) {
    // unofficial text
    ~mesbox("Ooops, look's like I've forgotten to bring the item in question.");
    return;
}
inv_del(inv, air_talisman, 1);
~mesbox("You hand the Talisman to the wizard.");
~chatnpc("default", "Wow! This 's... incredible!");
~chatnpc("default", "Th-this talisman you brought me...! It is the last piece of the puzzle, I think! Finally! The legacy of our ancestors... it will return to us once more!");
~chatnpc("default", "I need time to study this, <displayname()>. Can you please do me this task while I study this talisman you have brought me? In the mighty town of Varrock, which");
~chatnpc("default", "is located North East of here, there is a certain shop that sells magical runes. I have in this package all of the research I have done relating to the Rune Stones, and");
~chatnpc("default", "require somebody to take them to the shopkeeper so that he may share my research and offer me his insights. Do this thing for me, and bring back what he gives you,");
~chatnpc("default", "and if my suspicions are correct. I will let you into the knowledge of one of the greatest secrets this world has ever known! A secret so powerful that it destroyed the");
~chatnpc("default", "original Wizards' Tower all of those centuries ago! My research, combined with this mysterious talisman... I cannot believe the answer to the mysteries is so close now!");
~chatnpc("default", "Do this thing for me <displayname()>. Be rewarded in a way you can never imagine.");
def_int $option = ~p_choice2("Yes, certainly.", 1, "No, I'm busy.", 2);
if ($option = 1) {
    ~chatplayer("default", "Yes, certainly.");
    ~chatnpc("default", "Take this package, and head directly North from here. through Draynor village, until you reach the Barbarian Village. Then head East from there until you reach Varrock.");
    ~chatnpc("default", "Once in Varrock, take this package to the owner of the rune shop. His name is Aubury. You may find it helpful to ask one of Varrock's citizens for directions.");
    ~chatnpc("default", "as Varrock can be a confusing place for the first time visitor. He will give you a special item, bring it back to me, and I shall show you the mystery of the runes...");
    if (inv_freespace(inv) = 0) {
        ~objbox(research_package, 250, "Sedridor tries to hand you the Research Package, but you don't have enough room to take them.");
        return;
    }
    inv_add(inv, research_package, 1);
    %runemysteries_progress = 2;
    ~mesbox("The head wizard gives you a package.");
    ~chatnpc("default", "Best of luck with your quest, <displayname()>.");
} else if ($option = 2) {
    ~chatplayer("default", "No, I'm busy.");
    // TODO find out what happens here
    inv_add(inv, air_talisman, 1);
    ~mesbox("Sedridor hands you back the Talisman.");
    ~chatnpc("default", "Perhaphs I will see you later <displayname()>.");
}

[proc,rune_mysteries_handle_duke_horacio](boolean)
if (%runemysteries_progress = 0 | %runemysteries_progress > 0) {
    return(true);
}
return(false);

[label,rune_mysteries_duke_horacio]
def_int $option = ~p_choice2("Have you any quests for me?", 1, "Where can I find money?", 2);
if ($option = 1) {
     if(%runemysteries_progress = 1) {
        if(~obj_gettotal(air_talisman) = 0) {
            ~chatnpc("default", "Did you speak to the head wizard for me yet, adventurer?");
            ~chatplayer("default", "No, I lost that talisman that you gave me.");
            if(inv_freespace(inv) = 0) {
                ~objbox(air_talisman, 250, "The Duke tries to hand you a talisman, but you don't have enough room to take it.");
            } else {
                ~chatnpc("default", "Ah, that would explain it. One of my servants found this outside, and it seemed too much of a coincidence that more than one strange.");
                inv_add(inv, air_talisman, 1);
                ~chatnpc("default", "object would appear on my land in such a short period of time. Please take this to the head wizard at the Wizards' Tower, south-west of here, and don't lose it this time.");
            }
        } else {
            ~chatnpc("default", "The only task remotely approaching a quest is the delivery of that talisman I gave you to the head wizard of the Wizards' Tower,");
            ~chatnpc("default", "south-west of here. I suggest you deliver it to him as soon as possible. I have the oddest feeling that it is important...");
        }
     } else if (%runemysteries_progress > 0) {
        ~chatplayer("default", "Have you any quests for me?");
        ~chatnpc("default", "The only job I had was the delivery of that talisman, so I'm afraid not.");
     } else {
        ~chatplayer("default", "Have you any quests for me?");
        ~chatnpc("default", "Well it's not really a quest but I recently discovered this strange talisman.");
        ~chatnpc("default", "It seems to be a mystical and I have never seen anything like it before. Would you take it to the head wizard at");
        ~chatnpc("default", "the Wizards' Tower for me? It's just south-west of here and should not take you very long at all. I would be awfully grateful.");
        $option = ~p_choice2("Sure, no problem.", 1, "Not right now.", 2);
        if ($option = 1) {
            ~chatplayer("default", "Sure, no problem.");
            %runemysteries_progress = 1;
            ~send_quest_progress(quest_journal:runemysteries, %runemysteries_progress, 5);
            ~chatnpc("default", "Thank you very much, stranger. I am sure the head wizard will reward you for such an interesting find.");
            if(inv_freespace(inv) = 0) {
                ~objbox(air_talisman, 250, "The Duke tries to hand you the talisman, but you don't have enough room to take it.");
            } else {
                ~objbox(air_talisman, 250, "The Duke hands you an @blu@air talisman.");
                inv_add(inv, air_talisman, 1);
            }
        } else if($option = 2) {
            ~chatplayer("default", "Not right now.");
            ~chatnpc("default", "As you wish. Hopefully I can find someone else to help.");
        }
    }
} else if($option = 2) {
    @duke_horacio_money;
}

[proc,rune_mysteries_handle_aubury](boolean)
if(%runemysteries_progress < 5) {
    return(true);
}
return(false);

[label,rune_mysteries_aubury]
// aubury cannot teleport until this quest is complete
if (%runemysteries_progress = 0 | %runemysteries_progress = 1) {
    ~chatnpc(happy, "Do you want to buy some runes?");
    def_int $choice = ~p_choice2("Yes please!", 1, "Oh, it's a rune shop. No thank you, then.", 2);
    if ($choice = 1) {
        @aubury_open_shop;
        return;
    }
    if ($choice = 2) {
        @aubury_rune_shop;
        return;
    }
}

if (%runemysteries_progress = 2) {
    ~chatnpc(happy, "Do you want to buy some runes?");
    def_int $choice = ~p_choice3("Yes please!", 1, "Oh, it's a rune shop. No thank you, then.", 2, "I have been sent here with a package for you.", 3);

    if ($choice = 1) {
        @aubury_open_shop;
        return;
    }
    if ($choice = 2) {
        @aubury_rune_shop;
        return;
    }

    ~chatplayer("default", "I have been sent here with a package for you. It's from the head wizard at the Wizards' Tower.");
    ~chatnpc("default", "Really? But... surely he can't have..? Please, let me have it, it must be extremely importanr for him to have sent a stranger.");
    if (inv_total(inv, research_package) = 0) {
        // unofficial text
        ~mesbox("Ooops, look's like I've forgotten to bring the item in question.");
        return;
    }
    inv_del(inv, research_package, 1);
    %runemysteries_progress = 3;
    ~mesbox("You have Aubury the research package.");
    ~chatnpc("default", "This... is incredible. Please, give me a few moments to quickly look over this, and then talk to me again.");
    return;
}
if(%runemysteries_progress = 3) {
    ~chatnpc("default", "My gratitude to you adventurer for bringing me these research notes. I notice that you brought the head wizard a special talisman that was the key to our finally unlocking the puzzle.");
    ~chatnpc("default", "Combined with the information I had already already collated regarding the Rune Essence, I think we have finally unlocked the power to");
    ~chatnpc("default", "...no. I am getting ahead of myself. Please take this summary of my research back to the head wizard at the Wizards' Tower. I trust his judgement on whether to let you in on our little secret or not.");
    ~mesbox("Aubury gives you his research notes.");
    if (inv_freespace(inv) = 0) {
        ~objbox(notes, 250, "Aubury tries to hand you the Research Notes, but you don't have enough room to take them.");
        return;
    }
    %runemysteries_progress = 4;
    inv_add(inv, notes, 1);
    return;
}
if(%runemysteries_progress = 4) {
    ~chatnpc("default", "I suggest you take those research notes of mine back to the head wizard at the Wizards' Tower.");
    if(~obj_gettotal(notes) = 0) {
        ~chatplayer("default", "I can't... I lost them...");
        ~chatplayer("default", "Well, luckily I have duplicates. It's a good thing they are written in code, I would not want the wrong kind of person to get access to the information contained within.");
        if (inv_freespace(inv) = 0) {
            ~objbox(notes, 250, "Aubury tries to hand you the Research Notes, but you don't have enough room to take them.");
            return;
        }
        inv_add(inv, notes, 1);
    } else {
       ~chatplayer("default", "Ok then, I will do that.");
    }
    return;
}