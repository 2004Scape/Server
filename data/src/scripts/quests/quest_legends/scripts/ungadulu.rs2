[opnpc1,ungadulu] @ungadulu_start;
[opnpc2,ungadulu] @attack_ungadulu;
[apnpc2,ungadulu] @attack_ungadulu;
// opnpc1 here is NIH
[opnpc2,ungadulu_possessed] @attack_ungadulu;
[apnpc2,ungadulu_possessed] @attack_ungadulu;

[label,ungadulu_start]
if (%legends_progress < ^legends_defeated_nezikchened_fire) @ungadulu_run;
else if (%legends_progress >= ^legends_defeated_nezikchened_fire) @ungadulu_after_demon;
else mes("Unhandled dialogue stage"); // todo

[label,ungadulu_run]
~chatnpc("<p,neutral>Please run for your life...");
~mesbox("The Shaman seems to be fighting an inner battle.");
~chatnpc("<p,neutral>Go... go now...!");
~mesbox("The Shaman seems to change in front of your eyes...");
npc_changetype(ungadulu_possessed); // 100t
~chatnpc("<p,neutral>Welcome Vacu, to eternal...");
if_close;
mes("The Shaman starts an incantation...");
p_delay(1);
mes("You feel a strange power coming over you...");
p_delay(1);
mes("You feel very weak...");
p_delay(1);
mes("The Shaman seems to get stronger!");
p_delay(1);
npc_changetype(ungadulu);
mes("The Shaman seems to return to normal...");
~chatnpc("<p,neutral>Run, run away... Run like the leopard Bwana!");
// seems to be set after the dialogue is complete?
settimer(ungadulu_curse, 40);

[ai_timer,ungadulu_possessed]
npc_changetype(ungadulu);
npc_tele(0_43_145_40_47);
npc_settimer(0);

[timer,ungadulu_curse]
cleartimer(ungadulu_curse);

[proc,legends_spell_usage](int $spell)(boolean)
def_dbrow $spell_data = ~get_spell_data($spell);
def_int $duration = ~pvm_spell_cast($spell_data);
p_delay(1);
if (~inzone_coord_pair_table(legends_fire_wall_correct, coord) = false & random(3) = 0) { // seems to be random??
    ~mesbox("The spell fizzles and has no effect as it passes through the flames.");
    return (true);
}
if (gettimer(ungadulu_curse) > 0 & ~player_npc_hit_roll(^magic_style) = true) {
    spotanim_npc(db_getfield($spell_data, magic_spell_table:spotanim_target, 0), $duration);
    ~chatnpc("<p,shock>Thank you, Bwana! You have released me from the|demon's grasp, but only for a short while! You must|seek out the Book of Binding - it's hidden in these caves somewhere!.");
    ~chatnpc("<p,shock>There are many trials you must pass... but please,|you're my only hope of escape!");
    ~mesbox("The shaman's eyes roll back and he returns to his quiet, unassuming self.");
    cleartimer(ungadulu_curse);
    return (true);
}
~pvm_spell_fail($spell_data, $duration);
npc_say("What...what's happening...");
npc_anim(seq_848, 0);
p_delay(1);
~npc_retaliate(0);
mes("The shaman somehow resists the power of the spell.");
mes("Perhaps you should try again?");

[label,ungadulu_no_closer]
~chatnpc("<p,sad>Please come no closer... the flames will incinerate you.");
if (%legends_progress < ^legends_spoke_ungadulu) {
    %legends_progress = ^legends_spoke_ungadulu;
}
@multi2("How can I extinguish the flames?", ungadulu_extinguish,
        "Who are you?", ungadulu_who);

[label,ungadulu_extinguish]
~chatplayer("<p,neutral>How can I extinguish the flames?");
~chatnpc("<p,shock>Please don't try to extinguish...");
npc_changetype(ungadulu_possessed);
// https://media.discordapp.net/attachments/936714977685958678/1195794230690447400/419624878_10161030270160560_5504501485089454437_n.jpg?ex=67d321a9&is=67d1d029&hm=d5f81563fa219807006db3cbfd21c1c7e2fc5f3fcfcf1144482e22ddbc928114&=&format=webp&width=1126&height=845
~chatnpc("<p,angry>Yes, douse the flames with water,|pure water...|foo...");
// double space intentional
// https://youtu.be/atp7nZUwC8E?t=33
~chatnpc("<p,shock>Please, leave now... don't listen to me... I beg you, leave now,  don't touch the flames...");
%legends_map_runes = setbit(%legends_map_runes, ^legends_asked_ungadulu_where);
@multi2("Where do I get pure water from?", ungadulu_where,
        "Who are you?", ungadulu_who);

[label,ungadulu_where]
~chatplayer("<p,neutral>Where do I get pure water from?");
~chatnpc("<p,shock>Please, leave now...");
npc_changetype(ungadulu_possessed);
~chatnpc("<p,angry>...from the above lands... hurry and release me...");
~chatnpc("<p,shock>Leave here, please, go... now...");
npc_changetype(ungadulu_possessed);
~chatnpc("<p,madlaugh>Hurry, Vacu, the heat kills me... ha ha ha");
%legends_map_runes = setbit(%legends_map_runes, ^legends_ungadulu_called_vacu);
~mesbox("The Shaman throws himself down on the floor and starts convulsing.");

[label,ungadulu_who]
~chatplayer("<p,neutral>Who are you?");
~chatnpc("<p,sad>I am Ungadulu, trapped here many years now... Leave these caves and save yourself...");
npc_changetype(ungadulu_possessed);
~chatnpc("<p,angry>Wait... get pure water from the pool... above lands...");
%legends_map_runes = setbit(%legends_map_runes, ^legends_asked_ungadulu_where);
%legends_map_runes = setbit(%legends_map_runes, ^legends_asked_ungadulu_who);
~chatnpc("<p,shock>Please Bwana, don't listen to me... run, save yourself...");
@multi2("How can I extinguish the flames?", ungadulu_extinguish,
        "Where do I get pure water from?", ungadulu_where);

[label,ungadulu_after_demon]
~chatnpc("<p,neutral>Greetings bwana... Many thanks for defeating the|demon... and releasing me from this dreadful|possession... Pray tell me, what can I do to repay this|great favour?");
@multi3("I need to collect some Yommi tree seeds for Gujuo.", ungadulu_yommi,
        "How do I get out of here?", ungadulu_out,
        "Ok, thanks...", ungadulu_thanks);

[label,ungadulu_yommi]
~chatplayer("<p,neutral>I need to collect some Yommi tree seeds for Gujuo.");
~chatnpc("<p,neutral>Oh, yes, Bwana... you will be doing a great favour to|our people by doing this. However, you must know that|it is a difficult task, the Yommi tree is difficult to grow.");
~chatnpc("<p,neutral>You must have a natural ability with such things to have|a chance...");
~objbox(yommi_tree_seeds, "The Shaman holds out his gnarly old hand and reveals|three largish green seeds.", 250, 0, divide(^objbox_height, 2));
~chatnpc("<p,neutral>Here you are, accept these with my gratitude. You'll|need to soak them in sacred water before planting them.|I notice that you must already be familiar with sacred|water to have passed into the flaming octagram.");
inv_add(inv, yommi_tree_seeds, 3);
@multi3("How do I grow the Yommi tree?", ungadulu_grow,
        "What do you know about the pure water?", ungadulu_water,
        "Ok, thanks...", ungadulu_thanks);

[label,ungadulu_grow]
~chatplayer("<p,neutral>How do I grow the Yommi tree?");
~chatnpc("<p,neutral>A good question Bwana, but it is essentially quite simple.|First you will need to soak the seeds in some pure|water. This will help to germinate the seed and begin|the growing process.");
~chatnpc("<p,neutral>The tree should show some remarkable growth quite|early but will slow down, you may be able to speed the|process up by watering the tree with more pure water,|although it can be difficult to find it.");
@multi3("What will you do now?", ungadulu_what,
        "What do you know about the pure water?", ungadulu_water,
        "Ok, thanks...", ungadulu_thanks);

[label,ungadulu_water]
~chatplayer("<p,neutral>What do you know about the pure water?");
~chatnpc("<p,neutral>Hmmm, the pure water is sacred to us. It is from a|sacred spring which is fed from deep underground.");
~chatnpc("<p,neutral>Legend has it that the spring is protected by spirits of long dead adventurers who went in search of the source of the spring. It is likely a myth and the source of the spring is buried deep underground with no paths");
~chatnpc("<p,neutral>to it.");

[label,ungadulu_what]
~chatplayer("<p,neutral>What will you do now?");
~chatnpc("<p,neutral>I will remain here in the protection of the flaming|Octagram and continue my research into the spirit|world. I am somewhat of an authority with my recent|experience!");
// double spacing and Demon capitalisation here are authentic
~chatnpc("<p,neutral>But do remember me from time to time and come to|visit an old man. You never know, I may be able to|help you in the future, and repay you the favour  of|releasing me from that terrible Demon...");
@multi3("I need to collect some Yommi tree seeds for Gujuo.", ungadulu_yommi,
        "How do I get out of here?", ungadulu_out,
        "Ok, thanks...", ungadulu_thanks);


[label,ungadulu_out]
~chatplayer("<p,neutral>How do I get out of here?");
~chatnpc("<p,neutral>Now that you have defeated the demon you can come|and go as you please. I cast a spell on you which|means that you can pass through the flames without|harm.");
@multi3("I need to collect some Yommi tree seeds for Gujuo.", ungadulu_yommi,
        "What will you do now?", ungadulu_what,
        "Ok, thanks...", ungadulu_thanks);

[label,ungadulu_thanks]
~chatplayer("<p,neutral>Ok, thanks...");
~chatnpc("<p,neutral>My sincerest pleasure Bwana...");


// Ungadulu cannot be attacked with melee or ranged weapons; he will throw players out of the Octagram, 
// hitting them for between 4 and 10 damage. However, players can manually cast combat spells on him both inside
// and outside the Octagram to damage and kill him without being thrown by his spell. 
[label,attack_ungadulu]
if (~inzone_coord_pair_table(legends_fire_wall, coord) = false) {
    ~mesbox("You feel certain that your weapon would melt if you tried to attack through these magical flames.");
}
else {
    p_delay(1);
    npc_setmode(playerface);
    mes("The shaman casts a spell!");
    if(%legends_progress < ^legends_defeated_nezikchened_fire) {
        npc_changetype(ungadulu_possessed);
        npc_say("Ha ha ha ha! Die, Vacu!");
    } else {
        npc_say("Hey! That's no way to treat a shaman.");
    }
    spotanim_npc(zap, 100, 0);
    npc_anim(zap, 0);
    p_delay(1);
    // it's difficult to know the exact logic here, guessing that one of a few safe coords is picked
    // then a random 0-1 range around that tile
    def_coord $start_coord = coord;
    def_coord $closest_coord = ~closest_enum_coord(coord, legends_ungadulu_throw_coords);
    def_coord $throw_coord = map_findsquare($closest_coord, 0, 1, ^vis_lineofwalk);
    // not sure if the outcome is period authentic - no references
    anim(human_knockback_long, 0);
    p_exactmove($start_coord, $throw_coord, 31, calc(31 + (14 * distance($start_coord, $throw_coord))), ~coord_direction($throw_coord, $start_coord));
    p_delay(divide(distance($start_coord, $throw_coord), 2));
    anim(null, 0);
    ~set_walkbas(human_knockback_long_end);
    p_delay(1);
    ~update_bas;
    ~damage_self(calc(random(7) + 4));
    npc_setmode(wander);
    npc_settimer(10);
    ~mesbox("Ungadulu unleashes a savage bolt of energy at you. You're knocked out of the octagram by the power of the spell.");
    if(%legends_progress >= ^legends_defeated_nezikchened_fire) {
        ~chatnpc("<p,angry>I hope that teaches you to respect your elders!");
        return;
    }
    ~chatnpc("<p,madlaugh>Ha, ha ha Vacu... Come for me again and you'll taste more of my power!");
}

[opnpcu,ungadulu]
if(%legends_progress >= ^legends_complete) { // might just be on completion instead
    mes("You have no further business with Ungadulu.");
    return;
}
if (last_useitem = book_of_binding) {
    if (%legends_progress < ^legends_defeated_nezikchened_fire & %legends_progress >= ^legends_filled_bowl) {
        if(%legends_progress = ^legends_filled_bowl) %legends_progress = ^legends_summoned_nezikchened_fire;
        npc_setmode(null);
        npc_queue(10, 0, 6); // queued to run 6t from here
        ~mesbox("You open the Book of Binding in front of Ungadulu and a|supernatural light starts emanating from the pages, illuminating|Ungadulu whose face twists and contorts in spasms.");
        ~chatnpcnoturn("<p,angry>Curse you foul intruder... 'Ere near to death ye come|now that ye has meddled in my dealings..");
        ~mesbox("Without warning, the outline of a huge demon starts to emerge in|front of you as Ungadulu falls to the floor unconscious.");
        npc_add(map_findsquare(coord, 1, 2, ^map_findsquare_lineofwalk), nezikchened, 500);
        %npc_lastcombat = sub(^max_32bit_int, 8);
        %npc_aggressive_player = uid;
        mes("Nezikchened: Your faith will help you little here.");
        npc_say("Your faith will help you little here.");
        mes("A sense of hopelessness fills your body...");
        // 90% of current, use stat_drain
        stat_sub(prayer, 0, 90);
    }
}
else {
    ~displaymessage(^dm_default);
}

[ai_queue10,ungadulu]
npc_anim(seq_837, 0); // seems like the last frame of this seq is broken...
npc_setmode(none);
npc_delay(10); // delay 11t
npc_setmode(null);
npc_anim(null, 0);

// curse to start: 
//https://youtu.be/uYxkN3aJ-44?t=4317
// npc_say("What...what's happening...") + dizzy anim
// mes("The shaman somehow resists the power of the spell.")
// mes("Perhaps you should try again?");
// approaches you, becomes possessed, attacks, "Ha ha ha ha! Die, Vacu!", throws you out
// throwing out is pretty janky: https://youtu.be/uYxkN3aJ-44?t=4323

// when possessed form despawns, regular form spawns immediately. can be different tile (2 away from table is spawn loc)

// https://youtu.be/uYxkN3aJ-44?t=4486
// if you talk to him and go through possession -> normal, then cast curse/weakness/etc on him, get dialogue about book of binding
// then mesbox("The shaman's eyes roll back and he returns to his quiet, unassuming self.")
