[opnpc1,irvig_senay]
if (~player_in_combat_check = false) {
    return;
}
if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(hunk_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_hunk) = false) {
    npc_say("Your destiny awaits, adventurer...");
} else {
    npc_say("You have bested me. Go forth, and discover the source.");
}

[opnpc2,irvig_senay]
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    %aggressive_npc = npc_uid;
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(hunk_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_hunk) = false) {
        npc_say("Your destiny awaits, adventurer...");
    } else {
        npc_say("You have bested me. Go forth, and discover the source.");
    }
}
@player_combat_start;

[ai_opplayer2,irvig_senay]
if(inzone(0_43_45_0_0, 0_46_45_63_63, coord) = false & (%npc_aggressive_player ! uid & (%legends_progress >= ^legends_heart_in_recess | testbit(%legends_map_runes, ^legends_smelting_hunk) = true))) {
    npc_setmode(null);
    return;
}
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(hunk_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_hunk) = false) {
        mes("Irvig Senay: Greetings brave warrior, your destiny has arrived.");
        mes("Irvig Senay: Ready your weapon and defend yourself.");
        npc_say("Ready your weapon and defend yourself.");
    } else {
        npc_say("You have bested me. Go forth, and discover the source.");
    }
}
~npc_default_attack;

[ai_queue3,irvig_senay]
if(finduid(%npc_aggressive_player) = true) {
    if_close;
    if(p_finduid(uid) = true) {
        if(inzone(0_43_45_0_0, 0_46_45_63_63, coord) = true) {
            %legends_map_runes = setbit(%legends_map_runes, ^legends_defeated_irvig_final);
            mes("A nerve tingling scream echoes around you");
            mes("as you slay Irvig Senay.");
            p_delay(1);
            npc_say("Arrghhhhh");
            mes("Irvig Senay: Arrghhhhh");
            p_delay(1);
            npc_say("Forever must I live in this torment");
            mes("Irvig Senay: Forever must I live in this torment");
            p_delay(1);
            npc_say("till this beast is slain.");
            mes("Irvig Senay: till this beast is slain.");
            npc_del;
            p_delay(2);
            @summon_nezi_part3;
        }
        // doesn't queue, the npc will sit at 0 hp if no prot access
        npc_del;
        if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(hunk_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
            & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_hunk) = false) {
            inv_add(inv, hunk_of_crystal, 1); // no space check
            mes("Irvig Senay: You have proved yourself of the honour.");
            ~chatnpc("<p,neutral>You have proved yourself of the honour.");
            ~objbox(hunk_of_crystal, "A hunk of crystal forms mid-air and falls to the floor. You place the crystal hunk into your inventory.", 250, 0, ^objbox_height);
        } else {
            ~chatnpc("<p,neutral>You fought well, adventurer.");
        }
    }
}