[opnpc1,gujuo]
~chatplayer("<p,neutral>Hello there.");
@gujuo_start;

[ai_opplayer2,gujuo]
mes("Gujuo approaches.");
npc_setmode(none);
npc_walk(npc_coord);
@gujuo_start;

[opnpcu,gujuo]
switch_obj(last_useitem) {
    case goldbowl_empty, goldbowl_water, goldbowl_pure :
        if (%legends_progress < ^legends_asked_gujuo_holy_water) {
            // shouldn't be possible under normal circumstances
            ~displaymessage(^dm_default);
            return;
        }
        // slightly different dialogue here, full stop intentional
        ~chatnpc("<p,neutral>Aha Bwana, well done, you have made the golden bowl. Would you like me to show you how to bless it.");
        def_int $choice = ~p_choice2("Yes, I'd like to bless my gold bowl.", 1, "No thanks, I'll wait.", 2);
        if ($choice = 1) {
            ~chatplayer("<p,neutral>Yes, I'd like to bless my gold bowl.");
            @gujuo_start;
        }
        else {
            ~chatplayer("<p,neutral>No thanks, I'll wait.");
            ~chatnpc("<p,neutral>Very well, let me know when you want to try.");
            @gujuo_how_goes_ungadulu;
        }
    case default : 
        ~chatnpc("<p,neutral>Sorry, but I don't need that Bwana.");
}

[label,gujuo_start]
if (p_finduid(uid) = false) {
    return;
}
if(%legends_progress >= ^legends_asked_gujuo_holy_water) {
    if (inv_total(inv, goldbowl_empty) > 0 | inv_total(inv, goldbowl_water) > 0 | inv_total(inv, goldbowl_pure) > 0) {
        ~chatnpc("<p,neutral>Greetings Bwana. Ah I see you have the golden bowl! Would you like me to show you how to bless it?");
        def_int $choice = ~p_choice2("Yes, I'd like you to bless my gold bowl.", 1, "No thanks, I need help with something else.", 2);
        if ($choice = 1) {
            ~chatplayer("<p,happy>Yes, I'd like you to bless my gold bowl.");
            @gujuo_bless_bowl;
        }
    }
}
if (%legends_progress = ^legends_got_bullroarer | %legends_progress = ^legends_swung_bullroarer) {
    ~chatnpc("<p,neutral>Greetings Bwana... Why do you make such strange sounds and disturb the peace of the jungle?");
    @multi2("I was hoping to attract the attention of a native.", gujuo_attract,
            "Sorry, it was a mistake?", gujuo_sorry);
}
else if (%legends_progress >= ^legends_accepted_rescue_ungadulu & %legends_progress < ^legends_defeated_nezikchened_fire) {
    @gujuo_how_goes_ungadulu;
} else if (%legends_progress = ^legends_defeated_nezikchened_fire) {
    ~chatnpc("<p,neutral>How goes your quest to release Ungadulu?");
    @multi3("Ungadulu is free, he was possessed by a demon and I killed it.", gujuo_ungadulu_free, "I have the Yommi tree seeds.", gujuo_have_seeds, "What do I do now?", gujuo_what_now);
} else if (%legends_progress = ^legends_germinated_seeds) {
    ~chatnpc("<p,neutral>Congratulations on releasing Ungadulu! My people are very pleased... How goes the growing of the Yommi tree?");
    @multi3("I have germinated the Yommi tree seeds.", gujuo_germinated, "Where is the fertile soil?", gujuo_wheresoil, "Ok thanks for your help.", gujuo_thanks);
} else if (%legends_progress = ^legends_water_pool_dried_up) {
    ~chatnpc("<p,neutral>I have visited Ungadulu in the caves, he is hard at work studying. He looks well! Have you grown the Yommi tree yet?");
    @multi2("The water pool has dried up and I need more water.", gujuo_pool_dried, "The Yommi tree died.", gujuo_tree_died);
} else if (%legends_progress >= ^legends_talk_gujuo_pool & %legends_progress < ^legends_defeated_nezikchened_water) {
    ~chatnpc("<p,neutral>I have visited Ungadulu in the caves, he is hard at work studying. He looks well! How is your quest going Bwana?");
    if(%legends_progress = ^legends_talk_gujuo_pool) {
        @multi5("Where can I get more water for the Yommi tree?", gujuo_morewater, 
                "Where is the source of the spring of pure water?", gujuo_source, 
                "I searched the catacombs thoroughly but found nothing else.", gujuo_searched,
                "If I went in search of the source, could you help me?", gujuo_searchsource,
                "Ok thanks for your help.", gujuo_thanks);
    } else if(%legends_progress <= ^legends_heart_in_recess & %legends_progress >= ^legends_enter_lower_dungeon) {
        @multi5("I have found a way into the caves!", gujuo_foundcaves, 
                "Where is the source of the spring of pure water?", gujuo_source, 
                "I searched the catacombs thoroughly but found nothing else.", gujuo_searched,
                "If I went in search of the source, could you help me?", gujuo_searchsource,
                "Ok thanks for your help.", gujuo_thanks);
    }
} else if (%legends_progress >= ^legends_defeated_nezikchened_water & %legends_progress < ^legends_defeated_nezikchened_final) {
    ~chatnpc("<p,happy>Hello Bwana, I'm very pleased to see you again. Things seem much happier now in the Kharazi Jungle. I suspect that it is down to your good doings!");
    if(inv_total(inv, thtotempole) > 0) {
        @multi4("I made the Totem pole, now what do I do with it?", gujuo_madetotem, "I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "Ok thanks for your help.", gujuo_thanks);
    }
    @multi4("I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "How do I make the totem pole?", gujuo_totem, "Ok thanks for your help.", gujuo_thanks);
} else if(%legends_progress = ^legends_replaced_totem) {
    ~chatnpc("<p,neutral>Greetings Bwana, we saw your fight with the Demon from some distance away. My people are so pleased with your heroic efforts. Your strength and ability as a warrior are Legendary.");
    ~objbox(thtotempolegift, "Gujuo offers you an awe inspiring totem pole.", 250, 0, ^objbox_height);
    ~chatnpc("<p,neutral>Please accept this as a token of our appreciation.");
    inv_add(inv, thtotempolegift, 1); // no invspace check
    %legends_progress = ^legends_got_gilded_totem;
    ~chatnpc("<p,neutral>Please, now consider yourself a friend of my people. And visit us anytime.");
    if(inv_total(inv, yommiseeds_germ) > 0 | inv_total(bank, yommiseeds_germ) > 0) { // removes from both
        inv_del(bank, yommiseeds_germ, ^max_32bit_int);
        inv_del(inv, yommiseeds_germ, ^max_32bit_int);
        ~chatnpc("<p,neutral>I'll take those germinated Yommi tree seeds to Ungadulu, I'm sure he'll appreciate them.");
    }
    if(inv_total(inv, yommiseeds) > 0 | inv_total(bank, yommiseeds_germ) > 0) {
        inv_del(bank, yommiseeds, ^max_32bit_int);
        inv_del(inv, yommiseeds, ^max_32bit_int);
        ~chatnpc("<p,neutral>I'll take those Yommi tree seeds to Ungadulu, I'm sure he'll appreciate them.");
    }
    @gujuo_leave;
} else if(%legends_progress = ^legends_got_gilded_totem) { // no dialogue for stages between this and quest completion, confirm this on rs3
    if(~obj_gettotal(thtotempolegift) = 0) {
        ~chatnpc("<p,neutral>Good day Bwana, it's always good to see you.");
        @multi4("Do you have any news?", gujuo_news, "Where are all your people?", gujuo_people, "I've lost the tribal gift you gave me.", gujuo_lost_gift, "Ok thanks for your help.", gujuo_thanks);
    } else {
        ~chatnpc("<p,neutral>Good day Bwana. The Kharazi Jungle is especially beautiful today isn't it? My village people pass on their thanks to you.");
        @multi3("Do you have any news?", gujuo_news, "Where are all your people?", gujuo_people, "Ok thanks for your help.", gujuo_thanks);
    }
} else if (%legends_progress >= ^legends_radimus_training_1) {
    ~chatnpc("<p,neutral>Good day Bwana. The jungle is especially beautiful today isn't it? My village people pass on their thanks to you.");
    @multi3("Do you have any news?", gujuo_news, "Where are all your people?", gujuo_people, "Ok thanks for your help.", gujuo_thanks);
}

[label,gujuo_lost_gift]
~chatplayer("<p,neutral>I've lost the tribal gift you gave me.");
~chatnpc("<p,neutral>Well, that wasn't very nice of you. It took us a long time to make that Totem pole. Luckily, I made another one at the same time.");
if(~obj_gettotal(thtotempolegift) = 0) {
    inv_add(inv, thtotempolegift, 1);
    ~objbox(thtotempolegift, "Gujuo hands over another totem pole.", 250, 0, ^objbox_height);
}

[label,gujuo_news]
~chatplayer("<p,neutral>Do you have any news?");
~chatnpc("<p,neutral>Just that everything is fine in the jungle with us. And that we are grateful to you for your help.");
@multi3("Do you have any news?", gujuo_news, "Where are all your people?", gujuo_people, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_people]
~chatplayer("<p,neutral>Where are all your people?");
~chatnpc("<p,neutral>My people are all happy living in the jungle. They are still afraid of strangers and will not approach but they are around, none the less.");
~chatnpc("<p,neutral>Your story has been woven into the fabric of our society. And we all sing your many praises Bwana.");
@multi3("Do you have any news?", gujuo_news, "Where are all your people?", gujuo_people, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_madetotem]
~chatplayer("<p,neutral>I made the Totem pole, now what do I do with it?");
~chatnpc("<p,neutral>Well Bwana, it's a very interesting piece of work, not bad at all. Now we need to replace an existing corrupted totem pole with your newly constructed one. You should find those totems within the Kharazi Jungle.");
@multi4("I made the Totem pole, now what do I do with it?", gujuo_madetotem, "I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_how_goes_ungadulu]
~chatnpc("<p,neutral>How goes your quest to release Ungadulu Bwana?");
switch_int(%legends_progress) {
    case ^legends_accepted_rescue_ungadulu :
        @multi2("I can't find the caves...", gujuo_cant_find,
                "Ok thanks for your help.", gujuo_thanks);
    case ^legends_found_entrance :
        if (testbit(%legends_bits, ^legends_entered_cavern) = ^true) {
            @multi2("Ungadulu is trapped in some magical flames!", gujuo_ungadulu_trapped,
                    "I'm not sure what to do.", gujuo_idk);
        }
        else {
            @multi2("I've found the caves, but I don't know what to do.", gujuo_found_but_idk,
                    "Ok thanks for your help.", gujuo_thanks);
        }
    case ^legends_spoke_ungadulu, ^legends_asked_gujuo_holy_water, ^legends_filled_bowl, ^legends_summoned_nezikchened_fire :
        if (testbit(%legends_bits, ^legends_ungadulu_called_vacu) = ^true) {
            @multi5("Ungadulu looks a little strange.", gujuo_ungadulu_strange,
                    "I'm not sure what to do.", gujuo_idk,
                    "I need some pure water to douse some magic flames.", gujuo_pure_water,
                    "Ungadulu called me a 'Vacu'", gujuo_vacu,
                    "Ok thanks for your help.", gujuo_thanks);
        }
        else if (testbit(%legends_bits, ^legends_asked_ungadulu_where) = ^true) {
            @multi5("Ungadulu looks a little strange.", gujuo_ungadulu_strange,
                    "Sorry for bothering you.", gujuo_sorry_bothering,
                    "I'm not sure what to do.", gujuo_idk,
                    "I need some pure water to douse some magic flames.", gujuo_pure_water,
                    "Ok thanks for your help.", gujuo_thanks);
        }
        else {
            @multi5("Ungadulu looks a little strange.", gujuo_ungadulu_strange,
                    "Sorry for bothering you.", gujuo_sorry_bothering,
                    "I'm not sure what to do.", gujuo_idk,
                    "Ungadulu mumbled something about 'pure' water?", gujuo_ungadulu_mumbled,
                    "Ok thanks for your help.", gujuo_thanks);
        }
}

[label,gujuo_foundsource]
~chatplayer("<p,neutral>I found the source of the spring and I got the water.");
~chatnpc("<p,neutral>Great Bwana, you are truly a brave warrior. Now you can try to grow the Yommi tree in earnest and make the totem pole.");
@multi4("I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "How do I make the totem pole?", gujuo_totem, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_demon]
~chatplayer("<p,neutral>I killed the demon again.");
~chatnpc("<p,neutral>You are indeed very brave Bwana, we have noticed a difference in the Kharazi Jungle, the trees seem to sing again. And we have you to thank for it.");
@multi4("I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "How do I make the totem pole?", gujuo_totem, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_totem]
~chatplayer("<p,neutral>How do I make the totem pole?");
~chatnpc("<p,neutral>You will need to grow the Yommi tree to full height. And then, before it rots, you must chop it down. Once you have felled the tree, you need to trim the branches.");
~chatnpc("<p,neutral>And finally, you need to craft the totem pole out of the trunk. You'll need a very sharp, very tough axe to do all this. But once you have completed the totem pole,");
~chatnpc("<p,neutral>You will need to use it to replace a totem pole that already exists, as they're all placed on sacred areas to my people.");
@multi4("I found the source of the spring and I got the water.", gujuo_foundsource, "I killed the demon again.", gujuo_demon, "How do I make the totem pole?", gujuo_totem, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_foundcaves]
~chatplayer("<p,neutral>I have found a way into the caves!");
~chatnpc("<p,neutral>That's great Bwana, good luck with your quest and take care!");
@multi4("Do you know anything more about the caves?", gujuo_knowcaves, "Who is Viyeldi?", gujuo_whoviyeldi, "Where is the source of the spring of pure water?", gujuo_source, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_whoviyeldi]
~chatplayer("<p,neutral>Who is Viyeldi?");
~mesbox("Gujuo scratches his head for a moment.");
~chatnpc("<p,neutral>Well, I have heard that name before, perhaps from the eldars."); // intentional typo
~chatnpc("<p,neutral>Ah, yes, I think that is the name of the wizard who first went in search of the source. Be wary of him Bwana, he may try to trick you.");
@multi4("I have found a way into the caves!", gujuo_foundcaves, "Do you know anything more about the caves?", gujuo_knowcaves, "Where is the source of the spring of pure water?", gujuo_source, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_knowcaves]
~chatplayer("<p,neutral>Do you know anything more about the caves?");
~chatnpc("<p,neutral>I am sorry to say that I don't Bwana. You will need to explore that area, but use your wits, and you may be lucky.");
@multi4("I have found a way into the caves!", gujuo_foundcaves, "Who is Viyeldi?", gujuo_whoviyeldi, "Where is the source of the spring of pure water?", gujuo_source, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_morewater]
~chatplayer("<p,neutral>Where can I get more water for the Yommi tree?");
~chatnpc("<p,neutral>If the pool of sacred water has dried up, there may be a way to get to the source of the spring. But it is said to be very, very dangerous.");
@multi3("Where is the source of the spring of pure water?", gujuo_source, "If I went in search of the source, could you help me?", gujuo_searchsource, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_pool_dried]
~chatplayer("<p,neutral>The water pool has dried up and I need more pure water.");
~chatnpc("<p,neutral>This is indeed a bad omen Bwana, that pool is sacred to us. I have seen it and it is full of filth, it is not natural. I suspect that some evil is at work here.");
@multi2("Does the Yommi tree have to have pure water?", gujuo_have_pure, "Where is the source of the spring of pure water?", gujuo_source);

[label,gujuo_tree_died]
~chatplayer("<p,neutral>The Yommi tree died.");
~chatnpc("<p,neutral>Well, it requires pure sacred water for it to grow. It is a very special tree...");
@multi2("The sacred water pool has dried up and I need more water.", gujuo_pool_dried, "Does the Yommi tree have to have pure water?", gujuo_have_pure);

[label,gujuo_have_pure]
~chatplayer("<p,neutral>Does the Yommi tree have to have pure water?");
~chatnpc("<p,neutral>Yes, it is a magical tree and can only survive on the water from the sacred pool. This is indeed a tragedy.");
@multi2("Where is the source of the spring of pure water?", gujuo_source, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_source]
~chatplayer("<p,neutral>Where is the source of the spring of pure water?");
~mesbox("Gujuo looks very uncomfortable...");
~chatnpc("<p,neutral> I am not sure but I have heard that deeper in the catacombs where you found Ungadulu, deep underground there is a terrible place guarded by the spirits of the undead.");
~chatnpc("<p,neutral>Since they died trying to find the source of the stream, they are cursed to guard it for all eternity. The first to seek the source was said to be a high level sorcerer");
~chatnpc("<p,neutral>He created a powerful spell in the caves. Now, all those who venture near are overcome by a supernatural fear. With all my heart Bwana, I would never go near such a place.");
@multi3("Ok, I won't go...", gujuo_wontgo, "If I went, could you help me?", gujuo_helpme, "I searched the catacombs thoroughly but found nothing else.", gujuo_searched);

[label,gujuo_searched]
~chatplayer("<p,neutral>I searched the catacombs thoroughly but found nothing else.");
~chatnpc("<p,neutral>Perhaps the location has been hidden or buried under rubble? These stories were told to me as a child by the village elders. They were probably meant to frighten us away from the caves.");
~chatnpc("<p,neutral>It could all just be a myth! Perhaps there is another way to get to the source of the stream? But I am not sure where it is.");
@multi3("Ok, I won't go...", gujuo_wontgo, "Where is the source of the spring of pure water?", gujuo_source, "If I went, could you help me?", gujuo_helpme);

[label,gujuo_wontgo]
~chatplayer("<p,neutral>Ok, I won't go...");
~chatnpc("<p,neutral>I understand Bwana, it would be a waste of a perfectly good life. We will try to defeat the evil spirits in other ways. But I am not sure how we will do that.");
@multi2("If I went, could you help me?", gujuo_helpme, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_helpme]
~chatplayer("<p,neutral>If I went, could you help me?");
~chatnpc("<p,neutral>Well, if you are sure you want to go, I will assist as much as I can. You will need the bravery of the jungle lion, if you are to go into that forbidden place. I can give you the recipe for a potion to help with that.");
~chatnpc("<p,neutral>You will need to find two herbs, Snake weed and Ardrigal. Add them both to a vial of water, and you will walk with the bravery of the Kharazi lion.");
if(%legends_progress = ^legends_water_pool_dried_up) %legends_progress = ^legends_talk_gujuo_pool;
@multi5("Where can I find Snake weed?", gujuo_snakeweed, 
        "Where is the source of the spring of pure water?", gujuo_source, 
        "Where can I find ardrigal?", gujuo_ardrigal, // no caps
        "Will I need this potion? I feel brave enough as I am.", gujuo_potion, 
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_snakeweed]
~chatplayer("<p,neutral>Where can I find Snake weed?");
~chatnpc("<p,neutral>Snake weed is usually found by swampy marshy areas. It is not very common and it may be quite difficult to find.");
~chatnpc("<p,neutral>There is some marsh to the south of Tai Bwo Wannai village. The herb grows near jungle vines, so check all around very carefully.");
@multi5("Where can I find Ardrigal?", gujuo_ardrigal,
        "Where is the source of the spring of pure water?", gujuo_source,
        "If I went, could you help me?", gujuo_helpme,
        "Will I need this potion? I feel brave enough as I am.", gujuo_potion, 
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_ardrigal]
~chatplayer("<p,neutral>Where can I find Ardrigal?");
~chatnpc("<p,neutral>Ardrigal is often found growing near to large groups of palms. Such a collection exists in the north.");
~chatnpc("<p,neutral>If you head east out of Tai Bwo Wannai village you should come across them. The herb grows in the shade of the palm so check carefully.");
@multi4("Where is the source of the spring of pure water?", gujuo_source,
        "Where can I find Snake weed?", gujuo_snakeweed, 
        "If I went in search of the source, could you help me?", gujuo_searchsource, 
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_potion]
~chatplayer("<p,neutral>Will I need this potion? I feel brave enough as I am.");
~chatnpc("<p,neutral>I would urge you to take it, Bwana, I have heard that the caves are protected by a supernatural fear that renders even the bravest man to a trembling wreck.");
~chatnpc("<p,neutral>You will need all your wits about you when dealing with the terrors that exist down there.");
@multi4("Where can I find Snake weed?", gujuo_snakeweed, 
        "Where is the source of the spring of pure water?", gujuo_source,
        "Where can I find Ardrigal?", gujuo_ardrigal, 
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_searchsource]
~chatplayer("<p,neutral>If I went in search of the source, could you help me?");
~chatnpc("<p,neutral>Well, if you are sure you want to go, I will assist as much as I can. You will need the bravery of the jungle lion, if you are to go into that forbidden place.");
~chatnpc("<p,neutral>I can give you the recipe for a potion to help with that. You will need to find two herbs, Snake weed and Ardrigal. Add them both to a vial of water, and you will walk with the bravery of the lion.");
@multi5("Where is the source of the spring of pure water?", gujuo_source, 
        "Where can I find Snake weed?", gujuo_snakeweed, 
        "Where can I find Ardrigal?", gujuo_ardrigal, 
        "Will I need this potion? I feel brave enough as I am.", gujuo_potion, 
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_germinated]
~chatplayer("<p,neutral>I have germinated the Yommi tree seeds.");
~chatnpc("<p,neutral>Well done Bwana. With the blessings of the gods we will soon have our Totem Pole. Bwana, you now need to plant the seed in the fertile earth.");
@multi2("Where is the fertile soil?", gujuo_wheresoil, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_wheresoil]
~chatplayer("<p,neutral>Where is the fertile soil?");
~chatnpc("<p,neutral>You should be able to find many places where the ground is fertile in the Kharazi Jungle. Planting the Yommi tree seeds in fertile soil gives it a good chance to grow.");
~chatnpc("<p,neutral>My people are trying to grow the Yommi tree as well. But so far we have not met with any success. If you find a rotten tree or what looks like a rotten totem pole,");
~chatnpc("<p,neutral>You'll need to remove it yourself to get to the fertile soil. It will take a very sharp, robust axe to do it.");
@multi2("I have germinated the Yommi tree seeds.", gujuo_germinated, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_have_seeds]
~chatplayer("<p,neutral>I have the Yommi tree seeds.");
if(inv_total(inv, yommiseeds) <= 0) {
    ~chatnpc("<p,neutral>Hmmm, well I don't see them. You'd better go and ask Ungadulu for some more.");
    return;
}
~chatnpc("<p,neutral>That's great, Bwana. Now you just need to germinate the seeds and then plant them in some fertile soil. I'm sure that Ungadulu has explained all this to you already.");
@multi3("Ungadulu is free, he was possesed by a demon and I killed it.", gujuo_ungadulu_free, "What do I do now?", gujuo_what_now, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_ungadulu_free]
~chatplayer("<p,neutral>Ungadulu is free! He was possessed by a demon and I killed it.");
~chatnpc("<p,neutral>You are indeed brave Bwana, a truly fearsome warrior to take on such an enemy! Well Done!");
@multi3("I have the Yommi tree seeds.", gujuo_have_seeds, "What do I do now?", gujuo_what_now, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_what_now]
~chatplayer("<p,neutral>What do I do now?");
~chatnpc("<p,neutral>If you have the Yommi tree seeds, you will need to germinate them.");
~chatnpc("<p,neutral>Place the seeds into pure water, and they will begin to sprout tiny shoots. You can then plant them in fertile soil.");
@multi3("Ungadulu is free, he was possesed by a demon and I killed it.", gujuo_ungadulu_free, "What do I do now?", gujuo_what_now, "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_bless_bowl]
if (stat(prayer) < 42) {
    ~chatnpc("<p,neutral>Bwana, I am very sorry, but you are too inexperienced to bless this bowl.");
    // dialogue ends here
    mes("You need a Prayer level of at least 42 to complete this task.");
    return;
}
if (inv_total(inv, goldbowlbless_empty) > 0 | inv_total(inv, goldbowlbless_water) > 0 | inv_total(inv, goldbowlbless_pure) > 0) {
    // says hello again
    ~chatnpc("<p,sad>Hello Bwana, I see that you already have a blessed golden bowl. Have you fallen under the spell of the metal of the sun? Alas, I cannot allow you to bless the vessel if you're possessed by greed.");
    return;
}
~chatnpc("<p,neutral>Very well Bwana...");
if_close;
mes("Gujuo places the bowl on the floor in front of you, and leads you into a deep");
mes("meditation...");
p_delay(2);
npc_say("Ohhhhhmmmmmm");
p_delay(2);
say("Oooooommmmmmmmmm");
p_delay(2);
npc_say("Ohhhhhmmmmmm");
p_delay(2);
say("Oooooohhhhmmmmmmmmmm");
p_delay(2);
npc_say("Ohhhhhmmmmmm");
p_delay(2);
// guess but it seems pretty common
if(stat_random(prayer, 80, 250) = true) {
    stat_sub(prayer, 5, 0);
    ~mesbox("You were not able to go into a deep enough trance. You lose some prayer...");
    ~chatnpc("<p,neutral>Would you like to try again?");
    // this choice has slightly different wording ("like to" vs "like you to") - intentional
    def_int $choice = ~p_choice2("Yes, I'd like to bless my gold bowl.", 1, "No thanks, I'll wait.", 2);
    if ($choice = 1) {
        ~chatplayer("<p,happy>Yes, I'd like to bless my gold bowl.");
        @gujuo_bless_bowl;
    }
    else {
        ~chatplayer("<p,neutral>No thanks, I'll wait.");
        ~chatnpc("<p,neutral>Very well, let me know when you want to try.");
        @gujuo_start;
    }
}
else {
    if(inv_total(inv, goldbowl_empty) > 0) {
        ~objbox(goldbowlbless_empty, "You're surrounded by a totally peaceful aura as you bring the blessings of your god down on the bowl.", 250, 0, divide(^objbox_height, 2));
        inv_del(inv, goldbowl_empty, 1);
        inv_add(inv, goldbowlbless_empty, 1);
        ~objbox(goldbowlbless_empty, "The bowl is blessed!", 250, 0, divide(^objbox_height, 2));
    } else if(inv_total(inv, goldbowl_water) > 0) {
        ~objbox(goldbowlbless_water, "You're surrounded by a totally peaceful aura as you bring the blessings of your god down on the bowl.", 250, 0, divide(^objbox_height, 2));
        inv_del(inv, goldbowl_water, 1);
        inv_add(inv, goldbowlbless_water, 1);
        ~objbox(goldbowlbless_water, "The bowl is blessed!", 250, 0, divide(^objbox_height, 2));
    } else if(inv_total(inv, goldbowl_pure) > 0) {
        ~objbox(goldbowlbless_pure, "You're surrounded by a totally peaceful aura as you bring the blessings of your god down on the bowl.", 250, 0, divide(^objbox_height, 2));
        inv_del(inv, goldbowl_pure, 1);
        inv_add(inv, goldbowlbless_pure, 1);
        ~objbox(goldbowlbless_pure, "The bowl is blessed!", 250, 0, divide(^objbox_height, 2));
    }
    @gujuo_start;
}

[label,gujuo_ungadulu_trapped]
~chatplayer("<p,neutral>Ungadulu is trapped in some magical flames!");
~chatnpc("<p,neutral>Well, maybe you can get his attention somehow? Did you examine the nature of the magical flames? From where did they come do you think?");
@multi2("Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk);

[label,gujuo_cant_find]
~chatplayer("<p,neutral>I can't find the caves...");
~chatnpc("<p,neutral>Well, they were still there the last time I checked... Go as far to the west in the Kharazi Jungle as you can, until you reach the sea, then walk eastwards with the barrier rocks to the north.");
~chatnpc("<p,neutral>Walk past the covered pass and you'll find some rocks on slightly elevated ground. You'll find the caves there.");
@gujuo_leave;

[label,gujuo_found_but_idk]
~chatplayer("<p,neutral>I've found the caves, but I don't know what to do.");
~chatnpc("<p,neutral>Search the caves and try to talk to Ungadulu, there may be some clues to be had by searching all the items in the cave...");
@gujuo_leave;

[label,gujuo_ungadulu_mumbled]
~chatplayer("<p,neutral>Ungadulu mumbled something about 'pure' water?");
~chatnpc("<p,neutral>Hmm, that sounds strange. But it looks like the Shaman has lost his mind. I am sorry Bwana, but it looks like I've sent you on a fools errand. My apologies Bwana.");
@multi4("Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "Can we still get the people together?", gujuo_still_people,
        "Strange, why?", gujuo_strange_why);

[label,gujuo_strange_why]
~chatplayer("<p,neutral>Strange, why?");
~chatnpc("<p,neutral>Well, there is a pool of water that is sacred to us. It contains pure water, but I am not sure why he needs this. Perhaps you should go back and talk to him again and get more information.");
@multi3("Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "Ok, thanks... Goodbye.", gujuo_water_thanks);

[label,gujuo_ungadulu_strange]
~chatplayer("<p,neutral>Ungadulu looks a little strange.");
~chatnpc("<p,neutral>Be wary Bwana. There are many unknown spirits that reside in these dark areas. You may be tricked by an unknown force...");
if (testbit(%legends_bits, ^legends_asked_ungadulu_where) = ^true) {
    @multi5("Ok, thanks... Goodbye.", gujuo_water_thanks,
            "What kind of unknown force?", gujuo_force,
            "Sorry for bothering you.", gujuo_sorry_bothering,
            "I'm not sure what to do.", gujuo_idk,
            "I need some pure water to douse some magic flames.", gujuo_pure_water);
}
else if (testbit(%legends_bits, ^legends_ungadulu_called_vacu) = ^true) {
    @multi5("Ok, thanks... Goodbye.", gujuo_water_thanks,
            "What kind of unknown force?", gujuo_force,
            "Sorry for bothering you.", gujuo_sorry_bothering,
            "I need some pure water to douse some magic flames.", gujuo_pure_water,
            "Ungadulu called me 'Vacu', what does that mean?", gujuo_vacu);
}
else {
    @multi4("What kind of unknown force?", gujuo_force,
            "Sorry for bothering you.", gujuo_sorry_bothering,
            "I'm not sure what to do.", gujuo_idk,
            "Ungadulu mumbled something about 'pure' water?", gujuo_ungadulu_mumbled);
}

[label,gujuo_force]
~chatplayer("<p,neutral>What kind of unknown force?");
~chatnpc("<p,neutral>Strange spirits that our forefathers summoned for visions. They haunt the underworld and caves that exist in this area. Take not anything as it might first appear.");
@multi4("Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "How did they summon the spirits?", gujuo_spirits,
        "Ok, thanks... Goodbye.", gujuo_water_thanks);

[label,gujuo_spirits]
~chatplayer("<p,neutral>How did they summon the spirits?");
~chatnpc("<p,neutral>I am unlearned in such matters. But I am told of sacred patterns that are scored on the ground to bind the spirit and confine it... But that is all I know.");
@multi4("Ungadulu looks a little strange.", gujuo_ungadulu_strange,
        "Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "Ok, thanks... Goodbye.", gujuo_water_thanks);

[label,gujuo_idk]
~chatplayer("<p,neutral>I'm not sure what to do.");
if (%legends_progress >= ^legends_spoke_ungadulu) {
    ~chatnpc("<p,neutral>I am at a loss as well Bwana. At this time Ungadulu is the only one who might know of a solution for this puzzle and at this time, he is part of that same puzzle.");
}
else {
    ~chatnpc("<p,neutral>Perhaps you should investigate the caves further? Have you been able to speak to Ungadulu yet? He may have some information to give you that could help?");
}
@multi3("Ok, thanks... Goodbye.", gujuo_water_thanks,
        "Sorry for bothering you.", gujuo_sorry_bothering,
        "Ungadulu looks a little strange.", gujuo_ungadulu_strange);

[label,gujuo_pure_water]
~chatplayer("<p,neutral>I need some pure water to douse some magic flames.");
if (testbit(%legends_bits, ^legends_asked_ungadulu_who) = ^true) {
    ~chatnpc("<p,neutral>This sounds very strange Bwana... but maybe I can help. There is a pool of water that is sacred to us. It is located in the middle of the Kharazi Jungle.");
    ~chatnpc("<p,neutral>The water contains special properties but it can only be contained in a blessed vessel made from metal of the sun. The water is difficult to get to, but I am sure you will manage to claim some.");
    if (%legends_progress < ^legends_asked_gujuo_holy_water) {
        %legends_progress = ^legends_asked_gujuo_holy_water;
    }
    @multi5("Sorry for bothering you.", gujuo_sorry_bothering,
            "I'm not sure what to do.", gujuo_idk,
            "Ok, thanks... Goodbye.", gujuo_water_thanks,
            "Metal of the sun, what is that?", gujuo_metal,
            "What kind of a vessel?", gujuo_vessel);
}
else {
    ~chatnpc("<p,neutral>Well there is a pool of water that is very sacred to us. But I am unsure why he would need that to douse flames. Perhaps the poor Shaman has lost his mind in the darkness of the caves?");
    @multi5("Ok, thanks... Goodbye.", gujuo_water_thanks,
            "Sorry for bothering you.", gujuo_sorry_bothering,
            "I'm not sure what to do.", gujuo_idk,
            "Can we still get the people together?", gujuo_still_people,
            "Where is the pool of sacred water?", gujuo_pool_where);
}

[label,gujuo_water_thanks]
~chatplayer("<p,neutral>Ok, thanks... Goodbye.");
~chatnpc("<p,neutral>Farewell Bwana, good luck.");
npc_del;
mes("Gujuo disappears into the Kharazi Jungle as swiftly as he appeared...");

[label,gujuo_vacu]
~chatplayer("<p,neutral>Ungadulu called me 'Vacu', what does that mean?");
~chatnpc("<p,neutral>It seems that Ungadulu has started to lose his senses. In our native and ancient history, the 'Vacu' were the servants of the evil spirits from the underworld.");
~chatnpc("<p,neutral>The 'Vacu' were priests who summoned the spirits of our ancestors. But the priests were enslaved by the evil spirits of the underworld. It is but a myth, perhaps a story told to scare badly behaved children.");
@multi4("Ok, thanks... Goodbye.", gujuo_water_thanks,
        "Ungadulu looks a little strange.", gujuo_ungadulu_strange,
        "Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk);

[label,gujuo_still_people]
~chatplayer("<p,neutral>Can we still get the people together?");
~chatnpc("<p,neutral>I am sorry Bwana, my people are scattered throughout the Kharazi Jungle. Without the totem pole to allay their fears, I believe it is impossible to collect them together as a group.");
@multi4("Ungadulu looks a little strange.", gujuo_ungadulu_strange,
        "Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "Ok, thanks... Goodbye.", gujuo_water_thanks);
    
[label,gujuo_pool_where]
~chatplayer("<p,neutral>Where is the pool of sacred water?");
~chatnpc("<p,neutral>The pool of sacred water is very precious to us, it is located in the middle of the Kharazi Jungle.");
~chatnpc("<p,neutral>The water contains special properties which can only be retained if the water is contained in a blessed vessel made from metal of the sun.");
if (%legends_progress < ^legends_asked_gujuo_holy_water) {
    %legends_progress = ^legends_asked_gujuo_holy_water;
}
@multi5("Sorry for bothering you.", gujuo_sorry_bothering,
        "I'm not sure what to do.", gujuo_idk,
        "Ok, thanks... Goodbye.", gujuo_water_thanks,
        "Metal of the sun, what is that?", gujuo_metal,
        "What kind of a vessel?", gujuo_vessel);

[label,gujuo_sorry_bothering]
~chatplayer("<p,neutral>Sorry for bothering you.");
~chatnpc("<p,neutral>That's fine Bwana, not to worry. I hope your quest is going well in any case.");
@multi3("Ok, thanks... Goodbye.", gujuo_water_thanks,
        "I'm not sure what to do.", gujuo_idk,
        "Ungadulu looks a little strange.", gujuo_ungadulu_strange);

[label,gujuo_vessel]
~chatplayer("<p,neutral>What kind of a vessel?");
~chatnpc("<p,neutral>A vessel made of sun metal, but it can be of any shape. However, it must be blessed.");
if (inv_total(inv, goldbowlpic) = 0) {
    ~objbox(goldbowlpic, "Gujuo takes out a small scroll and some charcoal and draws a rough sketch. When he has finished, he gives the sketch to you.", 250, 0, 0);
    ~chatnpc("<p,neutral>Here, have this as an example... I pray that it will help you.");
    inv_add(inv, goldbowlpic, 1);
}
else {
    ~chatnpc("<p,neutral>Similar to the picture I have already given you.");
}
@multi2("How do I bless the vessel?", gujuo_bless,
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_metal]
~chatplayer("<p,neutral>Metal of the sun, what is that?");
~chatnpc("<p,neutral>It is a bright and precious metal that is much sought after, though uncommonly found. It is the same glorious colour as the sun and it never loses its wondrous lustre. A blessed vessel made of this metal");
~chatnpc("<p,neutral>protects the purity of the water.");
@multi3("Where can I find this metal?", gujuo_metal_where,
        "What kind of a vessel?", gujuo_vessel,
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_metal_where]
~chatplayer("<p,neutral>Where can I find this metal?");
~chatnpc("<p,neutral>It is found in some rocks and must be extracted. It has a magical ability over some people, it possesses them. They fall within its power and seek to gain more and more of this precious metal for themselves.");
~chatnpc("<p,neutral>A blessed vessel made of this metal protects the purity of the water.");
@multi3("Metal of the sun, what is that?", gujuo_metal,
        "What kind of a vessel?", gujuo_vessel,
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_bless]
~chatplayer("<p,neutral>How do I bless the vessel?");
~chatnpc("<p,neutral>When you have made the vessel, bring it to me. I will help but you need to ensure that you are devout and have faith. Your ability in prayer will be thoroughly tested.");
@multi2("What kind of a vessel?", gujuo_vessel,
        "Ok thanks for your help.", gujuo_thanks);

[label,gujuo_attract]
~chatplayer("<p,neutral>I was hoping to attract the attention of a native.");
~chatnpc("<p,neutral>Well, it had the desired effect... I am Gujuo, proud member of the Kharazi tribe. What did you want to talk about Bwana?");
@multi2("I want to develop friendly relations with your people.", gujuo_friendly,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_sorry]
~chatplayer("<p,neutral>Sorry, it was a mistake?");
~chatnpc("<p,neutral>Very good Bwana... however, it begs the question... What are you doing in the Kharazi Jungle?");
@multi2("I want to develop friendly relations with your people.", gujuo_friendly,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_friendly]
~chatplayer("<p,neutral>I want to develop friendly relations with your people.");
~mesbox("Gujuo smiles and shakes your hand warmly.");
~chatnpc("<p,neutral>Very good Bwana... this is indeed a very pleasant gesture. However, my people are very distributed throughout the Kharazi Jungle.");
@multi2("Can you get your people together?", gujuo_together,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_lost]
~chatplayer("<p,neutral>I'm lost, can you show me the way out?");
~chatnpc("<p,neutral>Yes Bwana. I can take you to the edge of the Kharazi Jungle. Would you like me to take you?");
@multi2("Yes please...", gujuo_accept_guide,
        "No thanks...", gujuo_decline_guide);

[label,gujuo_accept_guide]
// Capitalisation correct
~chatplayer("<p,neutral>Yes Please...");
~chatnpc("<p,neutral>Follow me...");
~mesbox("Gujuo takes you out of the jungle...");
p_teleport(^legends_gujuo_guide_coord);
npc_del;

[label,gujuo_decline_guide]
~chatplayer("<p,neutral>No thanks...");
~chatnpc("<p,neutral>As you wish... Again, Bwana, What is it that brings you to the Kharazi Jungle?");
@multi2("I want to develop friendly relations with your people.", gujuo_friendly,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_together]
~chatplayer("<p,neutral>Can you get your people together?");
~chatnpc("<p,neutral>All of my people normally congregate around a totem pole. But ours has been polluted by an evil spirit. It has been transformed, and now our people are afraid to approach it.");
~chatnpc("<p,neutral>We tried to drive the evil spirit out of the totem pole, but it does not seem to work.");
@multi2("What can we do instead then?", gujuo_what,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_what]
~chatplayer("<p,neutral>What can we do instead then?");
~chatnpc("<p,neutral>We could try to make a new totem pole. However, we need to make it from the trunk of the sacred Yommi tree.");
@multi2("How do we make the totem pole?", gujuo_totem_how,
        "I'm lost, can you show me the way out?", gujuo_lost);

[label,gujuo_totem_how]
~chatplayer("<p,neutral>How do we make a totem pole?");
~chatnpc("<p,neutral>First we need to plant a sacred Yommi tree. It is a magical tree of great power, however, our Shaman 'Ungadulu' is the only person with the seeds for this tree.");
// osrs mesanim
~chatnpc("<p,sad>And I fear that it is impossible to get some seeds. He is being held against his will in some caves in the north western part of the Kharazi Jungle.");
@multi2("I will release Ungadulu...", gujuo_release_ungadulu,
        "Oh well, sorry to hear about that.", gujuo_sorry_ungadulu);

[label,gujuo_release_ungadulu]
~chatplayer("<p,neutral>I will release Ungadulu...");
~chatnpc("<p,neutral>You make me very happy Bwana! In the north western part of the Kharazi Jungle area, near some great cliffs, you will find three rocks that form a roughly triangular shape.");
~chatnpc("<p,neutral>They are flanked by the palm which also forms the divine geometry. The stones are the entrance, search them well. That's where you'll find Ungadulu. If you can free him he will entrust you with some sacred");
~chatnpc("<p,neutral>Yommi tree seeds.");
%legends_progress = ^legends_accepted_rescue_ungadulu;
@gujuo_leave;

[label,gujuo_sorry_ungadulu]
~chatplayer("<p,neutral>Oh well, sorry to hear about that.");
~mesbox("Gujuo's expression of sadness deepens...");
~chatnpc("<p,sad>Yes Bwana, perhaps we will become friends sometime in the future... But not today... Ungadulu has problably lost his mind anyway... It is most likely a lost cause...");
@multi3("I will release Ungadulu...", gujuo_release_ungadulu,
        "Ok thanks for your help.", gujuo_thanks,
        "What's in it for me if I release Ungadulu?", gujuo_whatsforme);

[label,gujuo_thanks]
~chatplayer("<p,neutral>Ok thanks for your help.");
// No caps on bwana here
~chatnpc("<p,neutral>You are more than welcome bwana...");
@gujuo_leave;

[label,gujuo_leave]
switch_int(random(6)) {
    case 0 : 
        ~chatnpc("<p,neutral>I must go and hunt now Bwana...");
    case 1 : 
        ~chatnpc("<p,neutral>I am tired Bwana, I must go and rest...");
    case 2 : 
        ~chatnpc("<p,neutral>The mosquitos bite me bwana! I must go for a swim...");
    case 3 : 
        ~chatnpc("<p,neutral>I must visit my people now...");
    case 4 : 
        ~chatnpc("<p,neutral>I have work to do Bwana, I may see you again...");
    case 5 : 
        ~chatnpc("<p,neutral>I have to collect herbs now Bwana...");
}
npc_del;
mes("Gujuo disappears into the Kharazi Jungle as swiftly as he appeared...");

[label,gujuo_whatsforme]
~chatplayer("<p,neutral>What's in it for me if I release Ungadulu?");
~chatnpc("<p,neutral>We would be very grateful and would be pleased to develop friendly relations with you. Do this thing for us all, my people would be very happy.");
@multi2("I will release Ungadulu...", gujuo_release_ungadulu,
        "I'm lost, can you show me the way out?", gujuo_lost);