[opnpc1,ranalph_devere]
if (~player_in_combat_check = true) {
    return;
}
if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(lump_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_lump) = false) {
    npc_say("Your destiny awaits, adventurer...");
} else {
    npc_say("My duty is done. I hope the source brings you all that you hoped for...");
}

[opnpc2,ranalph_devere]
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    %aggressive_npc = npc_uid;
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(lump_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_lump) = false) {
        npc_say("Your destiny awaits, adventurer...");
    } else {
        npc_say("My duty is done. I hope the source brings you all that you hoped for...");
    }
}
@player_combat_start;

[ai_opplayer2,ranalph_devere]
if(%legends_progress >= ^legends_heart_in_recess | testbit(%legends_map_runes, ^legends_smelting_lump) = true) {
    npc_setmode(null);
    return;
}
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(lump_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_lump) = false) {
        mes("Ranalph Devere: Upon my honour, I will defend the source to the bitter end.");
        mes("Ranalph Devere: May your aim be true, challenger...");
        npc_say("May your aim be true, challenger...");
    } else {
        npc_say("My duty is done. I hope the source brings you all that you hoped for...");
    }
}
~npc_default_attack;

[ai_queue3,ranalph_devere]
if(finduid(%npc_aggressive_player) = true) {
    if_close;
    if(p_finduid(uid) = true) {
        // doesn't queue, the npc will sit at 0 hp if no prot access
        npc_del;
        if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(lump_of_crystal) = 0 & ~obj_gettotal(heart_crystal) = 0 
        & ~obj_gettotal(glowing_heart_crystal) = 0 & testbit(%legends_map_runes, ^legends_smelting_lump) = false) {
            inv_add(inv, lump_of_crystal, 1); // no space check
            mes("Ranalph Devere: You have proved yourself of the honour.");
            ~chatnpc("<p,neutral>You have proved yourself of the honour.");
            ~objbox(lump_of_crystal, "A lump of crystal forms mid-air and falls to the floor. You place the crystal lump into your inventory.", 250, 0, ^objbox_height);
        } else {
            ~chatnpc("<p,neutral>You fought well, adventurer.");
        }
    }
}