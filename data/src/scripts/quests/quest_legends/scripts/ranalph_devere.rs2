[opnpc1,ranalph_devere]
if (~player_in_combat_check = false) {
    return;
}
if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(heartcrystal_sectionc) = 0 & ~obj_gettotal(heartcrystal) = 0 
        & ~obj_gettotal(heartcrystal_glow) = 0 & testbit(%legends_bits, ^legends_smelting_lump) = ^false) {
    npc_say("Your destiny awaits, adventurer...");
} else {
    npc_say("My duty is done. I hope the source brings you all that you hoped for...");
}

[opnpc2,ranalph_devere]
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    %aggressive_npc = npc_uid;
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(heartcrystal_sectionc) = 0 & ~obj_gettotal(heartcrystal) = 0 
        & ~obj_gettotal(heartcrystal_glow) = 0 & testbit(%legends_bits, ^legends_smelting_lump) = ^false) {
        npc_say("Your destiny awaits, adventurer...");
    } else {
        npc_say("My duty is done. I hope the source brings you all that you hoped for...");
    }
}
@player_combat_start;

[ai_opplayer2,ranalph_devere]
if(inzone(0_43_45_0_0, 0_46_45_63_63, coord) = false & (%npc_aggressive_player ! uid & (~obj_gettotal(heartcrystal_sectionc) > 0 | ~obj_gettotal(heartcrystal) > 0 | ~obj_gettotal(heartcrystal_glow) > 0 | %legends_progress >= ^legends_heart_in_recess | testbit(%legends_bits, ^legends_smelting_lump) = ^true))) {
    npc_setmode(null);
    return;
}
if(add(%lastcombat, 8) < map_clock & %aggressive_npc ! npc_uid) {
    if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(heartcrystal_sectionc) = 0 & ~obj_gettotal(heartcrystal) = 0 
        & ~obj_gettotal(heartcrystal_glow) = 0 & testbit(%legends_bits, ^legends_smelting_lump) = ^false) {
        mes("Ranalph Devere: Upon my honour, I will defend the source to the bitter end.");
        mes("Ranalph Devere: May your aim be true, challenger...");
        npc_say("May your aim be true, challenger...");
    } else {
        npc_say("My duty is done. I hope the source brings you all that you hoped for...");
    }
}
~npc_default_attack;

[ai_queue3,ranalph_devere]
if(finduid(%npc_aggressive_player) = true) {
    if_close;
    if(p_finduid(uid) = true) {
        if(inzone(0_43_45_0_0, 0_46_45_63_63, coord) = true) {
            %legends_bits = setbit(%legends_bits, ^legends_defeated_ranalph_final);
            mes("A nerve tingling scream echoes around you");
            mes("as you slay Ranalph Devere.");
            p_delay(1);
            npc_say("Arrghhhhh");
            mes("Ranalph Devere: Arrghhhhh");
            p_delay(1);
            npc_say("Forever must I live in this torment");
            mes("Ranalph Devere: Forever must I live in this torment");
            p_delay(1);
            npc_say("till this beast is slain.");
            mes("Ranalph Devere: till this beast is slain.");
            npc_del;
            p_delay(2);
            @summon_nezi_part3;
        }
        // doesn't queue, the npc will sit at 0 hp if no prot access
        npc_setmode(none);
        npc_walk(npc_coord);
        npc_del;
        if(%legends_progress < ^legends_heart_in_recess & ~obj_gettotal(heartcrystal_sectionc) = 0 & ~obj_gettotal(heartcrystal) = 0 
        & ~obj_gettotal(heartcrystal_glow) = 0 & testbit(%legends_bits, ^legends_smelting_lump) = ^false) {
            inv_add(inv, heartcrystal_sectionc, 1); // no space check
            mes("Ranalph Devere: You have proved yourself of the honour.");
            ~chatnpc("<p,neutral>You have proved yourself of the honour.");
            ~objbox(heartcrystal_sectionc, "A lump of crystal forms mid-air and falls to the floor. You place the crystal lump into your inventory.", 250, 0, ^objbox_height);
        } else {
            ~chatnpc("<p,neutral>You fought well, adventurer.");
        }
    }
}