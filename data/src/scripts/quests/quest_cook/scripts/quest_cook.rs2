[label,cooks_assistant_start]
def_int $option = ~p_choice4("What's wrong?", 1, "Well you could give me all your money!", 2, "You don't look very happy.", 3, "Nice hat!", 4);

if($option = 1) {
    @cooks_assistant_whats_wrong;
} else if($option = 2) {
    ~chatplayer("<p,neutral>Well you could give me all your money!");
    ~chatnpc("<p,sad>Haha, very funny!");
} else if($option = 3) {
    ~chatplayer("<p,neutral>You don't look very happy.");
    ~chatnpc("<p,sad>No, I'm not.");
    $option = ~p_choice2("What's wrong?", 1, "I'd take the rest of the day off if I were you.", 2);
    if($option = 1) {
        @cooks_assistant_whats_wrong;
    } else if($option = 2) {
        ~chatplayer("<p,neutral>I'd take the rest of the day off if I were you.");
        ~chatnpc("<p,sad>No, that's the worst thing I could do - I'd get in terrible trouble.");
        @cooks_assistant_whats_wrong;
    }
} else if ($option = 4) {
    ~chatplayer("<p,happy>Nice hat!");
    ~chatnpc("<p,sad>Err thank you. It's a pretty ordinary cooks hat really.");
}

[label,cooks_assistant_whats_wrong]
~chatplayer("<p,neutral>What's wrong?");
~chatnpc("<p,scared>Ooh dear, I'm in a terrible mess!|It's the Duke's birthday today,|I'm meant to be making him a big cake for this evening.");
~chatnpc("<p,sad>Unfortunately, I've forgotten to buy some of the ingredients. I'll never get them in time now. I don't suppose you could help me?");
def_int $option = ~p_choice2("Yes, I'll help you.", 1, "No, I don't feel like it. Maybe later.", 2);

if($option = 1) {
    %cook_progress = 1;
    ~send_quest_progress(questlist:cook, %cook_progress, ^cook_complete);
    ~chatplayer("<p,happy>Yes, I'll help you.");
    // https://storage.googleapis.com/tannerdino/images/cook2.jpg
    ~chatnpc("<p,happy>Oh thank you, thank you. I need milk, an egg and flour.|I'd be very grateful if you can get them to me."); // auto break will break the line early
} else if($option = 2) {
    ~chatplayer("<p,neutral>No, I don't feel like it. Maybe later.");
    ~chatnpc("<p,sad>OK, suit yourself!");
}

[label,cooks_assistant_inprogress]
~chatnpc("<p,sad>How are you getting on with finding the ingredients?");

if(inv_total(inv, pot_flour) > 0 & inv_total(inv, egg) > 0 & inv_total(inv, bucket_milk) > 0) {
    @cooks_assistant_completion;
}

if(inv_total(inv, pot_flour) = 0 & inv_total(inv, egg) = 0 & inv_total(inv, bucket_milk) = 0) {
    ~chatplayer("<p,neutral>I'm afraid I don't have any yet!");
    ~chatnpc("<p,sad>Oh dear, oh dear!|I need flour, eggs and milk.|Without them I am doomed!");
    return;
} else {
    ~chatplayer("<p,happy>I have found some of the things you asked for.");
    if(inv_total(inv, bucket_milk) > 0) {
        ~chatplayer("<p,happy>I have some milk.");
    }
    if(inv_total(inv, pot_flour) > 0) {
        ~chatplayer("<p,happy>I have some flour.");
    }
    if(inv_total(inv, egg) > 0) {
        ~chatplayer("<p,happy>I have an egg.");
    }
}
~chatnpc("<p,sad>Great, but can you get the other ingredients as well?|You still need to find...");
if(inv_total(inv, bucket_milk) = 0) {
    ~chatnpc("<p,happy>Some milk.");
}
if(inv_total(inv, pot_flour) = 0) {
    ~chatnpc("<p,happy>Some flour.");
}
if(inv_total(inv, egg) = 0) {
    ~chatnpc("<p,happy>An egg.");
}
~chatplayer("<p,happy>OK, I'll try and find that for you.");

[label,cooks_assistant_completion]
~chatplayer("<p,happy>I now have everything you need for your cake!");
~chatplayer("<p,happy>Milk, flour, and an egg!");
~chatnpc("<p,happy>I am saved, thank you!");
~mesbox("You give some milk, an egg and some flour to the cook.");
inv_del(inv, egg, 1);
inv_del(inv, bucket_milk, 1);
inv_del(inv, pot_flour, 1);
queue(cooks_quest_complete, 0);

[queue,cooks_quest_complete]
%cook_progress = ^cook_complete;
stat_advance(cooking, 3000);
session_log(^log_adventure, "Quest complete: Cook's Assistant");
~send_quest_complete(questlist:cook, cake, 250, ^cook_questpoints, "You have completed the Cooks' Quest!");

[oplocu,cooksquestrange]
if (%cook_progress < ^cook_complete) {
    if(npc_find(coord, cook, 5, 0) = true) {
        ~chatnpc("<p,angry>Hey! Who said you could use that?");
    }
    return;
} else {
    @attempt_cook_item(last_useitem);
}