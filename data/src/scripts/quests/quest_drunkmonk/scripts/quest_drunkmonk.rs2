[softtimer,blanket_ladder]
if(%drunkmonk_progress >= 1 & coordx(coord) >= 2559 & coordx(coord) <= 2563 & coordz(coord) >= 3220 & coordz(coord) <= 3224) {
    if(~drunkmonk_ladder_active = false) {
        mes("A ladder mysteriously appears.");
        loc_add(^blanket_ladder_coord, loc_1765, 1, centrepiece_straight, 200);
    }
}

[proc,drunkmonk_ladder_active](boolean)
if(loc_find(^blanket_ladder_coord, loc_1765) = true) {
    return(true);
}
return(false);

[label,drunkmonk_party]
if_close;
// TODO: npc mode
~omad_dance("Party!", 0);
~spawn_drunkmonk_balloons(4);
~spawn_drunkmonk_balloons(4);
~drunkmonk_dance("Woop!", -1);
~spawn_drunkmonk_balloons(3);
~spawn_drunkmonk_balloons(3);
~spawn_drunkmonk_balloons(4);
~omad_dance("Let's boogie!", 2);
~drunkmonk_dance("Oh baby!", 2);
~omad_dance("Get down!", 2);
~drunkmonk_dance("Feel the rhythm!", 2);
~omad_dance("Oh my!", 2);
~drunkmonk_dance("Watch me go!", 2);
~omad_dance("You go!", 2);
queue(drunkmonk_complete, 0);

[proc,omad_dance](string $text, int $delay)
npc_facesquare(coord);
npc_anim(emote_dance, 0);
npc_say($text);
p_delay($delay);

[proc,drunkmonk_dance](string $text, int $delay)
anim(emote_dance, 0);
say($text);
if($delay >= 0) {
    p_delay($delay);
}

// only 115-121 are used here from my testing (locs)
[proc,spawn_drunkmonk_balloons](int $count)
while($count > 0) {
    def_coord $balloon_coord = ~random_balloon_coord;
    if(map_blocked($balloon_coord) = false) {
        loc_add($balloon_coord, ~random_balloon, random(4), centrepiece_straight, 200);
    }
    $count = calc($count - 1);
}
p_delay(0);

[proc,random_balloon](loc)
switch_int (random(7)) {
    case 0: return (loc_115);
    case 1: return (loc_116);
    case 2: return (loc_117);
    case 3: return (loc_118);
    case 4: return (loc_119);
    case 5: return (loc_120);
    case 6: return (loc_121);
    case default: return (loc_115);
}

[proc,random_balloon_coord](coord)
if(random(3) > 0) {
    return (movecoord(0_40_50_43_7, random(8), 0, random(5)));
} else {
    return (movecoord(0_40_50_45_12, random(3), 0, random(7)));
}

[queue,drunkmonk_complete]
%drunkmonk_progress = ^drunkmonk_complete;
~send_quest_complete(quest_journal:drunkmonk, childs_blanket, 250, 1, "You have completed the\\nMonk's Friend Quest!");
inv_add(inv, lawrune, 8);
givexp(woodcutting, 20000);

[debugproc,monk_end]
mes("set to second last drunkmonk stage");
%drunkmonk_progress = 7;