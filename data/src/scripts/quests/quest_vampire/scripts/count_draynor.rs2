[ai_queue4,count_draynor] gosub(npc_death);

// https://youtu.be/67aR5-_INcU
// - seems to be called on npc damage, and on npc hit
// - two calls from 66 hits
[proc,count_draynor_garlic]
if (inv_total(inv, garlic) > 0) {
    if (npc_stat(hitpoints) = npc_basestat(hitpoints)) { // osrs https://archive.is/djWKq
        npc_statsub(hitpoints, 10, 0);
        npc_statsub(attack, 10, 0);
        npc_statsub(strength, 10, 0);
        npc_statsub(defence, 40, 0);
        mes("The vampire seems to weaken.");
    } else if (random(64) = 0) {
        mes("The vampire seems to weaken.");
    }
}

[ai_opplayer2,count_draynor]
if (%death = ^true) {
    npc_setmode(null);
    return;
}
if (%npc_action_delay > map_clock) {
    return;
}
if (~npc_check_notcombat = false) {
    npc_setmode(null);
    return;
}
if (~npc_check_notcombat_self = false) {
    return;
}
anim(%com_defendanim, 0);
npc_anim(npc_param(attack_anim), 0);
if (npc_param(attack_sound) ! null) {
    sound_synth(npc_param(attack_sound), 0, 0);
}
~count_draynor_garlic;
~npc_meleeattack;

[ai_queue2,count_draynor]
if (finduid(%npc_aggressive_player) = true) {
    ~count_draynor_garlic;
}
~npc_default_damage(last_int);


[ai_queue3,count_draynor]
if (npc_findhero = ^true) {
    if (p_finduid(uid) = true) {
        @count_draynor_use_stake;
    }
}
npc_statheal(hitpoints, 1, 0);

[label,count_draynor_use_stake]
if (inv_total(inv, stake) = 0) {
    mes("The vampire seems to regenerate!");
    npc_statheal(hitpoints, 0, 100);
    npc_setmode(opplayer2);
    return;
}
if (inv_total(inv, hammer) = 0) {
    mes("You're unable to push the stake far enough in!");
    mes("The vampire seems to regenerate!");
    npc_statheal(hitpoints, 0, 100);
    npc_setmode(opplayer2);
    return;
}
p_delay(1);
mes("You hammer the stake into the vampire's chest!");
inv_del(inv, stake, 1);
anim(human_stake, 0);
npc_setmode(none);
npc_walk(npc_coord);
npc_queue(4, 0, 0);
if (%vampire_progress < ^vampire_complete) {
    queue(quest_vampire_complete, 3);
}

[ai_timer,count_draynor]
if (finduid(%npc_attacking_uid) = true) {
    if (npc_range(coord) > 10 | coordy(coord) ! coordy(npc_coord)) {
        npc_del;
    }
} else {
    npc_del;
}