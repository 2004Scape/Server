[opnpc1,david]
switch_int(%murder_progress) { 
    case ^murder_not_started: mes("He is ignoring you.");
    case ^murder_started:
        ~chatplayer(neutral, "I'm here to help the guards with their investigation.");
        ~chatnpc(neutral, "And? Make this quick, I have better things to do than be interrogated by halfwits all day.");
        def_int $option;
        if(testbit(%murder_evidence, ^murder_found_thread) = true) {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Do you recognise this thread?", 3);
        } else {
            $option = ~p_choice2("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2);
        }
        switch_int($option) {
            case 1:
                ~chatplayer(neutral, "Who do you think is responsible?");
                ~chatnpc(neutral, "I don't really know or care. Frankly, the old man deserved to die.");
                ~chatnpc(neutral, "There was a suspicious red headed man who came to the house the other day selling poison now I think about it. Last I saw he was headed towards the tavern in the Seers village.");
            case 2:
                ~chatplayer(neutral, "Where were you when the murder happened?");
                ~chatnpc(angry, "That is none of your business. Are we finished now, or are you just going to stand there irritating me with your idiotic questions all day?");
            case 3:
                ~chatplayer(confused, "Do you recognise this thread?");
                ~mesbox("You show him the thread you found on the study window");
                if(inv_total(inv, murder_green_thread) > 0) {
                    // RSC dialogue, confirm on osrs
                    ~chatnpc(angry, "Its some Green thread, like my trousers are made of. Are you finished? I'm not sure which I dislike more about you, your face or your general bad odour.");
                    return;
                }
                ~chatnpc(confused, "No. Can I go yet? Your face irritates me.");
        }
}