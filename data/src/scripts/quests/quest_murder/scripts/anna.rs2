[opnpc1,anna]
switch_int(%murder_progress) { 
    case ^murder_not_started: mes("She is ignoring you.");
    case ^murder_started:
        ~chatplayer(neutral, "I'm here to help the guards with their investigation.");
        ~chatnpc(neutral, "Oh really? What do you want to know then?");
        def_int $option;
        if(testbit(%murder_evidence, ^murder_found_thread) = true & %murder_poisonproof_progress >= ^poisonproof_spoken_salesman) {
            $option = ~p_choice4("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Do you recognise this thread?", 3, "Why'd you buy poison the other day?", 4);
        } else if(testbit(%murder_evidence, ^murder_found_thread) = true) {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Do you recognise this thread?", 3);
        } else if(%murder_poisonproof_progress >= ^poisonproof_spoken_salesman) {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Why'd you buy poison the other day?", 4);
        } else {
            $option = ~p_choice2("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2);
        }
        switch_int($option) {
            case 1:
                ~chatplayer(neutral, "Who do you think is responsible?");
                ~chatnpc(neutral, "It was clearly an intruder.");
                ~chatplayer(confused, "Well, I don't think it was.");
                ~chatnpc(angry, "It was one of our lazy servants then.");
            case 2:
                ~chatplayer(neutral, "Where were you when the murder happened?");
                ~chatnpc(neutral, "In the library. No one else was there so|you'll just have to take my word for it.");
            case 3:
                ~chatplayer(confused, "Do you recognise this thread?");
                ~mesbox("You show Anna the thread from the study.");
                if(inv_total(inv, murder_green_thread) > 0) {
                    ~chatnpc(neutral, "It's some Green thread. It's not exactly uncommon is it? My trousers are made of the same material.");
                    return;
                }
                ~chatnpc(neutral, "Not really, no. Thread is fairly common.");
            case 4:
                ~chatplayer(neutral, "Why'd you buy poison the other day?");
                ~chatnpc(angry, "That useless Gardener Stanford has let his compost heap fester. It's an eyesore to the garden! So I bought some poison from a travelling salesman so that I could kill off some of the wildlife living in it.");
                if(%murder_murderer_id = ^murderer_anna & %murder_poisonproof_progress = ^poisonproof_spoken_salesman) { 
                    %murder_poisonproof_progress = ^poisonproof_spoken_murderer;
                }
        }
    case ^murder_complete: ~chatnpc(neutral, "Apparently you aren't as stupid as you look.");
}