[opnpc1,stanford]
switch_int(%murder_progress) { 
    case ^murder_not_started:
        ~chatnpc(angry, "Have you no shame? We are all grieving at the moment.");
    case ^murder_started:
        ~chatplayer(neutral, "I'm here to help the guards with their investigation.");
        ~chatnpc(neutral, "How can I help?");
        def_int $option;
        if(%murder_poisonproof_progress >= ^poisonproof_spoken_salesman) {
            $option = ~p_choice4("Who do you think is responsible?", 1, "Where were you at the time of the murder?", 2, "Did you hear any suspicious noises at all?", 3, "Do you know why so much poison was bought recently?", 4);
        } else {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you at the time of the murder?", 2, "Did you hear any suspicious noises at all?", 3);
        }
        switch_int($option) {
            case 1:
                ~chatplayer(neutral, "Who do you think is responsible?");
                ~chatnpc(angry, "It was Anna. She is seriously unbalanced. She trashed the garden once then tried to blame it on me! I bet it was her. It's just the kind of thing she'd do!");
                ~chatnpc(neutral, "She really hates me and was arguing with Lord Sinclair about trashing the garden a few days ago.");
            case 2:
                ~chatplayer(confused, "Where were you at the time of the murder?");
                ~chatnpc(neutral, "Right here, by my little shed. It's very cosy to sit and think in.");
            case 3:
                ~chatplayer(confused, "Did you hear any suspicious noises at all?");
                ~chatnpc(confused, "Not that I remember.");
                ~chatplayer(confused, "So no sounds of a struggle between Lord Sinclair and an intruder?");
                ~chatnpc(neutral, "Not to the best of my recollection.");
                ~chatplayer(neutral, "How about the guard dog barking?");
                ~chatnpc(neutral, "Not that I can recall.");
            case 4:
                ~chatplayer(neutral, "Do you know why so much poison was bought recently?");
                ~chatnpc(neutral, "Well, Bob mentioned to me the other day he wanted to get rid of the bees in that hive over there. I think I saw him buying poison");
                ~chatnpc(neutral, "from that poison salesman the other day. I assume it was to sort out those bees. You'd really have to ask him though.");
        }
    case ^murder_complete: ~chatnpc(happy, "Thank you for all your help in solving the murder.");
}