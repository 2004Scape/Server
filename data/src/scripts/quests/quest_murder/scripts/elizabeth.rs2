[opnpc1,elizabeth]
switch_int(%murder_progress) { 
    case ^murder_not_started: mes("She is ignoring you.");
    case ^murder_started:
        ~chatplayer(neutral, "I'm here to help the guards with their investigation.");
        ~chatnpc(neutral, "What's so important you need to bother me with then?");
        def_int $option;
        if(testbit(%murder_evidence, ^murder_found_thread) = true & %murder_poisonproof_progress >= ^poisonproof_spoken_salesman) {
            $option = ~p_choice4("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Do you recognise this thread?", 3, "Why'd you buy poison the other day?", 4);
        } else if(testbit(%murder_evidence, ^murder_found_thread) = true) {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Do you recognise this thread?", 3);
        } else if(%murder_poisonproof_progress >= ^poisonproof_spoken_salesman) {
            $option = ~p_choice3("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2, "Why'd you buy poison the other day?", 4);
        } else {
            $option = ~p_choice2("Who do you think is responsible?", 1, "Where were you when the murder happened?", 2);
        }
        switch_int($option) {
            case 1:
                ~chatplayer(neutral, "Who do you think is responsible?");
                ~chatnpc(neutral, "Could have been anyone. The old man was an idiot. He's been asking for it for years.");
            case 2:
                ~chatplayer(neutral, "Where were you when the murder happened?");
                ~chatnpc(neutral, "I was out.");
                ~chatplayer(neutral, "Care to be any more specific?");
                ~chatnpc(angry, "Not really. I don't have to justify myself to the likes of you, you know. I know the King personally you know. Now are we finished here?");
            case 3:
                ~chatplayer(confused, "Do you recognise this thread?");
                ~mesbox("You show her the thread from the study window.");
                if(inv_total(inv, murder_blue_thread) > 0) {
                    ~chatnpc(neutral, "Looks like Blue thread to me. If you can't work that out for yourself I don't hold much hope of you solving this crime.");
                    ~chatplayer(neutral, "It looks a lot like the material your trousers are made of doesn't it?");
                    ~chatnpc(angry, "I suppose it does. So what?");
                    return;
                }
                ~chatnpc(confused, "It's some thread. You're not very good at this whole investigation thing are you?");
            case 4:
                ~chatplayer(neutral, "Why'd you buy poison the other day?");
                ~chatnpc(confused, "I don't see what on earth it has to do with you, but the drain outside was");
                ~chatnpc(angry, "blocked, and as nobody else here has the intelligence to even unblock a simple drain I felt I had to do it myself.");
                if(%murder_murderer_id = ^murderer_carol & %murder_poisonproof_progress = ^poisonproof_spoken_salesman) { // TODO: figure out exactly when these would get set for each murderer
                    %murder_poisonproof_progress = ^poisonproof_spoken_murderer;
                }
        }
    case ^murder_complete: ~chatnpc(neutral, "Apparently you aren't as stupid as you look.");
}