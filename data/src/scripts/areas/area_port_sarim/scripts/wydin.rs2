[opnpc1,wydin]
if (testbit(%hunt_store_employed, 1) = ^true) {
    @wydin_employed_chat;
}

~chatnpc("<p,happy>Welcome to my food store! Would you like to buy anything?");
if (%hunt_progress = 0) {
    @multi3("Yes please.", wydin_open_shop, "No, thank you.", wydin_no, "What can you recommend?", wydin_recommend);
} else if (%hunt_progress = 1) {
    @multi4("Yes please.", wydin_open_shop, "No, thank you.", wydin_no, "What can you recommend?", wydin_recommend, "Can I get a job here?", wydin_job);
}

[label,wydin_employed_chat]
~chatnpc("<p,quiz>Is it nice and tidy round the back now?");

def_int $choice = ~p_choice4("Yes, can I work out front now?", 1, "Yes, are you going to pay me yet?", 2, "No, it's a complete mess", 3, "Can I buy something please?", 4);
switch_int ($choice) {
    case 1 :
        ~chatplayer("<p,happy>Yes, can I work out front now?"); 
        ~chatnpc("<p,neutral>I'm the one who works here.");
    case 2 :
        ~chatplayer("<p,happy>Yes, are you going to pay me yet?");
        ~chatnpc("<p,shifty>Umm... No, not yet.");
    case 3 :
        ~chatplayer("<p,sad>No, it's a complete mess.");
        ~chatnpc("<p,neutral>Ah well, it'll give you something to do, won't it.");
    case 4 :
        ~chatplayer("<p,quiz>Can I buy something please?");
        ~chatnpc("<p,happy>Yes, of course.");
        p_opnpc(3);
}

[label,wydin_job]
~chatplayer("<p,quiz>Can I get a job here?");
~chatnpc("<p,neutral>Well, you're keen, I'll give you that. Okay, I'll give you a go. Have you got your own white apron?");

if (inv_total(inv, white_apron) = 0 & inv_total(worn, white_apron) = 0) {
    ~chatplayer("<p,sad>No, I haven't.");
    ~chatnpc("<p,neutral>Well, you can't work here unless you have a white apron. Health and safety regulations, you understand.");
    return;
}

~chatplayer("<p,happy>Yes, I have one right here.");
%hunt_store_employed = setbit(%hunt_store_employed, ^food_store);
~chatnpc("<p,happy>Wow - you are well prepared! You're hired. Go through to the back and tidy up for me, please.");

[label,wydin_open_shop]
~chatplayer("<p,happy>Yes please.");
p_opnpc(3);

[label,wydin_no]
~chatplayer("<p,neutral>No, thank you.");

[label,wydin_recommend]
~chatplayer("<p,quiz>What can you recommend?");
~chatnpc("<p,happy>We have this really exotic fruit all the way from Karamja. It's called a banana.");
@multi2("Hmm, I think I'll try one.", wydin_recommend_try, "I don't like the sound of that.", wydin_recommend_no);

[label,wydin_recommend_try]
~chatplayer("<p,neutral>Hmm, I think I'll try one.");
~chatnpc("<p,neutral>Great. You might as well take a look at the rest of my wares as well.");
p_opnpc(3);

[label,wydin_recommend_no]
~chatplayer("I don't like the sound of that."); // no anim
~chatnpc("<p,neutral>Well, it's your choice, but I do recommend them.");
