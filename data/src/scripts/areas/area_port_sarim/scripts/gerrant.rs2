[opnpc1,gerrant]
if(map_members = true & inv_total(inv, trail_clue_hard_riddle_exp004) > 0) {
    ~chatnpc("<p,neutral>You've done it!");
    ~progress_clue_hard(trail_clue_hard_riddle_exp004, "Gerrant has given you another clue scroll!");
    return;
}
~chatnpc("<p,happy>Welcome! You can buy fishing equipment at my store.|We'll also buy anything you catch off you.");
def_int $option;
if (~hero_in_progress = true) {
    $option = ~p_choice3("Let's see what you've got then.", 1, "Sorry, I'm not interested.", 2, "I want to find out how to catch a lava eel.", 3);
} else {
    $option = ~p_choice2("Let's see what you've got then.", 1, "Sorry, I'm not interested.", 2);
}
switch_int($option) {
    case 1 :
        ~chatplayer("<p,neutral>Let's see what you've got then.");
        ~openshop_activenpc;
    case 2 :
        ~chatplayer("<p,neutral>Sorry, I'm not interested.");
    case 3 :
        ~chatplayer("<p,neutral>I want to find out how to catch a lava eel.");
        ~chatnpc("<p,neutral>Lava eels eh? That's a tricky one that is, you'll need a|lava-proof fishing line. The method for making this would|be to take an ordinary fishing rod, and then cover it|with the fire-proof Blamish Oil.");
        if(inv_total(inv, blamish_snail_slime) = 0) {
            ~chatnpc("<p,neutral>You know... thinking about it... I may have a jar of|Blamish Slime around here somewhere... Now where did|I put it?");
            ~mesbox("Gerrant searches around a bit.");
            inv_add(inv, blamish_snail_slime, 1);
            ~chatnpc("<p,neutral>Aha! Here it is! Take this slime, mix it with some|Harralander and water and you'll have the Blamish Oil|you need for treating your fishing rod.");
        }
}
