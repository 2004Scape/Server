[opnpc1,aggie]
~chatnpc("<p,happy>What can I help you with?");
def_int $option;
if(%prince_progress >= ^prince_spoken_osman & %prince_progress < ^prince_saved) {
    $option = ~p_choice5("Could you think of a way to make skin paste?", 5, "What could you make for me?", 1, "Cool, do you turn people into frogs?", 2, "You mad old witch, you can't help me.", 3, "Can you make dyes for me please?", 4);
}else {
    $option = ~p_choice4("What could you make for me?", 1, "Cool, do you turn people into frogs?", 2, "You mad old witch, you can't help me.", 3, "Can you make dyes for me please?", 4);
}

if($option = 1) {
    ~chatplayer("<p,quiz>What could you make for me?");
    ~chatnpc("<p,neutral>I mostly just make what I find pretty. I sometimes make dye for the women's clothes, brighten the place up. I can make red, yellow and blue dyes. Would you like some?");
    @aggie_dyes;
} else if($option = 2) { 
    ~chatplayer("<p,happy>Cool, do you turn people into frogs?");
    ~chatnpc("<p,neutral>Oh, not for years, but if you meet a talking chicken, you have probably met the professor in the manor north of here. A few years ago it was flying fish. That machine is a menace.");
} else if($option = 3) {
    ~chatplayer("<p,angry>You mad old witch, you can't help me.");
    @aggie_fine;
} else if($option = 4) {
    ~chatplayer("<p,quiz>Can you make dyes for me please?");
    ~chatnpc("<p,quiz>What sort of dye would you like? Red, yellow or blue?");
    @aggie_dyes;
} else if($option = 5) {
    ~chatplayer("<p,quiz>Could you think of a way to make skin paste?");
    if(inv_total(inv, redberry) > 0 & inv_total(inv, pot_flour) > 0 & (inv_total(inv, bucket_water) > 0 | inv_total(inv, jug_water) > 0) & inv_total(inv, ashes) > 0) {
        ~chatnpc("<p,happy>Yes I can, I see you already have the ingredients. Would you like me to mix some for you now?");
        @multi2("Yes please. Mix me some skin paste.", aggie_mix_paste, "No thank you, I don't need any skin paste right now.", aggie_no_paste);
    } else {
        ~chatnpc("<p,happy>Why, it's one of my most popular potions.|The women here, they like to have smooth looking skin.|And I must admit, some of the men buy it as well.");
        ~chatnpc("<p,happy>I can make it for you, just get me what's needed.");
        ~chatplayer("<p,quiz>What do you need to make it?");
        ~chatnpc("<p,neutral>Well dearie, you need a base for the paste.|That's a mix of ash, flour and water.|Then you need redberries to colour it as you want.|Bring me those four items and I will make you some.");
    }
}

[opnpcu,aggie]
switch_obj(last_useitem) {
    case onion : @aggie_yellow_dye;
    case redberry : @aggie_red_dye;
    case woadleaf : @aggie_blue_dye;
    case default : ~displaymessage(^dm_default);
}

[label,aggie_fine]
if(inv_total(inv, coins) >= 21 & inv_total(inv, coins) < 101) {
    inv_del(inv, coins, 5);
    ~chatnpc("<p,angry>Oh, you like to call a witch names do you?");
    npc_anim(human_pickpocket, 0);
    sound_synth(pick, 0, 0);
    ~objbox(coins_250, "Aggie waves her hands about, and you seem to be 5 coins poorer.", 250, 0, divide(^objbox_height, 2));
    ~chatnpc("<p,neutral>That's a fine for insulting a witch. You should learn some respect.");
} else if(inv_total(inv, coins) >= 101) {
    inv_del(inv, coins, 20);
    ~chatnpc("<p,angry>Oh, you like to call a witch names do you?");
    npc_anim(human_pickpocket, 0);
    sound_synth(pick, 0, 0);
    ~objbox(coins_250, "Aggie waves her hands about, and you seem to be 20 coins poorer.", 250, 0, divide(^objbox_height, 2));
    ~chatnpc("<p,neutral>That's a fine for insulting a witch. You should learn some respect.");
} else if(inv_total(inv, pot_flour) >= 1) {
    inv_del(inv, pot_flour, 1);
    ~chatnpc("<p,angry>Oh, you like to call a witch names do you?");
    npc_anim(human_pickpocket, 0);
    sound_synth(pick, 0, 0);
    ~objbox(pot_flour, "Aggie waves her hands near you, and you seem to have lost some flour.", 250, 0, divide(^objbox_height, 2));
    ~chatnpc("<p,neutral>Thank you for your kind present of flour.|I am sure you never meant to insult me.");
} else {
    ~chatnpc("<p,angry>You should be careful about insulting a witch. You never know what shape you could wake up in.");
}

[label,aggie_dyes]
def_int $option = ~p_choice4("What do you need to make red dye?", 1, "What do you need to make yellow dye?", 2, "What do you need to make blue dye?", 3, "No thanks, I am happy the colour I am.", 4);
if($option = 1) {
    ~chatplayer("<p,quiz>What do you need to make red dye?");
    ~chatnpc("<p,neutral>3 lots of redberries and 5 coins to you.");
    @multi3("Okay, make me some red dye please.", aggie_red_dye, "I don't think I have all the ingredients yet.", aggie_ingredients, "I can do without dye at that price.", aggie_without_dye);
} else if($option = 2) {
    ~chatplayer("<p,quiz>What do you need to make yellow dye?");
    ~chatnpc("<p,neutral>Yellow is a strange colour to get, comes from onion skins. I need 2 onions and 5 coins to make yellow dye.");
    @multi3("Okay, make me some yellow dye please.", aggie_yellow_dye, "I don't think I have all the ingredients yet.", aggie_ingredients, "I can do without dye at that price.", aggie_without_dye);
} else if($option = 3) {
    ~chatplayer("<p,quiz>What do you need to make blue dye?");
    ~chatnpc("<p,neutral>2 woad leaves and 5 coins to you.");
    @multi3("Okay, make me some blue dye please.", aggie_blue_dye, "I don't think I have all the ingredients yet.", aggie_ingredients, "I can do without dye at that price.", aggie_without_dye);
} else if($option = 4) {
    ~chatplayer("<p,neutral>No thanks, I am happy the colour I am.");
    ~chatnpc("<p,neutral>You are easily pleased with yourself then.|When you need dyes, come to me.");
}

[label,aggie_yellow_dye]
if(inv_total(inv, onion) < 2) {
    ~mesbox("You don't have enough onions to make the yellow dye!");
    return;
} else if(inv_total(inv, coins) < 5) {
    ~mesbox("You don't have enough coins to pay for the dye!");
    return;
}
~chatplayer("<p,neutral>Okay, make me some yellow dye please.");
inv_del(inv, coins, 5);
inv_del(inv, onion, 2);
inv_add(inv, yellowdye, 1);
~objbox(yellowdye, "You hand the onions and payment to Aggie.|Aggie takes a yellow bottle from nowhere and hands it to you.", 250, 0, ^objbox_height);

[label,aggie_red_dye]
if(inv_total(inv, redberry) < 3) {
    ~mesbox("You don't have enough berries to make the red dye!");
    return;
} else if(inv_total(inv, coins) < 5) {
    ~mesbox("You don't have enough coins to pay for the dye!");
    return;
}
~chatplayer("<p,neutral>Okay, make me some red dye please.");
inv_del(inv, coins, 5);
inv_del(inv, redberry, 3);
inv_add(inv, reddye, 1);
~objbox(reddye, "You hand the berries and payment to Aggie.|Aggie takes a red bottle from nowhere and hands it to you.", 250, 0, ^objbox_height);

[label,aggie_blue_dye]
if(inv_total(inv, woadleaf) < 2) {
    ~mesbox("You don't have enough woad leaves to make the blue dye!");
    return;
} else if(inv_total(inv, coins) < 5) {
    ~mesbox("You don't have enough coins to pay for the dye!");
    return;
}
~chatplayer("<p,neutral>Okay, make me some blue dye please.");
inv_del(inv, coins, 5);
inv_del(inv, woadleaf, 2);
inv_add(inv, bluedye, 1);
~objbox(bluedye, "You hand the woad leaves and payment to Aggie.|Aggie takes a blue bottle from nowhere and hands it to you.", 250, 0, ^objbox_height);

[label,aggie_ingredients]
~chatplayer("<p,confused>I don't think I have all the ingredients yet.");
~chatnpc("<p,neutral>You know what you need to get now, come back when you have them. Goodbye for now.");

[label,aggie_without_dye]
~chatplayer("<p,angry>I can do without dye at that price.");
~chatnpc("<p,neutral>That's your choice, but I would think you have killed for less. I can see it in your eyes.");

[label,aggie_mix_paste]
~chatplayer("<p,happy>Yes please. Mix me some skin paste.");
~chatnpc("<p,happy>That should be simple. Hand the things to Aggie then.");
if(inv_total(inv, bucket_water) > 0) {
    inv_del(inv, bucket_water, 1);
} else if(inv_total(inv, jug_water) > 0) {
    inv_del(inv, jug_water, 1);
}
~mesbox("You hand the ash, flour, water and redberries to Aggie.|Aggie tips the ingredients into a cauldron|and mutters some words.");
~chatnpc("<p,confused>Tourniquet, Fenderbaum, Tottenham, Marshmallow, Marblearch.");
inv_del(inv, redberry, 1);
inv_del(inv, pot_flour, 1);
inv_del(inv, ashes, 1);
inv_add(inv, skinpaste, 1);
~mesbox("Aggie hands you the skin paste.");
~chatnpc("<p,happy>There you go dearie, your skin potion.|That will make you look good at the Varrock dances.");

[label,aggie_no_paste]
~chatplayer("<p,neutral>No thank you, I don't need any skin paste right now.");
~chatnpc("<p,neutral>Okay dearie, that's always your choice.");