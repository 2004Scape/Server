[opnpc1,traiborn]
if (%demon_progress = ^demon_got_traiborn_key & inv_total(inv, silverlight_key_1) < 1 & inv_total(bank, silverlight_key_1) < 1){
    @demon_slayer_traiborn_lost_key;
} else if (%demon_progress < ^demon_find_bones | %demon_progress >= ^demon_got_traiborn_key) {
    @traiborn_dialogue;
} else {
    @demon_slayer_traiborn_bones_dialogue;
}

[label,demon_slayer_traiborn_bones_dialogue]
~chatnpc("<p,confused>How are you doing finding bones?");

if(inv_total(inv, bones) < 1) {
    ~chatplayer("<p,bored>I haven't got any at the moment.");
    ~chatnpc("<p,confused>Never mind. Keep working on it.");
} else {
    ~chatplayer("<p,happy>I have some bones.");
    ~chatnpc("<p,confused>Give 'em here then.");
    @demon_slayer_traiborn_give_bones;
}

[label,demon_slayer_traiborn_give_bones]
if(%demon_progress >= ^demon_find_bones & %demon_progress < ^demon_got_traiborn_key & inv_total(inv, bones) > 0) {
    if_close();
    inv_del(inv, bones, 1);
    mes("You give Traiborn a set of bones.");
    p_delay(1);
    %demon_progress = calc(%demon_progress + 1);
    @demon_slayer_traiborn_give_bones;
} else {
    @demon_slayer_traiborn_stop_giving_bones;
}

[proc,traiborn_dialogue_options](int $demon_progress)(int)
if (%demon_progress < ^demon_key_hunt | %demon_progress >= ^demon_got_traiborn_key) {
   return(~p_choice2("What's a thingummywut?", 1, "Teach me to be a mighty and powerful wizard.", 2));
} else if ($demon_progress < ^demon_silverlight) {
   return (~p_choice3("What's a thingummywut?", 1, "Teach me to be a mighty and powerful wizard.", 2, "I need to get a key given to you by Sir Prysin.", 3));
}

[label,traiborn_dialogue]
~chatnpc("<p,shock>Ello young thingummywut.");
def_int $option = ~traiborn_dialogue_options(%demon_progress);

if ($option = 1) {
    ~chatplayer("<p,shock>What's a thingummywut?");
    @traiborn_thingummywut_shock;
} else if ($option = 2) {
    ~chatplayer("<p,neutral>Teach me to be a mighty and powerful wizard.");
    @traiborn_teach_me;
} else {
    ~chatplayer("<p,neutral>I need to get a key given to you by Sir Prysin.");
    ~chatnpc("<p,confused>Sir Prysin? Who's that? What would I want his key for?");
    def_int $option_key = ~p_choice3("He told me you were looking after it for him.", 1, "He's one of the King's knights.", 2, "Well, have you got any keys knocking around?", 3);

    if ($option_key = 1) {
        ~chatplayer("<p,neutral>He told me you were looking after it for him.");
        ~chatnpc("<p,confused>That wasn't very clever of him. I'd lose my head if it wasn't screwed on. Go and tell him to find someone else to look after his valuables in future.");
        def_int $option_insult_or_key = ~p_choice2("Okay, I'll go and tell him that.", 1, "Well, have you got any keys knocking around?", 2);

        if ($option_insult_or_key = 1) {
            ~chatplayer("<p,happy>Okay, I'll go and tell him that.");
            ~chatnpc("<p,happy>Oh that's great, if it wouldn't be too much trouble.");
            @demon_slayer_traiborn_be_off_keys_around;
        } else {
            @demon_slayer_traiborn_keys_around;
        }
    } else if ($option_key = 2) {
        ~chatplayer("<p,neutral>He's one of the King's knights.");
        ~chatnpc("<p,happy>Say, I remember a knight with a key. He had nice shoes... | ...and didn't like my homemade spinach rolls. Would you like a spinach roll?");
        @traiborn_spinach_roll;
    } else {
        @demon_slayer_traiborn_keys_around;
    }
}

[label,demon_slayer_traiborn_lost_key]
~chatplayer("<p,sad>I've lost the key you gave to me.");
~chatnpc("<p,confused>Yes I know, it was returned to me. If you want it back you're going to have to collect another 25 sets of bones.");
%demon_progress = ^demon_find_bones;

[label,traiborn_teach_me]
~chatnpc("<p,shock>Wizard eh? You don't want any truck with that sort. They're not to be trusted. That's what I've heard anyways.");
def_int $option = ~p_choice2("So aren't you a wizard?", 1, "Oh I'd better stop talking to you then.", 2);

if ($option = 1) {
    ~chatplayer("<p,neutral>So aren't you a wizard?");
    ~chatnpc("<p,angry>How dare you? Of course I'm a wizard. Now don't be so cheeky or I'll turn you into a frog.");
} else {
    ~chatplayer("<p,neutral>Oh I'd better stop talking to you then.");
    ~chatnpc("<p,happy>Cheerio then. It was nice chatting to you.");
}

[label,traiborn_thingummywut_shock]
~chatnpc("<p,shock>A thingummywut? Where? Where?");
~chatnpc("<p,shock>Those pesky thingummywuts. They get everywhere. They leave a terrible mess too.");
def_int $option = ~p_choice2("Err you just called me a thingummywut.", 1, "Tell me what they look like and I'll mash 'em.", 2);

if ($option = 1) {
    ~chatplayer("<p,happy>Err you just called me thingummywut.");
    @traiborn_thingummywut_player;
} else {
    ~chatplayer("<p,angry>Tell me what they look like and I'll mash 'em.");
    @traiborn_thingummywut_mash;
}

[label,traiborn_thingummywut_player]
~chatnpc("<p,shock>You're a thingummywut? I've never seen one up close before. They said I was mad!");
~chatnpc("<p,shock>Now you are my proof! There ARE thingummywuts in this tower. Now where can I find a cage big enough to keep you?");
def_int $option = ~p_choice2("Err I'd better be off really.", 1, "They're right, you are mad.", 2);

if ($option = 1) {
    @traiborn_be_off;
} else {
    @traiborn_mad;
}

[label,traiborn_thingummywut_mash]
~chatnpc("<p,shock>Don't be ridiculous. No-one has ever seen one.");
~chatnpc("<p,shock>They're invisible, or a myth, or a figment of my imagination. Can't remember which right now.");

[label,traiborn_be_off]
~chatplayer("<p,neutral>Err I'd better be off really.");
~chatnpc("<p,confused>Oh ok, have a good time, and watch out for sheep! They're more cunning than they look.");

[label,traiborn_mad]
~chatplayer("<p,happy>They're right, you are mad.");
~chatnpc("<p,sad>That's a pity. I thought maybe they were winding me up.");

[label,demon_slayer_traiborn_keys_around]
~chatplayer("<p,neutral>Well, have you got any keys knocking around?");
~chatnpc("<p,confused>Now you come to mention it, yes I do have a key. It's in my special closet of valuable stuff. Now how do I get into that?");
~chatnpc("<p,confused>I sealed it using one of my magic rituals. So it would make sense that another ritual would open it again.");
~chatplayer("<p,neutral>So do you know what ritual to use?");
~chatnpc("<p,neutral>Let me think a second.");
~chatnpc("<p,confused>Yes a simple drazier style ritual should suffice. Hmm, main problem with that is I'll need 25 sets of bones. Now where am I going to get hold of something like that?");

def_int $option = ~p_choice2(" Hmm, that's too bad. I really need that key.", 1, "I'll get the bones for you.", 2);
if ($option = 1) {
    ~chatplayer("<p,sad>Hmm, that's too bad. I really need that key.");
    ~chatnpc("<p,neutral>Ahh well, sorry I couldn't be any more help.");
} else {
    ~chatplayer("<p,neutral>I'll help get the bones for you.");
    ~chatnpc("<p,happy>Ooh that would be very good of you.");
    ~chatplayer("<p,neutral>Okay, I'll speak to you when I've got some bones.");
    %demon_progress = ^demon_find_bones;
}

[label,demon_slayer_traiborn_be_off_keys_around]
def_int $option = ~p_choice2("Err I'd better be off really.", 1, "Well, have you got any keys knocking around?", 2);
if ($option = 1) {
    @traiborn_be_off;
} else {
    @demon_slayer_traiborn_keys_around;
}

[label,traiborn_spinach_roll]
def_int $option = ~p_choice2("Yes please.", 1, "Just tell me if you have the key.", 2);

if ($option = 1) {
    ~chatplayer("<p,happy>Yes please.");
    ~objbox(spinach_roll, "Traiborn digs around in the pockets of his robes.|After a few moments he triumphantly presents you with|a spinach roll.", 250, 0, divide(^objbox_height, 2));
    inv_add(inv, spinach_roll, 1);
    ~chatplayer("<p,happy>Thank you very much.");
    @demon_slayer_traiborn_be_off_keys_around;
} else {
    ~chatplayer("<p,neutral>Just tell me if you have the key.");
    ~chatnpc("<p,confused>The key? The key to what?");
    ~chatnpc("<p,confused>There's more than one key in the world don't you know? Would be a bit odd if there was only the one.");
    def_int $option_silverlight_or_insult = ~p_choice2("It's the key to get a sword called Silverlight.", 1, "You've lost it, haven't you?", 2);

    if ($option_silverlight_or_insult = 1) {
        ~chatplayer("<p,neutral>It's the key to get a sword called Silverlight.");
        ~chatnpc("<p,confused>Silverlight? Never heard of that.|Sounds a good name for a ship. Are you sure it's not the name of a ship, rather than a sword?");
        def_int $option_ship_or_keys = ~p_choice2("Yeah, pretty sure.", 1, "Well, have you got any keys knocking around?", 2);

        if ($option_ship_or_keys = 1) {
            ~chatplayer("<p,neutral>Yeah, pretty sure.");
            ~chatnpc("<p,confused>That's a pity. Waste of a name.");
            @demon_slayer_traiborn_be_off_keys_around;
        } else {
            @demon_slayer_traiborn_keys_around;
        }
    } else {
        ~chatplayer("<p,neutral>You've lost it haven't you?");
        ~chatnpc("<p,angry>Me? Lose things? That's a nasty accusation.");
       @demon_slayer_traiborn_keys_around;
    }
}

[label,demon_slayer_traiborn_stop_giving_bones]
if (%demon_progress = ^demon_got_traiborn_key) {
    ~chatnpc("<p,happy>Hurrah! That's all 25 sets of bones.");
    @demon_slayer_traiborn_incantation;
} else {
    ~chatplayer("<p,neutral>That's all of them.");
    ~chatnpc("<p,confused>I still need more.");
    ~chatplayer("<p,neutral>Ok, I'll look for some more.");
}

[label,demon_slayer_traiborn_incantation]
~mesbox("Traiborn places the bones in a circle on the floor.");
~mesbox("Traiborn waves his arms about.");
~chatnpc("<p,confused>Wings of dark and colour too,|Spreading in the morning dew;|Locked away I have a key;|Return it now, please, unto me.");
~mesbox("The wizard waves his arms some more.");
if_close();
p_delay(2);
mes("Traiborn hands you a key");
inv_add(inv, silverlight_key_1, 1);
p_delay(2);
~chatplayer("<p,happy>Thank you very much.");
~chatnpc("<p,happy>Not a problem for a friend of Sir What's-his-face.");
