[opnpc1,traiborn]
if (%demon_progress < 3 | %demon_progress > 29 | inv_total(inv, demon_traiborn_key) > 0) {
    @traiborn_dialogue;
} else if (%demon_progress = 29 & inv_total(inv, demon_traiborn_key) < 1){
    @demon_slayer_traiborn_lost_key;
} else {
    @demon_slayer_traiborn_bones_dialogue;
}

[label,demon_slayer_traiborn_bones_dialogue]
~chatnpc(confused, "How are you doing finding bones?");

if(inv_total(inv, bones) < 1) {
    ~chatplayer(down, "I haven't got any at the moment.");
    ~chatnpc(confused, "Never mind. Keep working on it.");
} else {
    ~chatplayer(long, "I have some bones.");
    ~chatnpc(confused, "Give 'em here then.");
    @demon_slayer_traiborn_give_bones;
}

[label,demon_slayer_traiborn_give_bones]
if(inv_total(inv, bones) > 0 & %demon_progress > 2 & %demon_progress < 28) {
    if_close();
    inv_del(inv, bones, 1);
    mes("You give Traiborn a set of bones.");
    ~update_demon_progress;
    p_delay(1);
    @demon_slayer_traiborn_give_bones;
} else {
    @demon_slayer_traiborn_stop_giving_bones;
}

[proc,traiborn_dialogue_options](int $demon_progress)(int)
if ($demon_progress < 2 | %demon_progress > 28) {
   return(~p_choice2("What's a thingummywut?", 1, "Teach me to be a mighty and powerful wizard.", 2));
} else if ($demon_progress = 2) {
   return (~p_choice3("What's a thingummywut?", 1, "Teach me to be a mighty and powerful wizard.", 2, "I need to get a key given to you by Sir Prysin.", 3));
}

[label,traiborn_dialogue]
~chatnpc(shock, "Ello young thingummywut.");
def_int $option = ~traiborn_dialogue_options(%demon_progress);

if ($option = 1) {
    ~chatplayer(shock, "What's a thingummywut?");
    @traiborn_thingummywut_shock;
} else if ($option = 2) {
    ~chatplayer(quiz, "Teach me to be a mighty and powerful wizard.");
    @traiborn_teach_me;
} else {
    ~chatplayer(quiz, "I need to get a key given to you by Sir Prysin");
    ~chatnpc(confused, "Sir Prysin? Who's that? What would I want his key for?");
    def_int $option_key = ~p_choice3("He told me you were looking after it for him.", 1, "He's one of the King's knights.", 2, "Well, have you got any keys knocking around?", 3);

    if ($option_key = 1) {
        ~chatplayer(quiz, "He told me you were looking after it for him.");
        ~chatnpc(confused, "That wasn't very clever of him. I'd lose my head if it wasn't screwed on. Go and tell him to find someone else to look after his valuables in future.");
        def_int $option_insult_or_key = ~p_choice2("Okay, I'll go and tell him that.", 1, "Well, have you got any keys knocking around?", 2);

        if ($option_insult_or_key = 1) {
            ~chatplayer(long, "Okay, I'll go and tell him that.");
            ~chatnpc(long, "Oh that's great, if it wouldn't be too much trouble.");
            @demon_slayer_traiborn_be_off_keys_around;
        } else {
            @demon_slayer_traiborn_keys_around;
        }
    } else if ($option_key = 2) {
        ~chatplayer(quiz, "He's one of the King's knights.");
        ~chatnpc(neutral, "Say, I remember a knight with a key. He had nice shoes... | ...and didn't like my homemade spinach rolls. Would you like a spinach roll?");
        @traiborn_spinach_roll;
    } else {
        @demon_slayer_traiborn_keys_around;
    }
}

[label,demon_slayer_traiborn_lost_key]
~chatplayer(sad, "I've lost the key you gave to me.");
~chatnpc(confused, "Yes I know, it was returned to me. If you want it back you're going to have to collect another 25 sets of bones");
~set_demon_progress(3);

[label,traiborn_teach_me]
~chatnpc(shock, "Wizard eh? You don't want any truck with that sort. They're not to be trusted. That's what I've heard anyways.");
def_int $option = ~p_choice2("So aren't you a wizard?", 1, "Oh I'd better stop talking to you then.", 2);

if ($option = 1) {
    ~chatplayer(quiz, "So aren't you a wizard?");
    ~chatnpc(angry, "How dare you? Of course I'm a wizard. Now don't be so cheeky or I'll turn you into a frog.");
} else {
    ~chatplayer(quiz, "Oh I'd better stop talking to you then.");
    ~chatnpc(long, "Cheerio then. It was nice chatting to you.");
}

[label,traiborn_thingummywut_shock]
~chatnpc(shock, "A thingummywut? Where? Where?");
~chatnpc(shock, "Those pesky thingummywuts. They get everywhere. They leave a terrible mess too.");
def_int $option = ~p_choice2("Err you just called me a thingummywut.", 1, "Tell me what they look like and I'll mash 'em.", 2);

if ($option = 1) {
    ~chatplayer(long, "Err you just called me thingummywut.");
    @traiborn_thingummywut_player;
} else {
    ~chatplayer(angry, "Tell me what they look like and I'll mash 'em.");
    @traiborn_thingummywut_mash;
}

[label,traiborn_thingummywut_player]
~chatnpc(shock, "You're a thingummywut? I've never seen one up close before. They said I was mad!");
~chatnpc(shock, "Now you are my proof! There ARE thingummywuts in this tower. Now where can I find a cage big enough to keep you?");
def_int $option = ~p_choice2("Err I'd better be off really.", 1, "They're right, you are mad.", 2);

if ($option = 1) {
    @traiborn_be_off;
} else {
    @traiborn_mad;
}

[label,traiborn_thingummywut_mash]
~chatnpc(shock, "Don't be ridiculous. No-one has ever seen one.");
~chatnpc(shock, "They're invisible, or a myth, or a figment of my imagination. Can't remember which right now.");

[label,traiborn_be_off]
~chatplayer(quiz, "Err I'd better be off really.");
~chatnpc(confused, "Oh ok, have a good time, and watch out for sheep! They're more cunning than they look.");

[label,traiborn_mad]
~chatplayer(long, "They're right, you are mad.");
~chatnpc(sad, "That's a pity. I thought maybe there were winding me up.");

[label,demon_slayer_traiborn_keys_around]
~chatplayer(quiz, "Well, have you got any keys knocking around?");
~chatnpc(confused, "Now you come to mention it, yes I do have a key. It's in my special closet of valuable stuff. Now how do I get into that?");
~chatnpc(confused, "I sealed it using one of my magic rituals. So it would make sense that another ritual would open it again.");
~chatplayer(quiz, "So do you know what ritual to use?");
~chatnpc(quiz, "let me think a second.");
~chatnpc(confused, "Yes a simple drazier style ritual should suffice. Hmm, main problem with that is I'll need 25 sets of bones. Now where am I going to get hold of something like that?");

def_int $option = ~p_choice2(" Hmm, that's too bad. I really need that key.", 1, "I'll get the bones for you.", 2);
if ($option = 1) {
    ~chatplayer(sad, "Hmm, that's too bad. I really need that key");;
    ~chatnpc(quiz, "Ahh well, sorry I couldn't be any more help.");
} else {
    ~chatplayer(quiz, "I'll help get the bones for you.");
    ~chatnpc(long, "Ooh that would be very good of you.");
    ~chatplayer(quiz, "Okay, I'll speak to you when I've got some bones.");
    if (%demon_progress = 2) {
        ~update_demon_progress;
    }
}

[label,demon_slayer_traiborn_be_off_keys_around]
def_int $option = ~p_choice2("Err I'd better be off really.", 1, "Well, have you got any keys knocking around?", 2);
if ($option = 1) {
    @traiborn_be_off;
} else {
    @demon_slayer_traiborn_keys_around;
}

[label,traiborn_spinach_roll]
def_int $option = ~p_choice2("Yes please.", 1, "Just tell me if you have the key.", 2);

if ($option = 1) {
    ~chatplayer(neutral, "Yes please.");
    ~objbox(spinach_roll, "Traiborn digs around in the pockets of his robes.|After a few moments he triumphantly presents you with|a spinach roll.");
    inv_add(inv, spinach_roll, 1);
    ~chatplayer(neutral, "Thank you very much.");
    @demon_slayer_traiborn_be_off_keys_around;
} else {
    ~chatplayer(quiz, "Just tell me if you have the key.");
    ~chatnpc(confused, "The key? The key to what?");
    ~chatnpc(confused, "There's more than one key in the world don't you know? Would be a bit odd if there was only the one.");
    def_int $option_silverlight_or_insult = ~p_choice2("It's the key to get a sword called Silverlight.", 1, "You've lost it, haven't you?", 2);

    if ($option_silverlight_or_insult = 1) {
        ~chatplayer(quiz, "It's the key to get a sword called Silverlight.");
        ~chatnpc(confused, "Silverlight? Never heard of that.|Sounds a good name for a ship. Are you sure it's not the name of a ship, rather than a sword?");
        def_int $option_ship_or_keys = ~p_choice2("Yeah, pretty sure.", 1, "Are you sure it's not the name of a ship, rather than a sword?", 2);

        if ($option_ship_or_keys = 1) {
            ~chatplayer(quiz, "Yeah, pretty sure.");
            ~chatnpc(confused, "That's a pity. Waste of a name.");
            @demon_slayer_traiborn_be_off_keys_around;
        } else {
            @demon_slayer_traiborn_keys_around;
        }
    } else {
        ~chatplayer(quiz, "You've lost it haven't you?");
        ~chatnpc(angry, "Me? Lose things? That's a nasty accusation.");
       @demon_slayer_traiborn_keys_around;
    }
}

[label,demon_slayer_traiborn_stop_giving_bones]
if (%demon_progress = 28) {
    ~chatnpc(long, "Hurrah! That's all 25 sets of bones.");
    @demon_slayer_traiborn_incantation;
} else {
    ~chatplayer(quiz, "That's all of them.");
    ~chatnpc(confused, "I still need more.");
    ~chatplayer(quiz, "Ok, I'll look for some more.");
}

[label,demon_slayer_traiborn_incantation]
~mesbox("Traiborn places the bones in a circle on the floor.");
~mesbox("Traiborn waves his arms about.");
~chatnpc(confused, "Wings of dark and colour too,|Spreading in the morning dew;|Locked away I have a key;|Return it now, please, unto me.");
~mesbox("The wizard waves his arms some more.");
if_close();
p_delay(2);
mes("Traiborn hands you a key");
inv_add(inv, demon_traiborn_key, 1);
p_delay(2);
if (%demon_progress = 28) {
    ~update_demon_progress;
}
~chatplayer(long, "Thank you very much.");
~chatnpc(long, "Not a problem for a friend of Sir What's-his-face.");