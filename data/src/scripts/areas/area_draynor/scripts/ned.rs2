[opnpc1,ned]
// Treasure Trail
if(map_members = ^true & inv_total(inv, trail_clue_easy_simple021) = 1) {
    @trail_ned;
} else if (%dragon_progress = ^quest_dragon_sailed_to_crandor) {
    // really dont know if ned should still have this after the quest is finished. 
    // Osrs does and its kinda weird because you can ask him to sail you back, 
    // with him agreeing. Even though you cant access the ship anymore.
    // For now i'll just make it so he doesnt have it after the quest is finished.
    @ned_dragon_slayer_back_in_draynor_after_visiting_crandor;
} else if (%dragon_ned_hired > 0 & %dragon_progress < ^quest_dragon_sailed_to_crandor) {
    // ned got the job
    // looks like dragon slayer overrides neds standard dialogue https://youtu.be/kCMB4I_aJJM?t=129
    @ned_dragon_slayer_hired;
} else {
    @standard_ned;
}

[opnpcu,ned]
if (last_useitem = dragonmap) {
    @give_map_to_ned;
} else {
    ~displaymessage(^dm_default);
}

[label,trail_ned]
// https://www.youtube.com/watch?v=u70m7CofypU
~chatnpc("<p,happy>Well done!");
~progress_clue_easy(trail_clue_easy_simple021, "Ned has given you another clue!");

[label,standard_ned]
~chatnpc("<p,neutral>Why hello there, me friends call me Ned.|I was a man of the sea, but it's past me now.|Could I be making or selling you some rope?");
def_int $option;
if(%prince_progress >= ^prince_spoken_osman & %prince_progress < ^prince_saved) {
    if (~quest_dragon_getting_map_parts = true & %dragon_ned_hired = ^false) {
        @multi4("You're a sailor? Could you take me to the island of Crandor?", ned_dragon_slayer_before_hired, "Ned, could you make other things from wool?", ned_other_things, "Yes, I would like some rope.", ned_start_rope_purchase, "No thanks, Ned. I don't need any.", ned_decline_rope);
    } else {
        @multi3("Ned, could you make other things from wool?", ned_other_things, "Yes, I would like some rope.", ned_start_rope_purchase, "No thanks, Ned. I don't need any.", ned_decline_rope);
    }
} else {
    if (~quest_dragon_getting_map_parts = true & %dragon_ned_hired = ^false) {
        @multi3("You're a sailor? Could you take me to the island of Crandor?", ned_dragon_slayer_before_hired, "Yes, I would like some rope.", ned_start_rope_purchase, "No thanks, Ned. I don't need any.", ned_decline_rope);
    } else {
        @multi2("Yes, I would like some rope.", ned_start_rope_purchase, "No thanks, Ned. I don't need any.", ned_decline_rope);
    }
}

[label,ned_other_things]
~chatplayer("<p,quiz>Ned, could you make other things from wool?");
~chatnpc("<p,happy>I am sure I can. What are you thinking of?");
@multi3("Could you knit me a sweater?", ned_sweater, "How about some sort of wig?", ned_wig, "Could you repair the arrow holes in the back of my shirt?", ned_arrow_holes);

[label,ned_start_rope_purchase]
~chatplayer("<p,neutral>Yes, I would like some rope.");
~chatnpc("<p,happy>Well, I can sell you some rope for 15 coins.|Or I can be making you some if you gets me 4 balls of wool.|I strands them together I does, makes em strong.");
if(inv_total(inv, ball_of_wool) < 4) {
    @multi3("Okay, please sell me some rope.", ned_rope, "That's a little more than I want to pay.", ned_more_pay, "I will go and get some wool.", ned_get_wool);
} else {
    @multi3("Okay, please sell me some rope.", ned_rope, "That's a little more than I want to pay.", ned_more_pay, "I have some balls of wool. Could you make me some rope?", ned_have_wool);
}

[label,ned_rope]
if(inv_total(inv, coins) < 15) {
    ~mesbox("You don't have enough coins to buy any rope!");
    return;
}
~chatplayer("<p,neutral>Okay, please sell me some rope.");
~chatnpc("<p,happy>There you go, finest rope in RuneScape.");
~mesbox("You hand Ned 15 coins.|Ned gives you a coil of rope.");
inv_del(inv, coins, 15);
inv_add(inv, rope, 1);

[label,ned_decline_rope]
~chatplayer("<p,neutral>No thanks, Ned. I don't need any.");
~chatnpc("<p,neutral>Well, old Neddy is always here if you do.|Tell your friends. I can always be using the business.");

[label,ned_get_wool]
~chatplayer("<p,neutral>I will go and get some wool.");
~chatnpc("<p,neutral>Aye, you do that.|Remember, it takes 4 balls of wool to make strong rope.");

[label,ned_more_pay]
~chatplayer("<p,neutral>That's a little more than I want to pay.");
~chatnpc("<p,bored>Well, if you ever need rope that's the price. Sorry.|An old sailor needs money for a little drop o' rum.");

[label,ned_have_wool]
~chatplayer("<p,neutral>I have some balls of wool.|Could you make me some rope?");
~chatnpc("<p,happy>Sure I can.");
~mesbox("You hand over 4 balls of wool.|Ned gives you a coil of rope.");
inv_del(inv, ball_of_wool, 4);
inv_add(inv, rope, 1);

[label,ned_sweater]
~chatplayer("<p,neutral>Could you knit me a sweater?");
~chatnpc("<p,angry>Do I look like a member of a sewing circle?|Be off wi' you. I have fought monsters that would turn your hair blue.");
~chatnpc("<p,angry>I don't need to be laughed at just 'cos I am getting a bit old.");

[label,ned_arrow_holes]
~chatplayer("<p,quiz>Could you repair the arrow holes in the back of my shirt?");
~chatnpc("<p,neutral>Ah yes, it's a tough world these days.|There's a few brave enough to attack from 10 metres away.");
~mesbox("Ned pulls out a needle and attacks your shirt.");
~chatnpc("<p,happy>There you go, good as new.");
~chatplayer("<p,neutral>Thanks Ned. Maybe next time they will attack me face to face.");

[label,ned_wig]
~chatplayer("<p,quiz>How about some sort of wig?");
~chatnpc("<p,happy>Well... That's an interesting thought.|Yes, I think I could do something.|Give me 3 balls of wool and I might be able to do it.");
if(inv_total(inv, ball_of_wool) < 3) {
    ~chatplayer("<p,happy>Great, I will get some. I think a wig would be useful.");
    return;
}
@multi2("I have that now. Please, make me a wig.", ned_make_wig, "I will come back when I need you to make me one.", ned_come_back);

[label,ned_make_wig]
~chatplayer("<p,neutral>I have that now. Please, make me a wig.");
~chatnpc("<p,neutral>Okay, I will have a go.");
~mesbox("You hand Ned 3 balls of wool. Ned works with the wool.|His hands move with a speed you couldn't imagine.");
if(inv_total(inv, ball_of_wool) >= 3) {
    inv_del(inv, ball_of_wool, 3);
    inv_add(inv, plainwig, 1);
    ~chatnpc("<p,happy>Here you go, how's that for a quick effort?|Not bad I think!");
    ~mesbox("Ned gives you a pretty good wig.");
    ~chatplayer("<p,neutral>Thanks Ned, there's more to you than meets the eye.");
}

[label,ned_come_back]
~chatplayer("<p,neutral>I will come back when I need you to make me one.");

[label,ned_dragon_slayer_before_hired]
~chatplayer("<p,quiz>You're a sailor? Could you take me to the island of Crandor?");
~chatnpc("<p,sad>Well, I was a sailor. I've not been able to get work at|sea these days, though. They say I'm too old.");
// from https://web.archive.org/web/20060828222012/http://www.trillionareguild.com/runescape/dragon.php
// 14) From this point you must go to Draynor Village and talk to Ned, ask him 
// to sail you to Crandor on Lady Lumbridge, he will say if you find a seaworthy 
// ship he will take you anywhere.
// 
// this lines up perfectly with rsc dialogue, this dialogue is much different in osrs.
~mesbox("There is a wistfull look in Ned's eyes.");
~chatnpc("<p,sad>I miss those days.|If you could get me a ship I would take you anywhere."); // :3
%dragon_ned_hired = ^true;
if (%dragon_progress >= ^quest_dragon_spoken_to_oziach & %dragon_progress < ^quest_dragon_bought_ship) {
    ~chatplayer("<p,neutral>I will work on finding a sea worthy ship then."); //rsc
    return;
}
@ned_so_wheres_your_ship;


[label,ned_so_wheres_your_ship]
if (%dragon_progress = ^quest_dragon_repaired_ship) {
    ~chatplayer("<p,happy>As it happens I do have a ship ready to sail!");
    ~chatnpc("<p,happy>That'd be grand, where is it?");
    ~chatplayer("<p,neutral>It's called Lady Lumbridge and it's docked in Port Sarim."); // rsc says "the Lumbridge Lady"
    ~chatnpc("<p,neutral>I'll meet you there then."); // guessed
    ~chatnpc("<p,neutral>Just show me the map and we can get ready to go!");
    return;
}

if (%dragon_progress = ^quest_dragon_bought_ship) {
    ~chatplayer("<p,neutral>It's called Lady Lumbridge and it's docked in Port Sarim."); // rsc
    ~chatnpc("<p,neutral>That old pile of junk? Last I heard, she wasn't seaworthy."); // osrs
    ~chatplayer("<p,neutral>Yes, good point. I'd better go and fix her up."); // osrs
    return;
}

if (%dragon_progress >= ^quest_dragon_spoken_to_oziach & %dragon_progress < ^quest_dragon_bought_ship) {
    ~chatplayer("<p,neutral>Well, I don't actually have a ship yet."); // osrs
    ~chatnpc("<p,neutral>Ah, well. Let me know when you do. The place to look would be Port Sarim."); // osrs
    return;
}

[label,ned_dragon_slayer_hired]
~chatnpc("<p,neutral>Hello there lad.");
def_int $choice = ~p_choice2("So are you going to take me to Crandor Island now then?", 1, "So are you still up to sailing this ship?", 2);
if ($choice = 1) {
    // island is lowercase. Not a typo
    // https://youtu.be/kCMB4I_aJJM?list=PLn23LiLYLb1aqrojPTi1_Np81LJku2Nd0&t=134
    ~chatplayer("<p,quiz>So are you going to take me to Crandor island now then?");
    if (%dragon_progress < ^quest_dragon_repaired_ship) {
        ~chatnpc("<p,neutral>I said I would and old Ned is a man of his word!"); // osrs
        ~chatnpc("<p,quiz>So where's your ship?");
        @ned_so_wheres_your_ship;
    } else if ((%dragon_progress = ^quest_dragon_ned_given_map | %dragon_progress = ^quest_dragon_sailed_to_crandor)) {
        ~chatnpc("<p,neutral>I said I would and old Ned is a man of his word! I'll meet you on board the Lady Lumbridge in Port Sarim.");
        return;
    }
    ~chatnpc("<p,neutral>Ok, show me the map and we'll set sail now.");
    if (inv_total(inv, dragonmap) < 1) {
        return;
    }
    @give_map_to_ned;
}
if ($choice = 2) {
    ~chatplayer("<p,quiz>So are you still up to sailing this ship?");
    ~chatnpc("<p,neutral>I said I would and old Ned is a man of his word!"); // missing dialogue. So i took a part of his dialogue from osrs 
}

[label,ned_dragon_slayer_back_in_draynor_after_visiting_crandor]
~chatnpc("<p,neutral>Hello there lad.");
// repairing ship again existed: https://web.archive.org/web/20051104084237/http://www.nomadgaming.com/index.php?page=rs2_dragonslayer
//osrs
~chatplayer("<p,confused>How did you get back from Crandor?");
~chatnpc("<p,neutral>I got towed back by a passing friendly whale.");
~chatnpc("<p,quiz>Anyway, would you like me to make some rope while you're here?");
def_int $choice;
if(%prince_progress >= ^prince_spoken_osman & %prince_progress < ^prince_saved) {
    $choice = ~p_choice4("Can you take me to Crandor again?", 1, "Ned, could you make other things from wool?", 4, "Yes, I would like some rope.", 2, "No thanks Ned, I don't need any.", 3);
} else {
    $choice = ~p_choice3("Can you take me to Crandor again?", 1, "Yes, I would like some rope.", 2, "No thanks Ned, I don't need any.", 3);
}
if ($choice = 1) {
    ~chatplayer("<p,quiz>Can you take me to Crandor again?");
    ~chatnpc("<p,neutral>Aye, that I could. I'll meet you at the Port Sarim docks again. I'll try to steer a little better this time!");
    %dragon_ned_hired = ^true;
    return;
}
if ($choice = 2) {
    @ned_start_rope_purchase;
}
if ($choice = 3) {
    @ned_decline_rope;
}
if($choice = 4) {
    @ned_other_things;
}

[label,give_map_to_ned]
if (%dragon_ned_hired = 0 | %dragon_progress ! ^quest_dragon_repaired_ship) {
    ~displaymessage(^dm_default);
    return;
}
~mesbox("You give the map to Ned.");
inv_del(inv, dragonmap, inv_total(inv, dragonmap));
%dragon_progress = ^quest_dragon_ned_given_map;
~chatplayer("<p,quiz>Here you go.");
~chatnpc("<p,neutral>I'll meet you at the ship then.");