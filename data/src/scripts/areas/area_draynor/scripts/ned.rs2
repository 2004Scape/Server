[opnpc1,ned]
// Treasure Trail
if(inv_total(inv, trail_clue_easy_simple_exp021) = 1) {
    @trail_ned;
} else {
    @standard_ned;
}

[label,trail_ned]
~chatnpc("default", "Well done!");
~trail_clue_easy_progress;
inv_del(inv, trail_clue_easy_simple_exp021, 1);
if(~trail_clue_easy_complete = true) {
    ~trail_clue_easy_reward;
} else {
    inv_add(inv, ~trail_clue_easy_getrandom, 1);
    ~objbox(trail_clue_hard_map001, "Ned has given you another clue!");
}

[label,standard_ned]
def_string $noun = text_gender("lad", "lass");
~chatnpc(default, "Why, hello there, <$noun>. Me friends call me Ned. I was a man of the sea, but it's past me now. Could I be making or selling you some rope?");
def_int $option;
if(%prince_progress >= 2 & %prince_progress < 8) {
    $option = ~p_choice3("Ned, could you make other things from wool?", 3, "Yes, I would like some rope.", 1, "No thanks, Ned. I don't need any.", 2);
} else {
    $option = ~p_choice2("Yes, I would like some rope.", 1, "No thanks, Ned. I don't need any.", 2);
}
if($option = 1) {
    ~chatplayer(quiz, "Yes, I would like some rope.");
    ~chatnpc(default, "Well, I can sell you some rope for 15 coins. Or I can be making you some if you gets me 4 balls of wool. I strands them together I does, makes em strong.");
    if(inv_total(inv, ball_of_wool) < 4) {
        @multi3("Okay, please sell me some rope.", ned_rope, "That's a little more than I want to pay.", ned_more_pay, "I will go and get some wool.", ned_get_wool);
    } else {
        @multi3("Okay, please sell me some rope.", ned_rope, "That's a little more than I want to pay.", ned_more_pay, "I have some balls of wool. Could you make me some rope?", ned_have_wool);
    }
} else if($option = 2) {
    ~chatplayer(quiz, "No thanks, Ned. I don't need any.");
    ~chatnpc(default, "Well, old Neddy is always here if you do. Tell your friends. I can always be using the business.");
} else if($option = 3) {
    ~chatplayer(happy, "Ned, could you make other things from wool?");
    ~chatnpc(long, "I am sure I can. What are you thinking of?");
    @multi3("Could you knit me a sweater?", ned_sweater, "How about some sort of wig?", ned_wig, "Could you repair the arrow holes in the back of my shirt?", ned_arrow_holes);
}

[label,ned_rope]
if(inv_total(inv, coins) < 15) {
    ~mesbox("You don't have enough coins to buy any rope!");
    return;
}
~chatplayer(quiz, "Okay, please sell me some rope.");
~chatnpc(long, "There you go, finest rope in RuneScape.");
~mesbox("You hand Ned 15 coins. Ned gives you a coil of rope.");
inv_del(inv, coins, 15);
inv_add(inv, rope, 1);

[label,ned_get_wool]
~chatplayer(quiz, "I will go and get some wool.");
~chatnpc(default, "Aye, you do that. Remember, it takes 4 balls of wool to make strong rope.");

[label,ned_more_pay]
~chatplayer(quiz, "That's a little more than I want to pay.");
~chatnpc(drunk, "Well, if you ever need rope that's the price. Sorry. An old sailor needs money for a little drop o' rum.");

[label,ned_have_wool]
~chatplayer(default, "I have some balls of wool.|Could you make me some rope?");
~chatnpc(long, "Sure I can.");
~mesbox("You hand over four balls of wool. Ned gives you a coil of rope.");
inv_del(inv, ball_of_wool, 4);
inv_add(inv, rope, 1);

[label,ned_sweater]
~chatplayer(quiz, "Could you knit me a sweater?");
~chatnpc(angry, "Do I look like a member of a sewing circle? Be off wi' you. I have fought monsters that would turn your hair blue.");
~chatnpc(sad, " I don't need to be laughed at just 'cos I am getting a bit old.");

[label,ned_arrow_holes]
~chatplayer(happy, "Could you repair the arrow holes in the back of my shirt?");
~chatnpc(default, "Ah yes, it's a tough world these days. There's a few brave enough to attack from 10 metres away.");
~mesbox("Ned pulls out a needle and attacks your shirt.");
~chatnpc(long, "There you go, good as new.");
~chatplayer(default, "Thanks Ned. Maybe next time they will attack me face to face.");

[label,ned_wig]
~chatplayer(happy, "How about some sort of wig?");
~chatnpc(neutral, "Well... That's an interesting thought. Yes, I think I could do something. Give me 3 balls of wool and I might be able to do it.");
if(inv_total(inv, ball_of_wool) < 3) {
    ~chatplayer(neutral, "Great, I will get some. I think a wig would be useful.");
    return;
}
@multi2("I have that now. Please, make me a wig.", ned_make_wig, "I will come back when I need you to make me one.", ned_come_back);

[label,ned_make_wig]
~chatplayer(quiz, "I have that now. Please, make me a wig.");
~chatnpc(quiz, "Okay, I will have a go.");
~mesbox("You hand Ned 3 balls of wool. Ned works with the wool.|His hands move with a speed you couldn't imagine.");
if(inv_total(inv, ball_of_wool) >= 3) {
    inv_del(inv, ball_of_wool, 3);
    inv_add(inv, wig_grey, 1);
    ~chatnpc(neutral, "Here you go, how's that for a quick effort?|Not bad I think!");
    ~mesbox("Ned gives you a pretty good wig.");
    ~chatplayer(quiz, "Thanks Ned, there's more to you than meets the eye.");
}


[label,ned_come_back]
~chatplayer(quiz, "I will come back when I need you to make me one.");