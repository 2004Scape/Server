// https://youtu.be/6PFJRUDSKs4

[opnpc1,morgan]
switch_int (%vampire_progress) {
    case ^quest_vampire_not_started : @morgan_before_vampire_slayer;
    case ^quest_vampire_started, ^quest_vampire_spoke_to_harlow : @morgan_during_vampire_slayer;
    case ^vampire_complete : @morgan_after_vampire_slayer;
    case default : ~displaymessage(^dm_default);
}

[label,morgan_before_vampire_slayer]
~chatnpc("<p,shock>Please please help us, bold adventurer!");
~chatplayer("<p,happy>What's the problem?");
~chatnpc("<p,shock>Our little village has been dreadfully ravaged by an evil vampire! He lives in the basement of the manor to the north, we need someone to get rid of him once and for all!");

switch_int (~p_choice3("No, vampires are scary!", 1, "Ok, I'm up for an adventure.", 2, "Have you got any tips on killing the vampire?", 3)) {
    case 1 : @morgan_decline_vampire_slayer;
    case 2 : ~chatplayer("<p,quiz>Okay, I'm up for an adventure.");
    case 3 : ~chatplayer("<p,quiz>Have you got any tips on killing the vampire?");
    case default : ~displaymessage(^dm_default);
}

~chatnpc("<p,neutral>I think first you should seek help. I have a friend who is a retired vampire hunter, his name is Dr. Harlow. He may be able to give you some tips. He can normally be found in the Jolly Boar Inn in Varrock, he's a bit of");
~chatnpc("<p,quiz>an old soak these days. Mention his old friend Morgan, I'm sure he wouldn't want me killed by a vampire.");
~quest_vampire_set_progress(^quest_vampire_started);
~chatplayer("<p,quiz>I'll look him up then.");

[label,morgan_decline_vampire_slayer]
~chatplayer("<p,shock>No, vampires are scary!");
~chatnpc("<p,sad>I don't blame you.");

[label,morgan_during_vampire_slayer]
~chatnpc("<p,happy>How are you doing with the quest?");
~chatplayer("<p,confused>I'm still working on it.");
~chatnpc("<p,shock>Please hurry! Every day we live in fear that we will be the vampire's next victim!");

[label,morgan_after_vampire_slayer]
~chatplayer("<p,happy>I have slain the foul creature!");
~chatnpc("<p,neutral>Thank you, thank you! You will always be a hero in our village!");
