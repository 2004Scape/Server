[opnpc1,professor_oddenstein]
switch_int(%haunted_progress) {
    case ^haunted_not_started, ^haunted_complete: @oddenstein_nonquest;
    case ^haunted_started: @oddenstein_looking;
    case ^haunted_spoken_to_oddenstein: @oddenstein_items;
}

[label,oddenstein_nonquest]
~chatnpc(neutral, "Be careful in here.|Lots of dangerous equipment in here.");
@multi2("What does this machine do?", oddenstein_machine, "Is this your house?", oddenstein_house);

[label,oddenstein_machine]
~chatplayer(quiz, "What does this machine do?");
~chatnpc(neutral, "Nothing at the moment,|as it's broken.|It's meant to be a transmutation machine.");
~chatnpc(neutral, "It has also spent time as a time travel machine,|and a dramatic lightning generator,|and a thing for generating monsters.");

[label,oddenstein_house]
~chatplayer(quiz, "Is this your house?");
~chatnpc(neutral, "No, I'm just one of the tenants.|It belongs to the count|who lives in the basement.");

[label,oddenstein_looking]
def_int $option = ~p_choice3("I'm looking for a guy called Ernest.", 1, "What does this machine do?", 2, "Is this your house?", 3);
if($option = 1) {
    ~chatplayer(quiz, "I'm looking for a guy called Ernest.");
    ~chatnpc(happy, "Ah Ernest, top notch bloke.|He's helping me with my experiments.");
    ~chatplayer(quiz, "So you know where he is then?");
    ~chatnpc(happy, "He's that chicken over there.");
    ~chatplayer(confused, "Ernest is a chicken?|Are you sure?");
    ~chatnpc(happy, "Oh, he isn't normally a chicken,|or at least he wasn't|until he helped me test my pouletmorph machine.");
    ~chatnpc(happy, "It was originally going to be called a transmutation machine. But after testing pouletmorph seems more appropriate.");
    @multi2("I'm glad Veronica didn't actually get engaged to a chicken.", oddenstein_chicken, "Change him back this instant!", oddenstein_change_back);
} else if($option = 2) {
    @oddenstein_machine;
} else if($option = 3) {
    @oddenstein_house;
}

[label,oddenstein_chicken]
~chatplayer(neutral, "I'm glad Veronica didn't actually get engaged to a chicken.");
~chatnpc(quiz, "Who's Veronica?");
~chatplayer(neutral, "Ernest's fiancee.|She probably doesn't want to marry a chicken.");
~chatnpc(quiz, "Ooh I dunno.|She could have free eggs for breakfast.");
~chatplayer(neutral, "I think you'd better change him back.");
@oddenstein_not_easy;

[label,oddenstein_change_back]
~chatplayer(angry, "Change him back this instant!");
@oddenstein_not_easy;

[label,oddenstein_not_easy]
~chatnpc(confused, "Um, it's not so easy...");
~chatnpc(sad, "My machine is broken,|and the house gremlins|have run off with some vital bits.");
~chatplayer(neutral, "Well I can look out for them.");
~chatnpc(neutral, "That would be a help.|They'll be somewhere in the manor house or its grounds,|the gremlins never go further than the entrance gate.");
%haunted_progress = ^haunted_spoken_to_oddenstein;
~chatnpc(neutral, "I'm missing the pressure gauge and a rubber tube.|They've also taken my oil can,|which I'm going to need to get this thing started again.");

[label,oddenstein_items]
~chatnpc(quiz, "Have you found anything yet?");
if(inv_total(inv, pressure_gauge) > 0 & inv_total(inv, oil_can) > 0 & inv_total(inv, rubber_tube) > 0) {
    ~chatplayer(happy, "I have everything!");
    ~chatnpc(happy, "Give 'em here then.");
    if_close;
    // osrs has this as messages with p_delay, but based of rsc and similar scenarios in non-reworked quests, this is probably what it used to be (confirm this if possible)
    mes("You give a rubber tube, a pressure gauge, ");
    mes("and a can of oil to the professor.");
    p_delay(1);
    mes("Oddenstein starts up the machine.");
    p_delay(2);
    mes("The machine hums and shakes.");
    p_delay(2);
    mes("Suddenly a ray shoots out of the machine at the chicken.");
    p_delay(1);
    ~change_ernest;
    inv_del(inv, pressure_gauge, 1);
    inv_del(inv, oil_can, 1);
    inv_del(inv, rubber_tube, 1);
    queue(haunted_quest_complete, 0); // reward is queued as soon as changetype completes in OSRS
    ~chatnpc_specific("Ernest", ernest_human, happy, "Thank you <text_gender("sir", "m'lady")>.|It was dreadfully irritating being a chicken.|How can I ever thank you?");
    ~chatplayer(shifty, "Well a cash reward is always nice...");
    ~chatnpc_specific("Ernest", ernest_human, neutral, "Of course, of course.");
    ~mesbox("Ernest hands you 300 coins.");
} else if(inv_total(inv, pressure_gauge) > 0 | inv_total(inv, oil_can) > 0 | inv_total(inv, rubber_tube) > 0) {
    def_string $player_items = "";
    def_string $oddenstein_items = "";
    if(inv_total(inv, oil_can) > 0) {
        $player_items = append($player_items, "|I have a can of oil.");
    } else {
        $oddenstein_items = append($oddenstein_items, "|A can of oil.");
    }
    if(inv_total(inv, pressure_gauge) > 0) {
        $player_items = append($player_items, "|I have a pressure gauge.");
    } else {
        $oddenstein_items = append($oddenstein_items, "|A rubber tube.");
    }
    if(inv_total(inv, rubber_tube) > 0) {
        $player_items = append($player_items, "|I have a rubber tube.");
    } else {
        $oddenstein_items = append($oddenstein_items, "|A pressure gauge.");
    }
    mes($player_items);
    ~chatplayer(neutral, "I have found some of the things you need:<$player_items>");
    ~chatnpc(bored, "Well that's a start, you still need to find:<$oddenstein_items>");
    ~chatplayer(neutral, "Ok, I'll try and find them.");
} else {
    ~chatplayer(sad, "I'm afraid I don't have any yet!");
    ~chatnpc(neutral, "I need a rubber tube, a pressure gauge and a can of oil. Then your friend can stop being a chicken.");
}