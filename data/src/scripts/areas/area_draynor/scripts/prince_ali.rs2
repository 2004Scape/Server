[opnpc1,prince_ali]
if(%prince_progress = ^prince_tied_keli) {
    @prince_rescue;
}
~chatnpc("<p,neutral>I owe you my life for that escape.|You cannot help me this time, they know who you are.|Go in peace, friend of Al-Kharid.");

[label,prince_rescue]
~chatplayer("<p,happy>Prince, I come to rescue you.");
~chatnpc("<p,neutral>That is very very kind of you, how do I get out?");
~chatplayer("<p,happy>With a disguise. I have removed the Lady Keli.|She is tied up, but will not stay tied up for long.");
if(inv_total(inv, bronze_key) >= 1 & inv_total(inv, wig_blonde) >= 1 & inv_total(inv, pink_skirt) >= 1 & inv_total(inv, paste) >= 1) {
    ~chatplayer("<p,neutral>Take this disguise, and this key.");
    ~mesbox("You hand over the disguise and key over to Prince Ali.");
    inv_del(inv, bronze_key, 1);
    inv_del(inv, wig_blonde, 1);
    inv_del(inv, pink_skirt, 1);
    inv_del(inv, paste, 1);
    %prince_progress = ^prince_saved;
    npc_changetype(prince_ali_disguised, ^max_32bit_int);
    ~chatnpc_specific("Prince Ali", lady_keli, "<p,happy>Thank you my friend, I must leave you now.|My father will pay you well for this.");
    ~chatplayer("<p,happy>Go to Leela, she is close to here.");
    npc_del;
    ~mesbox("The prince has escaped, well done!|You are now a friend of Al-Kharid|and may pass through the Al-Kharid toll gate for free.");
} 


[opnpcu,prince_ali]
if(%prince_progress = ^prince_tied_keli & (last_useitem = pink_skirt | last_useitem = wig_blonde | last_useitem = paste)) {
    @prince_rescue;
}
~displaymessage(^dm_default);