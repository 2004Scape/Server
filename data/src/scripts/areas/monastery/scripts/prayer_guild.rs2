[label,isnt_this_place_built_a_bit_out_the_way]
~chatplayer(happy, "Isn't this place built a bit out the way?");
~chatnpc(quiz, "We like it that way actually! We get disturbed less. We|still get rather a large amount of travellers looking for|sanctuary and healing here as it is!");

[oploc1,loc_2641]
if (%prayer_guild < 1) {
    def_npc_uid $closest_npc = ~search_prayer_guild();
    if ($closest_npc = null) {
        // else just mesbox
        ~mesbox("A nearby monk glares at you. Maybe you should talk to him before climbing the ladder.");
        return;
    }
    // set active npc
    npc_finduid($closest_npc);
    facesquare(npc_coord);
    if (npc_type = monk) {
        ~chatnpc(quiz, "You'll need to talk to Abbot Langley about that. He's|usually to be found walking the halls of the monastery.");
        return;
    }
    if (npc_type = abbot_langley) {
        ~chatnpc(quiz, "Only members of our order can go up there.");
        @ask_to_join_abbot_langley2;
    }
}
@climb_ladder(movecoord(coord, 0, 1, 0), true);

[label,ask_to_join_abbot_langley2]
def_int $choice = ~p_choice2("Well can I join your order?", 1, "Oh, sorry.", 2);
if ($choice = 2) {
    ~chatplayer(sad, "Oh, sorry.");
    return;
}
~chatplayer(quiz, "Well can I join your order?");
if (stat(prayer) < 31) {
    ~chatnpc(quiz, "No. I am sorry, but I feel you are not devout enough.");
    return;
}
~chatnpc(neutral, "Ok, I see you are someone suitable for our order. You|may join.");
%prayer_guild = 1;

[proc,search_prayer_guild]()(npc_uid)
def_int $closest = 5;
def_npc_uid $closest_npc = null;
// search current zone for monk
npc_findallzone(coord);
while (npc_findnext = true & distance(coord, npc_coord) <= $closest) {
    if (npc_type = monk) {
        $closest_npc = npc_uid;
    } else if (npc_type = abbot_langley) {
        return(npc_uid);
    }
}
// search middle zone for monk
npc_findallzone(^prayer_guild_bottom_middle);
while (npc_findnext = true & distance(coord, npc_coord) <= $closest) {
    if (npc_type = monk) {
        $closest_npc = npc_uid;
    } else if (npc_type = abbot_langley) {
        return(npc_uid);
    }
}
// search middle top zone for monk
npc_findallzone(^prayer_guild_top_middle);
while (npc_findnext = true & distance(coord, npc_coord) <= $closest) {
    if (npc_type = monk) {
        $closest_npc = npc_uid;
    } else if (npc_type = abbot_langley) {
        return(npc_uid);
    }
}
// search top left zone for monk
npc_findallzone(^prayer_guild_top_left);
while (npc_findnext = true & distance(coord, npc_coord) <= $closest) {
    if (npc_type = monk) {
        $closest_npc = npc_uid;
    } else if (npc_type = abbot_langley) {
        return(npc_uid);
    }
}
// search top right zone for monk
npc_findallzone(^prayer_guild_top_right);
while (npc_findnext = true & distance(coord, npc_coord) <= $closest) {
    if (npc_type = monk) {
        $closest_npc = npc_uid;
    } else if (npc_type = abbot_langley) {
        return(npc_uid);
    }
}

return($closest_npc);