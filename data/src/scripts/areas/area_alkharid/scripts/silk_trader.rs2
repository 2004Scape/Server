// dialogue taken from osrs
[opnpc1,silk_trader]
~chatnpc("<p,neutral>Do you want to buy any fine silks?");
def_int $choice = 0;
if (%crest_progress = ^crest_caleb_where) {
    $choice = ~p_choice3("How much are they?", 1, "No. Silk doesn't suit me.", 2, "I'm in search of a man named Avan Fitzharmon.", 3);    
}
else {
    $choice = ~p_choice2("How much are they?", 1, "No. Silk doesn't suit me.", 2);
}

if ($choice = 2) {
    ~chatplayer("<p,neutral>No. Silk doesn't suit me.");
    return;
}
if ($choice = 3) {
    ~chatplayer("<p,neutral>I'm in search of a man named Avan Fitzharmon.");
    ~chatnpc("<p,neutral>Can't say as I've seen him... I'm sure if he's been to Al Kharid recently someone around here will have though.");
    return;
}

~chatplayer("<p,neutral>How much are they?");
~chatnpc("<p,neutral>3 gp.");

$choice = ~p_choice2("No. That's too much for me", 1, "Okay, that sounds good.", 2);

if ($choice = 2) {
    ~chatplayer("<p,neutral>Okay, that sounds good.");
    if (inv_total(inv, coins) < 3) {
        ~chatplayer("<p,sad>Oh dear. I don't have enough money.");
        ~chatnpc("<p,angry>Well, come back when you do have some money!");
        return;
    }
    ~objbox(silk, "You buy some silk for 3 gp.", 250, 0, divide(^objbox_height, 2));
    inv_del(inv, coins, 3);
    inv_add(inv, silk, 1);
    return;
}

~chatplayer("<p,neutral>No. That's too much for me.");
~chatnpc("<p,quiz>2 gp and that's as low as I'll go.");
~chatnpc("<p,sad>I'm not selling it for any less. You'll probably go and|sell it in Varrock for a profit, anyway.");

$choice = ~p_choice2("2 gp sounds good.", 1, "No, really. I don't want it.", 2);

if ($choice = 2) {
    ~chatplayer("<p,neutral>No, really. I don't want it.");
    ~chatnpc("<p,neutral>Okay, but that's the best price you're going to get.");
    return;
}

~chatplayer("<p,neutral>2 gp sounds good.");
if (inv_total(inv, coins) < 2) {
    ~chatplayer("<p,sad>Oh dear. I don't have enough money.");
    ~chatnpc("<p,angry>Well, come back when you do have some money!");
    return;
}
~objbox(silk, "You buy some silk for 2 gp.", 250, 0, divide(^objbox_height, 2));
inv_del(inv, coins, 2);
inv_add(inv, silk, 1);