[opnpc1,kebab_seller]
// https://www.youtube.com/watch?v=EJfNdZNjDvE
if(map_members = ^true & inv_total(inv, trail_clue_medium_anagram006) > 0) {
    @trail_kebab_seller;
}

~chatnpc("<p,quiz>Would you like to buy a nice kebab? Only one gold.");
if (%crest_progress = ^crest_caleb_where) {
    @multi3("I think I'll give it a miss.", kebab_seller_miss, "Yes please.", kebab_seller_buy, "I'm in search of a man named Avan Fitzharmon.", kebab_seller_crest);
}
else {
    @multi2("I think I'll give it a miss.", kebab_seller_miss, "Yes please.", kebab_seller_buy);
}

[label,kebab_seller_miss]
~chatplayer("<p,neutral>I think I'll give it a miss.");

[label,kebab_seller_buy]
~chatplayer("<p,neutral>Yes please.");
if(inv_total(inv, coins) < 1) {
    ~chatplayer("<p,sad>Oops, I forgot to bring any money with me.");
    ~chatnpc("<p,neutral>Come back when you have some.");
    return;
}
inv_del(inv, coins, 1);
inv_add(inv, kebab, 1);
mes("You buy a kebab.");

[label,trail_kebab_seller]
if(inv_total(inv, trail_clue_medium_anagram006_challenge) > 0) {
    @trail_challengenpc_prompt("<p,neutral>Please enter the answer to the riddle.", "<p,happy>Correct!", "<p,angry>No that's not right, think again.", 
    "The salesman has given you another clue scroll!", trail_clue_medium_anagram006, trail_clue_medium_anagram006_challenge);
}
~chatnpc("<p,confused>I've got a little problem for you to solve.");
inv_add(inv, trail_clue_medium_anagram006_challenge, 1);
~objbox(trail_clue_medium_anagram006_challenge, "The salesman has given you a challenge scroll!", 250, 0, 0);

[label,kebab_seller_crest]
~chatplayer("<p,neutral>I'm in search of a man named Avan Fitzharmon.");
~chatnpc("<p,neutral>Can't say as I've seen him... I'm sure if he's been to Al Kharid recently someone around here will have though.");