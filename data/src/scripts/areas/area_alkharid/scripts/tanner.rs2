[opnpc1,tanner]
// Treasure Trail
if(map_members = ^true & inv_total(inv, trail_clue_easy_simple008) = 1) {
    @trail_tanner;
}
~chatnpc("<p,happy>Greetings friend. I am a manufacturer of leather.");
def_int $total = calc(inv_total(inv, cow_hide) + inv_total(inv, dragonhide_green) + inv_total(inv, dragonhide_blue) + inv_total(inv, dragonhide_red) + inv_total(inv, dragonhide_black));
if($total > 0) {
    if ($total = 1) {
        ~chatnpc("<p,happy>I see you have brought me a hide.|Would you like me to tan it for you?");
    } else {
        ~chatnpc("<p,happy>I see you have brought me some hides.|Would you like me to tan them for you?");
    }
    switch_int(~p_choice2_header("Yes please.", 1, "No thanks.", 2, "What would you like to say?")) {
        case 1 : @tan_leather_choices;
        case 2 :
            ~chatplayer("<p,bored>No thanks.");
            ~chatnpc("<p,neutral>Very well, <text_gender("sir", "madam")>, as you wish.");
            return;
    }
}
switch_int (~p_choice2_header("Can I buy some leather then?", 1, "Leather is rather weak stuff.", 2, "What would you like to say?")) {
    case 1 :
        ~chatplayer("<p,quiz>Can I buy some leather then?");
        ~chatnpc("<p,neutral>I make leather from animal hides. Bring me some|cowhides and one gold coin per hide, and I'll tan them|into soft leather for you.");
    case 2 :
        ~chatplayer("<p,neutral>Leather is rather weak stuff.");
        ~chatnpc("<p,happy>Normal leather may be quite weak, but it's very cheap - I make it from cowhides for only 1 gp per hide - and it's so easy to craft that anyone can work with it.");
        ~chatnpc("<p,happy>Alternatively you could try hard leather. It's not so easy to craft, but I only charge 3 gp per cowhide to prepare it, and it makes much sturdier armour.");
        ~chatnpc("<p,happy>I can also tan dragonhides, suitable for crafting into the highest quality armour for rangers.");
        ~chatplayer("<p,bored>Thanks, I'll bear it in mind.");
}

[opnpcu,tanner]
switch_obj(last_useitem) {
    case cow_hide, dragonhide_green, dragonhide_blue, dragonhide_red, dragonhide_black : @tan_leather_choices;
    case leather, hard_leather, dragon_leather, dragon_leather_blue, dragon_leather_red, dragon_leather_black : ~chatnpc("<p,confused>Err... I have no use for that, I make the stuff!");
    case default : ~chatnpc("<p,confused>Er... Thanks, but no thanks!");
}

// https://web.archive.org/web/20040804232357/http://runehq.com/viewcityguide.php?id=00390
[label,tan_leather_choices]
// https://web.archive.org/web/20110920233323im_/http://runehq.com/images/cityguides/canifistanner.jpg
def_int $choice = ~p_choice4_header("Soft leather", 1, "Hard leather", 2, "Dragon hide", 3, "Cancel", 4, "What kind of leather do you wish to make?");
switch_int($choice) {
    case 1 : @tan_soft_leather;
    case 2 : @tan_hard_leather;
    case 3 : @tan_dragonhide;
}


[label,tan_soft_leather]
def_int $total_hides = inv_total(inv, cow_hide);
if ($total_hides < 1) {
    mes("You don't have any cowhides to tan."); // osrs
    return;
}
def_int $coins = inv_total(inv, coins);
if ($coins < ^tanner_soft_leather_cost) {
    mes("You haven't got enough coins to pay for soft leather."); // osrs
    return;
}
def_int $hides_to_be_tanned = min(divide($coins, ^tanner_soft_leather_cost), $total_hides);
~tan_leather(cow_hide, leather, ^tanner_soft_leather_cost);
if ($hides_to_be_tanned = 1) {
    mes("The tanner tans your cowhide."); // osrs
} else {
    mes("The tanner tans <tostring($hides_to_be_tanned)> cowhides for you.");
}
if (inv_total(inv, coins) < ^tanner_soft_leather_cost & $total_hides > $hides_to_be_tanned) {
    mes("You haven't got enough coins to pay for more soft leather."); // https://youtu.be/Ta4z2QML0jc?t=37
}

[label,tan_hard_leather]
def_int $total_hides = inv_total(inv, cow_hide);
if ($total_hides < 1) {
    mes("You don't have any cowhide to tan."); // osrs
    return;
}
def_int $coins = inv_total(inv, coins);
if ($coins < ^tanner_hard_leather_cost) {
    mes("You haven't got enough coins to pay for hard leather."); // osrs
    return;
}
def_int $hides_to_be_tanned = min(divide($coins, ^tanner_hard_leather_cost), $total_hides);
~tan_leather(cow_hide, hard_leather, ^tanner_hard_leather_cost);
if ($hides_to_be_tanned = 1) {
    mes("The tanner tans your cowhide."); // osrs
} else {
    mes("The tanner tans <tostring($hides_to_be_tanned)> cowhide for you.");
}
if (inv_total(inv, coins) < ^tanner_hard_leather_cost & $total_hides > $hides_to_be_tanned) {
    mes("You haven't got enough coins to pay for more hard leather.");
}

[label,tan_dragonhide]
def_int $total_dragon_hides = calc(inv_total(inv, dragonhide_green) + inv_total(inv, dragonhide_blue) + inv_total(inv, dragonhide_red) + inv_total(inv, dragonhide_black));
if ($total_dragon_hides < 1) {
    mes("You don't have any dragonhides to tan."); // guess
    return;
}
if (inv_total(inv, coins) < ^tanner_dragonhide_cost) {
    mes("You haven't got enough coins to pay for dragon leather."); // osrs
    return;
}
if (map_members = ^false) {
    mes("You need to be on a members' server to do that."); // guess
    return;
}
def_int $hides_to_be_tanned = min(divide(inv_total(inv, coins), ^tanner_dragonhide_cost), $total_dragon_hides);
def_string $hides_to_be_tanned_message = "The tanner tans <tostring($hides_to_be_tanned)> dragonhides for you."; // osrs
if ($hides_to_be_tanned = 1) {
    $hides_to_be_tanned_message = "The tanner tans your dragonhide."; // osrs
}
~tan_leather(dragonhide_green, dragon_leather, ^tanner_dragonhide_cost);
if (inv_total(inv, coins) < ^tanner_dragonhide_cost & $total_dragon_hides > $hides_to_be_tanned) {
    mes($hides_to_be_tanned_message);
    mes("You haven't got enough coins to pay for more dragon leather."); // guess based on https://youtu.be/Ta4z2QML0jc?t=37
    return;
}
~tan_leather(dragonhide_blue, dragon_leather_blue, ^tanner_dragonhide_cost);
if (inv_total(inv, coins) < ^tanner_dragonhide_cost & $total_dragon_hides > $hides_to_be_tanned) {
    mes($hides_to_be_tanned_message);
    mes("You haven't got enough coins to pay for more dragon leather.");
    return;
}
~tan_leather(dragonhide_red, dragon_leather_red, ^tanner_dragonhide_cost);
if (inv_total(inv, coins) < ^tanner_dragonhide_cost & $total_dragon_hides > $hides_to_be_tanned) {
    mes($hides_to_be_tanned_message);
    mes("You haven't got enough coins to pay for more dragon leather.");
    return;
}
~tan_leather(dragonhide_black, dragon_leather_black, ^tanner_dragonhide_cost);
if (inv_total(inv, coins) < ^tanner_dragonhide_cost & $total_dragon_hides > $hides_to_be_tanned) {
    mes($hides_to_be_tanned_message);
    mes("You haven't got enough coins to pay for more dragon leather.");
    return;
}
mes($hides_to_be_tanned_message);

[proc,tan_leather](namedobj $hide, namedobj $leather, int $cost)
def_int $total_hides = min(divide(inv_total(inv, coins), $cost), inv_total(inv, $hide));
if ($total_hides > 0) {
    inv_del(inv, $hide, $total_hides);
    inv_del(inv, coins, multiply($total_hides, $cost));
    inv_add(inv, $leather, $total_hides);
}

[label,trail_tanner]
// https://www.youtube.com/watch?v=M8lX77a6Mt8, https://www.youtube.com/watch?v=CFDUdiUYzJY
~chatnpc("<p,happy>Here you go!");
~progress_clue_easy(trail_clue_easy_simple008, "The tanner has given you another clue!");