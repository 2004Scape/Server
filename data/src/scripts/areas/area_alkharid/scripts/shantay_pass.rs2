// dialogue is all from osrs.
// though most of this dialogue has rsc relics (mes's instead of mesbox's)
// the "What is this place?" is forsure pre tourist trap rework. https://web.archive.org/web/20051117094041/http://runehq.com/RHQSpecialReportsView.php?id=00523
[opnpc1,shantay_guard]
~chatnpc("<p,neutral>Go talk to Shantay. I'm on duty and I don't have time to talk to the likes of you!");
~mesbox("The guard seems quite bad tempered, probably from having to wear heavy armour in this intense heat.");

[opnpc1,shantay_pass_guard]
~chatnpc("<p,neutral>Hello there! What can I do for you?");
def_int $choice = ~p_choice2("I'd like to go into the desert please.", 1, "Nothing thanks.", 2);
if ($choice = 2) {
    ~chatplayer("<p,neutral>Nothing thanks.");
    return;
}
~chatplayer("<p,neutral>I'd like to go into the desert please.");
if (inv_total(inv, shantay_pass) < 1) {
    ~chatnpc("<p,neutral>You need a Shantay pass to get through this gate. See|Shantay, he will sell you one for a very reasonable|price.");
    return;
}
if (map_members = false) {
    mes(^mes_members_gate); // guess
    return;
}
//https://youtu.be/VXwMnz9tTUs
if (inv_total(inv, thshantaydisc) < 1) {
    ~mesbox("There is a large billboard poster near the gateway. It reads:");
    ~mesbox("The Desert is a VERY Dangerous place. Do not enter if|you are scared of dying. Beware of high temperatures,|sand storms, robbers, and slavers.");
    ~mesbox("That seems pretty scary! Are you sure you want to go through?");
    $choice = ~p_choice2_header("Yeah, that poster doesn't scare me!", 1, "No, I'm having serious second thoughts now.", 2, "Go into Desert?");
    if ($choice = 2) {
        return;
    }
}
~chatnpc("<p,neutral>Can I see your Shantay Desert Pass please.");
~objbox(shantay_pass, "You hand over a Shantay Pass.", 250, 0, 0);
~chatplayer("<p,neutral>Sure, here you go!");
inv_del(inv, shantay_pass, 1);
if (inv_total(inv, thshantaydisc) < 1) {
    inv_add(inv, thshantaydisc, 1);
    ~chatnpc("<p,neutral>Here, have a disclaimer... It means that Shantay isn't|responsible if you die in the desert.");
}
if_close;
p_teleport(0_51_48_40_46);
p_delay(0);
facesquare(movecoord(coord, 0, 0, -3));
p_delay(0);
mes("You go through the gate.");
p_telejump(movecoord(coord, 0, 0, -3));
~start_desertheat_timer;


[oploc1,shantay_jail_door]
def_boolean $is_inside = ~check_axis_locactive(coord);
if (%shantay_jail_progress = ^put_in_shantay_jail & npc_find(coord, shantay, 12, 0) = true) {
    if ($is_inside = false) {
        %shantay_jail_progress = ^paid_shantay_jail_fine;
        ~chatnpc("<p,neutral>You should be in jail! Well, no doubt the authorities in|Port Sarim know what they're doing. But if you get|into any more trouble, you'll be stuck back in jail.");
        return;
    }
    ~chatnpc("<p,neutral>If you want to be let out, you have to pay a fine of five gold. Do you want to pay now?");
    @multi2("Yes, okay.", shantay_yes_okay_ill_pay_the_fine, "No thanks, you're not having my money.", shantay_no_thanks_youre_not_having_my_money);
}

~open_and_close_metal_gate(loc_1541, $is_inside, false);

[oploc1,shantay_pass]
if (map_members = false) {
    mes(^mes_members_gate); // guess
    return;
}
if (inv_total(inv, thanainabarrel) > 0 & %desertrescue_progress = ^desertrescue_escaped) {
    ~chatnpc_specific("Ana (in a Barrel)", anabarrel, "<p,neutral>@blu@-- Ana looks out of the barrel --|Hey great, we're at the Shantay pass!");
    @desertrescue_ana_rescued;
}
// if player is south of the pass, teleport them north of the pass scot-free.
if (coordz(coord) <= coordz(loc_coord)) {
    p_delay(0);
    p_telejump(movecoord(coord, 0, 0, 3));
    ~clear_desertheat_timer;
    return;
}
if (npc_find(coord, shantay_pass_guard, 5, 0) = false) {
    return;
}
if (inv_total(inv, shantay_pass) < 1) {
    ~chatnpc("<p,neutral>You need a Shantay pass to get through this gate. See|Shantay, he will sell you one for a very reasonable|price.");
    return;
}
//https://youtu.be/VXwMnz9tTUs
if (inv_total(inv, thshantaydisc) < 1) {
    ~mesbox("There is a large billboard poster near the gateway. It reads:");
    ~mesbox("The Desert is a VERY Dangerous place. Do not enter if|you are scared of dying. Beware of high temperatures,|sand storms, robbers, and slavers.");
    ~mesbox("That seems pretty scary! Are you sure you want to go through?");
    def_int $choice = ~p_choice2_header("Yeah, that poster doesn't scare me!", 1, "No, I'm having serious second thoughts now.", 2, "Go into Desert?");
    if ($choice = 2) {
        return;
    }
}
~chatnpc("<p,neutral>Can I see your Shantay Desert Pass please.");
~objbox(shantay_pass, "You hand over a Shantay Pass.", 250, 0, 0);
~chatplayer("<p,neutral>Sure, here you go!");
inv_del(inv, shantay_pass, 1);
if (inv_total(inv, thshantaydisc) < 1) {
    inv_add(inv, thshantaydisc, 1);
    ~chatnpc("<p,neutral>Here, have a disclaimer... It means that Shantay isn't|responsible if you die in the desert.");
}
if_close;
facesquare(movecoord(coord, 0, 0, -3));
p_delay(0);
mes("You go through the gate.");
p_telejump(movecoord(coord, 0, 0, -3));
~start_desertheat_timer;



[oploc2,shantay_pass]
~mesbox("You look at the huge stone gate.|Near the gate is a large billboard poster, it reads:");
// different than osrs https://web.archive.org/web/20060428031722/http://www.runecrypt.com:80/index.php?pid=202
~mesbox("The Desert is a VERY Dangerous place. Do not enter if|you are scared of dying. Beware of high temperatures,|sand storms, robbers, and slavers.");
~mesbox("Despite this warning lots of people seem to pass through the gate.");

// todo: Find a screenshot of this
[opheld1,thshantaydisc]
~mesbox("The Desert is a VERY Dangerous place. Do not enter if|you are scared of dying. Beware of high temperatures,|sand storms, robbers, and slavers."); // complete guess

//guess, matches osrs dialogue
[opheld1,thkebabinstructs]
~objbox(thkebabinstructs, "*** Delicious Ugthanki Kebab *** Ingredients: Cooked Ugthanki meat, Flour, Water, Onion, Tomato. The Ugthanki meat should be nicely grilled.", 200, 0, 0);
~objbox(thkebabinstructs, "Next take the flour and water and make some Pitta Bread. You'll need a range to do this.Take an onion and chop it into a bowl. Take a tomato and chop it into the onion mixture.", 200, 0, 0);
~objbox(thkebabinstructs, "Chop the meat into the Onion and Tomato mixture. Finally fill the pitta bread with the Ugthanki, Onion and Tomato mixture to make your delicious Ugthanki Kebab.", 200, 0, 0);