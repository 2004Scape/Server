[opnpc1,yanni_salika]
~chatplayer("<p,neutral>Hello there!");
// https://web.archive.org/web/20050204071450im_/http://www.runehq.com//images/cityguides/shilo9.jpg
~chatnpc("<p,neutral>Greetings Bwana!|My name is Yanni and I buy and sell antiques|and other interesting items.|If you have any interesting items that you might");
~chatnpc("<p,neutral>want to sell me, please let me see them and I'll|offer you a fair price.");
~chatnpc("<p,neutral>Would you like me to have a look at your items|and give you a quote?");
switch_int(~p_choice2("Yes please!", 1, "Maybe some other time?", 2)) {
    case 1 :
        ~chatplayer("<p,neutral>Yes please!");
        ~mesbox("Yanni looks through your items...");
        def_int $item_count = 0;
        if(inv_total(inv, bone_key) > 0) {
            ~chatnpc("<p,neutral>I'll give you 100 Gold for the Bone Key.");
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, stoneplaque) > 0) {
            ~chatnpc("<p,neutral>I'll give you 100 Gold for the Stone Plaque.");
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, tattered_scroll) > 0) {
            ~chatnpc("<p,neutral>I'll give you 100 Gold for the tattered scroll.");
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, crumpled_scroll) > 0) {
            ~chatnpc("<p,neutral>I'll give you 100 Gold for the crumpled scroll.");
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, bervirius_notes) > 0) {
            ~chatnpc("<p,neutral>I'll give you 100 Gold for the crumpled scroll."); // intentional
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, zqcrystal) > 0) {
            ~chatnpc("<p,neutral>I'll give you 500 Gold for your locating crystal.");
            $item_count = add($item_count,1);
        }
        if(inv_total(inv, zqbeadsofthedead) > 0) {
            ~chatnpc("<p,neutral>I'll give you 1000 Gold for your 'Beads of the Dead'.");
            $item_count = add($item_count,1);
        }
        if($item_count = 0) {
            ~chatnpc("<p,neutral>Sorry Bwana, you have nothing I am interested in.");
        } else if($item_count = 1) {
            ~chatnpc("<p,neutral>And that's the only item I am interested in.|If you want to sell me that item, simply show it to me.");
        } else {
            ~chatnpc("<p,neutral>Those are the items I am interested in Bwana. If you want to sell me those items, simply show them to me.");
        }
    case 2 :
        ~chatplayer("<p,neutral>Maybe some other time?");
        ~chatnpc("<p,neutral>Sure thing.|Have a nice day Bwana.");
}

[opnpcu,yanni_salika]
switch_obj(last_useitem) {
    case bone_key :
        ~chatnpc("<p,neutral>That's a great bone key. here's 100 Gold for it.");
        mes("You sell the bone key for 100 gold.");
        inv_del(inv, bone_key, ^max_32bit_int);
        inv_del(bank, bone_key, ^max_32bit_int);
        inv_add(inv, coins, 100);
    case stoneplaque :
        ~chatnpc("<p,neutral>That's a great stone-plaque. here's 100 Gold for it.");
        mes("You sell the stone-plaque for 100 gold.");
        inv_del(inv, stoneplaque, ^max_32bit_int);
        inv_del(bank, stoneplaque, ^max_32bit_int);
        inv_add(inv, coins, 100);
    case tattered_scroll :
        ~chatnpc("<p,neutral>That's a great tattered scroll. here's 100 Gold for it.");
        mes("You sell the tattered scroll for 100 gold.");
        inv_del(inv, tattered_scroll, ^max_32bit_int);
        inv_del(bank, tattered_scroll, ^max_32bit_int);
        inv_add(inv, coins, 100);
    case crumpled_scroll :
        ~chatnpc("<p,neutral>That's a great crumpled scroll. here's 100 Gold for it.");
        mes("You sell the crumpled scroll for 100 gold.");
        inv_del(inv, crumpled_scroll, ^max_32bit_int);
        inv_del(bank, crumpled_scroll, ^max_32bit_int);
        inv_add(inv, coins, 100);
    case bervirius_notes :
        ~chatnpc("<p,neutral>That's a great copy of Bervirius notes. here's 100 Gold for it.");
        mes("You sell the Bervirius notes for 100 gold.");
        inv_del(inv, bervirius_notes, ^max_32bit_int);
        inv_del(bank, bervirius_notes, ^max_32bit_int);
        inv_add(inv, coins, 100);
    case zqcrystal :
        ~chatnpc("<p,neutral>That's a great Locating Crystal. Here's 500 Gold for it.");
        mes("You sell the locating crystal for 500 gold.");
        inv_del(inv, zqcrystal, ^max_32bit_int);
        inv_del(bank, zqcrystal, ^max_32bit_int);
        inv_add(inv, coins, 500);
    case zqbeadsofthedead :
        ~chatnpc("<p,neutral>Impressive necklace there, here's 1000 Gold for it.");
        mes("You sell the 'Beads of the Dead' for 1000 gold.");
        inv_del(inv, zqbeadsofthedead, ^max_32bit_int);
        inv_del(bank, zqbeadsofthedead, ^max_32bit_int);
        inv_add(inv, coins, 1000);
    case default :
        ~chatnpc("<p,neutral>I'm sorry Bwana but I just don't have a use for that!");
}