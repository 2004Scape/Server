// doesn't exist on either game anymore, mesanims guessed
[opnpc1,yohnus]
~chatplayer("<p,neutral>Hello.");
@yohnus_blacksmiths;

[label,yohnus_blacksmiths]
// https://web.archive.org/web/20050204071450im_/http://www.runehq.com//images/cityguides/shilo7.jpg
~chatnpc("<p,neutral>Sorry but the blacksmiths is closed.|But I can let you use the furnace at the cost|of 20 gold pieces.");
switch_int(~p_choice2("Use Furnace - 20 Gold", 1, "No thanks!", 2)) {
    case 1 :
        if(inv_total(inv, coins) < 20) {
            ~chatnpc("<p,neutral>Sorry Bwana, it seems that you are short of funds."); // RSC
            return;
        }
        inv_del(inv, coins, 20);
        ~chatnpc("<p,neutral>Thanks Bwana! Enjoy the facilities!");
        if_close;
        p_delay(0);
        ~forcemove(0_44_46_40_19);
        if(loc_find(coord, loc_2266) = true) {
            def_int $x;
            def_int $z;
            def_coord $loc_coord = loc_coord;
            def_int $angle = loc_angle;
            def_locshape $shape = loc_shape;
            $x, $z = ~door_open($angle, loc_shape);
            // Temp note: dur updated
            loc_change(loc_83, 3);
            loc_add(movecoord($loc_coord, $x, 0, $z), loc_1532, modulo(add($angle, 1), 4), $shape, 3);
            sound_synth(door_open, 0, 0);
        }
        p_teleport(movecoord(coord, 0, 0, 1));
    case 2 :
        ~chatplayer("<p,neutral>No thanks!");
        ~chatnpc("<p,neutral>Very well Bwana, have a nice day.");
}

[oploc1,loc_2266]
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
if($entering = false) {
    ~open_and_close_door2(loc_1532, ~check_axis(coord, loc_coord, loc_angle), door_open);
} else if(npc_find(coord, yohnus, 5, 0) = true) {
    @yohnus_blacksmiths;
}