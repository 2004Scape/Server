[oploc1,closed_crystal_chest]
@open_crystal_chest;

[oplocu,closed_crystal_chest]
if(last_useitem = crystal_key) {
    @open_crystal_chest;
} else {
    ~displaymessage(^dm_default);
}

[label,open_crystal_chest]
p_arrivedelay;
if (inv_total(inv, crystal_key) > 0) {
    mes("You unlock the chest with your key.");
    anim(human_openchest, 0);
    sound_synth(chest_open, 0, 0);
    // Temp note: dur updated
    loc_change(opened_crystal_chest, 3);
    p_delay(0);
    inv_del(inv, crystal_key, 1);
    mes("You find some treasure in the chest!");
    @crystal_chest_reward;
} else {
    mes("This chest is securely locked shut.");
}

[label,crystal_chest_reward]
inv_add(inv, uncut_dragonstone, 1);

def_int $random = random(128);

if ($random < 34) {
    inv_add(inv, spinach_roll, 1);
    inv_add(inv, coins, 2000);
} else if ($random < 46) {
    inv_add(inv, airrune, 50);
    inv_add(inv, waterrune, 50);
    inv_add(inv, earthrune, 50);
    inv_add(inv, firerune, 50);
    inv_add(inv, bodyrune, 50);
    inv_add(inv, mindrune, 50);
    inv_add(inv, chaosrune, 10);
    inv_add(inv, deathrune, 10);
    inv_add(inv, cosmicrune, 10);
    inv_add(inv, naturerune, 10);
    inv_add(inv, lawrune, 10);
} else if ($random < 58) {
    inv_add(inv, ruby, 2);
    inv_add(inv, diamond, 2);
} else if ($random < 70) {
    inv_add(inv, runite_bar, 3);
} else if ($random < 80) {
    inv_add(inv, coins, 750);
    if (random(2) = 0) {
        inv_add(inv, keyhalf1, 1);
    } else {
        inv_add(inv, keyhalf2, 1);
    }
} else if ($random < 90) {
    inv_add(inv, cert_iron_ore, 150);
} else if ($random < 100) {
    inv_add(inv, cert_coal, 100);
} else if ($random < 108) {
    inv_add(inv, raw_swordfish, 5);
    inv_add(inv, coins, 1000);
} else if ($random < 110) {
    inv_add(inv, adamant_sq_shield, 1);
} else if ($random < 111) {
    switch_int (gender) {
        case ^gender_male :
        inv_add(inv, rune_platelegs, 1);        
        
        case ^gender_female :
        inv_add(inv, rune_plateskirt, 1);
    }
}