[opnpc1,ladyofthelake]
~chatnpc("<p,neutral>Good day to you <text_gender("sir", "madam")>.");
def_int $option;
if(%arthur_progress >= ^arthur_spoken_morgan_lefaye & ~obj_gettotal(excalibur) = 0) {
    $option = ~p_choice3("Who are you?", 1, "Good day.", 2, "I seek the sword Excalibur.", 3);
} else {
    $option = ~p_choice2("Who are you?", 1, "Good day.", 2);
}
switch_int($option) {
    case 1 :
        ~chatplayer("<p,neutral>Who are you?");
        ~chatnpc("<p,neutral>I am the Lady of the Lake.");
    case 2 :
        ~chatplayer("<p,neutral>Good day.");
    case 3 :
        ~chatplayer("<p,confused>I seek the sword Excalibur.");
        ~chatnpc("<p,neutral>Aye, I have that artefact in my possession.");
        ~chatnpc("<p,neutral>'Tis very valuable, and not an artefact to be given away lightly.");
        ~chatnpc("<p,neutral>I would want to give it away only to one who is worthy and good.");
        if(%arthur_progress = ^arthur_freed_merlin) {
            ~chatnpc("<p,neutral>...But you have already proved thyself to be worthy of wielding it once already. I shall return it to you if you can prove yourself to still be worthy.");
            ~chatplayer("<p,quiz>...And how can I do that?");
            ~chatnpc("<p,neutral>Why, by proving yourself to be above material goods.");
            ~chatplayer("<p,quiz>...And how can I do that?");
            ~chatnpc("<p,happy>500 gold coins ought to do it.");
            if(inv_total(inv, coins) < 500) {
                ~chatplayer("<p,sad>I don't have that kind of money...");
                ~chatnpc("<p,neutral>Well, come back when you do.");
                return;
            }
            ~chatplayer("<p,happy>Ok, here you go.");
            ~chatnpc("<p,happy>You are still worthy to wield Excalibur! And thanks for the cash! I felt like getting a new haircut!");
            inv_del(inv, coins, 500);
            inv_add(inv, excalibur, 1);
            return;
        }
        ~chatplayer("<p,quiz>And how am I meant to prove that?");
        ~chatnpc("<p,neutral>I shall set a test for you.");
        ~chatnpc("<p,neutral>First I need you to travel to Port Sarim.|Then go to the upstairs room of the jeweller's shop there.");
        ~chatplayer("<p,happy>Ok. That seems easy enough.");
        // OSRS probably uses a seperate varp for this since you can reset by retalking to the lady
        %excalibur_components_progress = clearbit_range(%excalibur_components_progress, ^excalibur_started, ^excalibur_rewarded); 
        %excalibur_components_progress = setbit(%excalibur_components_progress, ^excalibur_started);
}