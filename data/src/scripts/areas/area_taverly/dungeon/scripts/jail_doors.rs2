[oploc1,loc_2631] @unlock_taverly_jaildoor(false);
[oplocu,loc_2631]
if(last_useitem = jail_key) {
    @unlock_taverly_jaildoor(true);
} 
@nothing_interesting_happens;

[oploc1,loc_2623] @unlock_taverly_jaildoor(false);
[oplocu,loc_2623]
if(last_useitem = dusty_key) {
    @unlock_taverly_jaildoor(true);
} 
@nothing_interesting_happens;

[oploc1,loc_37] // Might be used somewhere else?
mes("This door doesn't seem to open from here...");

[label,unlock_taverly_jaildoor](boolean $key_used)
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
def_string $name = lowercase(loc_name);
if($entering = true & $key_used = false) {
    // the dusty key door loc is called "gate" in OSRS (and in 2007: https://youtu.be/_NuF1ZpxSXw?si=qskufTNPRUqU0rjZ&t=113),
    // these messages there use gate instead of door so we'll just futureproof here 
    mes("This <$name> is locked.");
    return;
}
if($key_used = true) {
    mes("You unlock the <$name>.");
} else {
    mes("The <$name> locks shut behind you.");
}
~open_taverly_jaildoor($entering);

[proc,open_taverly_jaildoor](boolean $entering)
def_int $x;
def_int $z;
def_int $angle = loc_angle;
def_locshape $shape = loc_shape;
def_coord $loc_coord = loc_coord;
$x, $z = ~door_open($angle);
def_coord $dest = $loc_coord;
sound_synth(door_open, 0, 0); // iron_gate_open in OSRS 
if ($entering = true) {
    if (coord ! $loc_coord) {
        p_walk($loc_coord);
        p_delay(1);
    }
    $dest = movecoord(loc_coord, $x, 0, $z);
}
p_teleport($dest);

loc_del(2);
loc_add($loc_coord, loc_83, $angle, $shape, 2);
loc_add(movecoord($loc_coord, $x, 0, $z), loc_37, modulo(add($angle, 1), 4), $shape, 2);
