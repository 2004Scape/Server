[oploc1,loc_2631] @unlock_taverley_jaildoor(false);
[oplocu,loc_2631]
if(last_useitem = jail_key) {
    @unlock_taverley_jaildoor(true);
} 
~displaymessage(^dm_default);

[oploc1,loc_2623] @unlock_taverley_jaildoor(false);
[oplocu,loc_2623]
if(last_useitem = dusty_key) {
    @unlock_taverley_jaildoor(true);
} 
~displaymessage(^dm_default);

[oploc1,ctratgatea] // Might be used somewhere else?
switch_coord(loc_coord) {
    case 0_40_150_35_57 : @clock_tower_first_door_for_rat_cage;
    case default : mes("This door doesn't seem to open from here...");
}

[label,unlock_taverley_jaildoor](boolean $key_used)
def_boolean $entering = ~check_axis(coord, loc_coord, loc_angle);
def_string $name = lowercase(loc_name);
if($entering = true & $key_used = false) {
    // the dusty key door loc is called "gate" in OSRS (and in 2007: https://youtu.be/_NuF1ZpxSXw?si=qskufTNPRUqU0rjZ&t=113),
    // these messages there use gate instead of door so we'll just futureproof here 
    mes("This <$name> is locked.");
    return;
}
if($key_used = true) {
    mes("You unlock the <$name>.");
} else {
    mes("The <$name> locks shut behind you.");
}
~open_and_close_door2(ctratgatea, $entering, grate_open);
