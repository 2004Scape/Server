[oploc1,loc_2114]
def_int $coal_to_remove = %coal_truck;
def_string $coal_mes = "You remove the coal from the truck.";
if($coal_to_remove = 0) {
    mes("There is no coal left in the truck.");
    return;
}
if(inv_freespace(inv) = 0) {
    mes("You do not have enough inventory space!");
    return;
}
if($coal_to_remove > inv_freespace(inv)) {
    $coal_to_remove = inv_freespace(inv);
    $coal_mes = "You remove some of the coal from the truck.";
}
// assuming sound_230 is our mine_ore equivalent (this uses mine_ore in osrs)
sound_synth(sound_230, 0, 0);
%coal_truck = calc(%coal_truck - $coal_to_remove);
inv_add(inv, coal, $coal_to_remove);
mes($coal_mes);

[oplocu,loc_2114]
if(last_useitem ! coal) {
    mes("The coal truck isn't meant to transport that.");
    return;
}
if(%coal_truck >= ^coal_truck_max) {
    mes("The coal truck is full.");
    return;
}
def_int $dep_amount = inv_total(inv, coal);
def_string $coal_mes = "You put the coal in the truck.";
if(calc($dep_amount + %coal_truck) > ^coal_truck_max) {
    $dep_amount = calc(^coal_truck_max - %coal_truck);
    $coal_mes = "You put some of the coal in the truck.";
}
sound_synth(sound_230, 0, 0);
%coal_truck = calc(%coal_truck + $dep_amount);
inv_del(inv, coal, $dep_amount);
mes($coal_mes);