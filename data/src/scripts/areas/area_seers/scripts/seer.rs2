[opnpc1,seer]
switch_int (%scorpcatcher_progress) {
    case ^scorpcatcher_started :
        ~seers_greetings;
        @multi3("I need to locate some scorpions.", seer_locate_scorp, "Your friend Thormac sent me to speak to you.", seer_thormac, "I seek knowledge and power!", seer_knowledge_and_power);
    case ^scorpcatcher_first_hint :
        if(inv_total(inv, scorpioncagefull) > 0) {
            ~seers_greetings;
            @multi2("Many greetings.", seer_many_greetings, "I seek knowledge and power!", seer_knowledge_and_power);
        } else if(inv_total(inv, scorpioncageab) > 0) {
            @seer_second_scorp;
        } else if(inv_total(inv, scorpioncagea) > 0 | inv_total(inv, scorpioncageac) > 0) {
            @seer_first_scorp;
        } else {
            ~seers_greetings;
            @multi2("Where did you say that scorpion was again?", seer_where_scorp, "I seek knowledge and power!", seer_knowledge_and_power);
        } 
    case ^scorpcatcher_second_hint :
        if(inv_total(inv, scorpioncagea) > 0 | inv_total(inv, scorpioncageac) > 0) {
            @seer_first_scorp;
        } else {
            ~seers_greetings;
            @multi2("Many greetings.", seer_many_greetings, "I seek knowledge and power!", seer_knowledge_and_power);
        } 
    case default :
        ~seers_greetings;
        @multi2("Many greetings.", seer_many_greetings, "I seek knowledge and power!", seer_knowledge_and_power);
}

[proc,seers_greetings] ~chatnpc("<p,happy>Many greetings.");

[label,seer_many_greetings]
~chatplayer("<p,happy>Many greetings.");
~chatnpc("<p,happy>Remember, whenever you set out to do something, something else must be done first.");

[label,seer_knowledge_and_power]
~chatplayer("<p,neutral>I seek knowledge and power!");
~chatnpc("<p,neutral>Knowledge comes from experience, power comes from battleaxes.");

[label,seer_locate_scorp]
~chatplayer("<p,neutral>I need to locate some scorpions.");
~chatnpc("<p,happy>Well you have come to the right place.|I am a master of animal detection.");
~chatnpc("<p,quiz>Do you need to locate any particular scorpion?|Scorpions are a creature somewhat in abundance.");
~chatplayer("<p,neutral>I'm looking for some lesser Kharid scorpions.|They belong to Thormac the Sorcerer.");
@seer_looking_glass;

[label,seer_thormac]
~chatplayer("<p,happy>Your friend Thormac sent me to speak to you.");
~chatnpc("<p,quiz>What does the old fellow want?");
~chatplayer("<p,sad>He's lost his valuable lesser Kharid scorpions.");
~chatnpc("<p,happy>Well you have come to the right place.|I am a master of animal detection.");
@seer_looking_glass;

[label,seer_looking_glass]
~chatnpc("<p,neutral>Let me look into my looking glass.");
if_close;
mes("The seer produces a small mirror");
p_delay(3);
mes("The seer gazes into the mirror");
p_delay(3);
mes("The seer smoothes his hair with his hand");
p_delay(3);
~chatnpc("<p,confused>I can see a scorpion that you seek.|It would appear to be near some nasty spiders.|I can see two coffins there as well.");
%scorpcatcher_progress = ^scorpcatcher_first_hint;
~chatnpc("<p,confused>The scorpion seems to be going through some crack in the wall. It's gone into some sort of secret room.");
~chatnpc("<p,neutral>Well see if you can find that scorpion then, and I'll try and get you some information on the others.");

[label,seer_where_scorp]
~chatplayer("<p,quiz>Where did you say that scorpion was again?");
@seer_looking_glass;

[label,seer_first_scorp]
~chatplayer("<p,happy>Hi, I've retrieved the scorpion from near the spiders.");
~chatnpc("<p,confused>Well, I've checked my looking glass.|There seems to be a kharid scorpion in a little village in the east,|surrounded by lots of uncivilised-looking warriors.|Some kind of merchant there seems to have picked it up.");
~chatnpc("<p,neutral>That's all I can tell you about that scorpion.");
%scorpcatcher_progress = ^scorpcatcher_second_hint;
~chatplayer("<p,happy>Any more scorpions?");
~chatnpc("<p,neutral>It's good that you should ask.|I have information on the last scorpion for you.");
@seer_last_scorp;

[label,seer_second_scorp]
~chatplayer("<p,happy>I've retrieved the second scorpion.");
~chatnpc("<p,neutral>That's lucky because I've got some information on the last scorpion for you.");
@seer_last_scorp;

[label,seer_last_scorp] 
~chatnpc("<p,confused>It seems to be in some sort of upstairs room.|There seems to be some sort of brown clothing lying on a table.");