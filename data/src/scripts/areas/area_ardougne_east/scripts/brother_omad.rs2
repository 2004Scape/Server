[opnpc1,brother_omad]
switch_int (%drunkmonk_progress) {
    case ^drunkmonk_not_started : @omad_whats_wrong;
    case ^drunkmonk_spoken_to_omad : @omad_have_blanket;
    case ^drunkmonk_retrieved_blanket : @omad_organize_party;
    case ^drunkmonk_looking_cedric,^drunkmonk_finding_water : @omad_need_wine;
    case ^drunkmonk_given_water,^drunkmonk_fixing_cart : @omad_hello_again;
    case ^drunkmonk_fixed_cart : @omad_party;
    case ^drunkmonk_complete : @omad_postparty;
}

[label,omad_whats_wrong]
~chatplayer("<p,quiz>Hello there, What's wrong?");
~chatnpc("<p,sad>*yawn*...oh, hello...*yawn* I'm sorry! I'm just so tired! I haven't slept in a week!");
switch_int (~p_choice2("Why can't you sleep, what's wrong?", 1, "Sorry! I'm too busy to hear your problems!", 2)) {
    case 1 : @omad_why;
    case 2 : ~chatplayer("<p,angry>Sorry! I'm too busy to hear your problems!");
}

[label,omad_why]
~chatplayer("<p,quiz>Why can't you sleep, what's wrong?");
~chatnpc("<p,sad>It's brother Androe's son! With his constant: Waaaaaah! Waaaaaaaaah! Androe said it's natural, but it's so annoying!");
~chatplayer("<p,happy>I suppose that's what kids do.");
~chatnpc("<p,sad>He was fine, up until last week! Thieves broke in! They stole his favourite sleeping blanket!");
~chatnpc("<p,sad>Now he won't rest until it's returned... ...and that means neither can I!");
switch_int (~p_choice2("Can I help at all?", 1, "I'm sorry to hear that! I hope you find his blanket.", 2)) {
    case 1 : @omad_help;
    case 2 : ~chatplayer("<p,neutral>I'm sorry to hear that! I hope you find his blanket.");
}

[label,omad_help]
~chatplayer("<p,quiz>Can I help at all?");
~chatnpc("<p,neutral>Please do. We won't be able to help you as we are peaceful men but we would be grateful for your help!");
~chatplayer("<p,neutral>Where are they?");
%drunkmonk_progress = ^drunkmonk_spoken_to_omad;
settimer(blanket_ladder, 1);
~send_quest_progress(questlist:drunkmonk, %drunkmonk_progress, ^drunkmonk_complete);
~chatnpc("<p,neutral>They hide in a secret cave in the forest. It's hidden under a ring of stones. Please, bring back the blanket!");

[label,omad_have_blanket]
~chatplayer("<p,neutral>Hello.");
~chatnpc("<p,sad>*yawn*...oh, hello again...*yawn*");
~chatnpc("<p,sad>Please tell me you have the blanket.");
if(inv_total(inv, childs_blanket) >= 1) {
    ~chatplayer("<p,happy>Yes! I've recovered it from the clutches of the evil thieves!");
    ~objbox(childs_blanket, "You hand the monk the childs blanket.", 250, 0, divide(^objbox_height, 2));
    ~chatnpc("<p,happy>Really, that's excellent, well done! Maybe now I will be able to get some rest.");
    ~chatnpc("<p,neutral>*yawn*..I'm off to bed! Farewell brave traveller!");
    inv_del(inv, childs_blanket, 1);
    %drunkmonk_progress = ^drunkmonk_retrieved_blanket;
    return;
}
~chatplayer("<p,sad>I'm afraid not.");
~chatnpc("<p,sad>I need some sleep!");

[label,omad_organize_party]
~chatplayer("<p,neutral>Hello, how are you?");
~chatnpc("<p,happy>Much better now I'm sleeping well! Now I can organise the party.");
~chatplayer("<p,happy>Ooh! What party?");
~chatnpc("<p,happy>The son of Brother Androe's birthday party. He's going to be one year old!");
~chatplayer("<p,happy>That's sweet!");
~chatnpc("<p,happy>It's also a great excuse for a drink!");
~chatnpc("<p,neutral>We just need Brother Cedric to return with the wine.");
def_int $option = ~p_choice2("Who's Brother Cedric?", 1, "Enjoy it! I'll see you soon!", 2);
if($option = 1) {
    ~chatplayer("<p,quiz>Who's Brother Cedric?");
    ~chatnpc("<p,neutral>Cedric is a member of the order too. We sent him out three days ago to collect wine. But he didn't return!");
    ~chatnpc("<p,angry>He most probably got drunk and lost in the forest!");
    ~chatnpc("<p,quiz>I don't suppose you could look for him?");
    @multi3("I've no time for that, sorry.", omad_no_time, "Where should I look?", omad_where_look, "Can I come to the party?", omad_come);
} else if($option = 2) {
    ~chatplayer("<p,happy>Enjoy it! I'll see you soon!");
    ~chatnpc("<p,neutral>Take care traveller.");
}

[label,omad_no_time]
~chatplayer("<p,neutral>I've no time for that, sorry.");
~chatnpc("<p,sad>Okay traveller, take care.");

[label,omad_where_look]
~chatplayer("<p,quiz>Where should I look?");
~chatnpc("<p,happy>Oh, he won't be far. Probably out in the forest.");
~chatplayer("<p,neutral>Ok, I'll go and find him.");
%drunkmonk_progress = ^drunkmonk_looking_cedric;

[label,omad_come]
~chatplayer("<p,happy>Can I come to the party?");
~chatnpc("<p,neutral>Of course, but we need the wine first.");

[label,omad_need_wine]
~chatplayer("<p,neutral>Hello brother Omad.");
if(%drunkmonk_progress = ^drunkmonk_looking_cedric) {
    ~chatnpc("<p,neutral>Hello adventurer, have you found Brother Cedric?");
    ~chatplayer("<p,neutral>Not yet.");
    ~chatnpc("<p,sad>Well, keep looking, we need that wine!");
} else if(%drunkmonk_progress = ^drunkmonk_finding_water) {
    ~chatnpc("<p,sad>Hello adventurer, have you found Brother Cedric?");
    ~chatplayer("<p,happy>Yes I've seen him, he's a bit drunk!");
    ~chatnpc("<p,happy>Well, try your best to get him back here!");
}

[label,omad_hello_again]
~chatplayer("<p,neutral>Hello again brother Omad.");
~chatnpc("<p,quiz>Hello adventurer, where's Brother Cedric?");
~chatplayer("<p,neutral>He's having a bit of trouble with his cart.");
~chatnpc("<p,sad>Hmmm! Maybe you could help?");

[label,omad_party]
~chatplayer("<p,happy>Hi Omad, Brother Cedric is on his way!");
~chatnpc("<p,happy>Good! Good! Now we can party!");
~chatnpc("<p,happy>I have little to repay you with but please! Take these Rune Stones.");
~objbox(lawrune, "Brother Omad gives you 8 Law Runes.", 250, 0, 0);
~chatplayer("<p,happy>Thanks Brother Omad!");
~chatnpc("<p,happy>OK, let's party!");
@drunkmonk_party;

[label,omad_postparty]
~chatnpc("<p,drunk>Dum dee do la la! *hiccup* That was some party!");