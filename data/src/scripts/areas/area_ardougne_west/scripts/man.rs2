[opnpc1,npc_351] @man_woman_curse_king_tyras; // man level 4
[opnpc1,npc_728] @man_woman_help_me_out_of_this_hell_hole; // fat man non attackable
[opnpc1,npc_729] @man_woman_go_away_outsider; // skinny man non attackable

[label,man_woman_curse_king_tyras]
~chatplayer("<p,happy>Good day.");
~chatnpc("<p,sad>We don't have good days here anymore. Curse King Tyras.");
switch_int(%elena_progress) {
    case ^quest_elena_freed_elena, ^elena_complete: @multi2("Oh okay, bad day then.", man_woman_bad_day_then, "Why, what has he done?", man_woman_what_has_he_done);
    case default: @multi3("Oh okay, bad day then.", man_woman_bad_day_then, "Why, what has he done?", man_woman_what_has_he_done, "I'm looking for a woman called <nc_name(elena)>.", man_woman_im_looking_for_a_woman);
}

[label,man_woman_bad_day_then]
~chatplayer("<p,confused>Oh okay, bad day then.");

[label,man_woman_what_has_he_done]
~chatplayer("<p,quiz>Why, what has he done?");
~chatnpc("<p,angry>His army curses our city with this plague then wanders off again, leaving us to clear up the pieces.");

[label,man_woman_im_looking_for_a_woman]
~chatplayer("<p,neutral>I'm looking for a woman called <nc_name(elena)>.");
~chatnpc("<p,neutral>Not heard of her.");
