[oploc1,loc_2034] 
if(~check_axis(coord, loc_coord, loc_angle) = false & inv_total(worn, doctors_gown) = 0) {
    mes("The mourner is refusing to open the door.");
    return;
}
~open_and_close_door2(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), door_open);

[oploc1,loc_2036] 
if(~check_axis(coord, loc_coord, loc_angle) = false) {
    ~open_and_close_door2(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), door_open);
} else {
    @west_ardougne_mourner_headquarters_doors;
}

[oploc1,loc_2048] @west_ardougne_open_city_doors;
[oploc1,loc_2049] @west_ardougne_open_city_doors;

[oploc1,loc_2528]
switch_int(%elena_progress) {
    case ^quest_elena_spoke_to_clerk, ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek, ^quest_elena_freed_elena, ^elena_complete, ^elena_complete_read_scroll: @bravek_enter_room;
    case default: @bravek_go_away_im_busy;
}

[label,west_ardougne_open_city_doors]
mes("You pull on the large wooden doors...");
if(%biohazard_progress = ^biohazard_complete) {
    def_coord $loc_coord = loc_coord;
    mes("...You open them and walk through.");
    if(loc_find(0_39_51_62_36, loc_2049) = true) {
        loc_del(4);
        loc_add(loc_coord, loc_2048, 2, loc_shape, 4);
    }
    if(loc_find(0_39_51_61_36, loc_2048) = true) {
        loc_del(4);
        loc_add(loc_coord, loc_2049, 0, loc_shape, 4);
    }
    if(loc_find(0_39_51_62_35, loc_2048) = true) {
        loc_del(4);
        loc_add(loc_coord, loc_2049, 2, loc_shape, 4);
    }
    if(loc_find(0_39_51_61_35, loc_2049) = true) {
        loc_del(4);
        loc_add(loc_coord, loc_2048, 0, loc_shape, 4);
    }
    sound_synth(coffin_open, 0, 0);
    if(coordx(coord) < coordx(loc_coord)) {
        ~forcemove(movecoord($loc_coord, 2, 0, 0));
    } else {
        ~forcemove(movecoord($loc_coord, -2, 0, 0));
    }
    return;
}
p_delay(1);
mes("...But they will not open.");

[label,west_ardougne_mourner_headquarters_doors]
switch_int(%biohazard_progress) {
    case ^biohazard_poisoned_stew, ^biohazard_found_distillator:
        if(inv_total(worn, doctors_gown) > 0) {
            ~chatnpc_specific(nc_name(npc_357), npc_357, "<p,neutral>In you go doc.");
            if_close;
            ~open_and_close_door2(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), door_open);
            return;
        }
        ~chatnpc_specific(nc_name(npc_357), npc_357, "<p,neutral>Stay away from there.");
        ~chatplayer("<p,quiz>Why?");
        // https://web.archive.org/web/20060111205550im_/http://www.runeweb.net/fireball/Biohazard%20Images/biohazard10.PNG
        ~chatnpc_specific(nc_name(npc_357), npc_357, "<p,neutral>Several mourners are ill with food poisoning,|we're waiting for a doctor.");
    case ^biohazard_climbed_ladder, ^biohazard_given_distillator, ^biohazard_spoken_chemist, ^biohazard_found_secret, ^biohazard_reported_elena, ^biohazard_complete: ~mesbox("The door is locked. You can hear the mourners eating... You need to distract them from their stew.");
    case default: ~chatnpc_specific(nc_name(npc_357), npc_357, "<p,angry>Who are you? Go away!");
}

[label,bravek_go_away_im_busy]
~chatnpc_specific(nc_name(bravek), bravek, "<p,angry>Go away, I'm busy! I'm... Umm... In a meeting!");

[label,bravek_enter_room]
~open_and_close_door2(loc_param(next_loc_stage), ~check_axis(coord, loc_coord, loc_angle), door_open);
