[opnpc1,clerk]
switch_int(%elena_progress) {
    case ^quest_elena_spoke_to_plague_house :
        ~chatnpc("<p,neutral>Hello, welcome to the Civic Office of West Ardougne. How can I help you?");
        @multi3("I need permission to enter a plague house.", clerk_i_need_permission, "Who is through that door?", clerk_who_is_through_that_door, "I'm just looking thanks.", clerk_im_just_looking_thanks);
    case ^quest_elena_spoke_to_clerk : @clerk_bravek_will_see_you_now;
    case default :
        ~chatnpc("<p,neutral>Hello, welcome to the Civic Office of West Ardougne. How can I help you?");
        @multi2("Who is through that door?", clerk_who_is_through_that_door, "I'm just looking thanks.", clerk_im_just_looking_thanks);
}

[label,clerk_who_is_through_that_door]
~chatplayer("<p,quiz>Who is through that door?");
~chatnpc("<p,neutral>The city warder <nc_name(bravek)> is in there.");
~chatplayer("<p,quiz>Can I go in?");
switch_int(%elena_progress) {
    case ^quest_elena_spoke_to_plague_house :
        ~chatnpc("<p,neutral>He has asked not to be disturbed.");
        @multi2("This is urgent though!", clerk_this_is_urgent_though, "Ok I will leave him alone.", clerk_ok_i_will_leave_him_alone);
    case ^quest_elena_spoke_to_bravek, ^quest_elena_spoke_cured_bravek, ^quest_elena_freed_elena, ^elena_complete, ^elena_complete_read_scroll : ~chatnpc("<p,neutral>I suppose so.");
    case default : ~chatnpc("<p,neutral>He has asked not to be disturbed.");
}

[label,clerk_im_just_looking_thanks]
~chatplayer("<p,neutral>I'm just looking thanks.");

[label,clerk_i_need_permission]
~chatplayer("<p,neutral>I need permission to enter a plague house.");
~chatnpc("<p,neutral>Rather you than me! Well, the mourners normally deal with that stuff, you should speak to them. Their headquarters are right near the city gate.");
@multi3("I'll try asking them then.", clerk_ill_try_asking_them_then, "Surely you don't let them run everything for you?", clerk_surely_you_dont_let_them_run_everything_for_you, "This is urgent though!", clerk_this_is_urgent_though);

[label,clerk_ill_try_asking_them_then]
~chatplayer("<p,neutral>I'll try asking them then.");

[label,clerk_surely_you_dont_let_them_run_everything_for_you]
~chatplayer("<p,quiz>Surely you don't let them run everything for you?");
~chatnpc("<p,neutral>Well, they do know what they're doing here. If they did start doing something badly <nc_name(bravek)>, the city warder, would have the power to override. I can't see that happening though.");
@multi2("I'll try asking them then.", clerk_ill_try_asking_them_then, "Can I speak to <nc_name(bravek)> anyway?", clerk_can_i_speak_to_bravek_anyway);

[label,clerk_can_i_speak_to_bravek_anyway]
~chatplayer("<p,quiz>Can I speak to <nc_name(bravek)> anyway?");
~chatnpc("<p,neutral>He has asked not to be disturbed.");
@multi2("This is urgent though!", clerk_this_is_urgent_though, "Ok I will leave him alone.", clerk_ok_i_will_leave_him_alone);

[label,clerk_ok_i_will_leave_him_alone]
~chatplayer("<p,neutral>Ok I will leave him alone.");

[label,clerk_this_is_urgent_though]
~chatplayer("<p,angry>This is urgent though!");
~chatnpc("<p,neutral>I'll see what I can do I suppose.");
~chatnpc("<p,neutral>Mr <nc_name(bravek)> there's a <text_gender("man", "lady")> here who really needs to speak to you.");
if (.npc_find(coord, bravek, 7, 0) = true) { // yes its possible this doesnt pop off if hes too far
    ~quest_elena_set_progress(^quest_elena_spoke_to_clerk);
    ~.chatnpc("<p,confused>I suppose they can come in then. If they keep it short.");
}

[label,clerk_bravek_will_see_you_now]
~chatnpc("<p,neutral><nc_name(bravek)> will see you now but keep it short!");
~chatplayer("<p,happy>Thanks, I won't take much of his time.");
