[opnpc1,bartender_dragoninn]
~chatnpc(quiz, "What can I get you?");
~chatplayer(quiz, "What's on the menu?");
// also sells beer in OSRS, just these 2 in RSC, sals and RHQ mentions it's only 2 drinks as well
// so i'm guessing it was changed with watchtower rework
~chatnpc(happy, "Dragon Bitter and Greenman's Ale.");
def_int $option;
if(testbit(%barcrawl_progress, ^dragoninn_index) = false & inv_total(inv, barcrawl_card) > 0) {
    $option = ~p_choice4("I'll give it a miss I think.", 1, "I'll try the Dragon Bitter.", 2, "Can I have some Greenman's Ale?", 3, "I'm doing Alfred Grimhand's Barcrawl.", 4);
} else {
    $option = ~p_choice3("I'll give it a miss I think.", 1, "I'll try the Dragon Bitter.", 2, "Can I have some Greenman's Ale?", 3);
}

switch_int($option) {
    case 1:
        ~chatplayer(neutral, "I'll give it a miss I think.");
        ~chatnpc(happy, "Come back when you're a little thirstier.");
    case 2:
        ~chatplayer(happy, "I'll try the Dragon Bitter.");
        ~chatnpc(happy, "Ok, that'll be two coins.");
        if (inv_total(inv, coins) < 2) {
            ~chatplayer(sad, "Oh dear. I don't seem to have enough money.");
        } else {
            inv_del(inv, coins, 2);
            inv_add(inv, dragon_bitter, 1);
            mes("You buy a pint of Dragon Bitter.");
        }
    case 3:
        ~chatplayer(quiz, "Can I have some Greenman's Ale?");
        ~chatnpc(happy, "Ok, that'll be ten coins.");
        if (inv_total(inv, coins) < 10) {
            ~chatplayer(sad, "Oh dear. I don't seem to have enough money.");
        } else {
            inv_del(inv, coins, 10);
            inv_add(inv, greenmans_ale, 1);
            mes("You buy a pint of Greenman's Ale.");
        }
    case 4: @dragoninn_barcrawl;
}

[opnpcu,bartender_dragoninn]
if(testbit(%barcrawl_progress, ^dragoninn_index) = false & last_item = barcrawl_card) {
    @dragoninn_barcrawl;
}

[label,dragoninn_barcrawl]
~chatplayer(happy, "I'm doing Alfred Grimhand's Barcrawl.");
~chatnpc(happy, "I suppose you'll be wanting some Fire Brandy.|That'll cost you 12 coins.");
if(inv_total(inv, coins) < 12) {
    ~chatplayer(sad, "Sorry I don't have 12 coins.");
    return;
}  
if_close;
inv_del(inv, coins, 12);
mes("The bartender hands you a small glass and sets light to the contents.");
p_delay(2);
mes("You blow out the flame and drink it.");
p_delay(3);
stat_sub(attack, calc(5 + ((stat(attack) * 10) / 100)), 0);
stat_sub(defence, calc(5 + ((stat(attack) * 10) / 100)), 0);
mes("Your vision blurs and you stagger slightly.");
p_delay(2);
%barcrawl_progress = setbit(%barcrawl_progress, ^dragoninn_index);
mes("You can just about make out the bartender signing your barcrawl card.");      