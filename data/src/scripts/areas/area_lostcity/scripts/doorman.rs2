// guessing, you can't talk to him directly in rsc
[opnpc1,doorman] mes("The Door man does not appear interested in talking.");

[label,door_man_chat]
~chatnpc("<p,neutral>You cannot pass through this door without paying the trading tax.");
~chatplayer("<p,quiz>What do I need to pay?");
~chatnpc("<p,neutral>One diamond.");
def_int $option = ~p_choice3("Okay...", 1, "A diamond? Are you crazy?", 2, "I haven't brought my diamonds with me.", 3);
if($option = 1) {
    ~chatplayer("<p,neutral>Okay...");
    if(inv_total(inv, diamond) = 0) {
        ~chatplayer("<p,sad>I haven't brought my diamonds with me.");
        return;
    }
    if_close;
    inv_del(inv, diamond, 1);
    mes("You give the doorman a diamond.");
    ~open_and_close_door(loc_1535, ~check_axis(coord, loc_coord, loc_angle), false);
} else if($option = 2) {
    ~chatplayer("<p,confused>A diamond?|Are you crazy?");
    ~chatnpc("<p,neutral>Nope. Those are the rules.");
} else if($option = 3) {
    ~chatplayer("<p,sad>I haven't brought my diamonds with me.");
}

[oploc1,loc_2411]
if(loc_coord = 0_50_149_38_22) {
    // eastern door
    if(coordx(coord) = coordx(loc_coord)) {
        ~open_and_close_door(loc_1535, ~check_axis(coord, loc_coord, loc_angle), false);
        return;
    }
    if (npc_find(0_50_149_35_22, doorman, 2, 0) = true) {
        @door_man_chat;
    }
} else if(loc_coord = 0_50_149_33_18) {
    // southern door
    if(coordz(coord) < coordz(loc_coord)) {
        ~open_and_close_door(loc_1535, ~check_axis(coord, loc_coord, loc_angle), false);
        return;
    }
    if (npc_find(0_50_149_33_20, doorman, 2, 0) = true) {
        @door_man_chat;
    }
}
