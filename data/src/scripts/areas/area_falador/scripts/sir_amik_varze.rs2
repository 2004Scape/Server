[opnpc1,sir_amik_varze]
switch_int (%blackknight_progress) {
    case 0: @black_knights_fortress_sir_amik_prequest;
    case 1,2,3: @black_knights_fortress_sir_amik_in_progress;
    case 4: @black_knights_fortress_sir_amik_postquest;
}

[label,black_knights_fortress_sir_amik_prequest]
~chatnpc(happy, "I am the leader of the White Knights of Falador.|Why do you seek my audience?");
def_int $option = ~p_choice2("I seek a quest!", 1, "I don't, I'm just looking around.", 2);

if ($option = 1) {
    ~chatplayer(quiz, "I seek a quest.");

    if (%questpoints < 12) {
        ~chatnpc(quiz, "Well I do have a task, but it is very dangerous and it's critical to us that no mistakes are made. I couldn't possibly let an unexperienced quester like yourself go.");
        mes("You need at least 12 quest points before you may attempt this quest.");
    } else {
        @black_knights_fortress_sir_amik_meets_requirements;
    }
} else {
    ~chatplayer(quiz, "I don't, I'm just looking around.");
    ~chatnpc(quiz, "Ok, don't break anything.");
}

[label,black_knights_fortress_sir_amik_meets_requirements]
~chatnpc(quiz, "Well, I need some spy work doing.|It's quite dangerous.|You will need to go into the Black Knight's fortress.");
def_int $option = ~p_choice2("I laugh in the face of danger!", 1, "I go and cower in a corner at the first sign of danger!", 2);

if ($option = 1) {
    ~chatplayer(long, "I laugh in the face of danger.");
    ~chatnpc(quiz, "Well that's good. Don't get too overconfident though.");
    @black_knights_fortress_sir_amik_come_along_at_the_right_time;
} else {
    ~chatplayer(happy, "I go and cower in a corner at the first sign of danger!");
    ~chatnpc(confused, "Err...");
    ~chatnpc(confused, "Well.");
    ~chatnpc(happy, "Spy work does involve a little hiding in corners I suppose.");
    def_int $give_it_a_go_or_no = ~p_choice2("Oh, I suppose I'll give it a go then.", 1, "No, I'm not convinced.", 2);

    if ($give_it_a_go_or_no = 1) {
        @black_knights_fortress_sir_amik_come_along_at_the_right_time;
    } else {
        ~chatplayer(quiz, "No, I'm not convinced.");
    }
}

[label,black_knights_fortress_sir_amik_come_along_at_the_right_time]
~chatnpc(quiz, "You've come along just right actually.|All of my knights are already known to the Black Knights.");
~chatnpc(sad, "Subtlety isn't exactly our strong point.");
~chatplayer(happy, "So what needs doing?");
~chatnpc(angry, "Well the Black Knights have started making strange threats to us. Demanding large amounts of money and land, and threatening to invade Falador if we don't pay.");
~chatnpc(quiz, "Now normally this wouldn't be a problem.");
~chatnpc(sad, "But they claim to have a powerful new secret weapon.");
~chatnpc(quiz, "What I want you to do is get inside their fortress.|Find out what their secreat weapon is.|And then sabotage it. You will be well paid.");
~chatplayer(quiz, "Ok, I'll give it a try.");

[label,black_knights_fortress_sir_amik_in_progress]
if (%blackknight_progress = 1) {
    ~chatnpc(happy, "How' the mission going?");
    ~chatplayer(quiz, "I haven't managed to find what the secret weapon is yet.");
} else if (%blackknight_progress = 2) {
    ~chatnpc(happy, "How' the mission going?");
    ~chatplayer(quiz, "I've found out what the Black Knight's secret weapon is. It's a potion of invincibility.");
    ~chatnpc(sad, "That is bad news.|If you can sabotage it somehow, you will be paid well.");
} else if (%blackknight_progress = 3) {
    ~chatplayer(quiz, "I have ruined the Black Knight's invincibility potion.|That should put a stop to your problem.");
    ~chatnpc(happy, "Yes we have just received a message from the Black Knights saying they withdraw their demands. Which confirms your story.");
    ~chatplayer(quiz, "You said you were going to pay me.");
    ~chatnpc(long, "Yes, that's right.");
    queue(black_knights_fortress_quest_complete, 0);
}

[label,black_knights_fortress_sir_amik_postquest]
~chatplayer(long, "Hello Sir Amik.");
~chatnpc(long, "Hello, friend!");