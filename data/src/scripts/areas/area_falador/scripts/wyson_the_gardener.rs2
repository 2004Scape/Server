[opnpc1,wyson] 
~chatnpc(neutral, "I'm the gardener around here.|Do you have any gardening that needs doing?");
def_int $option = ~p_choice2("I'm looking for woad leaves.", 1, "Not right now, thanks.", 2);
if($option = 1) {
    ~chatplayer(neutral, "I'm looking for woad leaves.");
    ~chatnpc(shifty, "How much are you willing to pay?");
    $option = ~p_choice4("How about 5 coins?", 1, "How about 10 coins?", 2, "How about 15 coins?", 3, "How about 20 coins?", 4);
    if($option = 1) {
        ~chatplayer(neutral, "How about 5 coins?");
        ~chatnpc(angry, "No no, that's far too little.|Woad leaves are hard to get.|I used to have plenty but someone|kept stealing them off me!");
    } else if($option = 2) {
        ~chatplayer(neutral, "How about 10 coins?");
        ~chatnpc(angry, "No no, that's far too little.|Woad leaves are hard to get.|I used to have plenty but someone|kept stealing them off me!");
    } else if($option = 3) {
        ~chatplayer(neutral, "How about 15 coins?");
        ~chatnpc(neutral, "Mmmm... okay, sounds fair.");
        if(inv_total(inv, coins) < 15) {
            ~chatplayer(sad, "I don't have enough coins to buy the leaves.|I'll come back later.");
        } else {
            inv_del(inv, coins, 15);
            inv_add(inv, woad_leaf, 1);
            mes("You give Wyson 15 coins.");
            mes("Wyson the gardener gives you a woad leaf.");
        }
    } else if($option = 4) {
        ~chatplayer(neutral, "How about 20 coins?");
        ~chatnpc(happy, "Okay, that's more than fair.");
        if(inv_total(inv, coins) < 20) {
            ~chatplayer(sad, "I don't have enough coins to buy the leaves.|I'll come back later.");
        } else {
            ~chatnpc(happy, "Here, have two, you're a generous person.");
            inv_del(inv, coins, 20);
            inv_add(inv, woad_leaf, 2);
            mes("You give Wyson 20 coins.");
            mes("Wyson the gardener gives you some woad leaves.");
        }
    }
} else if($option = 2) {
    ~chatplayer(neutral, "Not right now, thanks.");
}