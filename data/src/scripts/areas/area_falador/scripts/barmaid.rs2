[opnpc1,barmaid]
if(testbit(%barcrawl_progress, ^risingsun_index) = false & inv_total(inv, barcrawl_card) > 0) {
    @barmaid_barcrawl_dialogue;
}
@barmaid_what_ales;

[opnpcu,barmaid]
if(testbit(%barcrawl_progress, ^risingsun_index) = false & last_item = barcrawl_card) {
    @barmaid_barcrawl_dialogue;
}

[label,barmaid_barcrawl_dialogue]
def_int $option = ~p_choice2("Hi, what ales are you serving?", 1, "I'm doing Alfred Grimhand's Barcrawl.", 2);
if($option = 1) {
    @barmaid_what_ales;
} else if($option = 2) {
    ~chatplayer(happy, "I'm doing Alfred Grimhand's Barcrawl.");
    ~chatnpc(laugh, "Heehee, this'll be fun!");
    ~chatnpc(shifty, "You'll be after our Hand of Death cocktail, then.|Lots of expensive parts to the cocktail, though,|so it will cost you 70 coins.");
    if(inv_total(inv, coins) < 70) {
        ~chatplayer(sad, "I don't have that much money on me.");
        return;
    }  
    if_close;
    inv_del(inv, coins, 70);
    mes("You buy a Hand of Death cocktail.");
    p_delay(2);
    stat_sub(attack, calc(5 + ((stat(attack) * 5) / 100)), 0);
    stat_sub(defence, calc(5 + ((stat(defence) * 5) / 100)), 0);
    stat_sub(ranged, calc(5 + ((stat(ranged) * 5) / 100)), 0);
    stat_sub(fishing, calc(5 + ((stat(fishing) * 5) / 100)), 0);
    mes("You drink the cocktail.");
    p_delay(2);
    cam_reset;
    cam_shake(0, 0, 15, 2);
    mes("You stumble around the room.");
    p_delay(3);
    mes("The barmaid giggles.");
    p_delay(3);
    %barcrawl_progress = setbit(%barcrawl_progress, ^risingsun_index);
    mes("The barmaid signs your card.");
    // Not sure what the purpose of these 2 cmds are but OSRS sends them
    cam_lookat(0_45_52_32_0, 0, 0, 0);
    cam_moveto(0_45_52_32_0, 0, 0, 0);
    cam_reset;
    ~damage_self(calc((stat(hitpoints) * 5) / 100));
}

[label,barmaid_what_ales]
~chatplayer(quiz, "Hi, what ales are you serving?");
~chatnpc(happy, "Well, you can either have a nice Asgarnian Ale, a Wizard's Mind Bomb or a Dwarven Stout.");
def_int $option = ~p_choice4("One Asgarnian Ale, please.", 1, "I'll try the Mind Bomb.", 2, "Can I have a Dwarven Stout?", 3, "I don't feel like any of those.", 4);
if($option = 1) {
    ~chatplayer(happy, "One Asgarnian Ale, please.");
    ~chatnpc(happy, "That'll be two gold.");
    if(inv_total(inv, coins) < 2) {
        ~chatplayer(sad, "Oh dear, I don't seem to have enough money.");
        return;
    }   
    inv_del(inv, coins, 2);
    inv_add(inv, asgarnian_ale, 1);
    mes("You buy an Asgarnian Ale.");
} else if ($option = 2) {
    ~chatplayer(happy, "I'll try the Mind Bomb.");
    ~chatnpc(happy, "That'll be two gold.");
    if(inv_total(inv, coins) < 2) {
        ~chatplayer(sad, "Oh dear, I don't seem to have enough money.");
        return;
    }   
    inv_del(inv, coins, 2);
    inv_add(inv, wizards_mind_bomb, 1);
    mes("You buy a pint of Wizard's Mind Bomb.");    
} else if($option = 3) {
    ~chatplayer(quiz, "Can I have a Dwarven Stout?");
    ~chatnpc(happy, "That'll be three gold.");
    if(inv_total(inv, coins) < 3) {
        ~chatplayer(sad, "Oh dear, I don't seem to have enough money.");
        return;
    }   
    inv_del(inv, coins, 3);
    inv_add(inv, dwarven_stout, 1);
    mes("You buy a pint of Dwarven Stout.");
} else if($option = 4) {
    ~chatplayer(neutral, "I don't feel like any of those.");
}
