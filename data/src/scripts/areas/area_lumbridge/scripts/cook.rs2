[opnpc1,cook]
// https://www.youtube.com/watch?v=mtPhS3n2dAo
if(inv_total(inv, trail_clue_medium_challenge_exp002) > 0 & inv_total(inv, trail_clue_medium_anagram_exp002) > 0) {
    @challenge_cook;
}
if(inv_total(inv, trail_clue_medium_anagram_exp002) > 0) {
    @trail_cook;
}

if(%cook_progress = 0) {
    ~chatnpc("<p,sad>What am I to do?");
    @cooks_assistant_start;
} else if(%cook_progress = 1) {
    @cooks_assistant_inprogress;
} else if(%cook_progress = ^cook_complete) {
    ~chatnpc("<p,happy>How is the adventuring going, my friend?");

    def_int $option = ~p_choice4("I am getting strong and mighty.", 1, "I keep on dying.", 2, "Nice hat!", 3, "Can I use your range?", 4);

    if($option = 1) {
        ~chatplayer("<p,happy>I am getting strong and mighty.");
        ~chatnpc("<p,happy>Glad to hear it.");
    } else if($option = 2) {
        ~chatplayer("<p,sad>I keep on dying.");
        ~chatnpc("<p,happy>Ah well, at least you keep coming back to life!");
    } else if($option = 3) {
        ~chatplayer("<p,happy>Nice hat!");
        ~chatnpc("<p,sad>Err thank you. It's a pretty ordinary cooks hat really.");
    } else if($option = 4) {
        ~chatplayer("<p,neutral>Can I use your range?");
        ~chatnpc("<p,happy>Go ahead - it's a very good range.|It's easier to use than most other ranges.");
    }

}

[label,trail_cook]
~chatnpc("<p,confused>I've always wondered how many cannons the castle has. Could you find out for me?");
inv_add(inv, trail_clue_medium_challenge_exp002, 1);
~objboxt(trail_clue_medium_challenge_exp002, "The cook has given you a challenge scroll!");

[label,challenge_cook]
~chatnpc("<p,neutral>Please tell me how many cannons there are.");
if_close;
p_delay(0); // old videos have this it seems, I think it's a workaround otherwise the old chatnpc will appear for a tick after the countdialog is closed
p_countdialog;
def_int $entered_value = last_int;
if($entered_value = oc_param(trail_clue_medium_challenge_exp002, trail_challenge_answer)) {
    ~chatnpc("<p,happy>Cheers!");
    inv_del(inv, trail_clue_medium_challenge_exp002, 1);
    ~progress_clue_medium(trail_clue_medium_anagram_exp002, "The cook has given you your next clue!");
} else {
    ~chatnpc("<p,angry>Hmm, I don't think that's right.");
}