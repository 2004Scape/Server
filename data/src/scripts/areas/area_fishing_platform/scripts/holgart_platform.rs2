[opnpc1,holgart_platform]
switch_int(%seaslug_progress) {
    case ^seaslug_spoken_kennith: @holgart_something_strange;
    case ^seaslug_saved_kennith: @holgart_kid_shore;
    case default: @holgart_platform_standard;
}

[label,holgart_platform_standard]
~chatplayer("<p,happy>Hey, Holgart.");
~chatnpc("<p,shifty>Have you had enough of this place yet?|It's really starting to scare me.");
switch_int(~p_choice2("No, I'm going to stay a while.", 1, "Okay, let's go back.", 2)) {
    case 1:
        ~chatplayer("<p,neutral>No, I'm going to stay a while.");
        ~chatnpc("<p,shifty>Okay... you're the boss.");
    case 2:
        ~chatplayer("<p,neutral>Okay, let's go back.");
        ~chatnpc("<p,happy>Okay m'hearty, jump on.");
        ~board_fishing_platform_to_ardougne;
}

[label,holgart_something_strange]
~chatplayer("<p,confused>Holgart, something strange is going on here.");
~chatnpc("<p,confused>You're telling me, none of the sailors seem to remember who I am.");
~chatplayer("<p,confused>Apparently Kennith's father left for help a couple of days ago.");
~chatnpc("<p,confused>That's a worry, no-one's heard from him on shore.|Come on, we'd better go look for him.");
%seaslug_progress = ^seaslug_sailed_kent;
~board_fishing_platform_to_island;

[label,holgart_kid_shore]
~chatplayer("<p,quiz>Did you get the kid back to shore?");
~chatnpc("<p,neutral>Yes, he's safe and sound with his parents.|Your turn to return to land now adventurer.");
~chatplayer("<p,happy>Looking forward to it.");
~board_fishing_platform_to_ardougne;

[proc,board_fishing_platform_to_island]
if_close;
mes("You board the row boat.");
p_delay(2);
p_telejump(^seaslug_island_coord);
~mesbox("You arrive on a small island.");

[proc,board_fishing_platform_to_ardougne]
if_close;
mes("You board the small row boat.");
p_delay(2);
p_telejump(^seaslug_shore_coord);
~mesbox("You arrive back on shore.");