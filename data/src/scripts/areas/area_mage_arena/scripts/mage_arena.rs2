[oploc1,loc_2871]
p_arrivedelay;
@climb_ladder(movecoord(coord, -549, 0, 756), false);

[oploc1,loc_2872]
p_arrivedelay;
@climb_ladder(movecoord(coord, 549, 0, -756), true);


[oploc1,loc_2878]
def_int $width = 3;
def_int $length = 3;

def_int $x = calc(coordx(loc_coord) - coordx(coord));
def_int $z = calc(coordz(loc_coord) - coordz(coord));
def_coord $coord;
def_coord $coord2;
def_int $dir;
//mes("X: <tostring($x)>, Z: <tostring($z)>, Width: <tostring($width)>, Length: <tostring($length)>");
if ($x > 0) {
    $coord = movecoord(loc_coord, -1, 0, 1); // player is west
    $coord2 = movecoord($coord, 1, 0, 0);
    $dir = ^exact_east;
} else if ($z > 0) {
    $coord = movecoord(loc_coord, 1, 0, -1); // player is south
    $coord2 = movecoord($coord, 0, 0, 1);
    $dir = ^exact_north;
} else if (calc($z + $length - 1) < 0) {
    $coord = movecoord(loc_coord, 1, 0, 3); // player is north
    $coord2 = movecoord($coord, 0, 0, -1);
    $dir = ^exact_south;
} else if (calc($x + $width - 1) < 0) {
    $coord = movecoord(loc_coord, 3, 0, 1); // player is east
    $coord2 = movecoord($coord, -1, 0, 0);
    $dir = ^exact_west;
}

if ($coord ! coord) {
    ~forcewalk($coord);
}
facesquare($coord2);
mes("You step into the pool.");
p_delay(0);
// [proc,agility_exactmove]
//(seq $anim, int $anim_delay, int $movement_delay, coord $start_coord, coord $end_coord, int $start_cycle, int $end_cycle, int $direction, boolean $merge_with_loc)
~agility_exactmove(human_walk_f, 0, 0, coord, $coord2, 0, 20, $dir, true);
~agility_exactmove(human_walk_f, 0, 0, coord, $coord, 0, 20, $dir, true);
mes("Your boots get wet.");


// need to find screenshots to confirm these
[oploc1,loc_2880]
// if north of the loc
if (coordz(coord) > coordz(loc_coord)) {
    if (%magearena_progress < ^mage_arena_complete) {
        ~mesbox("You cannot enter without the permission of Kolodion."); // rsc, might be mes
        return;
    }
    if (~can_enter_mage_arena = false) {
        ~mesbox("You cannot enter the arena while carrying weapons or armour."); // rsc, might be mes
        return;
    }
    p_teleport(movecoord(coord, 0, 0, -1));
} else {
    p_teleport(movecoord(loc_coord, 0, 0, 1));
}
mes("You pass through the mystical barrier."); //rsc


[proc,can_enter_mage_arena]()(boolean)
if ((inv_totalcat(inv, armour_hands) > 0 | inv_totalcat(worn, armour_hands) > 0)
    | (inv_totalcat(inv, armour_helmet) > 0 | inv_totalcat(worn, armour_helmet) > 0) 
    | (inv_totalcat(inv, armour_body) > 0 | inv_totalcat(worn, armour_body) > 0) 
    | (inv_totalcat(inv, armour_legs) > 0 | inv_totalcat(worn, armour_legs) > 0) 
    | (inv_totalcat(inv, armour_shield) > 0 | inv_totalcat(worn, armour_shield) > 0) 
    | (inv_totalcat(inv, weapon_slash) > 0 | inv_totalcat(worn, weapon_slash) > 0) 
    | (inv_totalcat(inv, weapon_blunt) > 0 | inv_totalcat(worn, weapon_blunt) > 0) 
    | (inv_totalcat(inv, category_15) > 0 | inv_totalcat(worn, category_15) > 0)
    | (inv_totalcat(inv, weapon_stab) > 0 | inv_totalcat(worn, weapon_stab) > 0) 
    | (inv_totalcat(inv, weapon_crossbow) > 0 | inv_totalcat(worn, weapon_crossbow) > 0) 
    | (inv_totalcat(inv, weapon_axe) > 0 | inv_totalcat(worn, weapon_axe) > 0) 
    | (inv_totalcat(inv, weapon_pickaxe) > 0 | inv_totalcat(worn, weapon_pickaxe) > 0) 
    | (inv_totalcat(inv, weapon_javelin) > 0 | inv_totalcat(worn, weapon_javelin) > 0) 
    | (inv_totalcat(inv, weapon_2h_sword) > 0 | inv_totalcat(worn, weapon_2h_sword) > 0) 
    | (inv_totalcat(inv, weapon_spear) > 0 | inv_totalcat(worn, weapon_spear) > 0) 
    | (inv_totalcat(inv, weapon_spiked) > 0 | inv_totalcat(worn, weapon_spiked) > 0) 
    | (inv_totalcat(inv, weapon_thrown) > 0 | inv_totalcat(worn, weapon_thrown) > 0) 
    | (inv_totalcat(inv, weapon_scythe) > 0 | inv_totalcat(worn, weapon_scythe) > 0) 
    | (inv_totalcat(inv, weapon_bow) > 0 | inv_totalcat(worn, weapon_bow) > 0) 
    | (inv_totalcat(inv, arrows) > 0 | inv_totalcat(worn, arrows) > 0)
    | (inv_totalcat(inv, unstrung_bow) > 0)
    | (inv_total(inv, headless_arrow) > 0) // headless arrows
    | (inv_total(inv, arrow_shaft) > 0) 
    | (inv_total(inv, bow_string) > 0)
    | (inv_totalcat(inv, category_22) > 0) // logs
    | (inv_totalcat(inv, unstrung_bow) > 0)
    // not sure if they would think of adding some of these checks but really no way to know :(
    | (inv_totalcat(inv, trail_clue_easy) > 0)
    | (inv_totalcat(inv, trail_clue_medium) > 0)
    | (inv_totalcat(inv, trail_clue_hard) > 0)
    | (inv_totalcat(inv, cannon_parts) > 0)) {
    return (false);
}
return (true);

[proc,mage_arena_in_progress]()(boolean)
if (%magearena_progress < ^mage_arena_complete & %magearena_progress > ^mage_arena_started) {
    return (true);
}
return (false);

[proc,spawn_mage_arena_boss](int $stage, coord $coord)
// looks like the smoke appears 1t sooner than osrs (no 34 client tick delay as well)
// https://youtu.be/Tte53ezNIP4?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o&t=23
spotanim_map(spotanim_188, $coord, 124, 0);
sound_synth(smokepuff, 0, 0);
p_delay(0);
def_npc $boss = enum(int, npc, mage_arena_boss_stages, $stage);
npc_add($coord, $boss, 1000);
npc_anim(npc_param(spawn_anim), 0);


[queue,teleport_to_mage_arena](int $stage)
mes("player anim");
anim(human_castteleport, 60);
spotanim_pl(castteleport, 92, 60);
sound_synth(teleport_all, 0, 0);
p_delay(3);
p_teleport(^mage_arena_start_coord);
mes("Teleport");
anim(null, 0);
p_delay(3);
mes("Spawn");
~spawn_mage_arena_boss($stage, ~coord_lineofwalk_radius2(coord, 1));
npc_setmode(applayer2);
p_delay(1);
mes("Say");
npc_say("You must prove yourself... now!");

[ai_applayer2,kolodion_boss_1] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);
[ai_applayer2,kolodion_boss_2] ~npc_cast_spell(~random_range(^claws_of_guthix, ^saradomin_strike), 5);
[ai_applayer2,kolodion_boss_3] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^claws_of_guthix), 5);
[ai_applayer2,kolodion_boss_4] ~npc_cast_spell(~random_range(^saradomin_strike, ^claws_of_guthix), 5);
[ai_applayer2,kolodion_boss_5] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);

[ai_opplayer2,kolodion_boss_1] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);
[ai_opplayer2,kolodion_boss_2] ~npc_cast_spell(~random_range(^claws_of_guthix, ^saradomin_strike), 5);
[ai_opplayer2,kolodion_boss_3] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^claws_of_guthix), 5);
[ai_opplayer2,kolodion_boss_4] ~npc_cast_spell(~random_range(^saradomin_strike, ^claws_of_guthix), 5);
[ai_opplayer2,kolodion_boss_5] ~npc_cast_spell(~random_range(^flames_of_zamorak, ^saradomin_strike), 5);


[ai_queue3,kolodion_boss_1](int $arg)
if (finduid(%npc_aggressive_player) = true) {
    %magearena_progress = 2;
}
npc_setmode(none);
npc_anim(npc_param(death_anim), 0);
npc_delay(2);
npc_changetype(kolodion_boss_2);
npc_anim(npc_param(spawn_anim), 0);
spotanim_npc(spotanim_188, 124, 34);
npc_setmode(applayer2);


[debugproc,ma](int $stage)
queue(teleport_to_mage_arena, 0, $stage);