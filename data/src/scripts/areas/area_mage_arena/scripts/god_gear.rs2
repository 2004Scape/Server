[opheld2,saradomincape] if (~check_conflicting_god_staff(zamorakstaff, guthixstaff) = true) ~equip(last_slot);
[opheld2,zamorakcape] if (~check_conflicting_god_staff(saradominstaff, guthixstaff) = true) ~equip(last_slot);
[opheld2,guthixcape] if (~check_conflicting_god_staff(saradominstaff, zamorakstaff) = true) ~equip(last_slot);

[opheld2,saradominstaff] if (~check_conflicting_god_cape(zamorakcape, guthixcape) = true) ~equip(last_slot);
[opheld2,zamorakstaff] if (~check_conflicting_god_cape(saradomincape, guthixcape) = true) ~equip(last_slot);
[opheld2,guthixstaff] if (~check_conflicting_god_cape(saradomincape, zamorakcape) = true) ~equip(last_slot);


[proc,check_conflicting_god_cape](namedobj $item1, namedobj $item2)(boolean)
if (inv_total(worn, $item1) > 0 | inv_total(worn, $item2) > 0) {
    mes("You may not wield this staff whilst wearing the cape of another god.");
    mes("The conflicting powers of staff and cape drive them apart.");
    return(false);
}
return(true);

[proc,check_conflicting_god_staff](namedobj $item1, namedobj $item2)(boolean)
if (inv_total(worn, $item1) > 0 | inv_total(worn, $item2) > 0) {
    mes("You may not wear this cape whilst wielding the staff of another god.");
    mes("The conflicting powers of staff and cape drive them apart.");
    return(false);
}
return(true);

[opheld5,saradomincape] @drop_god_cape;
[opheld5,zamorakcape] @drop_god_cape;
[opheld5,guthixcape] @drop_god_cape;

[label,drop_god_cape]
inv_delslot(inv, last_slot);
sound_synth(put_down, 0, 0);
mes(oc_param(last_item, lose_cape_message)); // https://youtu.be/tOUAeowTkCM?list=PLn23LiLYLb1Y4vxMPWXM-CVEvOUfuAP_o&t=140