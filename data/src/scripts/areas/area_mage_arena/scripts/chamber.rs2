[oploc1,loc_2873] @pray_at_statue("Saradomin", saradomincape, ^mage_arena_saradomin);
[oploc1,loc_2874] @pray_at_statue("Zamorak", zamorakcape, ^mage_arena_zamorak);
[oploc1,loc_2875] @pray_at_statue("Guthix", guthixcape, ^mage_arena_guthix);

[label,pray_at_statue](string $name, namedobj $cape, int $stage)
~forcewalk2(movecoord(loc_coord, 0, 0, -2));
facesquare(loc_coord);
if (%magearena_progress <= ^mage_arena_prayed_at_statue) { // first time
    %magearena_progress = $stage;
    ~mesbox("You kneel and begin to chant to <$name>...");
    if_close;
    anim(human_pray, 10);
    p_delay(2);
    obj_add(movecoord(loc_coord, 0, 0, -1), $cape, 1, 50); // 50 ticks in osrs
    spotanim_map(smokepuff_large, movecoord(loc_coord, 0, 0, -1), 60, 0);
    sound_synth(smokepuff, 0, 0);
    ~mesbox("You feel a rush of energy charge through your veins. Suddenly a|cape appears before you.");
    return;
}
if (~obj_gettotal(saradomincape) > 0 | ~obj_gettotal(guthixcape) > 0 | ~obj_gettotal(zamorakcape) > 0) { // already have a cape
    anim(human_pray, 10);
    p_delay(2);
    ~mesbox("You kneel and chant to <$name>...|... but there is no response.");
    return;
}
if (inv_freespace(inv) < 1) { // osrs, similar to rsc dialogue too :)
    anim(human_pray, 10);
    p_delay(2);
    ~mesbox("You kneel and chant to <$name>...|... but there is no response.|You feel that making space in your inventory could help.");
    return;
}
if_close;
anim(human_pray, 10);
p_delay(2);
inv_add(inv, $cape, 1);
spotanim_map(smokepuff_large, coord, 60, 0);
sound_synth(smokepuff, 0, 0);
~mesbox("You kneel and chant to <$name>...|You feel a rush of energy charge through your veins.|Suddenly a cape appears in your pack.");