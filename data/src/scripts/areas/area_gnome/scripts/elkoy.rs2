[opnpc1,elkoy_outmaze]
switch_int(%tree_progress) {
    case ^tree_not_started: @elkoy_intro;
    case ^tree_started, ^tree_spoken_montai, ^tree_given_logs_montai, ^tree_finding_trackers, ^tree_ballista_fired: @elkoy_looking_orbs;
    case ^tree_retrieved_orb: @elkoy_retrieved_orb_outside;
}

[opnpc1,elkoy_maze]
switch_int(%tree_progress) {
    case ^tree_not_started: @elkoy_intro;
    case ^tree_started, ^tree_spoken_montai, ^tree_given_logs_montai, ^tree_finding_trackers, ^tree_ballista_fired: @elkoy_looking_orbs;
    case ^tree_retrieved_orb: @elkoy_retrieved_orb_inside;
}

[label,elkoy_intro]
~chatplayer(neutral, "Hello there.");
~chatnpc(happy, "Hello, welcome to our maze.|I'm Elkoy the tree gnome.");
~chatplayer(quiz, "I haven't heard of your sort.");
~chatnpc(sad, "There's not many of us left.|Once you could find tree gnomes|anywhere in the world, now we hide|in small groups to avoid capture.");
~chatplayer(quiz, "Capture by whom?");
~chatnpc(angry, "Tree gnomes have been hunted|for so called 'fun' since as|long as I can remember.");
~chatnpc(sad, "Our main threat nowadays|are General Khazard's troops.|They know no mercy, but are also very dense.|They'll never find their way through our maze.");
~chatnpc(neutral, "Have fun.");

[label,elkoy_looking_orbs]
if(%tree_progress = ^tree_started) {
    ~chatplayer(happy, "Hello Elkoy.");
    ~chatnpc(sad, "Oh my! Oh my!");
    ~chatplayer(quiz, "What's wrong?");
    ~chatnpc(sad, "The orb, they have the orb. We're doomed.|Do you need me to show you back to the village?");
} else {
    ~chatplayer(neutral, "Hello.");
    ~chatnpc(sad, "You must retrieve the orb, or the gnome village is doomed. Do you need me to show you through the maze?");
}
@elkoy_maze_preorb;

[label,elkoy_maze_preorb]
def_coord $dest_coord = ^elkoy_maze_coord;
if(npc_type = elkoy_maze) {
    $dest_coord = ^elkoy_entrance_coord;
}
switch_int(~p_choice2("Yes please.", 1, "Not now, thanks.", 2)) {
    case 1:
        ~chatplayer(happy, "Yes please.");
        ~chatnpc(happy, "Ok then, follow me.");
        p_telejump($dest_coord);
        ~chatnpc_specific("Elkoy", elkoy_maze, sad, "Please help us get our orb back.");
    case 2:
        ~chatplayer(neutral, "Not now, thanks.");
}

[label,elkoy_retrieved_orb_outside]
if(inv_total(inv, first_orb_of_protection) = 0) {
    @elkoy_missing_first_orb;
}
~chatplayer(happy, "Hello Elkoy.");
~chatnpc(shock, "You're back! And the orb?");
~chatplayer(happy, "I have it here.");
~chatnpc(happy, "You're our saviour. Please return it to the village and we are all saved. Would you like me to show you the way to the village?");
switch_int(~p_choice2("Yes please.", 1, "No thanks Elkoy.", 2)) {
    case 1:
        ~chatplayer(happy, "Yes please.");
        ~chatnpc(happy, "Ok then, follow me.");
        p_telejump(^elkoy_maze_coord);
        ~chatnpc_specific("Elkoy", elkoy_maze, happy, "Here we are. Take the orb to King Bolren, I'm sure he'll be pleased to see you.");
    case 2:
        ~chatplayer(sad, "No thanks Elkoy.");
        ~chatnpc(sad, "Please, we must have the orb if we are to survive.");
}

[label,elkoy_retrieved_orb_inside]
if(inv_total(inv, first_orb_of_protection) = 0) {
    @elkoy_missing_first_orb;
}
~chatplayer(happy, "Hello Elkoy. I have the orb.");
~chatnpc(happy, "Take it to King Bolren, I'm sure he'll be pleased to see you.");
switch_int(~p_choice2("Can you show me out of the village?", 1, "Okay.", 2)) {
    case 1:
        ~chatplayer(quiz, "Can you show me out of the village?");
        ~chatnpc(happy, "Ok then, follow me.");
        p_telejump(^elkoy_entrance_coord);
        ~chatnpc_specific("Elkoy", elkoy_maze, sad, "Please return with our orb soon.");
    case 2: ~chatplayer(happy, "Okay.");
}

[label,elkoy_missing_first_orb]
~chatplayer(happy, "Hello Elkoy.");
~chatnpc(shock, "You're back! And the orb?");
~chatplayer(sad, "No, I'm afraid not.");
~chatnpc(sad, "Please, we must have the orb if we are to survive.|Do you need me to show you through the maze?");
@elkoy_maze_preorb;

[label,elkoy_leavemaze_initial]
p_telejump(0_39_49_8_55);
if(npc_finduid(~npc_within_distance(coord, elkoy_outmaze, 4)) = true) {
    ~chatnpc(happy, "We're out of the maze now. Please hurry, we must have the orb if we are to survive.");
}