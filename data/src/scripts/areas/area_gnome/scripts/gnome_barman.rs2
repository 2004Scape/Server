[opnpc1,gnome_barman]
// https://media.discordapp.net/attachments/1126857340075905135/1267505354502049883/grandtreebarman.png?ex=66af9f59&is=66ae4dd9&hm=e372c1e1c50c273cde88e52a910f1717216f59e01b0a1d5666e73890a2fdf11d&=&format=webp&quality=lossless&width=491&height=105
// image from 2005, probably accidental space from removing linebreak (matches rsc)
~chatnpc("<p,happy>Good day to you.|What can I get you to drink?");
def_int $choice;
if (inv_totalcat(inv, category_83) > 0) {
    if(testbit(%barcrawl_progress, ^blurberry_index) = false & inv_total(inv, barcrawl_card) > 0) {
        $choice = ~p_choice4("I'm trying to do Alfred Grimhand's barcrawl.", 5, "What do you have?", 1, "Nothing thanks.", 2, "Actually I'd like to sell some drinks.", 3);
    } else {
        $choice = ~p_choice3("What do you have?", 1, "Nothing thanks.", 2, "Actually I'd like to sell some drinks.", 3);
    }
} else {
    if(testbit(%barcrawl_progress, ^blurberry_index) = false & inv_total(inv, barcrawl_card) > 0) {
        $choice = ~p_choice4("I'm trying to do Alfred Grimhand's barcrawl.", 5, "What do you have?", 1, "Nothing thanks.", 2, "Can I buy some ingredients?", 4);
    } else {
        $choice = ~p_choice3("What do you have?", 1, "Nothing thanks.", 2, "Can I buy some ingredients?", 4);
    }
}
switch_int($choice) {
    case 1 : 
        ~chatplayer("<p,quiz>What do you have.");
        ~chatnpc("<p,happy>Here, take a look at our menu.");
        ~openshop_activenpc;
        return;
    case 2 :
        ~chatplayer("<p,neutral>Nothing thanks.");
        ~chatnpc("<p,neutral>Ok, take it easy.");
        return;
    case 3 :
        ~chatplayer("<p,neutral>Actually I'd like to sell some drinks.");
        ~chatnpc("<p,neutral>If you want to sell any cocktails you've made yourself|I suggest you talk to Blurberry, he's the one who owns|the bar.");
        ~chatplayer("<p,neutral>Thank you.");
        return;
    case 5 : @gnome_barman_barcrawl;
}
~chatplayer("<p,quiz>I was just wanting to buy a cocktail ingredient actually.");
~chatnpc("<p,quiz>Sure thing, what did you want?");
def_namedobj $item;
$choice = ~p_choice4("A lemon", 1, "An orange.", 2, "A cocktail shaker.", 3, "Nothing thanks.", 4);
switch_int($choice) {
    case 1 :
        ~chatplayer("<p,neutral>A lemon.");
        $item = lemon;
    case 2 :
        ~chatplayer("<p,neutral>An orange.");
        $item = orange;
    case 3 :
        ~chatplayer("<p,neutral>A cocktail shaker.");
        $item = cocktail_shaker;
    case default :
        ~chatplayer("<p,neutral>Actually nothing thanks.");
        return;
}
~chatnpc("<p,neutral>20 coins please.");
if (inv_total(inv, coins) < 20) {
    if_close;
    mes("You do not have enough money to buy that.");
    return;
}
inv_del(inv, coins, 20);
inv_add(inv, $item, 1);

[label,gnome_barman_barcrawl]
~chatplayer("<p,happy>I'm trying to do Alfred Grimhand's barcrawl.");
~chatnpc("<p,laugh>Oh, another silly human come to have his mind melted? You should take that barcrawl card to Blurberry - he always likes to serve the Fire Toad Blast himself!");
~chatplayer("<p,confused>Um... thanks?!");

[opnpcu,gnome_barman]
if(testbit(%barcrawl_progress, ^blurberry_index) = false & last_useitem = barcrawl_card) {
    @gnome_barman_barcrawl;
}
~displaymessage(^dm_default);