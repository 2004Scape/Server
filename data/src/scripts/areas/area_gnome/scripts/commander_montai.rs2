[opnpc1,commander_montai]
switch_int(%tree_progress) {
    case ^tree_not_started :
        ~chatplayer("<p,neutral>Hello.");
        ~chatnpc("<p,angry>I can't talk now.|Can't you see we're trying to win a battle here?|If we can't hold back Khazard's men|we're all doomed.");
    case ^tree_started :
        ~chatplayer("<p,neutral>Hello.");
        ~chatnpc("<p,neutral>Hello traveller, are you here to help or just to watch?");
        ~chatplayer("<p,neutral>I've been sent by King Bolren to retrieve the orb of protection.");
        ~chatnpc("<p,happy>Excellent we need all the help we can get.");
        ~chatnpc("<p,neutral>I'm commander Montai.|The orb is in the Khazard stronghold to the north,|but until we weaken their defences|we can't get close.");
        ~chatplayer("<p,quiz>What can I do?");
        ~chatnpc("<p,neutral>Firstly we need to strengthen our own defences. We desperately need wood to make more battlements, once the battlements are gone it's all over. Six loads of normal logs should do it.");
        switch_int(~p_choice2("Ok, I'll gather some wood.", 1, "Sorry, I no longer want to be involved.", 2)) {
            case 1 :
                ~chatplayer("<p,neutral>Ok, I'll gather some wood.");
                %tree_progress = ^tree_spoken_montai;
                ~chatnpc("<p,neutral>Please be as quick as you can, I don't know how much longer we can hold out.");
            case 2 :
                ~chatplayer("<p,sad>Sorry I no longer want to be involved.");
                ~chatnpc("<p,sad>That's a shame, we could have done with your help.");
        }
    case ^tree_spoken_montai :
        ~chatplayer("<p,neutral>Hello.");
        if(inv_total(inv, logs) < 6) {
            ~chatnpc("<p,neutral>Hello again, we're still desperate for wood soldier. We need six loads of normal logs.");
            ~chatplayer("<p,neutral>I'll see what I can do.");
            ~chatnpc("<p,neutral>Thank you.");
            return;
        }
        ~chatnpc("<p,neutral>Hello again, we're still desperate for wood soldier.");
        inv_del(inv, logs, 6);
        %tree_progress = ^tree_given_logs_montai;
        ~chatplayer("<p,happy>I have some here.|@whi@(You give six loads of logs to the commander.)");
        ~chatnpc("<p,happy>That's excellent, now we can make more defensive battlements.  Give me a moment to organise the troops and then come speak to me. I'll inform you of our next phase of attack.");
    case ^tree_given_logs_montai :
        ~chatplayer("<p,neutral>How are you doing Montai?");
        ~chatnpc("<p,bored>We're hanging in there soldier.|For the next phase of our attack|we need to breach their stronghold.");
        ~chatnpc("<p,bored>The ballista can break through the stronghold wall,|and then we can advance and seize back the orb.");
        ~chatplayer("<p,quiz>So what's the problem?");
        ~chatnpc("<p,bored>From this distance we can't get an accurate enough shot. We need the correct coordinates of the stronghold for a direct hit. I've sent out three tracker gnomes to gather them.");
        ~chatplayer("<p,quiz>Have they returned?");
        ~chatnpc("<p,sad>I'm afraid not, and we're running out of time.|I need you to go into the heart of the battlefield,|find the trackers, and bring back the coordinates.");
        ~chatnpc("<p,quiz>Do you think you can do it?");
        switch_int(~p_choice2("No, I've had enough of your battle.", 1, "I'll try my best.", 2)) {
            case 1 :
                ~chatplayer("<p,sad>No, I've had enough of your battle.");
                ~chatnpc("<p,sad>I understand, this isn't your fight.");
            case 2 :
                ~chatplayer("<p,neutral>I'll try my best.");
                ~chatnpc("<p,happy>Thank you, you're braver than most.");
                %tree_progress = ^tree_finding_trackers;
                ~chatnpc("<p,bored>I don't know how long I will be able to hold out.|Once you have the coordinates|come back and fire the ballista|right into those monsters.");
                ~chatnpc("<p,bored>If you can retrieve the orb and bring safety back to my people, none of the blood spilled on this field will be in vain.");
        }
    case ^tree_finding_trackers :
        ~chatplayer("<p,neutral>Hello.");
        ~chatnpc("<p,bored>Hello warrior. We need the coordinates|for a direct hit from the ballista.|Once you have a direct hit you will be able|to enter the stronghold and retrieve the orb.");
    case ^tree_ballista_fired :
        ~chatplayer("<p,happy>I've breached the stronghold.");
        ~chatnpc("<p,happy>I saw, that was a beautiful sight. The Khazard troops didn't know what hit them.");
        ~chatnpc("<p,neutral>Now is the time to retrieve the orb. It's all in your hands. I'll be praying for you.");
    case ^tree_retrieved_orb :
        ~chatplayer("<p,happy>I have the orb of protection.");
        ~chatnpc("<p,happy>Incredible, for a human you really are something.");
        ~chatplayer("<p,confused>Thanks... I think!");
        ~chatnpc("<p,neutral>I'll stay here with my troops and try and hold Khazard's men back. You return the orb to the gnome village. Go as quick as you can, the village is still unprotected.");
    case ^tree_returned_first_orb, ^tree_defeated_warlord, ^tree_complete :
        ~chatplayer("<p,quiz>Hello Montai, how are you?");
        ~chatnpc("<p,bored>I'm ok, this battle is going|to take longer to win than I expected.|The Khazard troops won't give up even without the orb.");
        ~chatplayer("<p,happy>Hang in there.");
}

[ai_timer,commander_montai] 
def_coord $projectile_start = 0_39_50_31_5;
def_coord $dest_coord = map_findsquare(movecoord($projectile_start, -3, 0, 33), 0, 5, ^map_findsquare_none);
def_int $delay = calc(distance($projectile_start, $dest_coord) / 2);
if(loc_find(0_39_50_30_4, loc_2180) = true) {
    loc_anim(catapult_release_reset);
    ~coord_projectile($projectile_start, $dest_coord, catapultload, 50, 2, 15, 25, 21, 64, 10);
    world_delay($delay);
    spotanim_map(catapultimpact, $dest_coord, 0, 0); // yes, this does appear far after the projectile lands
    huntall($dest_coord, 1, 0);
    if(huntnext = true) {
        ~damage_self(random(4)); // https://x.com/JagexAsh/status/1538818765676236801
    }
    npc_huntall($dest_coord, 1, 0);
    while (npc_huntnext = true) {
        ~npc_default_damage(random(4));
    }

}