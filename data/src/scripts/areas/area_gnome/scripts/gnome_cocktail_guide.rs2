[opheld1,cocktail_guide] @open_cocktail_guide_index;
[if_button,blurberry_cocktail_guide:index] @open_cocktail_guide_index;
[if_button,blurberry_cocktail_guide2:rightarrow] 
if (%open_book = giannes_cook_book) {
    @flip_page_cook_book(1);
} else {
    @flip_page_cocktail_guide(1);
}
[if_button,blurberry_cocktail_guide2:blurberry_special] @flip_page_cocktail_guide(1);
[if_button,blurberry_cocktail_guide2:chocolate_saturday] @flip_page_cocktail_guide(2);
[if_button,blurberry_cocktail_guide2:drunk_dragon] @flip_page_cocktail_guide(3);
[if_button,blurberry_cocktail_guide2:fruit_blast] @flip_page_cocktail_guide(4);
[if_button,blurberry_cocktail_guide2:pineapple_punch] @flip_page_cocktail_guide(5);
[if_button,blurberry_cocktail_guide2:sgg] @flip_page_cocktail_guide(6);
[if_button,blurberry_cocktail_guide2:wizard_blizzard] @flip_page_cocktail_guide(7);
[if_button,blurberry_cocktail_guide:leftarrow] @flip_page_cocktail_guide(calc(%book_page - 1));
[if_button,blurberry_cocktail_guide:rightarrow] @flip_page_cocktail_guide(calc(%book_page + 1));


[label,open_cocktail_guide_index]
%book_page = 0;
%open_book = cocktail_guide;
if_sethide(blurberry_cocktail_guide2:gnome_food_index_layer, true);
if_sethide(blurberry_cocktail_guide2:cocktail_index_layer, false);
if_settext(blurberry_cocktail_guide2:book_title, "The Blurberry Cocktail Guide");
if_openmain(blurberry_cocktail_guide2);

[label,flip_page_cocktail_guide](int $page_number)
%book_page = $page_number;
if ($page_number = 0 | $page_number > enum_getoutputcount(cocktail_guide_title)) {
    @open_cocktail_guide_index;
}
if ($page_number = enum_getoutputcount(cocktail_guide_title)) {
    if_sethide(blurberry_cocktail_guide:rightarrow_layer, true);
} else {
    if_sethide(blurberry_cocktail_guide:rightarrow_layer, false);
}

if_settext(blurberry_cocktail_guide:page_title, enum(int, string, cocktail_guide_title, $page_number));
if_sethide(blurberry_cocktail_guide:cook_obj, false);
if_setobject(blurberry_cocktail_guide:cook_obj, enum(int, namedobj, cocktail_guide_model, $page_number), 300);
if_settext(blurberry_cocktail_guide:text_left, "Ingredients:\\n\\n<enum(int, string, cocktail_guide_text_left, $page_number)>");
if_settext(blurberry_cocktail_guide:text_right, "Mix the ingredients\\ntogether in the cocktail\\nshaker.  Shake well and\\npour into a glass.\\n\\n<enum(int, string, cocktail_guide_text_right, $page_number)>");
if_openmain(blurberry_cocktail_guide);