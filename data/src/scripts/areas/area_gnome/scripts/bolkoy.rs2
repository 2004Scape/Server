[opnpc1,bolkoy]
switch_int(%tree_progress) {
    case ^tree_retrieved_orb:
        ~chatplayer("<p,neutral>Hello.");
        if(inv_total(inv, orb_of_protection) > 0) {
            ~chatnpc("<p,shock>Amazing, you recovered the orb.");
            ~chatnpc("<p,happy>Well I am impressed.|Would you like to buy something?");
        } else {
            ~chatnpc("<p,neutral>I'm Bolkoy the village shopkeeper.|Would you like to buy something?");     
        }   
    case ^tree_returned_first_orb:
        ~chatplayer("<p,neutral>Hi.");
        ~chatnpc("<p,sad>Oh, hello there.|Have you heard? They took|the other orbs, it's terrible.|I suppose the show must go on.");
        ~chatnpc("<p,sad>Would you like to buy something?");
    case ^tree_defeated_warlord:
        ~chatplayer("<p,happy>Hello.");
        ~chatnpc("<p,happy>Hello there.|You're that hero who saved the orbs.|Soon we will perform the ritual|and the village will be safe again.");
        ~chatnpc("<p,happy>Anyway, would you like anything from my shop?");
    case ^tree_complete:
        ~chatplayer("<p,happy>Hello.");
        ~chatnpc("<p,happy>Welcome, welcome. It's good to see you again. The village is much safer now you have returned the orbs.");
        ~chatnpc("<p,happy>By the way, I'm the village shop keeper.|Would you like to buy something?");
    case default:
        ~chatplayer("<p,neutral>Hello there.");
        ~chatnpc("<p,neutral>Hello stranger, new to these parts?");
        ~chatnpc("<p,happy>I'm Bolkoy by the way.|I'm the village shopkeeper.|Would you like to buy something?");
}
switch_int(~p_choice2("What have you got?", 1, "No thank you.", 2)) {
    case 1: 
        ~chatplayer("<p,quiz>What have you got?");
        ~chatnpc("<p,neutral>Take a look.");
        ~openshop_activenpc;
    case 2: 
        ~chatplayer("<p,neutral>No thank you.");
        ~chatnpc("<p,neutral>Ok, maybe later.");
}