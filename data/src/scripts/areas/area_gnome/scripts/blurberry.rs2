[opnpc1,blurberry]
if(testbit(%barcrawl_progress, ^blurberry_index) = ^false & inv_total(inv, barcrawl_card) > 0) {
    @blurberry_barcrawl;
}
switch_int(%gnome_bar_progress) {
    case 0 : @gnome_bar_start;
    case 1 : @gnome_bar_assign_fruit_blast;
    case 2 : @gnome_bar_assign_drunk_dragon;
    case 3 : @gnome_bar_assign_ssg;
    case 4 : @gnome_bar_assign_chocolate_saturday;
    case 5 : @gnome_bar_assign_blurberry_special;
    case 6 : @gnome_bar_complete;
    case default :
        if (%gnome_bar_progress = 7 | %gnome_bar_progress > 12) {
            // rsc dialogue
            ~chatplayer("<p,neutral>Hello again Blurberry!");
            ~chatnpc("<p,neutral>Well hello traveller.");
            ~chatnpc("<p,neutral>I'm quite busy as usual, any chance you could help?");
            @gnome_bar_cocktail_job_start;
        }
        if (%gnome_bar_progress > 7 & %gnome_bar_progress < 13) {
            @gnome_bar_cocktail_job_finish;
        }
}

[opnpcu,blurberry]
if(testbit(%barcrawl_progress, ^blurberry_index) = ^false & last_useitem = barcrawl_card) {
    @blurberry_barcrawl;
}
def_category $category = oc_category(last_useitem);
if ($category = gnome_cocktail | $category = gnome_unf_odd_cocktail) {
    switch_int(%gnome_bar_progress) {
        case 0 : ~chatnpc("<p,neutral>Nice cocktail, but I don't need any right now...");
        case 1 : @gnome_bar_assign_fruit_blast;
        case 2 : @gnome_bar_assign_drunk_dragon;
        case 3 : @gnome_bar_assign_ssg;
        case 4 : @gnome_bar_assign_chocolate_saturday;
        case 5 : @gnome_bar_assign_blurberry_special;
        case 6 : @gnome_bar_complete;
        case default :
            if (%gnome_bar_progress = 7 | %gnome_bar_progress > 12) {
                ~chatnpc("<p,neutral>Nice cocktail, but I don't need any right now...");
            }
            if (%gnome_bar_progress > 7 & %gnome_bar_progress < 13) {
                @gnome_bar_cocktail_job_finish;
            }

    }
}

[label,blurberry_barcrawl]
~chatplayer("<p,neutral>I'm doing Alfred Grimhand's Barcrawl.");
~chatnpc("<p,neutral>Ah, you've come to the best stop on your list!|I'll give you my famous Fire Toad Blast!|It'll cost you 10 coins.");
if(inv_total(inv, coins) < 10) {
    ~chatplayer("<p,sad>I don't have 10 coins right now.");
    return;
}   
if_close;
inv_del(inv, coins, 10);
mes("You buy a Fire Toad Blast.");
p_delay(2);
mes("Your mouth and throat burn as you gulp it down.");
p_delay(2);
%barcrawl_progress = setbit(%barcrawl_progress, ^blurberry_index);
mes("Blurberry signs your card.");
p_delay(2);
~damage_self(calc((stat(hitpoints) * 5) / 100));
// In OSRS this closes so you never see it (because damage is strongqueued, which isn't the case in 225)
~chatplayer("<p,confused>Danks bery mush...");