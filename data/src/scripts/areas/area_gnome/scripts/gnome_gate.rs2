// Gnome stronghold entrance gate
[oploc1,loc_190]
if(%grandtree_progress = ^grandtree_released_prison & coordz(coord) > coordz(loc_coord)) { // only when leaving
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,angry>Halt, human!");
    ~chatplayer("<p,shock>What?, why?"); // intentional
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,angry>By order of the head tree guardian!|You can not leave!");
    ~chatplayer("<p,shock>That's crazy! Why?!");
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,angry>Humans are planning to attack our stronghold.|You could be a spy!");
    ~chatplayer("<p,angry>That's ridiculous!");
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,neutral>Maybe, but that's the orders, I'm sorry.");
    return;
}
if(%grandtree_progress = ^grandtree_obtained_lumber_order & coordz(coord) < coordz(loc_coord)) { // todo: is this before or after helping femi?
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,neutral>I'm afraid that we have orders not to let you in.");
    ~chatplayer("<p,angry>Orders from who?");
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,neutral>The head tree guardian, he say's you're a spy!");
    ~chatplayer("<p,angry>Glough!");
    ~chatnpc_specific(nc_name(gnomekingsguard), gnomekingsguard, "<p,neutral>I'm sorry but you'll have to leave.");
    return;
}
if(%femi_help = 0 & coordz(coord) < coordz(loc_coord) & npc_find(coord, femi, 6, 0) = true) {
    @femi_boxes;
}
// walk to start
def_coord $loc_coord = loc_coord;
def_coord $north_entrance = movecoord($loc_coord, 2, 0, 2);
def_coord $south_entrance = movecoord($loc_coord, 2, 0, -1);
if(coordz(coord) > coordz($loc_coord) & coord ! $north_entrance) {
    p_delay(0);
    p_teleport($north_entrance);
} else if (coordz(coord) <= coordz($loc_coord) & coord ! $south_entrance) {
    p_delay(0);
    p_teleport($south_entrance);
}
p_delay(0);
// replace gate
// Temp note: dur updated
loc_del(5);
loc_add(movecoord($loc_coord, 3, 0, 0), loc_192, 0, loc_shape, 5); // opened right side
loc_add(movecoord($loc_coord, 2, 0, 0), inviswall, 3, loc_shape, 5); // blocks whole path
loc_add(movecoord($loc_coord, 2, 0, 1), inviswall, 1, loc_shape, 5);
loc_add($loc_coord, loc_191, 2, loc_shape, 5); 
// walk to dest
if(coordz(coord) > coordz($loc_coord)) {
    ~forcemove(movecoord(coord, 0, 0, -3));
} else {
    ~forcemove(movecoord(coord, 0, 0, 3));
}

// Grand Tree gate
[oploc1,loc_1967] @open_tree_door(loc_1969, 2465);
[oploc1,loc_1968] @open_tree_door(loc_1970, 2466);

// pass start x because these door locs are also on the sides (maybe to increase the hitbox)
[label,open_tree_door](loc $open_door, int $start_x)
sound_synth(door_open, 0, 0);
// Temp note: dur updated
loc_change($open_door, 5);
p_teleport(movecoord(coord, calc($start_x - coordx(coord)), 0, 0));
p_delay(0);
def_coord $end_pos = movecoord(coord, 0, 0, -2);
if(coordz(coord) < coordz(loc_coord)) {
    $end_pos = movecoord(coord, 0, 0, 2);
}
~forcemove($end_pos);
p_delay(0);
