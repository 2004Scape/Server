// *********************** TUTORIAL ************************
[label,gnome_bar_start]
~chatplayer("<p,neutral>Hello.");
~chatnpc("<p,happy>Well hello there <displayname()>.|If you're looking for a cocktail|the barman will happily make you one."); // line breaks are guessed
~chatplayer("<p,neutral>He looks pretty busy.");
~chatnpc("<p,happy>I know, I just can't find any skilled staff.|I don't suppose you're looking for some part time work?|The pay isn't great but it's a good way to meet people.");
def_int $choice = ~p_choice2("No thanks, I prefer to stay this side of the bar.", 1, "Ok then I'll give it a go.", 2);
if ($choice = 1) {
    ~chatplayer("<p,neutral>No thanks, I prefer to stay this side of the bar.");
    return;
}
~chatplayer("<p,happy>Ok then I'll give it a go.");
~chatnpc("<p,happy>Excellent. It's not an easy job, I'll have to try you out first. I'm sure you'll be great though.");
inv_add(inv, cocktail_guide, 1);
%gnome_bar_progress = 1;
~chatnpc("<p,happy>Here, take this cocktail guide. The book tells you how to|make all the cocktails we serve. I'll tell you what I need|and you can make them.");
~chatplayer("<p,happy>Sounds easy enough.");
~chatnpc("<p,neutral>Take a look at the book then come and talk to me.");

[label,gnome_bar_assign_fruit_blast]
~chatplayer("<p,neutral>Hello Blurberry.");
~chatnpc("<p,happy>Hi, are you ready to make your first cocktail?");
~chatplayer("<p,happy>Absolutely.");
~chatnpc("<p,happy>Ok then, to start with make a Fruit Blast. Here, you'll|need these ingredients, but I'm afraid I can't give you|any more if you mess up.");
inv_add(inv, lemon, 2);
inv_add(inv, orange, 1);
inv_add(inv, pineapple, 1);
inv_add(inv, cocktail_glass_empty, 1);
inv_add(inv, knife, 1);
inv_add(inv, cocktail_shaker, 1);
%gnome_bar_progress = 2;
~mesbox("Blurberry gives you two lemons, one orange, one pineapple, a cocktail shaker, a glass and a knife.");
~chatnpc("<p,neutral>Let me know when you're done.");

[label,gnome_bar_assign_drunk_dragon]
~chatnpc("<p,neutral>So where's my Fruit Blast?");
if (inv_total(inv, fruit_blast) > 0) {
    ~chatplayer("<p,happy>Here you go.");
    ~mesbox("You give Blurberry the Fruit Blast. He takes a sip...");
    ~chatnpc("<p,happy>Hmmm... not bad, not bad at all.");
    // slightly different than rsc and osrs. rsc has the "I wont be able to give you any more" after he gives u items
    ~chatnpc("<p,quiz>Now can you make me a Drunk Dragon?");
    inv_del(inv, fruit_blast, 1);
    inv_add(inv, cocktail_glass_empty, 1);
    inv_add(inv, vodka, 1);
    inv_add(inv, gin, 1);
    inv_add(inv, dwellberry, 1);
    inv_add(inv, pineapple, 1);
    inv_add(inv, pot_of_cream, 1);
    %gnome_bar_progress = 3;
    ~mesbox("Blurberry gives you some vodka, some gin, some dwellberries, some pineapple, and some cream.");
    ~chatnpc("<p,neutral>I'm afraid I won't be able to give you anymore if you make a mistake though.");
    ~chatnpc("<p,neutral>Let me know when it's done.");
    return;
}
if (~gnome_bar_cocktail_checks(premade_fruit_blast, "Fruit Blast") = false) {
    return;
}
~chatnpc("<p,angry>I don't know what you have there but it's no Fruit|Blast!");

[label,gnome_bar_assign_ssg]
~chatplayer("<p,neutral>Hello Blurberry.");
~chatnpc("<p,quiz>Hello again <displayname()>, how did you do?");
if (inv_total(inv, drunk_dragon) > 0) {
    ~chatplayer("<p,happy>Here you go.");
    ~mesbox("You give Blurberry the Drunk Dragon. He takes a sip...");
    ~chatnpc("<p,happy>Woooo, that's some good stuff. I can sell that.|There you go, your share of the profit.");
    // not sure if this is an mesbox or even exists. exists in rsc. not in 2006
    inv_add(inv, coins, 1);
    ~mesbox("Blurberry gives you 1 gold coin.");
    ~chatplayer("<p,happy>Thanks.");
    ~chatnpc("<p,neutral>Okay then now I need an SGG.");
    ~chatplayer("<p,confused>A what?");
    ~chatnpc("<p,neutral>A Short Green Guy, and don't bring me a gnome.|Here's all you need.");
    inv_del(inv, drunk_dragon, 1);
    inv_add(inv, cocktail_glass_empty, 1);
    inv_add(inv, vodka, 1);
    inv_add(inv, lime, 4);
    inv_add(inv, equa_leaves, 1);
    %gnome_bar_progress = 4;
    ~mesbox("Blurberry gives you four limes, some vodka and some equa leaves.");
    return;
}
if (~gnome_bar_cocktail_checks(premade_drunk_dragon, "Drunk Dragon") = false) {
    return;
}
~chatnpc("<p,angry>I don't know what you have there but it's no Drunk|Dragon!");

[label,gnome_bar_assign_chocolate_saturday]
~chatplayer("<p,neutral>Hi Blurberry.");
~chatnpc("<p,quiz>So, have you got my SGG?");
if (inv_total(inv, sgg) > 0) {
    ~chatplayer("<p,happy>Here you go.");
    ~mesbox("You give Blurberry the SGG. He takes a sip...");
    ~chatnpc("<p,happy>Hmmm, not bad, not bad at all. I can sell that.|There you go, that's your share.");
    inv_add(inv, coins, 1);
    ~mesbox("Blurberry gives you 1 gold coin.");
    ~chatnpc("<p,neutral>You're doing quite well, I'm impressed.");
    ~chatnpc("<p,neutral>Ok let's try a Choc Saturday, I love them.");
    inv_del(inv, sgg, 1);
    inv_add(inv, cocktail_glass_empty, 1);
    inv_add(inv, whisky, 1);
    inv_add(inv, bucket_milk, 1);
    inv_add(inv, equa_leaves, 1);
    inv_add(inv, chocolate_bar, 1);
    inv_add(inv, pot_of_cream, 1);
    inv_add(inv, chocolate_dust, 1);
    %gnome_bar_progress = 5;
    ~mesbox("Blurberry gives you some wisky, some milk, some equa leaves, a chocolate bar, some cream, and some chocolate dust.");
    return;
}
if (~gnome_bar_cocktail_checks(premade_sgg, "SGG") = false) {
    return;
}
~chatnpc("<p,angry>I don't know what you have there but it's no SGG!");

[label,gnome_bar_assign_blurberry_special]
~chatplayer("<p,neutral>Hello Blurberry.");
~chatnpc("<p,neutral>Hello, how did it go with the Choc Saturday?");
if (inv_total(inv, chocolate_saturday) > 0) {
    ~chatplayer("<p,quiz>Here, try some.");
    ~mesbox("You give Blurberry the Choc Saturday. He takes a sip...");
    ~chatnpc("<p,happy>That's blurberry-tastic! You're quite a bartender.");
    ~chatnpc("<p,happy>Okay, let's test you once more. Make me a|Blurberry Special then we'll see if you have what it takes.|Here's your ingredients.");
    inv_del(inv, chocolate_saturday, 1);
    inv_add(inv, cocktail_glass_empty, 1);
    inv_add(inv, vodka, 1);
    inv_add(inv, gin, 1);
    inv_add(inv, brandy, 1);
    inv_add(inv, lemon, 3);
    inv_add(inv, orange, 2);
    inv_add(inv, lime, 1);
    inv_add(inv, equa_leaves, 1);
    %gnome_bar_progress = 6;
    ~chatplayer("<p,neutral>Ok, I'll try my best.");
    ~chatnpc("<p,neutral>I'm sure you'll make a great bartender.");
    return;
}
if (~gnome_bar_cocktail_checks(premade_chocolate_saturday, "Choc Saturday") = false) {
    return;
}
~chatplayer("<p,sad>I haven't managed to make it yet.");
~chatnpc("<p,neutral>Ok, it's one Choc Saturday I need.  Let me know when|it's done.");

[label,gnome_bar_complete]
~chatplayer("<p,neutral>Hi again.");
~chatnpc("<p,neutral>So how did you do?");
if (inv_total(inv, blurberry_special) > 0) {
    ~chatplayer("<p,quiz>I think I've made it right.");
    ~mesbox("You give Blurberry the Blurberry Special. He takes a sip...");
    inv_del(inv, blurberry_special, 1);
    inv_add(inv, cocktail_glass_empty, 1);
    %gnome_bar_progress = 7;
    ~chatnpc("<p,shock>Well I never... incredible. Not many manage to get that|right, but this is perfect!");
    ~chatnpc("<p,quiz>It would be an honour to have you on the team.");
    ~chatplayer("<p,quiz>Thanks!");
    ~chatnpc("<p,quiz>Now if you ever want to make some money or want|to improve your cooking skills, just come and see me.|I'll tell you what drinks we need, and if you can, you make them.");
    ~chatplayer("<p,confused>What about ingredients?");
    ~chatnpc("<p,happy>I'm afraid I can't give you anymore for free, but you can buy them from Heckel Funch the grocer.");
    ~chatnpc("<p,happy>I'll always pay you more for the cocktail than you paid for the ingredients and it's a great way to learn how to prepare food and drinks.");
    return;
}
if (~gnome_bar_cocktail_checks(premade_blurberry_special, "Blurberry Special") = false) {
    return;
}
~chatplayer("<p,sad>I haven't managed to make it yet.");
~chatnpc("<p,neutral>I need one Blurberry Special.  Let me know when|it's done.");


[proc,gnome_bar_cocktail_checks](namedobj $premade_cocktail, string $name)(boolean)
// Im not sure if this exists in our rev. It exists in osrs, but not in rsc. Since its kinda scuffed i assume it was added in 2006
// if (inv_total(inv, cocktail_shaker) < 1) {
//     def_int $choice = ~p_choice2("I'm afraid I lost the cocktail shaker you gave me.", 1, "I've not finished yet.", 2);
//     if ($choice = 1) {
//         ~chatplayer("<p,sad>I'm afraid I lost the cocktail shaker you gave me.");
//         inv_add(inv, cocktail_shaker, 1);
//         ~chatnpc("<p,happy>I can see how that would make it hard to mix cocktails.|I'll give you another one, but please try not lose any|more.");
//         return(false);
//     }
//     ~chatplayer("<p,neutral>I've not finished yet.");
//     ~chatnpc("<p,neutral>Ok, well let me know when you're done.");
//     return(false);
// }
if (inv_total(inv, $premade_cocktail) > 0) {
    ~chatplayer("<p,happy>I think I've got one.");
    ~chatnpc("<p,happy>No, I need you to make the <$name> yourself. I'm|afraid it doesn't really work if you try to sell the bar its|own drinks."); // osrs has no apostrophe for "its"
    return(false);
}
return(true);
// *********************** TUTORIAL ************************

// *********************** JOBS ************************
[label,gnome_bar_cocktail_job_start]
def_int $choice = ~p_choice2("I'm quite busy myself, sorry.", 1, "Ok then, what do you need?", 2);
if ($choice = 1) {
    ~chatplayer("<p,neutral>I'm quite busy myself, sorry.");
    ~chatnpc("<p,neutral>That's ok, come back when you're free.");
    return;
}
~chatplayer("<p,happy>Ok then, what do you need?");
def_int $rand = random(5);

%gnome_bar_progress = calc(8 + $rand);
switch_int($rand) {
    case 0 :
        ~chatnpc("<p,neutral>Can you make me one Pineapple Punch, one Choc Saturday and one Drunk Dragon!");
        ~chatplayer("<p,neutral>Ok then I'll be back soon.");
    case 1 :
        ~chatnpc("<p,neutral>Ok, I need two Wizard Blizzards and an SGG!");
        ~chatplayer("<p,neutral>No problem!");
    case 2 :
        ~chatnpc("<p,neutral>Ok, I need one Wizard Blizzard, one Pineapple Punch, one Blurberry Special and two Fruit Blasts!");
        ~chatplayer("<p,neutral>I'll do my best.");
    case 3 :
        ~chatnpc("<p,neutral>I just need two SGG's and one Blurberry Special!");
        ~chatplayer("<p,neutral>No problem!");
    case 4 :
        ~chatnpc("<p,neutral>I just need one Fruit Blast!");
        ~chatplayer("<p,neutral>No problem!");
}

[label,gnome_bar_cocktail_job_finish]
~chatplayer("<p,neutral>Hi.");
~chatnpc("<p,neutral>Have you made the order?");
switch_int(%gnome_bar_progress) {
    case 8 :
        ~gnome_bar_cocktail_job_checks (
            "one Pineapple Punch, one Choc Saturday and one Drunk Dragon", "That's blurberry-tastic!", 100, 
            pineapple_punch, 1,
            chocolate_saturday, 1,
            drunk_dragon, 1,
            null, null
        );
    case 9 :
        ~gnome_bar_cocktail_job_checks (
            "two Wizard Blizzards and an SGG", "That's excellent, here's your share of the profit!", 150, 
            wizard_blizzard, 2,
            sgg, 1,
            null, null,
            null, null
        );
    case 10 :
        ~gnome_bar_cocktail_job_checks (
            "one Wizard Blizzard, one Pineapple Punch, one Blurberry Special and two Fruit Blasts", "Wow fantastic, here's your share of the profit!", 179, 
            wizard_blizzard, 1,
            pineapple_punch, 1,
            blurberry_special, 1,
            fruit_blast, 2
        );
    case 11 :
        ~gnome_bar_cocktail_job_checks (
            "two SGG's and one Blurberry Special", "Great, here's your share of the profit!", 120, 
            sgg, 2,
            blurberry_special, 1,
            null, null,
            null, null
        );
    case 12 :
        ~gnome_bar_cocktail_job_checks (
            "one Fruit Blast", "that's frutty-licious!", 10, 
            fruit_blast, 1,
            null, null,
            null, null,
            null, null
        );
}
if (%gnome_bar_progress = 13) {
    ~chatnpc("<p,neutral>Could you make me another order?");
    @gnome_bar_cocktail_job_start;
}

[proc,gnome_bar_cocktail_job_checks](string $order, string $order_succeed, int $coins, namedobj $cocktail1, int $count1, namedobj $cocktail2, int $count2, namedobj $cocktail3, int $count3, namedobj $cocktail4, int $count4)
// https://web.archive.org/web/20060428061045/http://www.runehq.com/RHQSkillsView.php?id=00296
// "Making gnome cocktails earns 60 xp, with no bonus from Blurberry"
// no bonus xp for gnome bar
if (inv_total(inv, $cocktail1) >= $count1 & inv_total(inv, $cocktail2) >= $count2 & inv_total(inv, $cocktail3) >= $count3 & inv_total(inv, $cocktail4) >= $count4) {
    ~chatplayer("<p,neutral>Here you go, <$order>.");
    if ($cocktail1 ! null) {
        inv_del(inv, $cocktail1, $count1);
    }
    if ($cocktail2 ! null) {
        inv_del(inv, $cocktail2, $count2);
    }
    if ($cocktail3 ! null) {
        inv_del(inv, $cocktail3, $count3);
    }
    if ($cocktail4 ! null) {
        inv_del(inv, $cocktail4, $count4);
    }
    %gnome_bar_progress = 13;
    ~mesbox("You give blurberry <$order>.");
    ~chatnpc(append("<p,neutral>", $order_succeed));
    inv_add(inv, coins, $coins);
    ~mesbox("Blurberry gives you <tostring($coins)> gold coins.");
    return;
}
~chatplayer("<p,sad>Not yet.");
~chatnpc("<p,neutral>Ok, I need <$order>.");
~chatnpc("<p,neutral>Let me know when you're done.");