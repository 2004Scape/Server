// i used some transcribed dialogue from this. https://web.archive.org/web/20051104084237/http://www.nomadgaming.com/index.php?page=rs2_dragonslayer

[opnpc1,oziach]
if(map_members = ^true & inv_total(inv, trail_clue_hard_riddle014) > 0) {
    @trail_oziach;
}

if (%dragon_progress = ^quest_dragon_spoken_to_guildmaster) {
    // rsc
    ~chatnpc("<p,shifty>Aye, 'tis a fair day my friend.");
    @multi3("Can you sell me some Rune plate mail?", oziach_can_sell_rune_plate, "I'm not your friend.", oziach_not_friend, "Yes, it's a very nice day.", oziach_nice_day);
}
// osrs, very similar to rsc
if (%dragon_progress < ^dragon_complete & %dragon_progress > 0) {
    ~chatnpc("<p,quiz>So how is thy quest going?");
    @multi2("So where can I find this dragon?", oziach_find_dragon, "Where can I get an antidragon shield?", oziach_dragon_shield);
}
if (%dragon_progress >= ^dragon_complete) {
    @oziach_dragon_complete;
}
//default
~chatnpc("<p,shifty>Aye, 'tis a fair day my friend.");
@multi2("I'm not your friend.", oziach_not_friend, "Yes, it's a very nice day.", oziach_nice_day);

[opnpc3,oziach]
if (%dragon_progress < ^quest_dragon_spoken_to_oziach) {
    //osrs
    ~chatnpc("<p,angry>I ain't got nothing to sell ye, adventurer.");
    return;
}
if (%dragon_progress < ^dragon_complete) {
    // osrs
    ~chatnpc("<p,angry>I'm not sellin' ye anything till you've slayed that dragon!");
    return;
}
if (%dragon_progress = ^dragon_complete) {
    @oziach_dragon_complete;
}
~openshop_activenpc;

// rsc and osrs
[label,oziach_not_friend]
~chatplayer("<p,angry>I'm not your friend.");
~chatnpc("<p,angry>I'm surprised if you're anyone's friend with those kind of manners.");

[label,oziach_can_sell_rune_plate]
// rsc
~chatplayer("<p,quiz>Can you sell me some Rune plate mail?");
// rsc & osrs
~chatnpc("<p,shock>So, how does thee know I 'ave some?");
@multi2("The guildmaster of the Champions' Guild told me.", oziach_guildmaster, "I am a master detective.", oziach_detective);

[label,oziach_nice_day]
~chatplayer("<p,neutral>Yes, it's a very nice day.");
// rsc. osrs oziach is really mean here for no reason :(
~chatnpc("<p,neutral>Aye, may the gods walk by yer side.");

// i think part of some of the later dragon slayer reworks made oziach meaner
// so i took the mean part out of the osrs dialogue
[label,oziach_detective]
~chatplayer("<p,neutral>I am a master detective.");
//Line breaks are guessed, based off other dialogues of the time. dialogue also says rune platemail, so its probably old.
~chatnpc("<p,neutral>Well, however you found out about it,|I'm not going to let just anyone wear my rune platemail.|It's only for heroes.");

// before the rework, oziach would give instructions on map pieces and what not.
// the reworked dialogue shows oziach saying guild master tells the player oziach has a quest to give.
// Seems like that part didnt exist before the rework. So im using the rsc dialogue here
[label,oziach_guildmaster]
~chatplayer("<p,neutral>The guildmaster of the Champions' Guild told me.");
// i do line breaks here to match rsc's
~chatnpc("<p,neutral>Well if you're worthy of his advise, you must have something going for you.");
~chatnpc("<p,neutral>He has been known to let some weaklin's into his guild though.");
~chatnpc("<p,neutral>I don't want just any old pumpkinmush to have this armour jus cos they have a large amount of cash.");
~chatnpc("<p,neutral>This is armour fit for a hero to be sure.|So you'll need to prove to me that you're a hero before you can buy some.");
def_int $choice = ~p_choice2("So how am I meant to prove that?", 0, "That's a pity, I'm not a hero.", 1);
if ($choice = 1) {
    ~chatplayer("<p,sad>That's a pity, I'm not a hero.");
    return;
}

~chatplayer("<p,quiz>So how am I meant to prove that?");
// extra line break after "Evlarg,"
~chatnpc("<p,neutral>Well if you want to prove yourself, you could try and defeat Elvarg, the dragon of the Isle of Crandor.");
$choice = ~p_choice3("A dragon, that sounds like fun!", 0, "And will I need anything to defeat this dragon?", 1, "I may be a champion, but I don't think I'm up to dragon killing yet", 2);
if ($choice = 2) {
    ~chatplayer("<p,sad>I may be a champion, but I don't think I'm up to dragon killing yet");
    ~chatnpc("<p,neutral>Yes, I can understand that.");
    return;
}
if ($choice = 0) {
    ~chatplayer("<p,neutral>A dragon, that sounds like fun!");
} else if ($choice = 1) {
    ~chatplayer("<p,neutral>And will I need anything to defeat this dragon?");
    ~chatnpc("<p,neutral>It's funny you should say that.");
}

~chatnpc("<p,neutral>Elvarg really is one of the most powerful dragons|I really wouldn't recommend charging in without special equipment.");
~chatnpc("<p,neutral>Her breath is the main thing to watch out for.|You can get fried very fast, unless you have a special flameproof antidragon shield.");
~chatnpc("<p,neutral>It won't totally protect you|but it should prevent some of the damage to you.");
%dragon_progress = ^quest_dragon_spoken_to_oziach; //rsc does it here
@multi2("So where can I find this dragon?", oziach_find_dragon, "Where can I get an antidragon shield?", oziach_dragon_shield);


[label,oziach_dragon_shield]
~chatplayer("<p,quiz>Where can I get an antidragon shield?");
~chatnpc("<p,neutral>I believe the Duke of Lumbridge Castle may have one in his armoury.");
%dragon_shield = ^quest_dragon_knows_about_shield;
@multi2("So where can I find this dragon?", oziach_find_dragon, "Ok I'll try and get everything together.", oziach_farewell);

[label,oziach_find_dragon]
~chatplayer("<p,quiz>So where can I find this dragon?");
~chatnpc("<p,neutral>That is a problem too yes.|No one knows where the Isle of Crandor is located.");
~chatnpc("<p,neutral>There was a map, but it has torn up into three pieces which are now scattered across Asgarnia.");
~chatnpc("<p,neutral>You'll also struggle to find someone bold enough to take a ship to Crandor Island");

@multi4("Where is the first piece of the map?", oziach_first_piece, "Where is the second piece of the map?", oziach_second_piece, "Where is the third piece of the map?", oziach_third_piece, "Where can I get an antidragon shield?", oziach_dragon_shield);

[label,oziach_first_piece]
~chatplayer("<p,quiz>Where is the first piece of the map?");
~chatnpc("<p,neutral>Deep in a strange building known as Melzar's maze|located north west of Rimmington.");
// tipit forum post from 2006 mentioning talk/drop trick working for this. source: https://forum.tip.it/topic/42769-melzars-maze-key/
if (inv_total(inv, melzarkey) = 0 & inv_total(bank, melzarkey) = 0) {
    ~chatnpc("<p,neutral>You will need this to get in|this is the key to the front entrance to the maze.");
    inv_add(inv, melzarkey, 1);
    ~objbox(melzarkey, "Oziach hands you a key.", 250, 0, divide(^objbox_height, 2));
}
@multi4("Where can I get an antidragon shield?", oziach_dragon_shield, "Where is the second piece of the map?", oziach_second_piece, "Where is the third piece of the map?", oziach_third_piece, "Ok I'll try and get everything together.", oziach_farewell);

[label,oziach_second_piece]
~chatplayer("<p,quiz>Where is the second piece of the map?");
%dragon_oracle = ^quest_dragon_knows_about_oracle;
~chatnpc("<p,neutral>You will need to talk to the oracle on the ice mountain.");
@multi4("Where can I get an antidragon shield?", oziach_dragon_shield, "Where is the first piece of the map?", oziach_first_piece, "Where is the third piece of the map?", oziach_third_piece, "Ok I'll try and get everything together.", oziach_farewell);

[label,oziach_third_piece]
~chatplayer("<p,quiz>Where is the third piece of the map?");
~chatnpc("<p,neutral>That was stolen by one of the goblins from the goblin village.");
%dragon_goblin = ^quest_dragon_knows_about_goblin;
@multi4("Where can I get an antidragon shield?", oziach_dragon_shield, "Where is the first piece of the map?", oziach_first_piece, "Where is the second piece of the map?", oziach_second_piece, "Ok I'll try and get everything together.", oziach_farewell);

[label,oziach_farewell]
~chatplayer("<p,neutral>Ok I'll try and get everything together.");
~chatnpc("<p,neutral>Fare ye well.");

[label,oziach_dragon_complete]
// rsc
~chatplayer("<p,happy>I have slain the dragon!");
~chatnpc("<p,happy>Well done!");
def_int $choice = ~p_choice2("Can I buy a rune plate mail body now please?", 0, "Thank you.", 1);
if ($choice = 1) {
    ~chatplayer("<p,happy>Thank you.");
    return;
}
if ($choice = 0) {
    ~chatplayer("<p,happy>Can I buy a rune plate mail body now please?");
    ~openshop_activenpc;
    return;
}

[label,trail_oziach]
if(inv_total(inv, trail_clue_hard_riddle014_puzzlebox) > 0) {
    @puzzle_oziach;
}
~chatnpc("<p,neutral>The solving of this puzzle could be the key to your treasure.");
~give_trail_puzzle(trail_clue_hard_riddle014_puzzlebox, "Oziach has given you a puzzle box!");

[label,puzzle_oziach]
if(inv_total(inv, trail_clue_hard_riddle014_puzzlebox) = 0) {
    ~chatplayer("<p,neutral>I don't have the puzzle box with me.");
    ~chatnpc("<p,angry>That's not much use - go and fetch it!");
    return;
}
if(~trail_puzzle_complete = true) {
    ~chatnpc("<p,neutral>Well done, traveller.");
    inv_del(inv, trail_clue_hard_riddle014_puzzlebox, 1);
    ~progress_clue_hard(trail_clue_hard_riddle014, "Oziach has given you another clue scroll!");
    return;
} 
~chatnpc("<p,neutral>That isn't right, keep trying.");