[oploc1,loc_1804]
mes("The door is locked.");

[oplocu,loc_1804]
switch_obj(last_useitem) {
    case edgevilledungeonkey : @open_edgeville_dungeon_door;
    case default : ~displaymessage(^dm_default);
}


[label,open_edgeville_dungeon_door]
if (inv_total(inv, edgevilledungeonkey) > 0) {
    mes("You unlock the door.");
    sound_synth(locked, 0, 0);
    def_coord $loc_coord = loc_coord;
    def_int $angle = loc_angle;
    def_locshape $shape = loc_shape;
    def_loc $replacement = loc_param(next_loc_stage);
    def_int $x;
    def_int $z;
    $x, $z = ~door_open($angle, loc_shape);

    // walk through door, but if entering, walk next to door and then through
    def_boolean $entering = ~check_axis(coord,  $loc_coord, $angle);
    def_coord $dest = $loc_coord;
    if ($entering = true) {
        if (coord ! $loc_coord) {
            p_delay(0);
            p_teleport($loc_coord);
            sound_synth(door_open, 0, 0);
            p_delay(1);
        } else {
            p_delay(0);
            sound_synth(door_open, 0, 0);
        }
        $dest = movecoord($loc_coord, $x, 0, $z);
    } else {
        p_delay(0);
        sound_synth(door_open, 0, 0);
    }

    p_teleport($dest);

    // Temp note: dur updated
    loc_add($loc_coord, inviswall, $angle, $shape, 3);
    loc_add(movecoord($loc_coord, $x, 0, $z), $replacement, modulo(add($angle, 1), 4), $shape, 3);
} else {
    mes("The door is locked.");
}

[oploc1,oddwall_r] ~open_and_close_double_door2(~check_axis_locactive(coord), ^right, coffin_open);
[oploc1,oddwall_l] ~open_and_close_double_door2(~check_axis_locactive(coord), ^left, coffin_open);