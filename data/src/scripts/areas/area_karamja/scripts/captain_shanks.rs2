[opnpc1,captain_shanks]
if(%zombiequeen_progress < ^zombiequeen_complete) {
    ~chatnpc("<p,sad>Oh dear, this ship is in a terrible state. And I just can't get the items I need to repair it because Shilo village is overrun with zombies.");
    return;
}
~chatnpc("<p,happy>Hello there shipmate! I sail to Khazard Port and to Port Sarim. Where are you bound?");
if(inv_total(inv, shiloshipticket) = 0) {
    def_int $price = ~random_range(26, 45);
    ~chatnpc("<p,happy>I see you don't have a ticket for the ship, my colleague normally only sells them in Shilo village. But I could sell you one for a small additional charge. Shall we say <tostring($price)> gold pieces"); // no period
    switch_int(~p_choice2_header("Yes, I'll buy a ticket for the ship.", 1, "No thanks, not just at the moment.", 2, "Buy a ticket for <tostring($price)> gold pieces.")) {
        case 1 :
            ~chatplayer("<p,happy>Yes, I'll buy a ticket for the ship.");
            if(inv_total(inv, coins) < $price) {
                ~chatnpc("<p,sad>Sorry me old shipmate, but you seem to be  financially challenged at the moment. Come back when your coffers are full!");
                return;
            }
            ~chatnpc("<p,happy>It's a good deal and no mistake. Here you go me old shipmate, here's your ticket.");
            inv_del(inv, coins, $price);
            inv_add(inv, shiloshipticket, 1);
            ~chatnpc("<p,happy>Ok, now you have your ticket, do you want to sail anywhere?");
            @multi3_header("Khazard Port please.", shanks_khazard, "Port Sarim please.", shanks_sarim, "Nowhere just at the moment thanks.", shanks_nowhere, "Captain Shanks asks, 'Do you want to sail anywhere?'");
        case 2 :
            ~chatplayer("<p,happy>No thanks, not just at the moment.");
            ~chatnpc("<p,happy>Very well me old shipmate,  come back if you change your mind now.");
    }
    return;
}
@multi3_header("Khazard Port please.", shanks_khazard, "Port Sarim please.", shanks_sarim, "Nowhere just at the moment thanks.", shanks_nowhere, "Captain Shanks asks, 'Where are you bound?'");

[label,shanks_khazard]
~chatplayer("<p,happy>Khazard Port please.");
~chatnpc("<p,happy>Very well then me old shipmate, I'll just take your ticket and then we'll set sail.");
inv_del(inv, shiloshipticket, 1);
// puts you on top of a barrel, not 100% sure if thats correct but very possible
~set_sail_cairn("You arrive safely in the quiet fishing port of Khazard.", 0_41_49_56_14, ^sail_cairn_to_port_khazard, 9);

[label,shanks_sarim]
~chatplayer("<p,happy>Port Sarim please.");
~chatnpc("<p,happy>Very well then me old shipmate, I'll just take your ticket and then we'll set sail.");
inv_del(inv, shiloshipticket, 1);
~set_sail_cairn("The ship arrives in busy Port Sarim.", 0_47_50_39_35, ^sail_cairn_to_port_sarim, 15);

[label,shanks_nowhere]
~chatplayer("<p,happy>Nowhere just at the moment thanks.");
~chatnpc("<p,happy>Very well then me old shipmate, Just let me know if you change your mind.");

[proc,set_sail_cairn](string $arrival_mes, coord $destination, int $map_route, int $delay)
%boat_takeoff = $map_route;
if_openmain(ship_journey);
midi_jingle(^sailing_journey_jingle, ^sailing_journey_jingle_millis);


if_settab(null, 0);
if_settab(null, 1);
if_settab(null, 2);
if_settab(null, 3);
if_settab(null, 4);
if_settab(null, 5);
if_settab(null, 6);
if_settab(null, 10);
if_settab(null, 11);
if_settab(null, 12);
if_settab(null, 13);

p_delay($delay);

if(inv_total(inv, karamja_rum) > 0) {
    inv_del(inv, karamja_rum, ^max_32bit_int);
    mes("During the trip you lose your rum to a sailor in a game of dice.");
    mes("Better luck next time!");
}
~initalltabs;
if_close;
p_telejump($destination);
~mesbox($arrival_mes);