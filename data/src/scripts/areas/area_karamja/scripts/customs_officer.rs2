[opnpc1,customs_officer]
~chatnpc(quiz, "Can I help you?");
@multi2("Can I journey on this ship?", customs_journey, "Does Karamja have unusual customs then?", customs_unusual);

[label,customs_unusual]
~chatplayer(happy, "Does Karamja have any unusual customs then?");
~chatnpc(quiz, "I'm not that sort of customs officer.");

[label,customs_journey]
~chatplayer(happy, "Can I journey on this ship?");

if (%hunt_progress >= 2) {
    if (inv_total(inv, karamja_rum) > 0) {
        @customs_spot_inspection;
    }

    ~chatnpc(quiz, "Hey, I know you, you work at the plantation.");
    ~chatnpc(quiz, "I don't think you'll try smuggling anything, you just need to pay a boarding charge of 30 coins.");

    @customs_pass_search;
}

~chatnpc(happy, "You need to be searched before you can board.");
@multi3("Why?", customs_why, "Search away, I have nothing to hide.", customs_search, "You're not putting your hands on my things!", customs_dont_touch);

[label,customs_spot_inspection]
~chatnpc(quiz, "Spot inspection. You don't mind do you?");
~chatnpc(quiz, "Aha, trying to smuggle rum eh?");

~chatplayer(shifty, "Umm... it's for personal use?");

inv_del(inv, karamja_rum, 28);
mes("The customs officer confiscates your rum.");

[label,customs_why]
~chatplayer(happy, "Why?");
~chatnpc(quiz, "Because Asgarnia has banned the import of intoxicating|spirits.");
@multi2("Search away, I have nothing to hide.", customs_search, "You're not putting your hands on my things!", customs_dont_touch);

[label,customs_dont_touch]
~chatplayer(shock, "You're not putting your hands on my things!");
~chatnpc(bored, "You're not getting on this ship then.");

[label,customs_search]
~chatplayer(long, "Search away, I have nothing to hide.");

if (inv_total(inv, karamja_rum) > 0) {
    ~chatnpc(quiz, "Aha, trying to smuggle rum are we?");
    ~chatplayer(shifty, "Umm... it's for personal use?");

    inv_del(inv, karamja_rum, 28);
    mes("The customs officer confiscates your rum.");
    mes("You will need to find some way to smuggle it off the island...");
    return;
}

// interestingly this is said even if your inventory is empty
~chatnpc(quiz, "Well you've got some odd stuff, but it's all legal. Now|you need to pay a boarding charge of 30 coins.");
@customs_pass_search;

[label,customs_pass_search]
def_int $option = ~p_choice2("Ok.", 1, "Oh, I'll not bother then.", 2);
switch_int ($option) {
    case 1:
        @customs_pay;
    case 2:
        ~chatplayer(quiz, "Oh, I'll not bother then.");
}

[label,customs_pay]
~chatplayer(quiz, "Ok.");
if (inv_total(inv, coins) < 30) {
    ~chatplayer(sad, "Oh dear, I don't actually seem to have enough money.");
    return;
}

inv_del(inv, coins, 30);
mes("You pay 30 coins and board the ship.");

~set_sail("Port Sarim", 1_47_50_24_17, ^sail_karamja_to_port_sarim);
