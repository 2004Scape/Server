[opnpc1,customs_officer]
~chatnpc("<p,neutral>Can I help you?");
@multi2("Can I journey on this ship?", customs_journey, "Does Karamja have unusual customs then?", customs_unusual);

[label,customs_unusual]
~chatplayer("<p,quiz>Does Karamja have any unusual customs then?");
~chatnpc("<p,neutral>I'm not that sort of customs officer.");

[label,customs_journey]
~chatplayer("<p,quiz>Can I journey on this ship?");

if (%hunt_progress >= 2) {
    if (inv_total(inv, karamja_rum) > 0) {
        @customs_spot_inspection;
    }

    ~chatnpc("<p,neutral>Hey, I know you, you work at the plantation.");
    ~chatnpc("<p,neutral>I don't think you'll try smuggling anything, you just need to pay a boarding charge of 30 coins.");

    @customs_pass_search;
}

~chatnpc("<p,quiz>You need to be searched before you can board.");
@multi3("Why?", customs_why, "Search away, I have nothing to hide.", customs_search, "You're not putting your hands on my things!", customs_dont_touch);

[label,customs_spot_inspection]
~chatnpc("<p,neutral>Spot inspection. You don't mind do you?");
~chatnpc("<p,neutral>Aha, trying to smuggle rum eh?");

~chatplayer("<p,shifty>Umm... it's for personal use?");

inv_del(inv, karamja_rum, 28);
mes("The customs officer confiscates your rum.");

[label,customs_why]
~chatplayer("<p,quiz>Why?");
~chatnpc("<p,neutral>Because Asgarnia has banned the import of intoxicating|spirits.");
@multi2("Search away, I have nothing to hide.", customs_search, "You're not putting your hands on my things!", customs_dont_touch);

[label,customs_dont_touch]
~chatplayer("<p,shock>You're not putting your hands on my things!");
~chatnpc("<p,bored>You're not getting on this ship then.");

[label,customs_search]
~chatplayer("<p,happy>Search away, I have nothing to hide.");

if (inv_total(inv, karamja_rum) > 0) {
    ~chatnpc("<p,neutral>Aha, trying to smuggle rum are we?");
    ~chatplayer("<p,shifty>Umm... it's for personal use?");

    inv_del(inv, karamja_rum, 28);
    mes("The customs officer confiscates your rum.");
    mes("You will need to find some way to smuggle it off the island...");
    return;
}

// interestingly this is said even if your inventory is empty
~chatnpc("<p,neutral>Well you've got some odd stuff, but it's all legal. Now|you need to pay a boarding charge of 30 coins.");
@customs_pass_search;

[label,customs_pass_search]
def_int $option = ~p_choice2("Ok.", 1, "Oh, I'll not bother then.", 2);
switch_int ($option) {
    case 1 :
        @customs_pay;
    case 2 :
        ~chatplayer("<p,neutral>Oh, I'll not bother then.");
}

[label,customs_pay]
~chatplayer("<p,neutral>Ok.");
if (inv_total(inv, coins) < 30) {
    ~chatplayer("<p,sad>Oh dear, I don't actually seem to have enough money.");
    return;
}

inv_del(inv, coins, 30);
mes("You pay 30 coins..."); // https://imgur.com/myiOU5N may 2004
def_string $sail_msg = "and board the ship.";
if(coordx(npc_coord) < 2815) { // officer in Brimhaven
    ~set_sail("Ardougne", $sail_msg, 1_41_51_59_4, ^sail_brimhaven_to_ardougne, 7);
} else {
    ~set_sail("Port Sarim", $sail_msg, 1_47_50_24_17, ^sail_karamja_to_port_sarim, 7);
}
