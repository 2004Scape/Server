// NOTE: rs classic dialogue is slightly different, so this may be the updated dialogue after more jungle vines, more wall fungus, and seqs with sounds were added: https://www.youtube.com/watch?v=qm8ZwROiW8s
[opnpcu,trufitus]
if(%junglepotion_progress = ^junglepotion_complete) {
    switch_obj (last_useitem) {
        case snake_weed, ardrigal, sito_foil, volencia_moss, rogues_purse:
            def_obj $herb = last_useitem;
            ~chatnpc("<p,neutral>Many thanks for the herb Bwana. I'll add it to my collection.");
            def_int $coins_amount = ~random_range(1, 4);
		    inv_del(inv, $herb, 1);
		    inv_add(inv, coins, $coins_amount);
            if ($coins_amount = 1) {
    		    if ($herb = rogues_purse) {
                    mes("Trufitus gives you a coin for the rogues purse.");
                } else {
                    mes("Trufitus gives you a coin for the <lowercase(oc_name($herb))>");
                }
            } else {
    			if ($herb = rogues_purse) {
                    mes("Trufitus gives you <tostring($coins_amount)> coins for the rogues purse.");
                } else {
                    mes("Trufitus gives you <tostring($coins_amount)> coins for the <lowercase(oc_name($herb))>.");
                }
            }
        case unidentified_ardrigal, unidentified_rogues_purse, unidentified_sito_foil, unidentified_snake_weed, unidentified_volencia_moss: @trufitus_decline_herb;
        case default:
            mes("You hand over the item.");
            p_delay(2); // 3t
            ~chatnpc("<p,neutral>I'm sorry Bwana but I just don't have a use for that!");
    }
    return;
}
if((%junglepotion_progress = ^junglepotion_not_started) | (last_useitem ! unidentified_snake_weed & last_useitem ! unidentified_ardrigal & 
    last_useitem ! unidentified_sito_foil & last_useitem ! unidentified_volencia_moss & last_useitem ! unidentified_rogues_purse &
    last_useitem ! snake_weed & last_useitem ! ardrigal & last_useitem ! sito_foil & 
    last_useitem ! volencia_moss & last_useitem ! rogues_purse)) {
    ~displaymessage(^dm_default);
    return;
}
switch_int(%junglepotion_progress) {
    case ^junglepotion_get_snake_weed, ^junglepotion_found_snake_weed:
		if (last_useitem = snake_weed) {
			@trufitus_accept_herb(last_useitem);
		} else if (last_useitem = unidentified_snake_weed) {
            @trufitus_decline_herb;
        }
    case ^junglepotion_get_ardrigal, ^junglepotion_found_ardrigal:
		if (last_useitem = ardrigal) {
			@trufitus_accept_herb(last_useitem);
		} else if (last_useitem = unidentified_ardrigal) {
            @trufitus_decline_herb;
        } 
    case ^junglepotion_get_sito_foil, ^junglepotion_found_sito_foil:
		if (last_useitem = sito_foil) {
			@trufitus_accept_herb(last_useitem);
		}else if (last_useitem = unidentified_sito_foil) {
            @trufitus_decline_herb;
        }
    case ^junglepotion_get_volencia_moss, ^junglepotion_found_volencia_moss:
		if (last_useitem = volencia_moss) {
			@trufitus_accept_herb(last_useitem);
		}else if (last_useitem = unidentified_volencia_moss) {
            @trufitus_decline_herb;
        }
    case ^junglepotion_get_rogues_purse, ^junglepotion_found_rogues_purse:
		if (last_useitem = rogues_purse) {
			@trufitus_accept_herb(last_useitem);
		} else if (last_useitem = unidentified_rogues_purse) {
            @trufitus_decline_herb;
        }
}
@trufitus_wrong_herb;

[opnpc1,trufitus]
switch_int(%junglepotion_progress) {
    case ^junglepotion_not_started: @trufitus_pre_quest;
    case ^junglepotion_get_snake_weed, ^junglepotion_found_snake_weed: @trufitus_give_herb(snake_weed, unidentified_snake_weed);
    case ^junglepotion_get_ardrigal, ^junglepotion_found_ardrigal: @trufitus_give_herb(ardrigal, unidentified_ardrigal);
    case ^junglepotion_get_sito_foil, ^junglepotion_found_sito_foil: @trufitus_give_herb(sito_foil, unidentified_sito_foil);
    case ^junglepotion_get_volencia_moss, ^junglepotion_found_volencia_moss: @trufitus_give_herb(volencia_moss, unidentified_volencia_moss);
    case ^junglepotion_get_rogues_purse, ^junglepotion_found_rogues_purse: @trufitus_give_herb(rogues_purse, unidentified_rogues_purse);
    case ^junglepotion_found_all_herbs: @trufitus_finish_quest;
    case ^junglepotion_complete: @trufitus_post_quest;
}

[label,trufitus_pre_quest]
~chatnpc("<p,happy>Greetings Bwana!|I am <npc_name> Shakaya|of the Tai Bwo Wannai village.");
~chatnpc("<p,happy>Welcome to our humble village.");
def_int $option = ~p_choice3("What does Bwana mean?", 1, "Tai Bwo Wannai? What does that mean?", 2, "It's a nice village, where is everyone?", 3);
if($option = 1) {
    ~chatplayer("<p,happy>What does Bwana mean?");
    ~chatnpc("<p,happy>It means friend,|and friends come in peace.|I assume that you come in peace?");
    def_int $option2 = ~p_choice2("Yes, of course I do.", 1, "What does a warrior like me know about peace?", 2);
    if($option2 = 1) {
        ~chatplayer("<p,happy>Yes, of course I do.");
        ~chatnpc("<p,happy>Well, that is good news,|as I may have a proposition for you.");
        def_int $option3 = ~p_choice2("A proposition eh? Sounds interesting!", 1, "I am sorry, but I am very busy.", 2); 
        if($option3 = 1) {
            ~chatplayer("<p,happy>A proposition eh? Sounds interesting!");
            ~chatnpc("<p,neutral>I hoped you would think so.|My people are afraid to stay in the village.");
            ~chatnpc("<p,neutral>They have returned to the jungle|and I need to commune with the gods");
            ~chatnpc("<p,neutral>to see what fate befalls us.|You can help me by collecting|some herbs that I need.");
            def_int $option4 = ~p_choice2("Me? How can I help?", 1, "I am very sorry, but I don't have time for that.", 2); 
            if($option4 = 1) {
                @start_junglepotion;
            } else if($option4 = 2) {
                ~chatplayer("<p,happy>I am very sorry, but I don't have time for that.");
                @trufitus_farewell;
            }
        } else if($option3 = 2) {
            @trufitus_sorry;
        }
    } else if($option2 = 2) {
        ~chatplayer("<p,happy>What does a warrior like me know about peace?");
        ~chatnpc("<p,sad>When you grow weary of violence|and seek a more enlightened path,|please pay me a visit");
        ~chatnpc("<p,sad>as I may have a proposition for you.|Now I need to attend to the plight|of my people. Please excuse me...");
    }
} else if($option = 2) {
    ~chatplayer("<p,happy>Tai Bwo Wannai? What does that mean?");
    ~chatnpc("<p,happy>It means 'small clearing in the jungle' but it is now the name of our village.");
    @multi2("It's a nice village, where is everyone?", trufitus_ask_village, "I am sorry, but I am very busy.", trufitus_sorry);
 } else if($option = 3) {
    @trufitus_ask_village;
}

[label,start_junglepotion]
~chatplayer("<p,happy>Me? How can I help?");
~chatnpc("<p,neutral>I need to make a special brew!|A potion that helps me to commune with the gods.|For this potion, I need very|special herbs, that are only found in the deep jungle.");
~chatnpc("<p,neutral>I can only guide you so far as the herbs are not easy to find. With some luck, you will find each herb in turn and bring to me. I will give you details of where to find the next herb.");
~chatnpc("<p,neutral>In return for this great favour I will give you training in Herblore.");
def_int $option5 = ~p_choice2("Hmmm, sounds difficult, I don't know if I am ready for the challenge.", 1, "It sounds like just the challenge for me.", 2);
if($option5 = 2) {
    if (%druid_progress = ^druid_complete) { 
        ~chatplayer("<p,happy>It sounds like just the challenge for me.|And it would make a nice break from killing things!");
        %junglepotion_progress = ^junglepotion_get_snake_weed;
        ~send_quest_progress(questlist:junglepotion, %junglepotion_progress, ^junglepotion_complete);
        ~chatnpc("<p,neutral>That is excellent Bwana!|The first herb that you need to gather is called");
        ~chatnpc("<p,neutral>Snake Weed.");
        ~chatnpc("<p,neutral>It grows near the vines in an area to the south west where");
        ~chatnpc("<p,neutral>the ground turns soft and the water kisses your feet.");
    } else {
        ~mesbox("You do not meet all of the requirements to start|the Jungle Potion quest."); // TODO: what happens originally?
    }
} else if($option5 = 1) {
    ~chatplayer("<p,happy>Hmmm, sounds difficult, I don't know if I am ready for the challenge.");
    ~chatnpc("<p,neutral>Very well then Bwana,|maybe you will return to me invigorated|and ready to take up the challenge one day?");
}

[label,trufitus_sorry]
~chatplayer("<p,happy>I am sorry, but I am very busy.");
@trufitus_farewell;

[label,trufitus_farewell]
~chatnpc("<p,neutral>Very well then,|may your journeys bring you much joy.");
~chatnpc("<p,neutral>Maybe you will pass this way again and|you then take up my proposal?");
~chatnpc("<p,neutral>But for now, fare thee well.");

[label,trufitus_ask_village]
~chatplayer("<p,happy>It's a nice village, where is everyone?");
~chatnpc("<p,sad>My people are afraid to stay in the village.|They have returned to the jungle.|I need to commune with the gods|to see what fate befalls us.");
~chatnpc("<p,sad>You may be able to help with this.");
@multi2("Me? How can I help?", start_junglepotion, "I am sorry, but I am very busy.", trufitus_sorry);

[label,trufitus_give_herb](namedobj $herb, namedobj $unid)
switch_int(%junglepotion_progress) {
    case ^junglepotion_get_snake_weed, ^junglepotion_found_snake_weed: ~chatnpc("<p,neutral>Hello Bwana, do you have the Snake Weed?");
    case ^junglepotion_get_ardrigal, ^junglepotion_found_ardrigal: ~chatnpc("<p,neutral>Hello Bwana, have you been able to get the Ardrigal?");
    case ^junglepotion_get_rogues_purse, ^junglepotion_found_rogues_purse: ~chatnpc("<p,happy>Greetings Bwana, have you been successful in getting the Rogues Purse?");
    case ^junglepotion_get_volencia_moss, ^junglepotion_found_volencia_moss: ~chatnpc("<p,happy>Greetings Bwana, have you been successful in getting the Volencia Moss?");
    case ^junglepotion_get_sito_foil, ^junglepotion_found_sito_foil: ~chatnpc("<p,happy>Greetings Bwana, have you been successful in getting the Sito Foil?");
}
def_int $option6 = ~p_choice2("Of course!", 1, "Not yet, sorry, what's the clue again?", 2);
if($option6 = 1) {
    ~chatplayer("<p,happy>Of course!");
    if(inv_total(inv, $herb) > 0) {
		@trufitus_accept_herb($herb);
    } else if(inv_total(inv, $unid) > 0) {
		@trufitus_decline_herb;
    } else {
        switch_int(%junglepotion_progress) {
            case ^junglepotion_get_volencia_moss, ^junglepotion_found_volencia_moss:
                ~chatnpc("<p,neutral>Please don't try to deceive me!|I really need that Volencia Moss if I am to make this potion.");
            case ^junglepotion_get_rogues_purse, ^junglepotion_found_rogues_purse:
                ~chatnpc("<p,neutral>Please don't try to deceive me,|I really need that Rogues Purse if I am to make this potion.");
            case default:
                ~chatnpc("<p,neutral>Please don't try to deceive me.");
                ~trufitus_mention_herb($herb);
        }
    }
} else if($option6 = 2) {
    if(%junglepotion_progress = ^junglepotion_get_ardrigal | %junglepotion_progress = ^junglepotion_found_ardrigal) {
        ~chatplayer("<p,happy>Not yet, sorry, what's the clue again ?");
    } else {
        ~chatplayer("<p,happy>Not yet, sorry, what's the clue again?");
    }
    switch_int(%junglepotion_progress) {
        case ^junglepotion_get_snake_weed, ^junglepotion_found_snake_weed:
            ~chatnpc("<p,neutral>It grows near vines in an area to the south west|where the ground turns soft and the water kisses your feet.");
        case ^junglepotion_get_ardrigal, ^junglepotion_found_ardrigal:
            ~chatnpc("<p,neutral>You are looking for Ardrigal.|It is related to the palm and grows|in its brother's shady profusion.");
            ~chatnpc("<p,neutral>To the east you will find a small peninsula, it is just after the cliffs come down to meet the sands, here is where you should search for it.");
        case ^junglepotion_get_sito_foil, ^junglepotion_found_sito_foil:
            ~chatnpc("<p,neutral>You are looking for Sito Foil, and it grows best|where the ground has been blackened|by living flame.");
        case ^junglepotion_get_volencia_moss, ^junglepotion_found_volencia_moss:
            ~chatnpc("<p,neutral>You are looking for Volencia Moss.|It clings to rocks for its existence.|It is difficult to see, so you must search for it well.");
            ~chatnpc("<p,neutral>It prefers rocks of high metal content and a frequently disturbed environment. There is some, I believe to the south east of this village.");
        case ^junglepotion_get_rogues_purse, ^junglepotion_found_rogues_purse:
            ~chatnpc("<p,neutral>You are looking for Rogues Purse.");
            ~chatnpc("<p,neutral>It inhabits the darkness of the underground, and grows in caverns to the north. A secret entrance to the caverns is set into the northern cliffs, be careful Bwana.");
    }
    ~trufitus_mention_herb($herb);
}

[label,trufitus_accept_herb](obj $herb)
switch_int(%junglepotion_progress) {
case ^junglepotion_get_snake_weed, ^junglepotion_get_ardrigal, ^junglepotion_get_sito_foil, ^junglepotion_get_volencia_moss, ^junglepotion_get_rogues_purse: @trufitus_given_unid($herb); // TODO: need to confirm dialogue for the different unid herbs.
case ^junglepotion_found_snake_weed:
    %junglepotion_progress = ^junglepotion_get_ardrigal;
    inv_del(inv, $herb, 1);
    ~objbox($herb, "You give the Snake Weed to <npc_name>.");
    ~chatnpc("<p,neutral>Great, you have the Snake Weed!|Many thanks. Ok, the next herb is called <oc_name(ardrigal)>.|It is related to the palm and grows|to the east in its brother's shady profusion.");
    ~chatnpc("<p,neutral>To the east you will find a small peninsula, it is just after the cliffs come down to meet the sands, here is where you should search for it.");
case ^junglepotion_found_ardrigal:
    %junglepotion_progress = ^junglepotion_get_sito_foil;
    inv_del(inv, $herb, 1);
    ~objbox($herb, "You give the Ardrigal to <npc_name>.");
    ~chatnpc("<p,neutral>Great, you have the Ardrigal! Many thanks.");
    ~chatnpc("<p,neutral>You are doing well Bwana. The next|herb is called Sito Foil, and it grows best|where the ground has been blackened|by the living flame.");
case ^junglepotion_found_sito_foil:
    %junglepotion_progress = ^junglepotion_get_volencia_moss;
    inv_del(inv, $herb, 1);
    ~objbox($herb, "You give the Sito Foil to <npc_name>.");
    ~chatnpc("<p,neutral>Well done Bwana, just two more herbs to collect.");
    ~chatnpc("<p,neutral>The next herb is called Volencia Moss.|It clings to rocks for its existence.|It is difficult to see, so you must search for it well.");
    ~chatnpc("<p,neutral>It prefers rocks of high metal content and a frequently disturbed environment. There is some, I believe to the south east of this village.");
case ^junglepotion_found_volencia_moss:
    %junglepotion_progress = ^junglepotion_get_rogues_purse;
    inv_del(inv, $herb, 1);
    ~objbox($herb, "You give the Volencia Moss to <npc_name>.");
    ~chatnpc("<p,neutral>Ah Volencia Moss, beautiful. One final herb and the potion will be complete. This is the most difficult to find as it inhabits the darkness of the underground. It is called Rogues Purse, and is only to be found in");
    ~chatnpc("<p,neutral>caverns in the northern part of this island. A secret entrance to the caverns is set into the northern cliffs of this land. Take care Bwana as it may be dangerous.");
case ^junglepotion_found_rogues_purse:
    // https://youtu.be/9LG-opZVpfY?si=fQ-DSP4VpQUVWiSb&t=835
    %junglepotion_progress = ^junglepotion_found_all_herbs;
    inv_del(inv, $herb, 1);
    ~objbox($herb, "You give the Rogues Purse to <npc_name>.");
    ~chatnpc("<p,neutral>Most excellent Bwana!|You have returned all the herbs to me|and, I can finish the preparations|for the potion, and at last divine with the gods.");
    ~chatnpc("<p,neutral>Many blessings on you!|I must now prepare,|please excuse me while I make|the arrangements.");
    @trufitus_finish_quest;
}

[label,trufitus_given_unid](obj $herb)
// only rogues and snake weed are obtaininable in OSRS, theres a stage for each so i'm assuming a dialogue internally exists for all herbs
switch_obj($herb) {
    case rogues_purse: ~chatnpc("<p,angry>That's not fresh Rogues Purse, did you pick it yourself? Go get me some fresh Rogues Purse and remember to pick it yourself.");
    case default: ~chatnpc("<p,angry>That's not fresh <lowercase(oc_name($herb))>, did you pick it yourself? Go get me some fresh <lowercase(oc_name($herb))> and remember to pick it yourself.");
}

[label,trufitus_decline_herb]
~chatnpc("<p,confused>Sorry, Bwana, that herb is so dirty that I can't even tell whether it is fresh. Please clean it first.");

[label,trufitus_wrong_herb]
~chatnpc("<p,neutral>Many thanks Bwana, but I don't need that herb at the moment. Can you please get me the herb I asked for?");

[proc,trufitus_mention_herb](namedobj $herb)
switch_namedobj($herb) {
    case snake_weed: ~chatnpc("<p,neutral>I really need that Snake Weed if I am to make this potion.");
    case sito_foil: ~chatnpc("<p,neutral>I really need that Sito Foil if I am to make this potion.");
    case ardrigal: ~chatnpc("<p,neutral>I really need that Ardigal if I am to make this potion.");
    case rogues_purse: ~chatnpc("<p,neutral>I really need that Rogues Purse if I am to make this potion.");
    case volencia_moss: ~chatnpc("<p,neutral>I really need that Volencia Moss if I am to make this potion.");
}

[label,trufitus_finish_quest]
~mesbox("<npc_name> shows you some techniques in Herblore.|You gain some experience in Herblore.");
queue(junglepotion_quest_complete, 0);

[label,trufitus_post_quest]
~chatnpc("<p,neutral>My greatest respects Bwana,|I have communed with my gods and the future");
~chatnpc("<p,neutral>looks good for my people.|We are happy now that the gods are not angry with us.");
~chatnpc("<p,neutral>With some blessings we will be safe here.");
// varp value = 13 is set after talking to trufitus to start tai bwo wannai trio (sept 2004)
// ~chatnpc("<p,happy>You should deliver the good news to bwana Timfraku, chief of Tai Bwo Wannai. He lives in a raised hut not far from here.");