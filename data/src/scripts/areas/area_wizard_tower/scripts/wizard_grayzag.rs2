//todo: find some sources on wizard grayzag
// this can be pics, videos, forum posts, recounts, anything.

// auto retaliate
[ai_queue1,wizard_grayzag](int $arg) ~npc_default_retaliate_ap;


[ai_applayer2,wizard_grayzag]
if (map_clock < %npc_action_delay) {
    return;
}
npc_setmode(opplayer2);
// 1/10 chance and a 5 imp cap (completely guessed)
// .npc_count_within_distance so it doesnt mess up activenpc pointer.
if (random(10) = 0 & ~.npc_count_within_distance(npc_coord, imp_grayzag, 10) < 5) { // completely guessed
    ~npc_spawn_imp_attack;
    return;
}



[ai_opplayer2,wizard_grayzag]
if (map_clock < %npc_action_delay) {
    return;
}
// 1/10 chance and a 5 imp cap (completely guessed)
// .npc_count_within_distance so it doesnt mess up activenpc pointer.
if (random(10) = 0 & ~.npc_count_within_distance(npc_coord, imp_grayzag, 10) < 5) {
    ~npc_spawn_imp_attack;
    return;
}
~npc_default_attack;


// inspired by melzar the mad's cabbage spawn
[proc,npc_spawn_imp_attack]
if_close;
def_coord $imp_coord = ~coord_lineofwalk_radius(npc_coord, 7);
if ($imp_coord = npc_coord) {
    ~npc_spawn_imp_attack;
    return;
}
npc_anim(human_chaos_druid_cast_bind, 0);
spotanim_npc(bind_casting, 92, 0);
%npc_action_delay = calc(map_clock + 5);
world_delay(2); // delay spawn by 2 ticks
clearqueue(playerhit_n_retaliate);; // todo figure out if this is actually what happens. Could be npc delay? could be p_stopaction?
npc_add($imp_coord, imp_grayzag, 200); // complete guess!
// new active npc
npc_delay(1);
npc_setmode(opplayer2);
spotanim_map(small_smokepuff, $imp_coord, 124, 0);