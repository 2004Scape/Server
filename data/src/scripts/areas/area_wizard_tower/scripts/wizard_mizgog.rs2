[opnpc1,wizard_mizgog]
switch_int(%imp_progress) {
    case ^imp_not_started :
        ~chatnpc("<p,neutral>Hello there.");
        @multi2("Give me a quest!", mizgog_givequest, "Most of your friends are pretty quiet aren't they?", mizgog_quiet);
    case ^imp_started :
        ~chatnpc("<p,happy>So how are you doing finding my beads?");
        if(inv_total(inv, black_bead) > 0 & inv_total(inv, white_bead) > 0 & inv_total(inv, yellow_bead) > 0 & inv_total(inv, red_bead) > 0) {
            ~chatplayer("<p,happy>I've got all four beads.|It was hard work I can tell you.");
            ~chatnpc("<p,happy>Give them here and I'll sort out a reward.");
            ~mesbox("You give four coloured beads to Wizard Mizgog.");
            inv_del(inv, black_bead, 1);
            inv_del(inv, white_bead, 1);
            inv_del(inv, yellow_bead, 1);
            inv_del(inv, red_bead, 1);
            queue(imp_quest_complete, 0);
            ~chatnpc("<p,happy>Here's your reward then,|an amulet of accuracy.");
            mes("The Wizard hands you an amulet.");
            return;
        }
        if(inv_total(inv, black_bead) = 0 & inv_total(inv, white_bead) = 0 & inv_total(inv, yellow_bead) = 0 & inv_total(inv, red_bead) = 0) {
            ~chatplayer("<p,sad>I have not found any yet.");
            ~chatnpc("<p,neutral>Well get on with it.|I've lost a white bead, a red bead,|a black bead, and a yellow bead.|Go kill some imps!");
        } else {
            ~chatplayer("<p,happy>I have found some of your beads.");
            ~chatnpc("<p,neutral>Come back when you have them all.|The colour of the four beads that I need|are red, yellow, black, and white.|Go chase some imps!");
        }
    case ^imp_complete :
        ~chatnpc("<p,neutral>Hello there.");
        def_int $option = ~p_choice2("Got any more quests?", 1, "Most of your friends are pretty quiet aren't they?", 2);
        if($option = 1) {
            ~chatplayer("<p,neutral>Got any more quests?");
            ~chatnpc("<p,neutral>No, everything is good with the world today.");
        } else if($option = 2) {
            @mizgog_quiet;        
        }
}

[label,mizgog_givequest]
~chatplayer("<p,neutral>Give me a quest!");
~chatnpc("<p,happy>Give me a quest what?");
def_int $option = ~p_choice3("Give me a quest please.", 1, "Give me a quest or else!", 2, "Just stop messing around and give me a quest!", 3);
if($option = 1) {
    ~chatplayer("<p,neutral>Give me a quest please.");
    ~chatnpc("<p,happy>Well seeing as you asked nicely...|I could do with some help.");
    ~chatnpc("<p,sad>The wizard Grayzag next door|decided he didn't like me.|So he cast a spell of summoning,|and summoned hundreds of little imps.");
    ~chatnpc("<p,sad>These imps stole all sorts of my things.|Most of these things I don't really care about,|just eggs and balls of string and things.");
    ~chatnpc("<p,sad>But they stole my four magical beads.|There was a red one, a yellow one,|a black one, and a white one.");
    ~chatnpc("<p,sad>These imps have now spread out all over the kingdom.|Could you get my beads back for me?");
    %imp_progress = ^imp_started;
    ~send_quest_progress(questlist:imp, %imp_progress, ^imp_complete);
    ~chatplayer("<p,happy>I'll try.");
} else if($option = 2) {
    ~chatplayer("<p,sad>Give me a quest or else!");
    ~chatnpc("<p,happy>Or else what? You'll attack me?");
    ~chatnpc("<p,laugh>Hahaha!");
} else if($option = 3) {
     ~chatplayer("<p,angry>Just stop messing around and give me a quest!");
     ~chatnpc("<p,happy>Ah now you're assuming I have one to give.");
}

[label,mizgog_quiet]
~chatplayer("<p,neutral>Most of your friends are pretty quiet aren't they?");
~chatnpc("<p,neutral>Yes, they've mostly got their heads in the clouds,|thinking about magic.");