[opnpc1,candle_maker]
if(testbit(%excalibur_components_progress, ^blackcandle_index) = ^true) {
    ~chatnpc("<p,quiz>Have you got any wax yet?");
    if(inv_total(inv, bucket_wax) = 0) {
        ~chatplayer("<p,sad>Nope, not yet.");
        return;
    }
    ~chatplayer("<p,happy>Yes, I have some now.");
    ~mesbox("You exchange the wax with the candle maker for a black candle.");
    inv_del(inv, bucket_wax, 1);
    inv_add(inv, unlit_black_candle, 1);
    %excalibur_components_progress = togglebit(%excalibur_components_progress, ^blackcandle_index);
    return;
}
~chatnpc("<p,happy>Hi! Would you be interested|in some of my fine candles?");
def_int $option;
if(%arthur_progress = ^arthur_spoken_morgan_lefaye) {
    $option = ~p_choice3("Have you got any black candles?", 3, "Yes please.", 1, "No thank you.", 2);
} else {
    $option = ~p_choice2("Yes Please.", 1, "No thank you.", 2);
}
if($option = 1) {
    ~chatplayer("<p,neutral>Yes please.");
    ~openshop_activenpc;
} else if($option = 2) {
    ~chatplayer("<p,happy>No thank you.");
} else if($option = 3) {
    ~chatplayer("<p,quiz>Have you got any black candles?");
    ~chatnpc("<p,shock>BLACK candles???");
    ~chatnpc("<p,confused>Hmmm. In the candle making trade,|we have a tradition that|it's very bad luck to make black candles.");
    ~chatnpc("<p,confused>VERY bad luck.");
    ~chatplayer("<p,angry>I will pay good money for one...");
    ~chatnpc("<p,confused>I still dunno...");
    ~chatnpc("<p,confused>Tell you what. I'll supply you with a black candle...");
    ~chatnpc("<p,confused>IF you can bring me a bucket FULL of wax.");
    %excalibur_components_progress = togglebit(%excalibur_components_progress, ^blackcandle_index);
}