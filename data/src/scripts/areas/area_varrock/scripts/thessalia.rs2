// source: https://www.youtube.com/watch?v=VK_A2Vf9VE8

[opnpc1,thessalia]
if (%bunny_ears_unlocked = 0 & %scythe_unlocked = 0) { // player has not unlocked bunny_ears or scythe
    @thessalia_want_to_buy;
} else if (%bunny_ears_unlocked = 1 & %scythe_unlocked = 1) { // player unlocked both
    if ((inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) &
        (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1)) { // player unlocked both, and has both
        @thessalia_want_to_buy;
    } else if (inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) { // player unlocked both, but lost scythe
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe back?");
        @thessalia_lost_scythe;
    } else if (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1) { // player unlocked both, but lost bunny_ears
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your bunny ears back?");
        @thessalia_lost_bunny_ears;
    } else { // player unlocked both, but lost both
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe and bunny ears back?");
        @thessalia_lost_bunny_ears_and_scythe;
    }
} else if (%bunny_ears_unlocked = 1 & %scythe_unlocked = 0) { // player unlocked bunny_ears, not scythe
    if (inv_total(inv, bunnyears) >= 1 | inv_total(bank, bunnyears) >= 1 | inv_total(worn, bunnyears) >= 1) { //player unlocked bunny_ears, and has bunny_ears
        @thessalia_want_to_buy;
    } else { // player unlocked bunny_ears, but lost bunny ears
        ~chatnpc("<p,quiz>Do you want to buy any fine clothes? Or would you like your bunny ears back?");
        @thessalia_lost_bunny_ears;
    }
} else if (%bunny_ears_unlocked = 0 & %scythe_unlocked = 1) { // player unlocked scythe, not bunny_ears
    if (inv_total(inv, scythe) >= 1 | inv_total(bank, scythe) >= 1 | inv_total(worn, scythe) >= 1) { // player unlocked scythe, and has scythe
        @thessalia_want_to_buy;
    } else { // player unlocked scythe, but lost scythe
        ~chatnpc("<p,happy>Do you want to buy any fine clothes? Or would you like your scythe back?");
        @thessalia_lost_scythe;
    }
}

[label,thessalia_want_to_buy]
~chatnpc("<p,quiz>Do you want to buy any fine clothes?");
@thessalia_what_have_you_got;

[label,thessalia_what_have_you_got]
@multi2("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2);

[label,thessalia_lost_bunny_ears]
@multi3("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my bunny ears back?", thessalia_return_bunny_ears);

[label,thessalia_lost_scythe]
@multi3("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my scythe back?", thessalia_return_scythe);

[label,thessalia_lost_bunny_ears_and_scythe] 
@multi4("What have you got?", thessalia_b1_s1,
        "No, thank you.", thessalia_b1_s2,
        "Can I have my scythe back?", thessalia_return_scythe,
        "Can I have my bunny ears back?", thessalia_return_bunny_ears);

[label,thessalia_return_scythe]
~chatplayer("<p,quiz>Can I have my scythe back?");
~chatnpc("<p,happy>Certainly. Here you go.");
inv_add(inv, scythe, 1);
~chatnpc("<p,quiz>Now, would you like to buy any clothes?");
@thessalia_what_have_you_got;

[label,thessalia_return_bunny_ears]
~chatplayer("<p,quiz>Can I have my bunny ears back?");
~chatnpc("<p,happy>Certainly. Here you go.");
inv_add(inv, bunnyears, 1);
~chatnpc("<p,quiz>Now, would you like to buy any clothes?");
@thessalia_what_have_you_got;

[label,thessalia_b1_s1]
~chatplayer("<p,quiz>What have you got?"); 
~chatnpc("<p,happy>Well, I have a number of fine pieces of clothing on sale or, if you prefer I can offer you an exclusive, total clothing makeover?");
@multi2("Tell me more about this makeover.", thessalia_b2_s1,
        "I'd just like to buy some clothes.", thessalia_b2_s2);

[label,thessalia_b1_s2]
~chatplayer("<p,neutral>No, thank you.");
~chatnpc("<p,happy>Well, please return if you change your mind.");

[label,thessalia_b2_s1]
~chatplayer("<p,neutral>Tell me more about this makeover.");
~chatnpc("<p,happy>Certainly!");
~chatnpc("<p,happy>Here at Thessalia's fine clothing boutique, we offer a unique service where we will totally revamp your outfit to your choosing, for... wait for it...");
~chatnpc("<p,happy>A fee of only 500 gold coins! Tired of always wearing the same old outfit, day in, day out? This is the service for you!");
~chatnpc("<p,happy>So what do you say? Interested? We can change either your top, or your legwear for only 500 gold an item!");
@multi4("I'd like to change my top please.", thessalia_b3_s1,
        "I'd like to change my legwear please.", thessalia_b3_s2,
        "I'd just like to buy some clothes.", thessalia_b2_s2,
        "No, thank you.", thessalia_b1_s2);

[label,thessalia_b3_s1]
%option1 = 0;
%option2 = 0;
%option3 = 0;

~chatnpc("<p,happy>Just select what style and colour you would like from this catalogue, and then give me the 500 coins when you've picked.");

if (gender = ^gender_male) {
    if_openmain(player_kit_tailor_torso_man);
} else {
    if_openmain(player_kit_tailor_torso_woman);
}

[label,thessalia_b3_s2]
%option1 = 0;
%option2 = 0;
%option3 = 0;

~chatnpc("<p,happy>Just select what style and colour you would like from this catalogue, and then give me the 500 coins when you've picked.");

if (gender = ^gender_male) {
    if_openmain(player_kit_tailor_legs_man);
} else {
    if_openmain(player_kit_tailor_legs_woman);
}

[label,thessalia_b2_s2]
~chatplayer("<p,neutral>I'd just like to buy some clothes.");
~openshop_activenpc;

// ----

[if_button,player_kit_tailor_torso_man:plain]
%option1 = 1;

[if_button,player_kit_tailor_torso_man:light_buttons]
%option1 = 2;

[if_button,player_kit_tailor_torso_man:jacket]
%option1 = 3;

[if_button,player_kit_tailor_torso_man:dark_buttons]
%option1 = 4;

[if_button,player_kit_tailor_torso_man:stitching]
%option1 = 5;

[if_button,player_kit_tailor_torso_man:shirt]
%option1 = 6;

[if_button,player_kit_tailor_torso_man:two_toned]
%option1 = 7;

[if_button,player_kit_tailor_torso_man:regular]
%option2 = 1;

[if_button,player_kit_tailor_torso_man:musclebound]
%option2 = 2;

[if_button,player_kit_tailor_torso_man:loose_sleeved]
%option2 = 3;

[if_button,player_kit_tailor_torso_man:large_cuffed]
%option2 = 4;

[if_button,player_kit_tailor_torso_man:thin_sleeved]
%option2 = 5;

[if_button,player_kit_tailor_torso_man:shoulder_pads]
%option2 = 6;

[if_button,player_kit_tailor_torso_man:khaki]
%option3 = 1;

[if_button,player_kit_tailor_torso_man:black]
%option3 = 2;

[if_button,player_kit_tailor_torso_man:crimson]
%option3 = 3;

[if_button,player_kit_tailor_torso_man:navy]
%option3 = 4;

[if_button,player_kit_tailor_torso_man:straw]
%option3 = 5;

[if_button,player_kit_tailor_torso_man:white]
%option3 = 6;

[if_button,player_kit_tailor_torso_man:red]
%option3 = 7;

[if_button,player_kit_tailor_torso_man:blue]
%option3 = 8;

[if_button,player_kit_tailor_torso_man:green]
%option3 = 9;

[if_button,player_kit_tailor_torso_man:yellow]
%option3 = 10;

[if_button,player_kit_tailor_torso_man:purple]
%option3 = 11;

[if_button,player_kit_tailor_torso_man:orange]
%option3 = 12;

[if_button,player_kit_tailor_torso_man:rose]
%option3 = 13;

[if_button,player_kit_tailor_torso_man:lime]
%option3 = 14;

[if_button,player_kit_tailor_torso_man:cyan]
%option3 = 15;

[if_button,player_kit_tailor_torso_man:emerald]
%option3 = 16;

[if_button,player_kit_tailor_torso_man:confirm]
if (%option1 = 0 | %option2 = 0 | %option3 = 0) {
    mes("You must make a selection from all three categories.");
    return;
}
if_close;
if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%option3 - 1);

if (%option1 = 1) {
    setidkit(man_torso_plain, $color);
} else if (%option1 = 2) {
    setidkit(man_torso_light_buttons, $color);
} else if (%option1 = 3) {
    setidkit(man_torso_jacket, $color);
} else if (%option1 = 4) {
    setidkit(man_torso_dark_buttons, $color);
} else if (%option1 = 5) {
    setidkit(man_torso_stitching, $color);
} else if (%option1 = 6) {
    setidkit(man_torso_shirt, $color);
} else if (%option1 = 7) {
    setidkit(man_torso_two_toned, $color);
}

if (%option2 = 1) {
    setidkit(man_arms_regular, $color);
} else if (%option2 = 2) {
    setidkit(man_arms_musclebound, $color);
} else if (%option2 = 3) {
    setidkit(man_arms_loose_sleeved, $color);
} else if (%option2 = 4) {
    setidkit(man_arms_large_cuffed, $color);
} else if (%option2 = 5) {
    setidkit(man_arms_thin_sleeved, $color);
} else if (%option2 = 6) {
    setidkit(man_arms_shoulder_pads, $color);
}

@thessalia_finished_makeover;

// ----

[if_button,player_kit_tailor_legs_man:plain]
%option1 = 1;

[if_button,player_kit_tailor_legs_man:shorts]
%option1 = 2;

[if_button,player_kit_tailor_legs_man:flares]
%option1 = 3;

[if_button,player_kit_tailor_legs_man:turn_ups]
%option1 = 4;

[if_button,player_kit_tailor_legs_man:khaki]
%option3 = 2;

[if_button,player_kit_tailor_legs_man:black]
%option3 = 3;

[if_button,player_kit_tailor_legs_man:crimson]
%option3 = 4;

[if_button,player_kit_tailor_legs_man:navy]
%option3 = 5;

[if_button,player_kit_tailor_legs_man:straw]
%option3 = 6;

[if_button,player_kit_tailor_legs_man:white]
%option3 = 7;

[if_button,player_kit_tailor_legs_man:red]
%option3 = 8;

[if_button,player_kit_tailor_legs_man:blue]
%option3 = 9;

[if_button,player_kit_tailor_legs_man:green]
%option3 = 10;

[if_button,player_kit_tailor_legs_man:yellow]
%option3 = 11;

[if_button,player_kit_tailor_legs_man:purple]
%option3 = 12;

[if_button,player_kit_tailor_legs_man:orange]
%option3 = 13;

[if_button,player_kit_tailor_legs_man:rose]
%option3 = 14;

[if_button,player_kit_tailor_legs_man:lime]
%option3 = 15;

[if_button,player_kit_tailor_legs_man:cyan]
%option3 = 16;

[if_button,player_kit_tailor_legs_man:emerald]
%option3 = 1;

[if_button,player_kit_tailor_legs_man:confirm]
if (%option1 = 0 | %option3 = 0) {
    mes("You must make a selection from both categories.");
    return;
}

if_close;

if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%option3 - 1);

if (%option1 = 1) {
    setidkit(man_legs_plain, $color);
} else if (%option1 = 2) {
    setidkit(man_legs_shorts, $color);
} else if (%option1 = 3) {
    setidkit(man_legs_flares, $color);
} else if (%option1 = 4) {
    setidkit(man_legs_turn_ups, $color);
}

@thessalia_finished_makeover;

[if_button,player_kit_tailor_torso_woman:plain] %option1 = 1;
[if_button,player_kit_tailor_torso_woman:crop_top] %option1 = 2;
[if_button,player_kit_tailor_torso_woman:polo_neck] %option1 = 3;
[if_button,player_kit_tailor_torso_woman:simple] %option1 = 4;
[if_button,player_kit_tailor_torso_woman:torn] %option1 = 5;

[if_button,player_kit_tailor_torso_woman:short_sleeves] %option2 = 1;
[if_button,player_kit_tailor_torso_woman:bare_arms] %option2 = 2;
[if_button,player_kit_tailor_torso_woman:muscley] %option2 = 3;
[if_button,player_kit_tailor_torso_woman:long_sleeved] %option2 = 4;
[if_button,player_kit_tailor_torso_woman:large_cuffs] %option2 = 5;

[if_button,player_kit_tailor_torso_woman:com_91] %option3 = 1;
[if_button,player_kit_tailor_torso_woman:com_92] %option3 = 2;
[if_button,player_kit_tailor_torso_woman:com_93] %option3 = 3;
[if_button,player_kit_tailor_torso_woman:com_94] %option3 = 4;
[if_button,player_kit_tailor_torso_woman:com_95] %option3 = 5;
[if_button,player_kit_tailor_torso_woman:com_96] %option3 = 6;
[if_button,player_kit_tailor_torso_woman:com_97] %option3 = 7;
[if_button,player_kit_tailor_torso_woman:com_98] %option3 = 8;
[if_button,player_kit_tailor_torso_woman:com_99] %option3 = 9;
[if_button,player_kit_tailor_torso_woman:com_100] %option3 = 10;
[if_button,player_kit_tailor_torso_woman:com_101] %option3 = 11;
[if_button,player_kit_tailor_torso_woman:com_102] %option3 = 12;
[if_button,player_kit_tailor_torso_woman:com_103] %option3 = 13;
[if_button,player_kit_tailor_torso_woman:com_104] %option3 = 14;
[if_button,player_kit_tailor_torso_woman:com_105] %option3 = 15;
[if_button,player_kit_tailor_torso_woman:com_106] %option3 = 16;

[if_button,player_kit_tailor_torso_woman:confirm]
if (%option1 = 0 | %option2 = 0 | %option3 = 0) {
    mes("You must make a selection from all three categories.");
    return;
}
if_close;
if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%option3 - 1);

if (%option1 = 1) {
    setidkit(woman_torso_plain, $color);
} else if (%option1 = 2) {
    setidkit(woman_torso_crop_top, $color);
} else if (%option1 = 3) {
    setidkit(woman_torso_polo_neck, $color);
} else if (%option1 = 4) {
    setidkit(woman_torso_simple, $color);
} else if (%option1 = 5) {
    setidkit(woman_torso_torn, $color);
}

if (%option2 = 1) {
    setidkit(woman_arms_short_sleeves, $color);
} else if (%option2 = 2) {
    setidkit(woman_arms_bare_arms, $color);
} else if (%option2 = 3) {
    setidkit(woman_arms_muscley, $color);
} else if (%option2 = 4) {
    setidkit(woman_arms_long_sleeved, $color);
} else if (%option2 = 5) {
    setidkit(woman_arms_large_cuffs, $color);
}

@thessalia_finished_makeover;

[if_button,player_kit_tailor_legs_woman:com_133] %option1 = 1;
[if_button,player_kit_tailor_legs_woman:com_134] %option1 = 2;
[if_button,player_kit_tailor_legs_woman:com_135] %option1 = 3;
[if_button,player_kit_tailor_legs_woman:com_136] %option1 = 4;
[if_button,player_kit_tailor_legs_woman:com_137] %option1 = 5;
[if_button,player_kit_tailor_legs_woman:com_138] %option1 = 6;
[if_button,player_kit_tailor_legs_woman:com_139] %option1 = 7;
[if_button,player_kit_tailor_legs_woman:com_140] %option1 = 8;

[if_button,player_kit_tailor_legs_woman:com_91] %option3 = 2;
[if_button,player_kit_tailor_legs_woman:com_92] %option3 = 3;
[if_button,player_kit_tailor_legs_woman:com_93] %option3 = 4;
[if_button,player_kit_tailor_legs_woman:com_94] %option3 = 5;
[if_button,player_kit_tailor_legs_woman:com_95] %option3 = 6;
[if_button,player_kit_tailor_legs_woman:com_96] %option3 = 7;
[if_button,player_kit_tailor_legs_woman:com_97] %option3 = 8;
[if_button,player_kit_tailor_legs_woman:com_98] %option3 = 9;
[if_button,player_kit_tailor_legs_woman:com_99] %option3 = 10;
[if_button,player_kit_tailor_legs_woman:com_100] %option3 = 11;
[if_button,player_kit_tailor_legs_woman:com_101] %option3 = 12;
[if_button,player_kit_tailor_legs_woman:com_102] %option3 = 13;
[if_button,player_kit_tailor_legs_woman:com_103] %option3 = 14;
[if_button,player_kit_tailor_legs_woman:com_104] %option3 = 15;
[if_button,player_kit_tailor_legs_woman:com_105] %option3 = 16;
[if_button,player_kit_tailor_legs_woman:com_106] %option3 = 1;

[if_button,player_kit_tailor_legs_woman:confirm]
if (%option1 = 0 | %option3 = 0) {
    mes("You must make a selection from both categories.");
    return;
}

if_close;

if (inv_total(inv, coins) < 500) {
    mes("You do not have enough money.");
    return;
}

def_int $color = calc(%option3 - 1);

if (%option1 = 1) {
    setidkit(woman_legs_plain, $color);
} else if (%option1 = 2) {
    setidkit(woman_legs_shorts, $color);
} else if (%option1 = 3) {
    setidkit(woman_legs_flares, $color);
} else if (%option1 = 4) {
    setidkit(woman_legs_turn_ups, $color);
} else if (%option1 = 5) {
    setidkit(woman_legs_turn_ups_2, $color);
} else if (%option1 = 6) {
    setidkit(woman_legs_turn_ups_3, $color);
} else if (%option1 = 7) {
    setidkit(woman_legs_turn_ups_4, $color);
} else if (%option1 = 8) {
    setidkit(woman_legs_turn_ups_5, $color);
}

@thessalia_finished_makeover;

[label,thessalia_finished_makeover]
buildappearance(worn);
inv_del(inv, coins, 500);
if (npc_find(coord, thessalia, 4, 0) = false) {
    return;
}
def_int $rand = random(6);
if ($rand = 0) {
    ~chatnpc("<p,happy>Totally cool! That outfit looks great!");
} else if ($rand = 1) {
    ~chatnpc("<p,happy>Very stylish! It really suits you!");
} else if ($rand = 2) {
    ~chatnpc("<p,happy>Woah! Fabulous! You look absolutely great!");
} else if ($rand = 3) {
    ~chatnpc("<p,happy>Wow! Good choice! You're looking great!");
} else if ($rand = 4) {
    ~chatnpc("<p,happy>WoooOOOooo! Hot stuff!");
} else if ($rand = 5) {
    ~chatnpc("<p,happy>Fantastic! You should be a model!");
}
~chatplayer("<p,happy>Thanks!");