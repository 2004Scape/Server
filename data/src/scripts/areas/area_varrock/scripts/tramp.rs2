[opnpc1,tramp_orange]
~chatnpc("<p,sad>Spare some change guv?");
switch_int(~p_choice4("Sorry, I haven't got any.", 1, "Go get a job!", 2, "Ok. Here you go.", 3, "Is there anything down this alleyway?", 4)) {
    case 1:
        ~chatplayer("<p,sad>Sorry, I haven't got any.");
        ~chatnpc("<p,sad>Thanks anyway.");
    case 2:
        ~chatplayer("<p,angry>Go get a job!");
        ~chatnpc("<p,angry>You startin?|I hope your nose falls off!");
    case 3:
        ~chatplayer("<p,happy>Ok. Here you go.");
        if(inv_total(inv, coins) = 0) {
            ~chatplayer("<p,sad>Oh wait. I haven't got any, sorry.");
            ~chatnpc("<p,sad>Thanks anyway.");
            return;
        }
        inv_del(inv, coins, 1);
        ~chatnpc("<p,happy>Hey, thanks a lot!");
        def_int $choice = ~p_choice2("No problem.", 1, "Don't I get some sort of quest hint or something now?", 2);
        if($choice = 1) {
            ~chatplayer("<p,neutral>No problem.");
        } else if($choice = 2) {
            ~chatplayer("<p,quiz>So...don't I get some sort of quest hint or something now?");
            ~chatnpc("<p,sad>No, that's not why I'm asking for money.|I just need to eat...");
        }
    case 4:
        ~chatplayer("<p,quiz>Is there anything down this alleyway?");
        ~chatnpc("<p,neutral>Yes, there is actually.|A notorious gang of thieves and hoodlums,|called the Black Arm Gang.");
        def_int $option = ~p_choice2("Thanks for the warning!", 1, "Do you think they would let me join?", 2);
        if($option = 1) {
            ~chatplayer("<p,happy>Thanks for the warning!");
            ~chatnpc("<p,happy>Don't worry about it.");
        } else if($option = 2) {
            ~chatplayer("<p,quiz>Do you think they would let me join?");
            if(%blackarmgang_progress >= ^blackarmgang_joined) { // In Black arm
                ~chatnpc("<p,confused>I was under the impression you were already a member...");
                return;
            }
            if(%phoenixgang_progress >= ^phoenixgang_joined) { // In phoenix
                @tramp_phoenixmember;
            }
            if(%blackarmgang_progress = 0) {
                %blackarmgang_progress = 1;
                ~send_quest_progress(questlist:blackarmgang, %blackarmgang_progress, ^blackarmgang_complete);
            }
            ~chatnpc("<p,neutral>You never know.|You'll find a lady down there called Katrine.|Speak to her.");
            ~chatnpc("<p,neutral>But don't upset her, she's pretty dangerous.");
        }

}

[label,tramp_phoenixmember]
~chatnpc("<p,neutral>No. You're a collaborator with the Phoenix Gang. There's no way they'll let you join now.");
switch_int(~p_choice2("How did you know I was in the Phoenix Gang?", 1, "Any ideas how I could get in there then?", 2)) {
    case 1:
        ~chatplayer("<p,quiz>How did YOU know I was in the Phoenix Gang?");
        ~chatnpc("<p,neutral>In my current profession I spend a lot of time on the streets and you hear these sorta things sometimes"); // intentional typo
    case 2:
        ~chatplayer("<p,quiz>Any ideas how I could get in there then?");
        ~chatnpc("<p,neutral>Hmmm. I dunno.");
        ~chatnpc("<p,neutral>Your best bet would probably be to find someone else... Someone who ISN'T a member of the Phoenix Gang, and get them to infiltrate the ranks of the Black Arm Gang for you.");
        ~chatnpc("<p,neutral>If you find someone like that,|tell 'em to come to me first.");
        def_int $option = ~p_choice2("Ok. Good plan!", 1, "Like who?", 2);
        if($option = 1) {
            ~chatplayer("<p,happy>Ok. Good plan!");
        } else if($option = 2) {
            ~chatplayer("<p,quiz>Like who?");
            ~chatnpc("<p,neutral>There's plenty of other adventurers about|besides yourself.|I'm sure if you asked one of them nicely|they would help you.");
        }
}