[opnpc1,reldo]
~chatnpc(default, "Hello stranger.");
if(%squire_progress = 1 & %phoenixgang_progress = 0) { // knights sword started and arrav not started
    @multi4("I'm in search of a quest", reldo_phoenixstart, "Do you have anything to trade?", reldo_trade, "What do you do?", reldo_what_do, "What do you know about the Imcando dwarves?", reldo_imcando_dwaves);
} else if(%squire_progress = 1) { // knights sword started and arrav in progress
    @multi3("Do you have anything to trade?", reldo_trade, "What do you do?", reldo_what_do, "What do you know about the Imcando dwarves?", reldo_imcando_dwaves);
} else if(%phoenixgang_progress = 0) { // knights sword started and arrav not started
    @multi3("I'm in search of a quest", reldo_phoenixstart, "Do you have anything to trade?", reldo_trade, "What do you do?", reldo_what_do);
} else {
    @multi2("Do you have anything to trade?", reldo_trade, "What do you do?", reldo_what_do);
}

[label,reldo_trade]
~chatplayer(quiz, "Do you have anything to trade?");
~chatnpc(neutral, "Only knowledge.");
~chatplayer(quiz, "How much do you want for that then?");
~chatnpc(laugh, "No, sorry, that was just my little joke. I'm not the trading type.");
~chatplayer(neutral, "Ah well.");

[label,reldo_what_do]
~chatplayer(quiz, "What do you do?");
~chatnpc(neutral, "I am the palace librarian.");
~chatplayer(neutral, "Ah. That's why you're in the library then.");
~chatnpc(neutral, "Yes.");
~chatnpc(neutral, "Although I would probably be in here even if I didn't work here. I like reading. Someday I hope to catalogue all of the information stored in these books so all may read it.");

[label,reldo_phoenixstart]
~chatplayer(neutral, "I'm in search of a quest.");
~chatnpc(neutral, "Hmmm. I don't... believe there are any here...");
~chatnpc(confused, "Let me think actually...");
~chatnpc(neutral, "If you look in a book called 'The Shield of Arrav',|you'll find a quest in there.");
%phoenixgang_progress = 1; // quest journal
~send_quest_progress(quest_journal:blackarmgang, %phoenixgang_progress, 7);
~chatnpc(neutral, "I'm not sure where the book is mind you...|but I'm sure it's around here somewhere.");
