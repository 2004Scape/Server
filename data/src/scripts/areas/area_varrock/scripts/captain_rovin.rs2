[opnpc1,captain_rovin]
@demon_slayer_captain_rovin;

[label,demon_slayer_captain_rovin]
~chatnpc("<p,angry>What are you doing up here? Only the palace guards are allowed up here.");
def_int $option = ~demon_slayer_captain_rovin_dialogue_options;

if ($option = 1) {
    ~chatplayer("<p,neutral>I am one of the palace guards.");
    ~chatnpc("<p,angry>No, you're not! I know all the palace guards.");
    def_int $palace_guard_option  = ~p_choice2("I'm a new recruit.", 1, "I've had extensive plastic surgery.", 2);

    if ($palace_guard_option = 1) {
        ~chatplayer("<p,neutral>I'm a new recruit.");
        ~chatnpc("<p,neutral>I interview all the new recruits. I'd know if you were one of them.");
        ~chatplayer("<p,happy>That blows that story out of the water then.");
        ~chatnpc("<p,angry>Get out of my sight.");
    } else if ($palace_guard_option = 2) {
        ~chatplayer("<p,neutral>I've had extensive plastic surgery.");
        ~chatnpc("<p,neutral>What sort of surgery is that? I've never heard of it. Besides, you look reasonably healthy..");
        ~chatnpc("<p,angry>Why is this relevant anyway? You still shouldn't be here.");
    }
} else if ($option = 2) {
    ~chatplayer("<p,neutral>What about the King? Surely you'd let him up here.");
    ~chatnpc("<p,neutral>Well, yes, I suppose we'd let him up. He doesn't generally want to come up here, but if he did want to, he could.");
    ~chatnpc("<p,angry>Anyway, you're not the King either. So get out of my sight.");
} else if ($option = 3) {
    ~chatplayer("<p,neutral>Yes, I know, but this is important.");
    ~chatnpc("<p,neutral>Ok, I'm listening. Tell me what's so important.");
    def_int $important_option = ~p_choice3("There's a demon who wants to invade this city.", 1, "Erm I forgot.", 2, "The castle has just received its ale delivery.", 3);

    if ($important_option = 1) {
        ~chatplayer("<p,neutral>There's a demon who wants to invade the city.");
        ~chatnpc("<p,neutral>Is it a powerful demon?");
        ~chatplayer("<p,sad>Yes, very.");
        ~chatnpc("<p,scared>As good as the palace guards are, I don't know if they're up to taking on a very powerful demon.");
        ~chatplayer("<p,neutral>It's not them who are going to fight the demon, it's me.");
        ~chatnpc("<p,shock>What, all by yourself? How are you going to do that?");
        ~chatplayer("<p,neutral>I'm going to use the powerful sword Silverlight, which I believe you have one of the keys for?");
        // RSC let you repeat this OSRS is what added blocks
        ~chatnpc("<p,quiz>Yes you are right. Here you go.");
        if_close();
        p_delay(2);
        mes("Captain Rovin hands you a key.");
        inv_add(inv, silverlight_key_2, 1);
    } else if ($important_option = 2) {
        ~chatplayer("<p,sad>Erm I forgot.");
        ~chatnpc("<p,angry>Well it can't be that important then.");
        ~chatplayer("<p,neutral>How do you know?");
        ~chatnpc("<p,angry>Just go away.");
    } else if ($important_option = 3) {
        ~chatplayer("<p,happy>The castle has just received its ale delivery.");
        ~chatnpc("<p,neutral>Now that is important. However I'm the wrong person to speak to about it. Go talk to the kitchen staff.");
    }
}

[proc,demon_slayer_captain_rovin_dialogue_options]()(int)
if (%demon_progress < ^demon_key_hunt | %demon_progress >= ^demon_silverlight) {
    return(~p_choice2("I am one of the palace guards.", 1, "What about the King?", 2));
} else if (%demon_progress >= ^demon_key_hunt  & %demon_progress < ^demon_silverlight) {
    return(~p_choice3("I am one of the palace guards.", 1, "What about the King?", 2, "Yes I know, but this is important.", 3));
}