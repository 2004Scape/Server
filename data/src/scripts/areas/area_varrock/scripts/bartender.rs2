[opnpc1,bartender_jollyboar] // Jolly Boar Inn
~chatnpc("<p,happy>Can I help you?");
def_int $option;
if(testbit(%barcrawl_progress, ^jollyboar_index) = false & inv_total(inv, barcrawl_card) > 0) {
    $option = ~p_choice4("I'll have a beer please.", 1, "Any hints where I can go adventuring?", 2, "Heard any good gossip?", 3, "I'm doing Alfred Grimhands Barcrawl.", 4);
} else {
    $option = ~p_choice3("I'll have a beer please.", 1, "Any hints where I can go adventuring?", 2, "Heard any good gossip?", 3);
}
switch_int ($option) {
    case 1:
        ~chatplayer("<p,happy>I'll have a pint of beer please.");
        ~chatnpc("<p,happy>Ok, that'll be two coins please.");
        if (inv_total(inv, coins) < 2) {
            ~chatplayer("<p,sad>Oh dear, I don't seem to have enough money.");
        } else {
            inv_del(inv, coins, 2);
            inv_add(inv, beer, 1);
            mes("You buy a pint of beer.");
        }
    case 2:
        ~chatplayer("<p,happy>Any hints on where I can go adventuring?");
        ~chatnpc("<p,quiz>Ooh, now. Let me see...");
        ~chatnpc("<p,quiz>Well there is the Varrock sewers. There are tales of untold horrors coming out at night and stealing babies from houses.");
        ~chatplayer("<p,happy>Sounds perfect! Where's the entrance?");
        ~chatnpc("<p,quiz>It's just to the east of the palace.");
    case 3:
        ~chatplayer("<p,happy>Heard any gossip?");
        ~chatnpc("<p,confused>I'm not that well up on the gossip out here. I've heard that the bartender in the Blue Moon Inn has gone a little crazy, he keeps claiming he is part of something called an online game.");
        ~chatnpc("<p,confused>What that means, I don't know. That's probably old news by now though.");
    case 4: @jollyboar_barcrawl;
    case default: ~displaymessage(^dm_default);
}

[opnpcu,bartender_jollyboar]
if(testbit(%barcrawl_progress, ^jollyboar_index) = false & last_useitem = barcrawl_card) {
    @jollyboar_barcrawl;
}

[label,jollyboar_barcrawl]
~chatplayer("<p,neutral>I'm doing Alfred Grimhand's Barcrawl.");
~chatnpc("<p,neutral>Ah, there seems to be a fair few doing that one these days. My supply of Olde Suspiciouse is starting to run low, it'll cost you 10 coins.");
if(inv_total(inv, coins) < 10) {
    ~chatplayer("<p,sad>I don't have 10 coins right now.");
    return;
}   
if_close;
inv_del(inv, coins, 10);
mes("You buy a pint of Olde Suspiciouse.");
p_delay(2);
mes("You gulp it down.");
p_delay(2);
stat_sub(attack, calc(5 + ((stat(attack) * 5) / 100)), 0);
stat_sub(defence, calc(5 + ((stat(defence) * 5) / 100)), 0);
stat_sub(magic, calc(5 + ((stat(magic) * 5) / 100)), 0);
stat_sub(crafting, calc(5 + ((stat(crafting) * 5) / 100)), 0);
stat_sub(mining, calc(5 + ((stat(mining) * 5) / 100)), 0);
mes("Your head is spinning.");
p_delay(2);
%barcrawl_progress = setbit(%barcrawl_progress, ^jollyboar_index);
mes("The bartender signs your card.");  
p_delay(2);
~chatplayer("<p,confused>Thanksh very mush...");


[opnpc1,bartender_varrock]
//Treasure Trails
if(inv_total(inv, trail_clue_easy_simple_exp010) = 1) {
    @trail_bartender_varrock;
}
~chatnpc("<p,quiz>What can I do yer for?");
def_int $option;
if(testbit(%barcrawl_progress, ^bluemoon_index) = false & inv_total(inv, barcrawl_card) > 0) {
    $option = ~p_choice4("A glass of your finest ale please.", 1, "Can you recommend where an adventurer might make his fortune?", 2, "Do you know where I can get some good equipment?", 3, "I'm doing Alfred Grimhand's Barcrawl.", 4);
} else {
    $option = ~p_choice3("A glass of your finest ale please.", 1, "Can you recommend where an adventurer might make his fortune?", 2, "Do you know where I can get some good equipment?", 3);
}
if($option = 1) {
    ~chatplayer("<p,happy>A glass of your finest ale please.");
    ~chatnpc("<p,happy>No problemo. That'll be 2 coins.");
    if (inv_total(inv, coins) < 2) {
        ~chatplayer("<p,sad>Oh dear. I don't seem to have enough money.");
        return;
    } 
    inv_del(inv, coins, 2);
    inv_add(inv, beer, 1);
    mes("You buy a pint of beer.");
} else if($option = 2) {
    ~chatplayer("<p,quiz>Can you recommend where an adventurer might make his fortune?");
    ~chatnpc("<p,shifty>Ooh I don't know if I should be giving away information, makes the computer game too easy.");
    switch_int(~p_choice3("Oh ah well...", 1, "Computer game? What are you talking about?", 2, "Just a small clue?", 3)) {
        case 1:
            ~chatplayer("<p,confused>Oh ah well...");
        case 2:
            ~chatplayer("<p,confused>Computer game?|What are you talking about?");
            ~chatnpc("<p,shifty>This world around us...|is a computer game... called RuneScape.");
            ~chatplayer("<p,confused>Nope, still don't understand what you are talking about. What's a computer?");
            ~chatnpc("<p,shifty>It's a sort of magic box thing,|which can do all sorts of stuff.");
            ~chatplayer("<p,angry>I give up.|You're obviously completely mad!");
        case 3:
            ~chatplayer("<p,quiz>Just a small clue?");
            ~chatnpc("<p,shifty>Go and talk to the bartender at the Jolly Boar Inn,|he doesn't seem to mind giving away clues.");
    }
} else if($option = 3) {
    ~chatplayer("<p,quiz>Do you know where I can get some good equipment?");
    ~chatnpc("<p,neutral>Well, there's the sword shop across the road,|or there's also all sorts of shops up around the market.");
} else if($option = 4) {
    @bluemoon_barcrawl;
}

[opnpcu,bartender_varrock]
if(testbit(%barcrawl_progress, ^bluemoon_index) = false & last_useitem = barcrawl_card) {
    @bluemoon_barcrawl;
}

[label,bluemoon_barcrawl]
~chatplayer("<p,happy>I'm doing Alfred Grimhand's Barcrawl.");
~chatnpc("<p,sad>Oh no not another of you guys.|These barbarian barcrawls cause too much damage to my bar.");
~chatnpc("<p,neutral>You're going to have to pay 50 gold for the Uncle Humphrey's Gutrot.");
if(inv_total(inv, coins) < 50) {
    ~chatplayer("<p,sad>I don't have 50 coins.");
    return;
}  
if_close;
inv_del(inv, coins, 50);
mes("You buy some Uncle Humphrey's Gutrot.");
p_delay(2);
mes("You drink the Uncle Humphrey's Gutrot.");
p_delay(2);
stat_sub(attack, calc(5 + ((stat(attack) * 5) / 100)), 0);
stat_sub(defence, calc(5 + ((stat(attack) * 5) / 100)), 0);
stat_sub(strength, calc(5 + ((stat(attack) * 5) / 100)), 0);
stat_sub(smithing, calc(5 + ((stat(attack) * 5) / 100)), 0);
mes("Your insides feel terrible.");
p_delay(2);
%barcrawl_progress = setbit(%barcrawl_progress, ^bluemoon_index);
mes("The bartender signs your card.");
p_delay(2);
~damage_self(calc((stat(hitpoints) * 3) / 100));
say("Blearrgh!");

[label,trail_bartender_varrock]
~chatnpc("<p,happy>Nicely solved!");
~progress_clue_easy(trail_clue_easy_simple_exp010, "The bartender hands you another scroll!");