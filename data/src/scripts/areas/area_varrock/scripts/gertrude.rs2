[opnpc1,gertrude]
switch_int (%fluffs_progress) {
    case ^fluffs_not_started:
        ~chatplayer("<p,happy>Hello, are you okay?");
        ~chatnpc("<p,angry>Do I look ok? Those kids drive me crazy.");
        ~chatnpc("<p,sad>I'm sorry. It's just that I've lost her.");
        ~chatplayer("<p,quiz>Lost who?");
        ~chatnpc("<p,sad>Fluffs, poor Fluffs. She never hurt anyone.");
        ~chatplayer("<p,quiz>Who's Fluffs?");
        ~chatnpc("<p,sad>My beloved feline friend Fluffs. She's been purring by my side for almost a decade. Please, could you go search for her while I look over the kids?");
        @fluffs_start;
    case ^fluffs_started:
        ~chatplayer("<p,neutral>Hello Gertrude.");
        ~chatnpc("<p,sad>Have you seen my poor Fluffs?");
        ~chatplayer("<p,neutral>I'm afraid not.");
        ~chatnpc("<p,sad>What about Shilop?");
        ~chatplayer("<p,neutral>No sign of him either.");
        ~chatnpc("<p,confused>Hmmm...strange, he should be at the market.");
    case ^fluffs_paid_boy: //guessed linebreaks
        ~chatplayer("<p,neutral>Hello Gertrude.");
        ~chatnpc("<p,neutral>Hello again, did you manage to find Shilop?|I can't keep an eye on him for the life of me.");
        ~chatplayer("<p,neutral>He does seem quite a handful.");
        ~chatnpc("<p,neutral>You have no idea! Did he help at all?");
        ~chatplayer("<p,neutral>I think so, I'm just going to look now.");
        ~chatnpc("<p,neutral>Thanks again, adventurer.");
    case ^fluffs_gave_milk: //guessed linebreaks based off rsc
        ~chatplayer("<p,neutral>Hello Gertrude.");
        ~chatnpc("<p,neutral>Hello. How's it going?|Any luck?");
        ~chatplayer("<p,happy>Yes, I've found Fluffs!");
        ~chatnpc("<p,happy>Well well, you are clever! Did you bring her back?");
        ~chatplayer("<p,neutral>Well, that's the thing, she refuses to leave.");
        ~chatnpc("<p,sad>Oh dear, oh dear! Maybe she's just hungry.|She loves doogle sardines but I'm all out.");
        ~chatplayer("<p,quiz>Doogle sardines?");
        ~chatnpc("<p,neutral>Yes, raw sardines seasoned with doogle leaves.|Unfortunately I've used all my doogle leaves,|but you may find some in the woods out back.");
    case ^fluffs_gave_sardine:
        ~chatplayer("<p,neutral>Hi!");
        ~chatnpc("<p,neutral>Hey traveller, did Fluffs eat the sardines?");
        ~chatplayer("<p,neutral>Yeah, she loved them, but she still won't leave.");
        ~chatnpc("<p,confused>Well that is strange, there must be a reason.");
    case ^fluffs_rescued:
        ~chatplayer("<p,neutral>Hello Gertrude. Fluffs ran off with her kitten.");
        ~chatnpc("<p,happy>You're back! Thank you! Thank you!|Fluffs just came back!|I think she was just upset|as she couldn't find her kitten."); //authentic
        ~mesbox("Gertrude gives you a hug.");
        ~chatnpc("<p,happy>If you hadn't found her kitten it would have died out there!");
        ~chatplayer("<p,neutral>That's okay, I like to do my bit.");
        ~chatnpc("<p,happy>I don't know how to thank you.|I have no real material possessions.|I do have kittens!|I can only really look after one."); //guessed linebreaks
        ~chatplayer("<p,neutral>Well, if it needs a home.");
        ~chatnpc("<p,neutral>I would sell it to my cousin in West Ardougne.|I hear there's a rat epidemic there.|But it's too far."); //guessed linebreaks
        ~chatnpc("<p,happy>Here you go, look after her and thank you again!");
        //in osrs she explains kittens only grow outside the backpack
        ~mesbox("Gertrude hands you a kitten.");
        ~gertrude_give_cat;
        %fluffs_progress = ^fluffs_complete;
        %questpoints = calc(%questpoints + ^fluffs_questpoints);
        queue(fluffs_complete, 0);
    case ^fluffs_complete:
        if (getbit_range(%cat_growth, 20, 26) = 100 & inv_total(inv, cat_training_medal) = 0) {
            ~chatplayer("<p,happy>Hello again Gertrude!");
            ~chatnpc("<p,neutral>Well, hello adventurer! How are you?");
            ~chatplayer("<p,happy>My cat has caught 100 rats!");
            ~chatnpc("<p,neutral>Well well! You are good with cats!");
            if(inv_freespace(inv) = 0) {
            ~chatnpc("<p,neutral>Free up a little space so I can give you a present."); //104433
            return;
            }
            ~chatnpc("<p,neutral>Here, I have a little present for you...");
            inv_add(inv, cat_training_medal, 1);
            ~objbox(cat_training_medal, "Gertrude gives you a small medal.");
            ~chatplayer("<p,neutral>Hey, thanks Gertrude.");
            return;
        } //this is guessed off RSC. assuming her post-quest dialogue was changed later on.
        if(inv_totalcat(inv, kitten) > 0 | inv_totalcat(bank, kitten) > 0 | inv_totalcat(inv, cat) > 0 | inv_totalcat(bank, cat) > 0 | %follower_obj ! null & oc_category(%follower_obj) = kitten | %follower_obj ! null & oc_category(%follower_obj) = cat) {
            ~chatplayer("<p,neutral>Hello again Gertrude.");
            ~chatnpc("<p,neutral>Well hello adventurer, how are you?");
            ~chatplayer("<p,neutral>Pretty good, thanks. Yourself?");
            ~chatnpc("<p,neutral>Same old, running after Shilop most of the time.");
            ~chatplayer("<p,neutral>Nevermind, I'm sure he'll calm down with age.");
        } else {
            ~chatplayer("<p,quiz>Do you have any more kittens?");
            ~chatnpc("<p,neutral>Indeed I have. They are 100 coins each, do you want one?");
            def_int $option = ~p_choice2("Yes please.", 1, "No thanks.", 2);
            switch_int ($option) {
                case 1:
                    if (inv_total(inv, coins) < 100) {
                    ~chatplayer("<p,neutral>Oops, looks like I'm a bit short.|I'll have to come back later.");
                    return;
                    } 
                    ~chatnpc("<p,neutral>Ok then, here you go.");
                    ~chatplayer("<p,happy>Thanks!");
                    inv_del(inv, coins, 100);
                    ~gertrude_give_cat;
                    ~mesbox("Gertrude gives you another kitten.");
                case 2:
                    ~chatplayer("No thanks.");
            }
        }
}

[label,fluffs_start]
def_int $option = ~p_choice3("Well, I suppose I could.", 1, "What's in it for me?", 2, "Sorry, I'm too busy to play pet rescue.", 3);
if($option = 1) { //linebreaks guessed
    ~chatplayer("<p,neutral>Well, I suppose I could.");
    ~chatnpc("<p,happy>Really? Thank you so much!|I really have no idea where she could be!");
    ~chatnpc("<p,default>I think my sons, Shilop and Wilough, saw the cat last.|They'll be out in the market place.");
    ~chatplayer("<p,default>Alright then, I'll see what I can do.");
    %fluffs_progress = ^fluffs_started;
    ~send_quest_progress(questlist:fluffs, %fluffs_progress, ^fluffs_complete);
} else if($option = 2) { //linebreaks guessed
    ~chatplayer("<p,quiz>What's in it for me?");
    ~chatnpc("<p,sad>I'm sorry, I'm too poor to pay you anything.|The best I could offer is a warm meal.|So, can you help?");
    @fluffs_start;
} else if($option = 3) {
    ~chatplayer("<p,neutral>Sorry, I'm too busy to play pet rescue.");
    ~chatnpc("<p,sad>Well, okay then. I'll have to find someone else.");
}

[proc,gertrude_give_cat]
if (inv_totalcat(inv, kitten) = 0 | inv_totalcat(bank, kitten) = 0 | inv_totalcat(inv, cat) = 0 | inv_totalcat(bank, cat) = 0 | npc_finduid(%follower_uid) = false | %follower_obj ! null & oc_category(%follower_obj) ! kitten | %follower_obj ! null & oc_category(%follower_obj) ! cat) {
    def_npc $cat;
    switch_int (random(6)) {
        case 0: $cat = follower_kitten_greyblack;
        case 1: $cat = follower_kitten_white;
        case 2: $cat = follower_kitten_brown;
        case 3: $cat = follower_kitten_black;
        case 4: $cat = follower_kitten_greybrown;
        case 5: $cat = follower_kitten_greyblue;
    }
    npc_add(coord, $cat, ^max_32bit_int);
    %follower_obj = nc_param($cat, pet_item_id);
    %follower_uid = npc_uid;
    %cat_growth = 0;
    %npc_attacking_uid = uid;
    npc_setmode(playerfollow);
    npc_settimer(150);
    npc_say("Miaow!");
    p_delay(0);
    npc_say("Meeow!");
}
