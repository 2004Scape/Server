[opnpc1,romeo]
switch_int (%romeojuliet_progress) {
    case ^romeojuliet_not_started : @romeojuliet_start;
    case ^romeojuliet_spoken_romeo : ~chatnpc("<p,sad>Please find my Juliet, I am so, so sad.");
    case ^romeojuliet_spoken_juliet : @romeo_messagefrom;
    case ^romeojuliet_passed_message :
        ~chatnpc("<p,sad>Please friend, how goes our quest?");
        ~chatnpc("<p,sad>Father Lawrence must be told. Only he can help.");
    case ^romeojuliet_spoken_father : @romeo_father;
    case ^romeojuliet_spoken_apothecary : @romeo_potion;
    case ^romeojuliet_juliet_crypt : @romeo_allset;
    case ^romeojuliet_complete :
        ~chatnpc("<p,sad>I heard Juliet had died. Terrible business.");
        ~chatnpc("<p,happy>Her cousin and I are getting on well though.|Thanks for your help!");
}

[label,romeojuliet_start]
~chatnpc("<p,sad>Juliet, Juliet, Juliet!  Wherefore Art thou?"); // space is intentional
~chatnpc("<p,sad>Kind friend, have you seen Juliet?");
~chatnpc("<p,sad>She's disappeared and I can't find her anywhere.");
switch_int(~p_choice3("Yes, I have seen her.", 1, "No, but that's girls for you.", 2, "Can I help find her for you?", 3)) {
    case 1 :
        ~chatplayer("<p,happy>Yes, I have seen her.");
        ~chatplayer("<p,shifty>I think it was her... Blonde? A bit stressed?");
        ~chatnpc("<p,happy>Yes, that sounds like her.|Please tell her I long to be with her.");
        @multi2("Yes, I will tell her.", romeo_tell_her, "Sorry, I am too busy. Maybe later?", romeo_busy);
    case 2 :
        ~chatplayer("<p,happy>No, but that's girls for you.");
        ~chatnpc("<p,sad>Not my dear Juliet, she is different.");
        ~chatnpc("<p,quiz>Could you find her for me?|Please tell her I long to be with her.");
        @multi2("Yes, I will tell her how you feel.", romeo_tell_her, "I can't, it sounds like work to me.", romeo_work);
    case 3 :
        ~chatplayer("<p,happy>Can I help find her for you?");
        ~chatnpc("<p,happy>Oh would you? That would be wonderful!");
        ~chatnpc("<p,happy>Please tell her I long to be with her.");
        @romeo_tell_her;  
}

[label,romeo_tell_her]
~chatplayer("<p,neutral>Yes, I will tell her how you feel.");
~chatnpc("<p,happy>You are the saviour of my heart, thank you.");
~chatplayer("<p,neutral>Err, yes. Ok. Thats.... Nice.");
%romeojuliet_progress = ^romeojuliet_spoken_romeo;
~send_quest_progress(questlist:romeojuliet, %romeojuliet_progress, ^romeojuliet_complete);

[label,romeo_busy]
~chatplayer("<p,quiz>Sorry, I am too busy. Maybe later?");
~chatnpc("<p,sad>Well if you do find her, I would be most grateful.");

[label,romeo_work]
~chatplayer("<p,bored>I can't, it sounds like work to me.");
~chatnpc("<p,angry>Well, I guess you are not the romantic type.|Goodbye!");

[label,romeo_messagefrom]
~chatplayer("<p,happy>Romeo, I have a message from Juliet.");
~mesbox("You pass Juliet's message to Romeo.");
inv_del(inv, julietmessage, 1);
%romeojuliet_progress = ^romeojuliet_passed_message;
~chatnpc("<p,sad>Tragic news. Her father is opposing our marriage.");
~chatnpc("<p,sad>If her father sees me, he will kill me.");
~chatnpc("<p,sad>I dare not go near his lands.");
~chatnpc("<p,sad>She says Father Lawrence can help us.");
~chatnpc("<p,sad>Please find him for me. Tell him of our plight.");

[label,romeo_father]
~chatnpc("<p,quiz>Did you find the Father? What did he suggest?");
def_int $option = ~p_choice2("He sent me to the Apothecary.", 1, "He seems keen for you to marry Juliet.", 2);
if($option = 1) {
    ~chatplayer("<p,neutral>He sent me to the Apothecary.");
    ~chatnpc("<p,neutral>I know him.");
    ~chatnpc("<p,neutral>He lives near the town square,|the small house behind the sloped building.");
    ~chatnpc("<p,happy>Good luck.");
} else if($option = 2) {
    ~chatplayer("<p,happy>He seems keen for you to marry Juliet.");
    ~chatnpc("<p,neutral>I think he wants some peace.");
    ~chatnpc("<p,happy>He was our messenger before you|were kind enough to help us.");
}

[label,romeo_potion]
if(inv_total(inv, cadava) > 0) {
    ~chatnpc("<p,neutral>Ah, you have the potion.");
    ~chatnpc("<p,neutral>I was told what to do by the good Father.");
    ~chatnpc("<p,neutral>Better get it to Juliet. She knows what is happening.");
    return;
}
~chatnpc("<p,happy>I hope the potion is near ready.");
~chatnpc("<p,happy>It is the last step for the great plan.");
~chatnpc("<p,happy>I hope I will be with my dear one soon.");

[label,romeo_allset]
~chatplayer("<p,happy>Romeo, it's all set. Juliet has the potion.");
~chatnpc("<p,neutral>Ah right.");
~chatnpc("<p,neutral>What potion would that be then?");
~chatplayer("<p,neutral>The one to get her to the crypt.");
~chatnpc("<p,neutral>Ah right.");
~chatnpc("<p,sad>So she is dead then. Aww that's a shame.");
~chatnpc("<p,sad>Thanks for your help anyway.");
queue(romeo_and_juliet_complete, 0);