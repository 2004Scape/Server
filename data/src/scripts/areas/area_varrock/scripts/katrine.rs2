[opnpc1,katrine]
if(%phoenixgang_progress >= ^phoenixgang_joined) {
    ~chatnpc("<p,angry>You've got some guts coming here,|Phoenix <text_gender("guy", "gal")>!");
    ~mesbox("Katrine spits.");
    ~chatnpc("<p,angry>Now get lost!");
    return;
}
switch_int(%blackarmgang_progress) {
    case ^blackarmgang_spoken_katrine : @katrine_got_yet;
    case ^blackarmgang_joined, ^blackarmgang_complete : @katrine_blackarmmember;
    case default : @katrine_standard;
}

[label,katrine_standard]
~chatplayer("<p,quiz>What is this place?");
~chatnpc("<p,neutral>It's a private business.|Can I help you at all?");
def_int $option;
if(%blackarmgang_progress = ^blackarmgang_started) {
    $option = ~p_choice3("I've heard you're the Black Arm Gang.", 3, "What sort of business?", 1, "I'm looking for fame and riches.", 2);
} else {
    $option = ~p_choice2("What sort of business?", 1, "I'm looking for fame and riches.", 2);
}
switch_int($option) {
    case 1 :
        ~chatplayer("<p,quiz>What sort of business?");
        ~chatnpc("<p,neutral>A small, family business.|We give financial advice to other companies.");
    case 2 :
        ~chatplayer("<p,neutral>I'm looking for fame and riches.");
        ~chatnpc("<p,shock>And you expect to find it up the back streets of Varrock?");
    case 3 :
        ~chatplayer("<p,quiz>I've heard you're the Black Arm Gang.");
        ~chatnpc("<p,angry>Who told you that?");
        $option = ~p_choice3("I'd rather not reveal my sources.", 1, "It was the tramp outside.", 2, "Everyone knows - it's no great secret.", 3);
        if($option = 1) {
            ~chatplayer("<p,neutral>I'd rather not reveal my sources.");
            ~chatnpc("<p,neutral>Yes, I can understand that.|So what do you want with us?");
        } else if($option = 2) {
            ~chatplayer("<p,neutral>It was the tramp outside.");
            ~chatnpc("<p,neutral>Is that guy still out there?|He's getting to be a nuisance.|Remind me to send someone to kill him.");
            ~chatnpc("<p,neutral>So now you've found us,|what do you want?");
        } else if($option = 3) {
            ~chatplayer("<p,neutral>Everyone knows.|It's no great secret.");
            ~chatnpc("<p,shock>I thought we were safe back here!");
            ~chatplayer("<p,neutral>Oh no, not at all...|It's so obvious!|Even the town guard have caught on...");
            ~chatnpc("<p,neutral>Wow! We MUST be obvious! I guess they'll be expecting bribes again soon in that case.");
            ~chatnpc("<p,neutral>Thanks for the information.");
            ~chatnpc("<p,quiz>Is there anything else you want to tell me?");
        }
        @multi3("I want to become a member of your gang.", katrine_gangmember, "I want some hints for becoming a thief.", katrine_hint, "I'm looking for the door out of here.", katrine_doorout);
}

[label,katrine_gangmember]
~chatplayer("<p,neutral>I want to become a member of your gang.");
~chatnpc("<p,neutral>How unusual.");
~chatnpc("<p,neutral>Normally we recruit for our gang|by watching local thugs and thieves in action.|People don't normally waltz in here|saying 'hello, can I play'.");
~chatnpc("<p,neutral>How can I be sure you can be trusted?");
switch_int (~p_choice2("Well, you can give me a try can't you?", 1, "Well, people tell me I have an honest face.", 2)) {
    case 1 :
        ~chatplayer("<p,neutral>Well, you can give me a try can't you?");
        ~chatnpc("<p,neutral>I'm not so sure.");
    case 2 :
        ~chatplayer("<p,happy>Well, people tell me I have an honest face!");
        ~chatnpc("<p,neutral>... How unusual. Someone honest wanting to join a gang of thieves. Excuse me if I remain unconvinced."); 
}
~chatnpc("<p,neutral>Thinking about it... I may have a solution actually.");
~chatnpc("<p,neutral>Our rival gang - the Phoenix Gang -|has a weapons stash a little east of here.");
~chatnpc("<p,neutral>We're fresh out of crossbows,|so if you could steal a couple of crossbows for us|it would be very much appreciated.");
~chatnpc("<p,neutral>Then I'll be happy to call you a Black Arm.");
switch_int (~p_choice2("Ok, no problem.", 1, "Sounds a little tricky. Got anything easier?", 2)) {
    case 1 :
        ~chatplayer("<p,happy>Ok, no problem.");
        %blackarmgang_progress = 2;
        ~chatnpc("<p,neutral>Great! You'll find the Phoenix gang's weapon stash just next to a temple, due east of here.");
    case 2 :
        ~chatplayer("<p,confused>Sounds a little tricky...");
        ~chatplayer("<p,quiz>Got anything easier?");
        ~chatnpc("<p,angry>If you're not up to a little bit of danger|I don't think you've got anything to offer our gang."); 
}

[label,katrine_hint]
~chatplayer("<p,neutral>I want some hints for becoming a thief.");
~chatnpc("<p,neutral>Well, I'm sorry luv,|I'm not giving away any of my secrets.");
~chatnpc("<p,neutral>Not to people who ain't Black Arm members anyway.");

[label,katrine_doorout]
~chatplayer("<p,neutral>I'm looking for the door out of here.");
~mesbox("Katrine groans.");
~chatnpc("<p,confused>Try... the one you just came in?");

[label,katrine_got_yet]
~chatnpc("<p,quiz>Have you got those crossbows for me yet?");
if(inv_total(inv,phoenix_crossbow) >= 2) {
    ~chatplayer("<p,happy>Yes, I have.");
    ~mesbox("You give the crossbows to Katrine.");
    inv_del(inv, phoenix_crossbow, 2);
    %blackarmgang_progress = ^blackarmgang_joined;
    ~chatnpc("<p,neutral>Ok. You can join our gang now.|Feel free to enter any of the rooms of the ganghouse.");
    return;
} else if(inv_total(inv,phoenix_crossbow) = 1) {
    ~chatplayer("<p,neutral>I have one...");
    ~chatnpc("<p,neutral>I need two.|Come back when you have them.");
    return;
}
~chatplayer("<p,sad>No, I haven't found them yet.");
~chatnpc("<p,neutral>I need two crossbows|stolen from the Phoenix Gang weapons stash,|which if you head east for a bit,|is a building on the south side of the road.");
~chatnpc("<p,neutral>Come back when you got 'em.");

[label,katrine_blackarmmember]
~chatplayer("<p,neutral>Hey.");
~chatnpc("<p,neutral>Hey.");
switch_int (~determine_katrine_option) {
    case 1 : // default opt 1
        ~chatplayer("<p,quiz>Who are all those people in there?");
        ~chatnpc("<p,neutral>They're just various rogues and thieves.");
        ~chatplayer("<p,quiz>They don't say a lot...");
        ~chatnpc("<p,neutral>Nope.");
    case 2 : // default opt 2
        ~chatplayer("<p,neutral>Teach me to be a top class criminal.");
        ~chatnpc("<p,neutral>Teach yourself.");  
    case 3 : // Heroes in progress chat option
        ~chatplayer("<p,quiz>Is there any way I can get the rank of master thief?");
        ~chatnpc("<p,neutral>Master thief? Ain't we the ambitious one!"); 
        ~chatnpc("<p,neutral>Well, you're gonna have to do something pretty amazing."); 
        ~chatplayer("<p,quiz>Anything you can suggest?");
        ~chatnpc("<p,neutral>Well some of the MOST coveted prizes in thiefdom right now are in the pirate town of Brimhaven on Karamja."); 
        ~chatnpc("<p,neutral>The pirate leader Scarface Pete has a pair of extremely valuable candlesticks."); 
        ~chatnpc("<p,neutral>His security is VERY good."); 
        ~chatnpc("<p,neutral>We, of course, have gang members in a town like Brimhaven who may be able to help you."); 
        ~chatnpc("<p,neutral>Visit our hideout in the alleyway on palm street."); 
        ~chatnpc("<p,neutral>To get in you will need to tell them the secret password 'four leafed clover'."); 
        if (%hero_progress = ^hero_started) {
            %hero_progress = ^hero_blackarm_gangmember_spoken;
        }
    case 4 : // Showing candlestick
        // https://youtu.be/KRhwwgo8YHM?si=L2w_eELFNMxWG88A&t=570
        ~chatplayer("<p,happy>I have a candlestick now!");
        ~chatnpc("<p,shock>Wow.... is... it REALLY it?");
        ~chatnpc("<p,happy>This really is a FINE bit of thievery.");
        ~chatnpc("<p,happy>Us thieves have been trying to get ahold of this one for a while!");
        ~chatnpc("<p,happy>You wanted to be ranked as a master thief didn't you? Well, I guess this just about ranks as good enough!");
        ~mesbox("Katrine gives you a master thief armband.");
        inv_del(inv, petecandlestick, 1);
        inv_add(inv, master_thief_armband, 1);
        %hero_progress = ^hero_blackarm_obtained_armband;
    case 5 : // Showing candlestick (did not progress quest correctly)
        ~chatplayer("<p,neutral>I have a candlestick now!");
        ~chatnpc("<p,neutral>Good for you. I'll give a master thief's armband to the one who retrieved that. I know it wasn't you.");
    case 6 : // Lost armband
        ~chatplayer("<p,sad>I have lost my master thief's armband...");
        ~chatnpc("<p,neutral>Lucky I 'ave a spare ain't it? Don't lose it again.");
        inv_add(inv, master_thief_armband, 1);
}

[proc,determine_katrine_option]()(int)
if (%hero_progress >= ^hero_blackarm_obtained_armband & ~obj_gettotal(master_thief_armband) < 1) {
    return(6);
}
else if (%hero_progress < ^hero_started | %hero_progress >= ^hero_blackarm_obtained_armband) {
    return(~p_choice2("Who are all those people in there?", 1, "Teach me to be a top class criminal.", 2));
}
else if (%hero_progress >= ^hero_blackarm_gangmember_spoken & inv_total(inv, petecandlestick) >= 1) {
    def_int $option;
    if (%hero_progress = ^hero_blackarm_looted_chest) {
        $option = 4; // success
    } else {
        $option = 5; // invalid candlestick (did not obtain correctly)
    }
    return(~p_choice2("Who are all those people in there?", 1, "I have a candlestick now!", $option));
}
else if (%hero_progress = ^hero_started) {
    return(~p_choice2("Who are all those people in there?", 1, "Is there any way I can get the rank of master thief?", 3));
}
// todo? find out if there are dialogue options if you have already asked about becoming a master thief but not yet turned in the candlestick
// (Defaulting to same dialogue as above for now)
return(~p_choice2("Who are all those people in there?", 1, "Is there any way I can get the rank of master thief?", 3));
