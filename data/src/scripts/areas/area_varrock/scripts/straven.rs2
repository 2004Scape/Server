[opnpc1,straven]
if(%phoenixgang_progress >= ^phoenixgang_complete) {
    @straven_gangmember;
} else if(%blackarmgang_progress >= ^blackarmgang_joined) {
    @straven_blackarmdog;
} else if(%phoenixgang_progress = ^phoenixgang_spoken_straven) {
    @straven_mission;
} else {
    ~chatplayer("<p,quiz>What's through that door?");
    @straven_cant_enter;
}

[opnpcu,straven]
if(last_useitem = intelligence_report) {
    if(%phoenixgang_progress = ^phoenixgang_spoken_straven) {
        @straven_mission;
    } else if(%phoenixgang_progress >= ^phoenixgang_joined) {
        ~chatnpc("<p,quiz>You've already given me that intelligence report!|Why are you offering me another copy of it?");
        return;
    }
}
~displaymessage(^dm_default);

[label,straven_cant_enter]
~chatnpc("<p,neutral>Hey! You can't go in there. Only authorised personnel of the VTAM Corporation are allowed beyond this point.");
def_int $option;
if(%phoenixgang_progress = 4) {
    $option = ~p_choice3("I know who you are!", 1, "How do I get a job with the VTAM corporation?", 2, "Why not?", 3);
} else {
    $option = ~p_choice2("How do I get a job with the VTAM corporation?", 2, "Why not?", 3);
}
switch_int ($option) {
    case 1 :
        ~chatplayer("<p,sad>I know who you are!");
        ~chatnpc("<p,neutral>I see.|Carry on?");
        ~chatplayer("<p,sad>This is the headquarters of the Phoenix Gang,|the most powerful crime syndicate this city has ever seen!");
        ~chatnpc("<p,neutral>And supposing we were this crime gang,|what would you want with us?");
        $option = ~p_choice2("I'd like to offer you my services.", 1, "I want nothing. I was just making sure you were them.", 2);
        if($option = 1) {
            ~chatplayer("<p,happy>I'd like to offer you my services.");
            ~chatnpc("<p,neutral>You mean you'd like to join the Phoenix Gang?");
            ~chatnpc("<p,neutral>Well, the Phoenix Gang doesn't let people join just like that.");
            ~chatnpc("<p,neutral>You can't be too careful, you understand.");
            ~chatnpc("<p,neutral>Generally someone has to prove their loyalty before they can join.");
            ~chatplayer("<p,quiz>How would I go about doing that?");
            ~chatnpc("<p,neutral>Let me think...|I have an idea.");
            ~chatnpc("<p,neutral>A rival gang of ours,|called the Black Arm Gang|is supposedly meeting a contact from Port Sarim today.|In the Blue Moon Inn");
            %phoenixgang_progress = 8;
            ~chatnpc("<p,neutral>by the south entrance to this city.|The name of the contact is Jonny the Beard.|Kill him and bring back his intelligence report.");
            ~chatplayer("<p,happy>Ok, I'll get right on it.");
        } else if($option = 2) {
            ~chatplayer("<p,happy>I want nothing. I was just making sure you were them.");
            ~chatnpc("<p,neutral>Well then get lost and stop wasting my time.");
        }
    case 2 :
        ~chatplayer("<p,quiz>How do I get a job with the VTAM corporation?");
        ~chatnpc("<p,neutral>Get a copy of the Varrock Herald.|If we have any positions right now,|they'll be advertised in there.");
    case 3 :
        ~chatplayer("<p,quiz>Why not?");
        ~chatnpc("<p,neutral>Sorry. That's classified information.");
}

[label,straven_mission]
~chatnpc("<p,neutral>How's your little mission going?");
if(inv_total(inv, intelligence_report) > 0) {
    ~chatplayer("<p,happy>I have the intelligence report!");
    ~chatnpc("<p,neutral>Let's see it then.");
    ~mesbox("You hand over the report.|The man reads the report.");
    ~chatnpc("<p,neutral>Yes. Yes, this is very good.");
    ~chatnpc("<p,happy>Ok! You can join the Phoenix Gang!|I am Straven, one of the gang leaders.");
    ~chatplayer("<p,neutral>Nice to meet you.");
    ~chatnpc("<p,neutral>Take this key.");
    %phoenixgang_progress = 9;
    inv_del(inv, intelligence_report, 1);
    inv_add(inv, phoenixkey2, 1);
    ~objbox(phoenixkey2, "Straven hands you a key.", 250, 0, divide(^objbox_height, 2));
    ~chatnpc("<p,neutral>This key will give you access to our weapons supply depot round the front of this building.");
    return;
}
~chatplayer("<p,sad>I haven't managed to find the report yet...");
~chatnpc("<p,neutral>You need to kill Jonny the Beard,|who should be in the Blue Moon Inn.");

[label,straven_gangmember]
if (%hero_progress >= ^hero_phoenix_obtained_armband & ~obj_gettotal(master_thief_armband) < 1) {
    @straven_gangmember_dialogues(1);
} 
else if (%hero_progress < ^hero_started | %hero_progress >= ^hero_phoenix_obtained_armband) {
    ~chatnpc("<p,neutral>Greetings fellow gang member.");
    if(inv_total(inv, phoenixkey2) = 0 & ~obj_gettotal(phoenixkey2) < 1) {
        @straven_gangmember_dialogues(2);
    } else {
        @straven_gangmember_dialogues(~p_choice3("I've heard you've got some cool treasures in this place...", 3, "Any suggestions for where I can go thieving?", 4, "Where's the Black Arm Gang hideout?", 5));
    }
}
else if (%hero_progress > ^hero_started & inv_total(inv, petecandlestick) >= 1) {
    ~chatplayer("<p,happy>I have retrieved a candlestick!");
    if (%hero_progress = ^hero_phoenix_killed_grip) {
        @straven_gangmember_dialogues(6);
    } else {
        @straven_gangmember_dialogues(7);
    }
}
else if (%hero_progress = ^hero_started) {
    @straven_gangmember_dialogues(8);
}
@straven_gangmember_dialogues(9);

[label,straven_gangmember_dialogues](int $option)
switch_int($option) {
    case 1 : // Lost armband
        ~chatplayer("<p,sad>I'm afraid I've lost my master thief armband.");
        ~chatnpc("<p,neutral>Lucky for you I have a spare. Don't lose it again!");
        inv_add(inv, master_thief_armband, 1);
    case 2 : // Lost weapon store key
        ~chatplayer("<p,sad>I'm afraid I've lost the key you gave me...");
        ~chatnpc("<p,neutral>You really need to be more careful.|We don't want that key falling into the wrong hands.|Ah well...|Have this spare. Don't lose THIS one.");
        inv_add(inv, phoenixkey2, 1);
        ~objbox(phoenixkey2, "Straven hands you a key.", 250, 0, divide(^objbox_height, 2));
    case 3 : // After arrav, or after heroes complete option 1
        ~chatplayer("<p,neutral>I've heard you've got some cool treasures in this place...");
        ~chatnpc("<p,happy>Oh yeah, we've all stolen some stuff in our time.|Those candlesticks down here, for example,|were quite a challenge to get out of the palace.");
        ~chatplayer("<p,quiz>And the shield of Arrav? I heard you got that!");
        ~chatnpc("<p,shock>Woah... that's a blast from the past!|We stole that years and years ago!|We don't even have all the shield anymore.");
        ~chatnpc("<p,neutral>About five years ago|we had a massive fight in our gang|and the shield got broken in half during that fight.");
        ~chatnpc("<p,neutral>Shortly after the fight|some gang members decided|they didn't want to be part of our gang anymore.|So they split off to form their own gang.");
        ~chatnpc("<p,angry>The Black Arm Gang.");
        ~chatnpc("<p,neutral>On their way out|they looted what treasures they could from us - |which included one of the halves of the shield.|We've been rivals with the Black Arms ever since.");
    case 4 : // After arrav, or after heroes complete option 2
        ~chatplayer("<p,quiz>Any suggestions for where I can go thieving?");
        ~chatnpc("<p,neutral>You can always try the marketplace in Ardougne.|LOTS of opportunity there!");
    case 5 : // After arrav, or after heroes complete option 3
        ~chatplayer("<p,happy>Where's the Black Arm Gang hideout?|I wanna go sabotage 'em!");
        ~chatnpc("<p,neutral>That would be a little tricky;|their security is pretty good.");
        ~chatnpc("<p,neutral>Not as good as ours, obviously.");
        ~chatnpc("<p,neutral>But still good.");
        ~chatnpc("<p,neutral>If you really want to go there,|it's in the alleyway|to the west as you come in the south gate.");
        ~chatnpc("<p,neutral>One of our operatives is often near the alley,|a red haired tramp.|He may be able to give you some ideas.");
        ~chatplayer("<p,happy>Thanks for the help!");
    case 6 : // After heroes started, brought back valid candlestick
        ~chatnpc("<p,neutral>Hmmm. Not bad, not bad. Let's see it, make sure it's|genuine.");
        ~mesbox("You hand Straven the candlestick.");
        ~chatplayer("<p,quiz>So is this enough to get me a master thief armband?");
        ~chatnpc("<p,neutral>Hmm...");
        ~chatnpc("<p,neutral>I dunno...");
        ~chatnpc("<p,neutral>Aww, go on then. I suppose I'm in a generous mood|today.");
        ~mesbox("Straven hands you a master thief armband."); // imgur.com/EXYKfbv
        inv_del(inv, petecandlestick, 1);
        inv_add(inv, master_thief_armband, 1);
        %hero_progress = ^hero_phoenix_obtained_armband;
    case 7 : // After heroes started, brought back invalid candlestick
        ~chatnpc("<p,neutral>Well, in all honesty, you didn't actually retrieve it yourself, did you? You don't qualify as a master thief unless you actually steal it yourself, you know...");
    case 8 : // First chat after heroes started
        ~chatplayer("<p,quiz>How would I go about getting a master thief armband?");
        ~chatnpc("<p,neutral>Ooh... tricky stuff. Took me YEARS to get that rank.");
        ~chatnpc("<p,neutral>Well, what some of the more aspiring thieves in|our gang are working on right now is to|steal some very valuable rare candlesticks From|Scarface Pete - the pirate leader on Karamja."); // imgur.com/wpqzghp
        ~chatnpc("<p,neutral>His security is excellent, and the target very valuable so|that might be enough to get you the rank.");
        ~chatnpc("<p,neutral>Go talk to our man Alfonse, the waiter in the Shrimp|and Parrot.");
        ~chatnpc("<p,neutral>Use the secret word 'gherkin' to show you're one of us.");
        %hero_progress = ^hero_phoenix_gangmember_spoken;
    case 9 : // After heroes started, chatting before getting candlestick
        ~chatplayer("<p,quiz>What am I supposed to be doing again?");
        ~chatnpc("<p,neutral>You told me you wanted to get a Master thief's armband! Now pay attention.");
        ~chatnpc("<p,neutral>Some of the more aspiring thieves in our gang are working on right now is to steal some very valuable candlesticks from Scarface Pete - the pirate leader on Karamja.");
        ~chatnpc("<p,neutral>His security is excellent, and the target very valuable so|that might be enough to get you the rank.");
        ~chatnpc("<p,neutral>Go talk to our man Alfonse, the waiter in the Shrimp|and Parrot.");
        ~chatnpc("<p,neutral>Use the secret word 'gherkin' to show you're one of us.");
}

[label,straven_blackarmdog]
~chatnpc("<p,neutral>You stay away from here!|Black Arm dog!");
