[opnpc1,aubury]
if(%runemysteries_progress = ^runemysteries_given_package) {
    ~chatnpc("<p,neutral>My gratitude to you adventurer for bringing me these research notes. I notice that you brought the head wizard a special talisman that was the key to our finally unlocking the puzzle.");
    ~chatnpc("<p,neutral>Combined with the information I had already already collated regarding the Rune Essence, I think we have finally unlocked the power to");
    ~chatnpc("<p,neutral>...no. I am getting ahead of myself. Please take this summary of my research back to the head wizard at the Wizards' Tower. I trust his judgement on whether to let you in on our little secret or not.");
    %runemysteries_progress = ^runemysteries_received_notes;
    inv_add(inv, research_notes, 1);
    ~mesbox("Aubury gives you his research notes.");
} else if(%runemysteries_progress = ^runemysteries_received_notes) {
    ~chatnpc("<p,neutral>I suggest you take those research notes of mine back to the head wizard at the Wizards' Tower.");
    if(~obj_gettotal(research_notes) = 0) {
        ~chatplayer("<p,neutral>I can't... I lost them...");
        ~chatnpc("<p,neutral>Well, luckily I have duplicates. It's a good thing they are written in code, I would not want the wrong kind of person to get access to the information contained within.");
        inv_add(inv, research_notes, 1);
    } else {
       ~chatplayer("<p,neutral>Ok then, I will do that.");
       ~chatnpc("<p,neutral>Unless you were talking to me because you wished to buy some runes?");
       @multi2("Yes please!", aubury_shop, "Oh, it's a rune shop. No thank you, then.", aubury_no);
    }
    return;
} else {
    ~chatnpc("<p,happy>Do you want to buy some runes?");
    if(%runemysteries_progress = ^runemysteries_complete) {
        @multi3("Yes please!", aubury_shop, "Oh, it's a rune shop. No thank you, then.", aubury_no, "Can you teleport me to the Rune Essence?", aubury_ess);
    } else if (%runemysteries_progress = ^runemysteries_received_package & inv_total(inv, research_package) > 0) {
        @multi3("Yes please!", aubury_shop, "Oh, it's a rune shop. No thank you, then.", aubury_no, "I have been sent here with a package for you.", aubury_package);
    } else {
        @multi2("Yes please!", aubury_shop, "Oh, it's a rune shop. No thank you, then.", aubury_no);
    }
}

[opnpcu,aubury]
if(last_useitem = research_package & %runemysteries_progress = ^runemysteries_received_package) @aubury_package;
~displaymessage(^dm_default);

[label,aubury_shop] ~openshop_activenpc;

[label,aubury_no]
~chatplayer("<p,happy>Oh, it's a rune shop. No thank you, then.");
~chatnpc("<p,happy>Well, if you find someone who does want runes, please send them my way.");

[label,aubury_ess]
~chatplayer("<p,quiz>Can you teleport me to the Rune Essence?");
@teleport_to_essence_mine(^essence_mine_to_aubury);

[label,aubury_package]
~chatplayer("<p,neutral>I have been sent here with a package for you. It's from the head wizard at the Wizards' Tower.");
~chatnpc("<p,shock>Really? But... surely he can't have..? Please, let me have it, it must be extremely important for him to have sent a stranger.");
if(inv_total(inv, research_package) = 0) {
    ~chatplayer("<p,confused>Uh... yeah... about that... I kind of don't have it with me...");
    ~chatnpc("<p,confused>What kind of person tells me they have a delivery for me, but not with them? Honestly.");
    ~chatnpc("<p,neutral>Come back when you do.");
    return;
}
~mesbox("You hand Aubury the research package.");
inv_del(inv, research_package, 1);
%runemysteries_progress = ^runemysteries_given_package;
~chatnpc("<p,neutral>This... is incredible. Please, give me a few moments to quickly look over this, and then talk to me again.");
