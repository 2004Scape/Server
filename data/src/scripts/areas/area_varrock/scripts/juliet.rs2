[opnpc1,juliet]
switch_int (%romeojuliet_progress) {
    case ^romeojuliet_not_started : @juliet_start;
    case ^romeojuliet_spoken_romeo : @juliet_from_romeo;
    case ^romeojuliet_spoken_juliet : @juliet_deliver_letter;
    case ^romeojuliet_passed_message : @juliet_message_passed;
    case ^romeojuliet_spoken_father : @juliet_found_father;
    case ^romeojuliet_spoken_apothecary : @juliet_potion_made;
    case ^romeojuliet_juliet_crypt :
        ~chatnpc("<p,quiz>Have you seen Romeo?|He will reward you for your help.");
        ~chatnpc("<p,happy>He is the wealth in this story.|I am just the glamour.");
    case ^romeojuliet_complete : @juliet_postquest;
}

[label,juliet_start]
~chatnpc("<p,sad>Romeo, Romeo, wherefore art thou Romeo?");
~chatnpc("<p,sad>Bold adventurer,|have you seen Romeo on your travels?");
~chatnpc("<p,sad>Skinny guy, a bit wishy washy, head full of poetry.");
def_int $option = ~p_choice4("Yes I have met him.", 1, "No, I think I would have remembered if I had.", 2, "I guess I could find him.", 3, "I think you could do better.", 4);
if($option = 1) {
    ~chatplayer("<p,neutral>I did see Romeo somewhere.|He seemed a bit depressed.");
    ~chatnpc("<p,quiz>Yes, that would be him.|Could you please deliver a message to him?");
    $option = ~p_choice2("Certainly, I will do so straight away!", 1, "No, he was a little too weird for me.", 2);
    if($option = 1) {
        @juliet_agree_message;
    } else if($option = 2) {
        ~chatplayer("<p,neutral>No.");
        ~chatnpc("<p,sad>Oh dear, that will be the ruin of our love.");
        ~chatnpc("<p,sad>Well, I will just stay here and worry.");
        ~chatnpc("<p,sad>You unromantic soul.");
    }
} else if($option = 2) {
    ~chatplayer("<p,neutral>No, I think I would have remembered.");
    ~chatnpc("<p,quiz>Could you please deliver a message to him?");
    $option =  ~p_choice2("Certainly, I will do so straight away!", 1, "No, I have better things to do.", 2);
    if($option = 1) {
        @juliet_agree_message;
    } else if($option = 2) {
        ~chatplayer("<p,neutral>No, I have better things to do.");
        ~chatnpc("<p,sad>I will not keep you from them. Goodbye.");
    }
} else if($option = 3) {
    ~chatplayer("<p,happy>I guess I could find him.");
    ~chatnpc("<p,happy>That is most kind of you!");
    ~chatnpc("<p,happy>Could you please deliver a message to him?");
    @juliet_agree_message;
} else if($option = 4) {
    ~chatplayer("<p,neutral>I think you could do better.");
    ~chatnpc("<p,sad>He has his good points.");
    ~chatnpc("<p,angry>He doesn't spend all day on the internet, at least.");
}

[label,juliet_from_romeo]
~chatplayer("<p,neutral>Juliet, I come from Romeo.");
~chatplayer("<p,neutral>He begs I tell you he cares still.");
~chatnpc("<p,sad>Please, take this message to him.");
@juliet_agree_message;

[label,juliet_agree_message]
~chatplayer("<p,happy>Certainly, I will deliver your message straight away.");
~chatnpc("<p,sad>It may be our only hope.");
inv_add(inv, julietmessage, 1);
%romeojuliet_progress = ^romeojuliet_spoken_juliet;
~send_quest_progress(questlist:romeojuliet, %romeojuliet_progress, ^romeojuliet_complete);
~mesbox("Juliet gives you a message.");

[label,juliet_deliver_letter]
if(inv_total(inv, julietmessage) > 0 | inv_total(bank, julietmessage) > 0) {
    ~chatnpc("<p,sad>Please, deliver the message to Romeo with all speed!");
} else {
    ~chatnpc("<p,angry>How could you lose this most important message?");
    ~chatnpc("<p,sad>Please, take this message to him, and please don't lose it.");
    ~mesbox("Juliet gives you another message.");
    inv_add(inv, julietmessage, 1);
}

[label,juliet_message_passed]
~chatplayer("<p,happy>I have passed on your message.");
~chatplayer("<p,happy>Now I go to Father Lawrence for help.");
~chatnpc("<p,neutral>Yes, he knows many things that can be done.");
~chatnpc("<p,happy>I hope you find him soon!");

[label,juliet_found_father]
~chatplayer("<p,happy>I found the Father. Now I seek the Apothecary.");
~chatnpc("<p,neutral>I do not know where he lives.");
~chatnpc("<p,sad>But please, make haste. My father is close.");

[label,juliet_potion_made]
if(inv_total(inv, cadava) = 0) {
    ~chatplayer("<p,happy>I have to get a potion made for you.");
    ~chatplayer("<p,neutral>Not done that bit yet though. Still trying.");
    ~chatnpc("<p,neutral>Fair luck to you, the end is close.");
    return;
}
~chatplayer("<p,happy>I have a Cadaver potion from Father Lawrence.");
~chatplayer("<p,neutral>It should make you seem dead,|and get you away from this place.");
~mesbox("You pass the potion to Juliet.");
inv_del(inv, cadava, 1);
%romeojuliet_progress = ^romeojuliet_juliet_crypt;
~chatnpc("<p,happy>Wonderful. I just hope Romeo can|remember to get me from the crypt.");
~chatnpc("<p,happy>Many thanks kind friend.");
~chatnpc("<p,neutral>Please go to Romeo, make sure he understands.");
~chatnpc("<p,sad>He can be a bit dense sometimes.");

[label,juliet_postquest]
~chatnpc("<p,angry>I sat in that cold crypt for ages waiting for Romeo.");
~chatnpc("<p,angry>That useless fool never showed up.");
~chatnpc("<p,angry>And all I got was indigestion.|I am done with men like him.");
~chatnpc("<p,angry>Now go away before I call my father!");