[ai_timer,_freshfish] @move_fishing_spot;
[ai_timer,_rarefish] @move_fishing_spot;
[ai_timer,_memberfish] @move_fishing_spot;
[ai_timer,_saltfish] @move_fishing_spot;
[ai_timer,npc_800] @move_fishing_spot; // lava eel

[label,move_fishing_spot]
if (npc_param(is_whirlpool) = ^true) {
    npc_delay(2);
    npc_del;
    return;
}
def_coord $rand_coord = ~fishing_spot_random_coord(npc_type);
npc_delay(2);
npc_tele($rand_coord);
npc_settimer(calc(280 + random(250)));

[proc,fishing_spot_random_coord](npc $npc)(coord)
def_int $output_count = enum_getoutputcount(nc_param($npc, fishing_movement_enum));
def_npc_uid $original_fishing_spot = npc_uid;
def_coord $rand_coord;
def_int $i;
while ($i < 50) {
    $rand_coord = enum(int, coord, nc_param($npc, fishing_movement_enum), random($output_count));
    //npc_say("<tostring($int)>, <~coord_tostring($rand_coord)>");
    if (~check_fishing_spot_empty($rand_coord) = true) {
        npc_finduid($original_fishing_spot);
        return ($rand_coord);
    }
    $i = calc($i + 1);
}
npc_finduid($original_fishing_spot);
return (npc_coord);

//proc checks if fishing spot is empty
[proc,check_fishing_spot_empty](coord $rand_coord)(boolean)
npc_findallzone($rand_coord);
while (npc_findnext = true) {
    if (npc_coord = $rand_coord & (nc_category(npc_type) = saltfish | nc_category(npc_type) = freshfish | nc_category(npc_type) = saltfish | nc_category(npc_type) = rarefish | nc_category(npc_type) = memberfish | npc_type = npc_800)) {
        return (false);
    }
}
return (true);