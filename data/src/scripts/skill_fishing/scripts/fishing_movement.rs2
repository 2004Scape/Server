[ai_timer,_freshfish] @move_fishing_spot;
[ai_timer,_rarefish] @move_fishing_spot;
[ai_timer,_memberfish] @move_fishing_spot;
[ai_timer,_saltfish] @move_fishing_spot;
[ai_timer,0_45_152_lava_eel] @move_fishing_spot; // lava eel

[label,move_fishing_spot]
def_coord $rand_coord = ~fishing_spot_random_coord(npc_type);
npc_delay(2);
npc_tele($rand_coord);
npc_settimer(calc(280 + random(250)));

[proc,fishing_spot_random_coord](npc $npc)(coord)
if (nc_param($npc, fishing_movement_enum) = null) {
    return (npc_coord);
}
def_int $output_count = enum_getoutputcount(nc_param($npc, fishing_movement_enum));
def_coord $rand_coord;
def_int $i;
while ($i < 50) {
    $rand_coord = enum(int, coord, nc_param($npc, fishing_movement_enum), random($output_count));
    if (~check_fishing_spot_empty($rand_coord) = true) {
        return ($rand_coord);
    }
    $i = calc($i + 1);
}
return(npc_coord);

//proc checks if fishing spot is empty
[proc,check_fishing_spot_empty](coord $rand_coord)(boolean)
npc_findallany($rand_coord, 0, 0);
while (.npc_findnext = true) {
    if (.npc_coord = $rand_coord & (nc_category(.npc_type) = saltfish | nc_category(.npc_type) = freshfish | nc_category(.npc_type) = saltfish | nc_category(.npc_type) = rarefish | nc_category(.npc_type) = memberfish | .npc_type = 0_45_152_lava_eel)) {
        return (false);
    }
}
return (true);