[opnpc1,_lure_bait_fishing] @attempt_fish_trout_salmon;
[opnpc3,_lure_bait_fishing] @attempt_fish_pike;

[opnpcu,_lure_bait_fishing]
~check_correct_fish_equipment(fishing_rod, fly_fishing_rod);
switch_obj(last_item) {
    case fishing_rod : @attempt_fish_pike;
    case fly_fishing_rod : @attempt_fish_trout_salmon;
    case default : @nothing_interesting_happens;
}

[label,attempt_fish_pike]
// check level
if (stat(fishing) < 25) {
    ~mesbox("You need at least 25 Fishing to bait these fish.");
}
// check if they have fishing equipment & bait
~check_fish_equipment(fishing_rod);

if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + 5);
    %skill_anim = calc(map_clock + 1);
    mes("<tostring(map_clock)>: You cast out your line...");
    mes("<tostring(map_clock)>: You attempt to catch a fish.");
    p_opnpc(3);
} else {
    @fish_pike;
}

[label,fish_pike]
// check if inv is full
if (inv_freespace(inv) = 0) {
    ~mesbox("You can't carry any more fish.");
}
if (%skill_clock = map_clock) {
    ~fish_roll(raw_pike, null, fishing_bait);
    if (randominc(5) = 5) { // 1/5 chance to recast
        if (%skill_anim = map_clock) {
            anim(seq_622, 0);
        }
    } else {
        %skill_clock = calc(map_clock + 5);
        anim(seq_623, 0);
    }
    p_opnpc(3);
}
if (%skill_anim = map_clock) {
    anim(seq_622, 0);
}
p_opnpc(3);


// trout and salmon
[label,attempt_fish_trout_salmon]
// check level
if (stat(fishing) < 20) {
    ~mesbox("You need at least 20 Fishing to lure these fish.");
}
// check if they have fishing equipment & bait
~check_fish_equipment(fly_fishing_rod);

if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + 5);
    %skill_anim = calc(map_clock + 1);
    mes("<tostring(map_clock)>: You cast out your line...");
    mes("<tostring(map_clock)>: You attempt to catch a fish.");
    p_opnpc(1);
} else {
    @fish_trout_salmon;
}

[label,fish_trout_salmon]
// check if inv is full
if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
}
if (%skill_clock = map_clock) {
    if (stat(fishing) >= 30) {
        ~fish_roll(raw_trout, raw_salmon, feather);
    } else {
        ~fish_roll(raw_trout, null, feather);
    }
    %skill_clock = calc(map_clock + 5);
    anim(seq_623, 0);
    p_opnpc(1);
}
if (%skill_anim = map_clock) {
    anim(null, 0);
    anim(seq_622, 0);
}
p_opnpc(1);