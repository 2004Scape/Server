[opnpc1,_net_bait_fishing] 
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@attempt_fish_shrimp;
[opnpc3,_net_bait_fishing]
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@attempt_fish_sardine;

// hidden ops
[opnpc2,_net_bait_fishing] 
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@fish_shrimp;
[opnpc4,_net_bait_fishing]
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@fish_sardine;

[opnpcu,_net_bait_fishing]
if (last_item = fly_fishing_rod) {
    @nothing_interesting_happens;
}
if (last_item = net_big) {
    ~mesbox("The fish here aren't suitable for catching with a Big Net.");
    return;
}
~check_correct_fish_equipment(net, fishing_rod);
switch_obj(last_item) {
    case fishing_rod : @attempt_fish_sardine;
    case net : @attempt_fish_shrimp;
    case default : @nothing_interesting_happens;
}


[label,attempt_fish_shrimp]
// check if they have fishing equipment
~check_fish_equipment(net);
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
    p_opnpc(1);
} 
if (%action_delay = calc(map_clock + 3)) {
    mes("You cast out your net...");
    anim(human_smallnet, 0);
    sound_synth(fishing_cast, 0, 15);
    %skill_anim = calc(map_clock + 5);
    p_opnpc(2);
}
if (%action_delay = map_clock) { 
    if (stat(fishing) >= 15) {
        ~fish_roll(raw_anchovies, raw_shrimps, null);
    } else {
        ~fish_roll(raw_shrimps, null, null);
    }
    anim(human_smallnet, 0);
    sound_synth(fishing_cast, 0, 15);
}


[label,fish_shrimp]
// check if inv is full again
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
} else
if (%action_delay = map_clock) {
    if (stat(fishing) >= 15) {
        ~fish_roll(raw_anchovies, raw_shrimps, null);
    } else {
        ~fish_roll(raw_shrimps, null, null);
    }
}
// play anim every 6 ticks, and sound every 3 ticks
if (%skill_anim < map_clock) {
    %skill_anim = calc(map_clock + 5);
    anim(human_smallnet, 0);
    sound_synth(fishing_cast, 0, 15);
} else
if (%skill_anim = calc(map_clock + 2)) {
    sound_synth(fishing_cast, 0, 15);
}
p_opnpc(2);


[label,attempt_fish_sardine]
// check level
if (stat(fishing) < 5) {
    anim(null, 0);
    ~mesbox("You need at least 5 Fishing to catch these fish.");
    return;
}
// check if they have fishing equipment
~check_fish_equipment(fishing_rod);
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
    p_opnpc(3);
} 
if (%action_delay = calc(map_clock + 4)) {
    mes("You cast out your line...");
    anim(human_fishing_casting, 0);
    p_opnpc(3);
}
if (%action_delay = calc(map_clock + 3)) {
    mes("You attempt to catch a fish.");
    p_opnpc(4);
}
if (%action_delay = map_clock) {
    if (stat(fishing) >= 10) {
        ~fish_roll(raw_sardine, raw_herring, fishing_bait);
    } else {
        ~fish_roll(raw_sardine, null, fishing_bait);
    }
}

[label,fish_sardine]
// check level
if (stat(fishing) < 5) {
    anim(null, 0);
    ~mesbox("You need at least 5 Fishing to catch these fish.");
    return;
}
// check if they still have fishing equipment & bait
~check_fish_equipment(fishing_rod);
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
    sound_synth(fishing_cast, 0, 0);
    anim(human_fish_onspot, 0);
}
if (%action_delay = map_clock) {
    if (stat(fishing) >= 10) {
        ~fish_roll(raw_sardine, raw_herring, fishing_bait);
    } else {
        ~fish_roll(raw_sardine, null, fishing_bait);
    }
}
p_opnpc(4);
