[opnpc1,npc_800] @attempt_fish_lava_eel;
[opnpc2,npc_800] @fish_lava_eel;
[opnpcu,npc_800] @oil_rod_fishing_use_item;

[apnpc1,npc_800]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@attempt_fish_lava_eel;

[apnpc2,npc_800]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@fish_lava_eel;

[apnpcu,npc_800]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@oil_rod_fishing_use_item;

[label,oil_rod_fishing_use_item]
switch_obj (last_item) {
    case obj_1585: @attempt_fish_lava_eel;
    case net: @burnup_fishing_equipment(net, "net");
    case net_big: @burnup_fishing_equipment(net_big, "net");
    case harpoon: @burnup_fishing_equipment(harpoon, "harpoon");
    case fishing_rod: @burnup_fishing_equipment(fishing_rod, "rod");
    case fly_fishing_rod: @burnup_fishing_equipment(fly_fishing_rod, "rod");
    case default: @nothing_interesting_happens;
}

[label,burnup_fishing_equipment](namedobj $equipment, string $name)
sound_synth(fire_lit, 0, 0);
inv_del(inv, $equipment, 1);
~objbox($equipment, "The <$name> burns up in the lava!");

[label,attempt_fish_lava_eel]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + 5);
    p_opnpc(1);
} else if (%skill_clock = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    sound_synth(fishing_cast, 0, 39);
    mes("You cast out your line...");
    p_opnpc(1);
} else if (%skill_clock = calc(map_clock + 3)) {
    mes("You attempt to catch an eel.");
    p_opnpc(2);
} else {
    p_opnpc(1);
}
if (%skill_clock = map_clock) {
    ~fish_roll(raw_lava_eel, null, fishing_bait);
}

[label,fish_lava_eel]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%skill_clock < map_clock) {
    %skill_clock = calc(map_clock + 4);
    anim(human_fish_onspot, 0);
}
if (%skill_clock = map_clock) {
    ~fish_roll(raw_lava_eel, null, fishing_bait);
}
p_opnpc(2);

[proc,oil_rod_fishing_check_requirements]()(boolean)
// check level
if (stat(fishing) < 53) {
    anim(null, 0);
    ~mesbox("You need to be at least level 53 Fishing to bait these eels.");
    return(false);
}
// check if they have fishing equipment
if (inv_total(inv, obj_1585) < 1) {
    anim(null, 0);
    ~mesbox("You need a flame-proof fishing rod to bait these fish.");
    return(false);
}
if (inv_total(inv, fishing_bait) < 1) {
    anim(null, 0);
    ~mesbox("You don't have any fishing bait left.");
    return(false);
}
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more eels.");
    return(false);
}
return(true);
