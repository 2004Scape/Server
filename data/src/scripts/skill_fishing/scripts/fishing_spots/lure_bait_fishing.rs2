[opnpc1,_freshfish] 
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@attempt_fish_trout_salmon;
[opnpc3,_freshfish] 
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@attempt_fish_pike;

// hidden options
[opnpc2,_freshfish]
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@fish_trout_salmon;
[opnpc4,_freshfish] 
//mes("<tostring(map_clock)>, <tostring(%action_delay)>");
@fish_pike;


[opnpcu,_freshfish]
switch_obj(last_useitem) {
    case fishing_rod : p_opnpc(3);
    case fly_fishing_rod : p_opnpc(1);
    case default : @fishing_wrong_spot_message(last_useitem);
}

// trout and salmon
[label,attempt_fish_trout_salmon]
// check level
if (stat(fishing) < 20) {
    anim(null, 0);
    ~mesbox("You need at least 20 Fishing to lure these fish.");
    return;
}
// check if they have fishing equipment & bait
if (~check_fish_equipment(fly_fishing_rod) = false) {
    return;
}
%skill_anim = calc(%skill_anim + 1);
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%skill_anim < map_clock) {
    //mes("<tostring(map_clock)>, <tostring(%action_delay)>, <tostring(%skill_anim)>: Start ANIM"); // debug
    %skill_anim = calc(map_clock + 5);
}
// separation of messages is correct: https://youtu.be/neAoU0yiGFY?t=16
mes("You cast out your line...");
p_opnpc(2);
// random events didnt prevent you from continuing to fish: https://youtu.be/M_AOjwLD9Og?list=PLn23LiLYLb1am4weMQzkWn5m9b3m9fXv9&t=199
if (afk_event = ^true & npc_param(is_whirlpool) = ^false) {
    @macro_event_fishing(fly_fishing_rod);
}


[label,fish_trout_salmon]
// check level
if (stat(fishing) < 20) {
    anim(null, 0);
    ~mesbox("You need at least 20 Fishing to lure these fish.");
    return;
}
// check if they still have fishing equipment & bait
if (~check_fish_equipment(fly_fishing_rod) = false) {
    return;
}
// check if inv is full
if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%skill_anim = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    sound_synth(fishing_cast, 0, 39);
    mes("You attempt to catch a fish.");
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%action_delay = map_clock) {
    if (stat(fishing) >= 30) {
        ~fish_roll(raw_trout, raw_salmon, fly_fishing_rod, feather);
    } else {
        ~fish_roll(raw_trout, null, fly_fishing_rod, feather);
    }
    %skill_anim = calc(map_clock - 1);
    anim(human_fish_onspot, 0);
    p_opnpc(2);
}
p_opnpc(2);


// pike
[label,attempt_fish_pike]
// check level
if (stat(fishing) < 25) {
    anim(null, 0);
    ~mesbox("You need at least 25 Fishing to bait these fish.");
    return;
}
// check if they have fishing equipment & bait
if (~check_fish_equipment(fishing_rod) = false) {
    return;
}
%skill_anim = calc(%skill_anim + 1);
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
}
if (%skill_anim < map_clock) {
    %skill_anim = calc(map_clock + 5);
}
mes("You cast out your line...");
p_opnpc(4);
if (afk_event = ^true & npc_param(is_whirlpool) = ^false) {
    @macro_event_fishing(fishing_rod);
}


[label,fish_pike]
// check level
if (stat(fishing) < 25) {
    anim(null, 0);
    ~mesbox("You need at least 25 Fishing to bait these fish.");
    return;
}
// check if they still have fishing equipment & bait
if (~check_fish_equipment(fishing_rod) = false) {
    return;
}
// check if inv is full
if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%skill_anim = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    sound_synth(fishing_cast, 0, 39);
    mes("You attempt to catch a fish.");
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%action_delay = map_clock) {
    %skill_anim = calc(map_clock - 1);
    ~fish_roll(raw_pike, null, fishing_rod, fishing_bait);
    if (randominc(5) = 5) { // 1/5 chance to recast
        //mes("<tostring(map_clock)>: Recast.");
        p_opnpc(3);
    } else {
        //mes("<tostring(map_clock)>: Dont recast.");
        %action_delay = calc(map_clock + 5);
        anim(human_fish_onspot, 0);
        p_opnpc(4);
    }
} else {
    p_opnpc(4);
}