[oploc1,loc_2630] @attempt_fish_lava_eel_loc;
[oploc3,loc_2630] @fish_lava_eel_loc;
[oplocu,loc_2630] @oil_rod_fishing_use_item_loc;

[aploc1,loc_2630]
if (distance(loc_coord, coord) > 1) {
    p_aprange(1);
    return;
}
@attempt_fish_lava_eel_loc;

[aploc3,loc_2630]
if (distance(loc_coord, coord) > 1) {
    p_aprange(1);
    return;
}
@fish_lava_eel_loc;

[aplocu,loc_2630]
if (distance(loc_coord, coord) > 1) {
    p_aprange(1);
    return;
}
@oil_rod_fishing_use_item_loc;

[label,oil_rod_fishing_use_item_loc]
switch_obj (last_useitem) {
    case oily_fishing_rod : @attempt_fish_lava_eel_loc;
    case net : @burnup_fishing_equipment(net, "net");
    case big_net : @burnup_fishing_equipment(big_net, "net");
    case harpoon : @burnup_fishing_equipment(harpoon, "harpoon");
    case fishing_rod : @burnup_fishing_equipment(fishing_rod, "rod");
    case fly_fishing_rod : @burnup_fishing_equipment(fly_fishing_rod, "rod");
    case default : ~displaymessage(^dm_default);
}

[label,attempt_fish_lava_eel_loc]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
    p_oploc(1);
    return;
} else if (%action_delay = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    mes("You cast out your line...");
    p_oploc(1);
    return;
} else if (%action_delay = calc(map_clock + 3)) {
    mes("You attempt to catch an eel.");
    p_oploc(3);
} else {
    p_oploc(1);
}
if (%action_delay = map_clock) {
    ~fish_roll_loc(raw_lava_eel, null, fishing_bait);
}

[label,fish_lava_eel_loc]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
    anim(human_fish_onspot, 0);
} else if (%action_delay = map_clock) {
    ~fish_roll_loc(raw_lava_eel, null, fishing_bait);
}
p_oploc(3);