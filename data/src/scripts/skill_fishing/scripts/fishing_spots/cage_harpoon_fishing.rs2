[opnpc1,_cage_harpoon_fishing]
@attempt_fish_lobster;
[opnpc3,_cage_harpoon_fishing]
@attempt_fish_swordfish;

// hidden ops
[opnpc2,_cage_harpoon_fishing]
@fish_lobster;
[opnpc4,_cage_harpoon_fishing]
@fish_swordfish;

[opnpcu,_cage_harpoon_fishing]
if (last_useitem = net_big) {
    anim(null, 0);
    ~mesbox("The fish here aren't suitable for catching with a big net.");
    return;
}
~check_correct_fish_equipment(lobster_pot, harpoon);
switch_obj(last_useitem) {
    case harpoon : @attempt_fish_swordfish;
    case lobster_pot : @attempt_fish_lobster;
    case default : @nothing_interesting_happens;
}


[label,attempt_fish_lobster]
// check level
if (stat(fishing) < 40) {
    anim(null, 0);
    ~mesbox("You need at least 40 Fishing to catch Lobsters.");
    return;
}
// check if they have fishing equipment
~check_fish_equipment(lobster_pot);
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more lobsters.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
    p_opnpc(1);
    return;
}  else
if (%action_delay = calc(map_clock + 3)) {
    %skill_anim = calc(map_clock + 8);
    %skill_sound = calc(map_clock + 2);
    anim(human_lobster, 0);
    sound_synth(fishing_cast, 0, 20);
    // https://youtu.be/WGq6FZpnH7Y?t=70
    mes("You attempt to catch a Lobster.");
    p_opnpc(2);
}
if (%action_delay = map_clock) { 
    ~fish_roll(raw_lobster, null, null);
    anim(human_lobster, 0);
    sound_synth(fishing_cast, 0, 20);
}
if (1 = ^true & %macro_event = ^no_macro_event) {
    @macro_event_fishing(lobster_pot);
}


[label,fish_lobster]
// check level
if (stat(fishing) < 40) {
    anim(null, 0);
    ~mesbox("You need at least 40 Fishing to catch Lobsters.");
    return;
}
// check if they have fishing equipment
~check_fish_equipment(lobster_pot);
// check if inv is full again
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more lobsters.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
} else
if (%action_delay = map_clock) {
    ~fish_roll(raw_lobster, null, null);
}
if (%skill_anim < map_clock) {
    %skill_anim = calc(map_clock + 8);
    anim(human_lobster, 0);
}
// play skill sound every ~2.5 ticks
if (%skill_sound < map_clock) {
    %skill_sound = calc(map_clock + 2);
    sound_synth(fishing_cast, 0, 20);
}
p_opnpc(2);

//--- Tuna and swordfish
[label,attempt_fish_swordfish]
// check level for tuna
if (stat(fishing) < 35) {
    anim(null, 0);
    ~mesbox("You need at least 35 Fishing to harpoon these fish.");
    return;
}
// check if they have fishing equipment
~check_fish_equipment(harpoon);
// check if inv is full
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
    p_opnpc(3);
    return;
} else
if (%action_delay = calc(map_clock + 4)) {
    mes("You start Harpooning fish.");
    %skill_anim = calc(map_clock + 7);
    %skill_sound = calc(map_clock + 7);
    anim(human_harpoon, 0);
    p_opnpc(4);
} else
if (%action_delay = map_clock) { 
    if (stat(fishing) >= 50) {
        ~fish_roll(raw_swordfish, raw_tuna, null);
    } else {
        ~fish_roll(raw_tuna, null, null);
    }
    anim(human_harpoon, 0);
    sound_synth(fishing_cast, 0, 10);
}
if (1 = ^true & %macro_event = ^no_macro_event) {
    @macro_event_fishing(harpoon);
}

[label,fish_swordfish]
// check level for tuna
if (stat(fishing) < 35) {
    anim(null, 0);
    ~mesbox("You need at least 35 Fishing to harpoon these fish.");
    return;
}
// check if they have fishing equipment
~check_fish_equipment(harpoon);
// check if inv is full again
if (inv_freespace(inv) < 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
} else
if (%action_delay = map_clock) {
    if (stat(fishing) >= 50) {
        ~fish_roll(raw_swordfish, raw_tuna, null);
    } else {
        ~fish_roll(raw_tuna, null, null);
    }
}
// play animation every 8 ticks
if (%skill_anim < map_clock) {
    %skill_anim = calc(map_clock + 7);
    %skill_sound = calc(map_clock + 7);
    anim(human_harpoon, 0);
}
// play skill sound every 7 and 4 ticks
if (%skill_sound = calc(map_clock + 6)) {
    sound_synth(fishing_cast, 0, 0);
} else
if (%skill_sound = calc(map_clock + 3)) {
    sound_synth(fishing_cast, 0, 10);
}
p_opnpc(4);
