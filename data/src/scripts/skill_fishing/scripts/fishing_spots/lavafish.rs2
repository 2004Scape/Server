// i assume no fishing random events for these guys
[opnpc1,0_45_152_lava_eel] @attempt_fish_lava_eel;
[opnpc3,0_45_152_lava_eel] @fish_lava_eel;
[opnpcu,0_45_152_lava_eel] @oil_rod_fishing_use_item;

[apnpc1,0_45_152_lava_eel]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@attempt_fish_lava_eel;

[apnpc3,0_45_152_lava_eel]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@fish_lava_eel;

[apnpcu,0_45_152_lava_eel]
if (npc_range(coord) > 1)
{
  p_aprange(1);
  return;
}
@oil_rod_fishing_use_item;

[label,oil_rod_fishing_use_item]
switch_obj (last_useitem) {
    case oily_fishing_rod : @attempt_fish_lava_eel;
    case net : @burnup_fishing_equipment(net, "net");
    case big_net : @burnup_fishing_equipment(big_net, "net");
    case harpoon : @burnup_fishing_equipment(harpoon, "harpoon");
    case fishing_rod : @burnup_fishing_equipment(fishing_rod, "rod");
    case fly_fishing_rod : @burnup_fishing_equipment(fly_fishing_rod, "rod");
    case default : ~displaymessage(^dm_default);
}

[label,burnup_fishing_equipment](namedobj $equipment, string $name)
sound_synth(fire_lit, 0, 0);
inv_del(inv, $equipment, 1);
~objbox($equipment, "The <$name> burns up in the lava!", 250, 0, divide(^objbox_height, 2));

[label,attempt_fish_lava_eel]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
    p_opnpc(1);
    return;
} else if (%action_delay = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    mes("You cast out your line...");
    p_opnpc(1);
    return;
} else if (%action_delay = calc(map_clock + 3)) {
    mes("You attempt to catch an eel.");
    p_opnpc(3);
} else {
    p_opnpc(1);
}
if (%action_delay = map_clock) {
    ~fish_roll(raw_lava_eel, null, fishing_rod, fishing_bait);
}


[label,fish_lava_eel]
if (~oil_rod_fishing_check_requirements = false) {
    return;
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
    anim(human_fish_onspot, 0);
} else if (%action_delay = map_clock) {
    ~fish_roll(raw_lava_eel, null, fishing_rod, fishing_bait);
}
p_opnpc(3);

[proc,oil_rod_fishing_check_requirements]()(boolean)
if (map_members = false) {
    anim(null, 0);
    ~mesbox(^mes_members_fish_here);
    return(false);
}
// check level
if (stat(fishing) < 53) {
    anim(null, 0);
    ~mesbox("You need to be at least level 53 Fishing to bait these eels.");
    return(false);
}
// check if they have fishing equipment
if (inv_total(inv, oily_fishing_rod) < 1) {
    anim(null, 0);
    ~mesbox("You need a flame-proof fishing rod to bait these fish.");
    return(false);
}
if (inv_total(inv, fishing_bait) < 1) {
    anim(null, 0);
    ~mesbox("You don't have any fishing bait left.");
    return(false);
}
// check if inv is full
if (inv_freespace(inv) < 1 & inv_total(inv, fishing_bait) > 1) {
    anim(null, 0);
    ~mesbox("You can't carry any more eels.");
    return(false);
}
return(true);
