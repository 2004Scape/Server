[oploc1,loc_2027]
// check level
if (stat(fishing) < 20) {
    anim(null, 0);
    ~mesbox("You need at least 20 Fishing to lure these fish.");
    return;
}
// check if they have fishing equipment & bait
~check_fish_equipment(fly_fishing_rod);
%skill_anim = calc(%skill_anim + 1);
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%skill_anim < map_clock) {
    //mes("<tostring(map_clock)>, <tostring(%action_delay)>, <tostring(%skill_anim)>: Start ANIM"); // debug
    %skill_anim = calc(map_clock + 5);
}
mes("You cast out your line...");
p_oploc(3);


[oploc3,loc_2027]
// check level
if (stat(fishing) < 20) {
    anim(null, 0);
    ~mesbox("You need at least 20 Fishing to lure these fish.");
    return;
}
// check if they still have fishing equipment & bait
~check_fish_equipment(fly_fishing_rod);
// check if inv is full
if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%skill_anim = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    sound_synth(fishing_cast, 0, 39);
    mes("You attempt to catch a fish.");
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%action_delay = map_clock) {
    if (stat(fishing) >= 30) {
        ~fish_roll(raw_trout, raw_salmon, feather);
    } else {
        ~fish_roll(raw_trout, null, feather);
    }
    %skill_anim = calc(map_clock - 1);
    anim(human_fish_onspot, 0);
    p_oploc(3);
}
p_oploc(3);


[oploc2,loc_2027]
// check level
if (stat(fishing) < 25) {
    anim(null, 0);
    ~mesbox("You need at least 25 Fishing to bait these fish.");
    return;
}
// check if they have fishing equipment & bait
~check_fish_equipment(fishing_rod);
%skill_anim = calc(%skill_anim + 1);
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 5);
}
if (%skill_anim < map_clock) {
    %skill_anim = calc(map_clock + 5);
}
mes("You cast out your line...");
p_oploc(4);

[oploc4,loc_2027]
// check level
if (stat(fishing) < 25) {
    anim(null, 0);
    ~mesbox("You need at least 25 Fishing to bait these fish.");
    return;
}
// check if they still have fishing equipment & bait
~check_fish_equipment(fishing_rod);
// check if inv is full
if (inv_freespace(inv) = 0) {
    anim(null, 0);
    ~mesbox("You can't carry any more fish.");
    return;
}
if (%skill_anim = calc(map_clock + 4)) {
    anim(human_fishing_casting, 0);
    sound_synth(fishing_cast, 0, 39);
    mes("You attempt to catch a fish.");
}
if (%action_delay < map_clock) {
    %action_delay = calc(map_clock + 4);
}
if (%action_delay = map_clock) {
    %skill_anim = calc(map_clock - 1);
    ~fish_roll(raw_pike, null, fishing_bait);
    if (randominc(5) = 5) { // 1/5 chance to recast
        //mes("<tostring(map_clock)>: Recast.");
        p_oploc(2);
    } else {
        //mes("<tostring(map_clock)>: Dont recast.");
        %action_delay = calc(map_clock + 5);
        anim(human_fish_onspot, 0);
        p_oploc(4);
    }
} else {
    p_oploc(4);
}

[oplocu,loc_2027]
~check_correct_fish_equipment(fishing_rod, fly_fishing_rod);
switch_obj(last_useitem) {
    case fishing_rod : p_oploc(2);
    case fly_fishing_rod : p_oploc(1);
    case default : @nothing_interesting_happens;
}