[opheldu,dragonshield_a]
if (last_useitem = dragonshield_b) {
    // supposed to be both shield halves in the obj box
    ~objbox(dragonshield_a, "You'll need to repair the shield at an anvil, with a hammer.", 250, 0, divide(^objbox_height, 2));
    return;
}
[opheldu,dragonshield_b]
if (last_useitem = dragonshield_a) {
    // supposed to be both shield halves in the obj box
    ~objbox(dragonshield_a, "You'll need to repair the shield at an anvil, with a hammer.", 250, 0, divide(^objbox_height, 2));
    return;
}


[label,make_dragon_sq]
if (map_members = ^false) {
    mes("You can only make that on a members' server.");
    return;
}
// check if player has both halfs
if (inv_total(inv, dragonshield_a) < 1 | inv_total(inv, dragonshield_b) < 1) {
    ~mesbox("You need to find the other half of this shield in order to repair it.");
    return;
}
// check if high enough level
if (stat(smithing) < 60) {
    ~mesbox("You need a Smithing level of at least 60 to repair this shield.");
    return;
}
// check if player has a hammer
if (inv_total(inv, hammer) < 1) {
    ~mesbox("You'll need a hammer if you want to attempt to repair this shield.");
    return;
}
// display message
~mesbox("You set to work trying to fix the ancient shield. It's seen some heavy action and needs some serious work doing to it.");
if_close; // have to close for some reason
// play anim and sound
anim(human_smithing, 0);
sound_synth(anvil_4, 0, 0);
// 6 tick delay https://youtu.be/YJmY-RN3MuA?t=86
p_delay(6);
~mesbox("Even for an experienced armourer it is not an easy|task, but eventually it is ready.  You have restored|the dragon square shield to it's former glory.");
// https://storage.googleapis.com/tannerdino/images/dragonsquare.png // archive date 2005-03-22
//~mesbox("Even for an experienced armourer it is not an easy task, but eventually it is ready. You have restored the dragon square shield to its former glory.");
// delete shield halfs
inv_del(inv, dragonshield_a, 1);
inv_del(inv, dragonshield_b, 1);
// add dragon sq shield
inv_add(inv, dragon_sq_shield, 1);
// osrs has 75 xp gain
stat_advance(smithing, 750);