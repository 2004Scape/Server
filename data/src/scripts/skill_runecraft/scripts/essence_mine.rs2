[label,teleport_to_essence_mine](coord $tele_back_coord)
if_close;
%exit_essence_mine_coord = $tele_back_coord;
// this is lowercase
npc_say("Senventior disthine molenko!");
if ($tele_back_coord ! ^essence_mine_to_brimstail) { // brimstall anim buggy, need gnome equivalent (or no anim like OSRS)
    npc_anim(human_castcurse, 0); // No anim in osrs
}
spotanim_npc(curse_casting, 92, 0);
sound_synth(curse_all, 0, 0);
def_int $duration = ~player_projectile(npc_coord, coord, uid, curse_travel, 31, 31, 61, 16, 0, 128, 5);
// osrs is 100 but 70 lines up better with our different sound synth
spotanim_pl(curse_impact, 124, 70);
// IncompleteProjectile(id = 109, startHeight = 31, endHeigsht = 31, delay = 61, angle = 16, distOffset = 128)	
p_delay(4);
// select random coord
def_coord $coord = enum(int, coord, essence_mine_teleports, random(enum_getoutputcount(essence_mine_teleports)));
// select random coord at a radius of 1
$coord = map_findsquare($coord, 0, 1, ^map_findsquare_lineofsight);
p_telejump($coord);

// portal
[oploc1,loc_2492]
// IncompleteProjectile(id = 109, startHeight = 15, endHeight = 31, delay = 0, angle = 16, distOffset = 128)	| distance = 1, flightDuration = 30, visualStart = Location(x = 9557, y = 3778, z = 0)
def_int $duration = ~player_projectile(loc_coord, coord, uid, curse_travel, 15, 31, 0, 16, 0, 128, 5);
spotanim_pl(curse_impact, 124, 30);
sound_synth(teleport_all, 0, 0);
p_delay(1);
if (%exit_essence_mine_coord = null) {
    %exit_essence_mine_coord = ^essence_mine_to_sedridor;
} else if (%exit_essence_mine_coord ! ^essence_mine_to_aubury & %exit_essence_mine_coord ! ^essence_mine_to_sedridor & map_members = ^false) {
    %exit_essence_mine_coord = ^essence_mine_to_sedridor; // in osrs, if you hop to a f2p world from f2p when inside the ess mine, if your exit coord is in a p2p area it reset to sedridor!
}
def_coord $coord = map_findsquare(%exit_essence_mine_coord, 0, 2, ^map_findsquare_lineofwalk);
p_telejump($coord);