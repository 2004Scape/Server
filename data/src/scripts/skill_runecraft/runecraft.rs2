[oploc1,_rc_exit_portal]
def_coord $coord = loc_param(rc_ruin_coord);
mes("You step through the portal...");
p_telejump($coord);

[oploc1,_rc_alter]
def_int $levelreq = loc_param(rc_level_req);
def_namedobj $rune = loc_param(rc_rune);
def_int $stat = stat(runecraft);
if ($stat < $levelreq) {
    ~mesbox("You need a Runecrafting level of at least <tostring($levelreq)> to craft <oc_name($rune)>.");
    return;
}

def_int $total_ess = inv_total(inv, blankrune);
if ($total_ess = 0) {
    ~mesbox("You do not have any rune essences to bind.");
    return;
}

anim(runecrafting, 0);
spotanim_pl(runecrafting, 96, 0);

def_int $rune_count_per_ess = calc(($stat / loc_param(rc_rune_multiplier)) + 1);
def_int $free_space = calc(^max_32bit_int - inv_total(inv, $rune));
def_int $craft_count = min($free_space, calc($total_ess * $rune_count_per_ess));

// check if we can hold the minimum amount
if($rune_count_per_ess > $free_space) {
    mes("Your inventory is too full to hold any more <oc_name($rune)>s.");
    return;
}

// if $craft_count uses free_space as the value, it may not round exactly to $rune_count_per_ess
$total_ess = calc($craft_count / $rune_count_per_ess);

inv_del(inv, blankrune, $total_ess);
givexp(runecraft, calc($total_ess * loc_param(rc_xp)));
mes("You bind the temple's power into <loc_param(rc_rune_name)> Runes.");
inv_add(inv, $rune, calc($total_ess * $rune_count_per_ess));
p_delay(3);

[opheld4,_talismans] @locate_ruins(last_item);

[oplocu,_rc_ruins]
if (last_item ! loc_param(rc_ruin_talisman)) {
    @nothing_interesting_happens;
}
@teleport_to_ruins(loc_param(rc_altar_coord));

[label,teleport_to_ruins] (coord $dest)
anim(bury, 0);
mes("You hold the <oc_name(last_item)> talisman towards the mysterious ruins.");
mes("You feel a powerful force take hold of you...");
p_delay(0);
p_telejump($dest);

[label,locate_ruins] (obj $last_item)
def_coord $ruin_coord = oc_param($last_item, rc_ruin_coord);
def_coord $current_coord = coord;

def_int $delta_x = calc(coordx($ruin_coord) - coordx($current_coord));
def_int $delta_y = calc(coordz($ruin_coord) - coordz($current_coord));

if ($delta_x > 0 & $delta_y > 0) {
    mes("The talisman pulls towards the south-east.");
} else if ($delta_x < 0 & $delta_y < 0) {
    mes("The talisman pulls towards the north-west.");
} else if ($delta_x > 0 & $delta_y < 0) {
    mes("The talisman pulls towards the south-west.");
} else if ($delta_x < 0 & $delta_y > 0) {
    mes("The talisman pulls towards the north-east.");
} else if ($delta_x > 0) {
    mes("The talisman pulls towards the east.");
} else if ($delta_x < 0) {
    mes("The talisman pulls towards the west.");
} else if ($delta_y > 0) {
    mes("The talisman pulls towards the south.");
} else if ($delta_y < 0) {
    mes("The talisman pulls towards the north.");
}