[ai_queue1,dark_wizard_earth] ~npc_default_retaliate_ap;
[ai_queue1,dark_wizard_water] ~npc_default_retaliate_ap;

[ai_applayer2,dark_wizard_earth] ~dark_wizard_earth_attack;
[ai_opplayer2,dark_wizard_earth] ~dark_wizard_earth_attack;
[ai_applayer2,dark_wizard_water] ~dark_wizard_water_attack;
[ai_opplayer2,dark_wizard_water] ~dark_wizard_water_attack;

[proc,dark_wizard_earth_attack]
if (%npc_action_delay > map_clock) {
    npc_setmode(applayer2);
    return;
}
if (~npc_can_attack_player = false) {
    npc_setmode(null);
    return;
}

npc_setmode(applayer2);

if (busy = true & %demon_progress = 29)  { // in osrs dark wizards wont attack if player is busy and ready to fight delrith
    // dark wizards will still try to hunt you
    return;
}

if (stat_base(strength) <= stat(strength) & random(2) = 0) { // guessed
    ~npc_cast_spell(^weaken, 4);
} else {
    ~npc_cast_spell(^earth_strike, 4);
}


[proc,dark_wizard_water_attack]
if (%npc_action_delay > map_clock) {
    npc_setmode(applayer2);
    return;
}
if (~npc_can_attack_player = false) {
    npc_setmode(null);
    return;
}

if (busy = true & %demon_progress = 29)  {
    return;
}

if (stat_base(attack) <= stat(attack) & random(2) = 0) { // guessed
    ~npc_cast_spell(^confuse, 4);
} else {
    ~npc_cast_spell(^water_strike, 4);
}

npc_setmode(applayer2);

