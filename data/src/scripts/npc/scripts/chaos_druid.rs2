// ranged huntmode, but once switching to op it doesn't seem to ever go back
[ai_applayer2,chaos_druid]
if (%npc_action_delay > map_clock) {
    return;
}
def_dbrow $spell_data = ~get_spell_data(^chaos_druid_attack);
~chaos_druid_spell($spell_data);
if(random(3) ! 0) npc_setmode(opplayer2);

[ai_opplayer2,chaos_druid]
if (%npc_action_delay > map_clock) {
    return;
}
def_dbrow $spell_data = ~get_spell_data(^chaos_druid_attack);
// ~1/4 based off 100 attacks in osrs
if(random(3) = 0) {
    ~chaos_druid_spell($spell_data);
} else {
    ~npc_default_attack;
}

[proc,chaos_druid_spell](dbrow $spell_data)
def_int $duration = ~npc_spell_cast($spell_data, 4);
if (~npc_player_hit_roll(^magic_style) = true) {
    ~npc_spell_success($spell_data, null, $duration);
    if(~npc_debuff_allowed($spell_data) = true) ~npc_stat_change_effect($spell_data);
    if(%frozen <= map_clock) queue(nomes_npc_freeze, 0, db_getfield($spell_data, magic_spell_table:freeze_time, 0));
} else {
    ~npc_spell_fail($spell_data, $duration);
}
~npc_set_attack_vars;