[opnpc1,baraek]
if (inv_total(inv, fur) > 0) {
    @multi3("Can you sell me some furs?", baraek_sell, "Hello. I am in search of a quest.", baraek_quest, "Would you like to buy my fur?", baraek_buy);
} else {
    @multi2("Can you sell me some furs?", baraek_sell, "Hello. I am in search of a quest.", baraek_quest);
}

// branch 1
[label,baraek_sell]
chatplayer("default", "Can you sell me some furs?");
chatnpc("default", "Yeah, sure. They're 20 gold coins each.");

@multi2("Yeah, okay, here you go.", baraek_sell_yes, "20 gold coins? That's an outrage!", baraek_sell_no);

// branch 1 -> branch 1
[label,baraek_sell_yes]
chatplayer("default", "Yeah, OK, here you go.");

if (inv_total(inv, coins) >= 20) {
    inv_del(inv, coins, 20);
    inv_add(inv, fur, 1);
    objbox(fur, "Baraek sells you a fur.");
} else {
    chatplayer("default", "Oh dear, I don't have enough money!");
    chatnpc("default", "Well, my best price is 18 coins.");

    @multi2("OK, here you go.", baraek_sell_yes_cheaper, "No thanks, I'll leave it.", baraek_sell_no_cheaper);
}

// branch 1 -> branch 1 -> branch 1
[label,baraek_sell_yes_cheaper]
chatplayer("default", "OK, here you go.");

if (inv_total(inv, coins) >= 18) {
    inv_del(inv, coins, 18);
    inv_add(inv, fur, 1);
    objbox(fur, "Baraek sells you a fur.");
} else {
    chatplayer("default", "Oh dear, I don't have that either.");
    chatnpc("default", "Well, I can't go any cheaper than that mate. I have a|family to feed.");
    chatplayer("default", "Oh well, never mind.");
}

// branch 1 -> branch 1 -> branch 2
[label,baraek_sell_no_cheaper]
chatplayer("default", "No thanks, I'll leave it.");
chatnpc("default", "It's your loss mate.");

// branch 1 -> branch 2
[label,baraek_sell_no]
chatplayer("default", "20 gold coins? That's an outrage!");
chatnpc("default", "Well, I can't go any cheaper than that mate. I have a|family to feed.");

// branch 2
[label,baraek_quest]
chatplayer("default", "Hello! I am in search of a quest.");
chatnpc("default", "Sorry kiddo, I'm a fur trader not a damsel in distress.");

// branch 3
[label,baraek_buy]
chatplayer("default", "Would you like to buy my fur?");
chatnpc("default", "Let's have a look at it.");
objbox(fur, "You hand Baraek your fur to look at.");

p_delay(2);

chatnpc("default", "It's not in the best condition. I guess I could give you 12 coins for each one.");

@multi2("Yeah, that'll do.", baraek_buy_yes, "I think I'll keep a hold of it actually.", baraek_buy_no);

// branch 3 -> branch 1
[label,baraek_buy_yes]
p_delay(1);

def_int $furs = inv_total(inv, fur);
def_int $price = calc($furs * 12);

inv_del(inv, fur, $furs);
inv_add(inv, coins, $price);

chatplayer("default", "Thanks!");

// branch 3 -> branch 2
[label,baraek_buy_no]
chatplayer("default", "I think I'll keep a hold of it actually!");
chatnpc("default", "Oh ok. Didn't want it anyway!");
