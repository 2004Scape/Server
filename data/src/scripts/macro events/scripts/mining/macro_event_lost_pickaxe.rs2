[proc,macro_event_lost_pickaxe_spawn]
def_obj $pickaxe = ~pickaxe_checker;
if ($pickaxe = null) {
    anim(null, 0);
    ~mesbox("You need a pickaxe to mine this rock. You do not have a pickaxe which you have the Mining level to use.");
    return;
}
def_namedobj $pickaxe_head = oc_param($pickaxe, pickaxe_head);

// todo: use inv_changeslot?
if (inv_total(worn, $pickaxe) > 0) {
    inv_del(worn, $pickaxe, 1);
    inv_setslot(worn, ^wearpos_rhand, macro_pickaxehandle, 1);
    ~update_all($pickaxe);
} else if (inv_total(inv, $pickaxe) > 0) {
    inv_del(inv, $pickaxe, 1);
    inv_add(inv, macro_pickaxehandle, 1);
}
def_coord $pickaxe_coord = map_findsquare(coord, 1, 7, ^map_findsquare_lineofwalk);
anim(human_mining_pickaxe_handle, 0);
sound_synth(lost_pickaxe, 0, 0);
%macro_event = ^no_macro_event;
mes("You swing your pick at the rock.");
def_int $delay = ~coord_projectile(coord, $pickaxe_coord, pickaxe_head_launch, 35, 0, 14, 16, -2, 64, 10);
// world_delay(calc($delay/60));
// https://youtu.be/tev6FR2iowQ?t=16 Looks like it delays the player
p_delay(calc($delay/60));
obj_add($pickaxe_coord, $pickaxe_head, 1, ^lootdrop_duration);
session_log(^log_adventure, "Lost their <oc_name($pickaxe)> head");

[label,reattach_pickaxe](obj $pickaxe_head)
def_namedobj $pickaxe = oc_param($pickaxe_head, fixed);
if (inv_total(inv, macro_pickaxehandle) > 0 & inv_total(inv, $pickaxe_head) > 0) {
    inv_del(inv, macro_pickaxehandle, 1);
    inv_del(inv, $pickaxe_head, 1);
    inv_add(inv, $pickaxe, 1);
    sound_synth(attach_handle, 0, 0);
    mes("You carefully attach the two pieces together."); // https://youtu.be/tev6FR2iowQ?t=34
}

[opheldu,macro_pickaxehandle]
switch_obj(last_useitem) {
    case macro_bronze_pickaxehead, macro_iron_pickaxehead, macro_steel_pickaxehead, macro_mithril_pickaxehead, macro_adamant_pickaxehead, macro_rune_pickaxehead : @reattach_pickaxe(last_useitem);
    case default : ~displaymessage(^dm_default);
}

[opheldu,macro_bronze_pickaxehead] @check_pickaxe_head;
[opheldu,macro_iron_pickaxehead] @check_pickaxe_head;
[opheldu,macro_steel_pickaxehead] @check_pickaxe_head;
[opheldu,macro_mithril_pickaxehead] @check_pickaxe_head;
[opheldu,macro_adamant_pickaxehead] @check_pickaxe_head;
[opheldu,macro_rune_pickaxehead] @check_pickaxe_head;

[label,check_pickaxe_head]
if (last_useitem = macro_pickaxehandle) {
    @reattach_pickaxe(last_item);
} else {
    ~displaymessage(^dm_default);
}