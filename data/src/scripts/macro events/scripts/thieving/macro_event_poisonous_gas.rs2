// todo: find a video of this
[proc,macro_event_macro_gas_chest]
mes("You search the chest for traps.");

mes("You find a trap on the chest,");
p_delay(1);
mes("You disable the trap.");
sound_synth(locked, 0, 0);
p_delay(1);
mes("You open the chest.");
sound_synth(chest_open, 0, 0);
p_delay(1);
anim(human_openchest, 0);
loc_change(chest_macro_gas, 100); // timing is guessed
%chest_macro_gas_coord = loc_coord; // set the active loc
p_delay(0);
settimer(poison_gas, 1); // start the timer

say("I better run!"); // 2004 https://i.imgur.com/nwQIetY.png

// timer is set when chest_macro_gas is called.
// timer is cleared when player is not within 10 tiles of chest_macro_gas
// - Should this damage other players? If so, how is that possible without an npc?
// - Could use a ~loc_within_distance approach but varp is safer. 
[timer,poison_gas]
if (loc_find(%chest_macro_gas_coord, chest_macro_gas) = true) { // set active loc
    // if player is within 1 tile of gas, apply damage (2-3)
    if (distance(loc_coord, coord) <= 1 & coordy(loc_coord) = coordy(coord)) {
        // todo: figure out the mes for this thing
        sound_synth(gas_hiss, 0, 0);
        ~poison_damage(~random_range(2, 3)); // poison damage doesnt actually poison, its just a green hitsplat
    }
    settimer(poison_gas, 4);
} else {
    %chest_macro_gas_coord = null;
    cleartimer(poison_gas); // stop the timer
}

[proc,check_chest_macro_gas]
if (%chest_macro_gas_coord = null) {
    return;
}
if (loc_find(%chest_macro_gas_coord, chest_macro_gas) = true) {
    settimer(poison_gas, 1);
}