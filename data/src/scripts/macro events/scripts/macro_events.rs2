// timer is initiated on login, in scripts\player\scripts\global.rs2
[timer,general_macro_events]
if (afk_event = ^false | ~macro_event_active = true) {
    return;
}
// random events arent restricted in wildy?
// https://youtu.be/uPKaH7yg4To?t=421

@macro_event_general_spawn;


[label,macro_event_general_spawn]
def_npc $event = ~macro_event_general;
def_coord $event_coord = ~macro_event_coord;
if ($event_coord = null) {
    return;
}
npc_add($event_coord, $event, 1000);
@macro_event_spawn;

[label,macro_event_spawn]
switch_int (%macro_event) {
    case ^macro_event_swarm : @macro_event_swarm_spawn;
    case ^macro_event_strange_plant_friendly : @macro_event_strange_plant_spawn;
    case ^macro_event_drunken_dwarf : @macro_event_drunken_dwarf_spawn;
    case ^macro_event_genie : @macro_event_genie_spawn;
    case ^macro_event_mysterious_old_man_general : @macro_event_mysterious_old_man_general_spawn;
}

[proc,macro_event_general]()(npc)
def_npc $event;
if (map_members = true) {
    %macro_event = ~random_range(1, enum_getoutputcount(general_macro_events_members));
    $event = enum(int, npc, general_macro_events_members, %macro_event);
} else {
    %macro_event = ~random_range(1, enum_getoutputcount(general_macro_events_free));
    $event = enum(int, npc, general_macro_events_free, %macro_event);
}
return($event);

// used for skilling random events
[proc,macro_event_general_count]()(int)
if (map_members = true) {
    return(enum_getoutputcount(general_macro_events_members));
}
return(enum_getoutputcount(general_macro_events_free));


// teleports the player to a random location, using the coords in configs/macro_events_teleports.enum
[proc,macro_event_fail_teleport]
def_coord $teleport_coord;
if (map_members = true) {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_members, random(enum_getoutputcount(macro_event_fail_teleports_members)));
} else {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_free, random(enum_getoutputcount(macro_event_fail_teleports_free)));
}
// https://youtu.be/MS7ADB-hh18
// doesnt if_close
queue(macro_event_fail_teleport, 0, $teleport_coord);

[queue,macro_event_fail_teleport](coord $teleport_coord)
// https://youtu.be/g5k3XuXxyYQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=37
spotanim_map(spotanim_86, coord, 124, 0);
sound_synth(smokepuff2, 0, 0);
//--
p_telejump($teleport_coord);
// TODO: Confirm this
~teleport_anti_smuggle_checks;

[proc,macro_event_fail_scatter_inv]
def_int $i = 0;
while ($i < inv_size(inv)) {
    def_coord $coord = ~coord_lineofwalk_radius(coord, 5); // 11x11 for now. todo: confirm how scattering works
    if (inv_getobj(inv, $i) ! null) {
        inv_dropslot(inv, $coord, $i, 200);
    }
    spotanim_map(spotanim_86, $coord, 124, 0);
    $i = calc($i + 1);
}

// called when the npc needs to be poofed away
[proc,macro_event_disappear]
if (npc_findhero = true) {
    sound_synth(smokepuff2, 0, 0);
}
spotanim_map(spotanim_86, npc_coord, 124, 0);
npc_del;

// check if other macro events already exist
[proc,macro_event_active]()(boolean)
if (%macro_event > 0) { 
    return (true);
}
return (false);

[proc,macro_event_give_reward](namedobj $reward, int $count)
if (inv_freespace(inv) < 1) {
    // drop on the ground for 3 hours https://twitter.com/JagexAsh/status/1574393627421417478
    obj_add(coord, $reward, $count, 18000);
} else {
    inv_add(inv, $reward, $count);
}

[proc,macro_event_lost]()(boolean)
if (finduid(%npc_attacking_uid) = false) {
    return(true);
}

if ((npc_range(coord) > 10 | coordy(coord) ! coordy(npc_coord))) {
    // def_coord $event_coord = ~coord_lineofwalk_radius(coord, 1);
    // def_int $i = 0;
    // while ($event_coord = coord) {
    //     $event_coord = ~coord_lineofwalk_radius(coord, 1);
    //     $i = calc($i + 1);
    //     if ($i = 25) {
    //         // after 25 tries, just disappear
    //         npc_anim(emote_cry, 20);
    //         npc_say("I lose more friends that way.");
    //         ~macro_event_disappear;
    //         return(true);
    //     }
    // }
    npc_tele(coord); // osrs
    npc_setmode(playerfollow); // only starts following after teleport? https://youtu.be/HpmsgVshwxg?t=303
}

// based off https://web.archive.org/web/20060814111255/http://www.zybez.net/misc.php?id=11
[proc,macro_event_combat_level]()(int)
def_int $combat_level = ~player_combat_level;
if ($combat_level < 11) {
    return(^macro_event_cmb_14);
}
if ($combat_level < 21) {
    return(^macro_event_cmb_29);
}
if ($combat_level < 41) {
    return(^macro_event_cmb_49);
}
if ($combat_level < 61) {
    return(^macro_event_cmb_79);
}
if ($combat_level < 91) {
    return(^macro_event_cmb_120);
}

return(^macro_event_cmb_159);

[proc,macro_event_coord]()(coord)
def_coord $event_coord = ~coord_lineofwalk_radius(coord, 1);

def_int $i = 0;
while ($event_coord = coord) {
    $event_coord = ~coord_lineofwalk_radius(coord, 1);
    $i = calc($i + 1);
    if ($i = 25) {

        // after 25 tries, dont spawn anything
        return(null);
    }
}
return($event_coord);