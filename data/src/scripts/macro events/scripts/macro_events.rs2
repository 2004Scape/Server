// timer is initiated on login, in scripts\player\scripts\global.rs2
[timer,general_macro_events]
if (afk_event = ^false) {
    return;
}
// macro_event varp resets when a random event is due to spawn? To prevent more than one general event at a time
if (%macro_event > 0) {
    // delete npc if player already has a random event somehow. I think this only applies to general random events?
    // This is just a fail safe, no idea if this existed back then
    if (~macro_event_general_active = true & npc_finduid(%macro_event_uid) = true) {
        npc_del;
    }
    %macro_event = ^no_macro_event;
}
// random events arent restricted in wildy?
// https://youtu.be/uPKaH7yg4To?t=421

~macro_event_general_spawn(~macro_event_set_random);


[proc,macro_event_general_spawn](int $event)
def_coord $event_coord = ~macro_event_coord;
if ($event_coord = null) {
    return;
}
%macro_event = $event;
def_npc $event_npc = ~macro_event_npc($event);
npc_add($event_coord, $event_npc, 1000);
%macro_event_uid = npc_uid;

switch_int ($event) {
    case ^macro_event_swarm : ~macro_event_swarm_spawn;
    case ^macro_event_triffid_friendly : ~macro_event_triffid_spawn;
    case ^macro_event_drunken_dwarf : ~macro_event_drunken_dwarf_spawn;
    case ^macro_event_genie : ~macro_event_genie_spawn;
    case ^macro_event_mysterious_old_man_general : ~macro_event_mysterious_old_man_general_spawn;
}

// called when the player logs in
[proc,macro_event_general_check]
if (~macro_event_general_active = true & nc_param(~macro_event_npc(%macro_event), follow_player_on_logout) = ^true) {
    ~macro_event_general_spawn(%macro_event);
}

[proc,macro_event_npc](int $event)(npc)
def_npc $event_npc;
if (map_members = true) {
    $event_npc = enum(int, npc, general_macro_events_members, $event);
} else {
    $event_npc = enum(int, npc, general_macro_events_free, $event);
}
return($event_npc);

[proc,macro_event_set_random]()(int)
if (map_members = true) {
    return(~random_range(1, enum_getoutputcount(general_macro_events_members)));
} else {
    return(~random_range(1, enum_getoutputcount(general_macro_events_free)));
}

// used for skilling random events
[proc,macro_event_general_count]()(int)
if (map_members = true) {
    return(enum_getoutputcount(general_macro_events_members));
}
return(enum_getoutputcount(general_macro_events_free));

// teleports the player to a random location, using the coords in configs/macro_events_teleports.enum
[proc,macro_event_fail_teleport]
if (p_finduid(uid) = false) {
    return;
}
def_coord $teleport_coord;
if (map_members = true) {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_members, random(enum_getoutputcount(macro_event_fail_teleports_members)));
} else {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_free, random(enum_getoutputcount(macro_event_fail_teleports_free)));
}
// https://youtu.be/MS7ADB-hh18
// doesnt if_close
%macro_event = ^no_macro_event;
if (npc_finduid(%macro_event_uid) = true) {
    npc_say("No one ignores me!");
    npc_anim(human_castteleport, 0);
    p_delay(1);
    npc_del;
}
p_delay(0);
// https://youtu.be/g5k3XuXxyYQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=37
spotanim_map(spotanim_86, coord, 124, 0);
sound_synth(smokepuff2, 0, 0);
//--
p_telejump($teleport_coord);
// TODO: Confirm this
~teleport_anti_smuggle_checks;

[proc,macro_event_fail_scatter_inv]
if (p_finduid(uid) = false) {
    return;
}
%macro_event = ^no_macro_event;
if (npc_finduid(%macro_event_uid) = true) {
    npc_say("No one ignores me!");
    npc_anim(human_castteleport, 0);
    p_delay(1);
    npc_del;
}
p_delay(0);
def_int $i = 0;
while ($i < inv_size(inv)) {
    def_coord $coord = ~coord_lineofwalk_radius(coord, 5); // 11x11 for now. todo: confirm how scattering works
    if (inv_getobj(inv, $i) ! null) {
        inv_dropslot(inv, $coord, $i, 200);
    }
    spotanim_map(spotanim_86, $coord, 124, 0);
    $i = calc($i + 1);
}


// called when the npc needs to be poofed away
[proc,macro_event_disappear]
if (npc_findhero = true) {
    sound_synth(smokepuff2, 0, 0);
}
spotanim_map(spotanim_86, npc_coord, 124, 0);
npc_del;

// check if other general macro events already exist
[proc,macro_event_general_active]()(boolean)
if (%macro_event < 1) {
    return(false);
}
if (%macro_event < add(~macro_event_general_count, 1)){
    return(true);
}

[proc,macro_event_give_reward](namedobj $reward, int $count)
if (inv_freespace(inv) < 1) {
    // drop on the ground for 3 hours https://twitter.com/JagexAsh/status/1574393627421417478
    obj_add(coord, $reward, $count, 18000);
} else {
    inv_add(inv, $reward, $count);
}

[proc,macro_event_lost]()(boolean)
if (finduid(%npc_attacking_uid) = false) {
    return(true);
}

if ((npc_range(coord) > 10 | coordy(coord) ! coordy(npc_coord))) {
    // def_coord $event_coord = ~coord_lineofwalk_radius(coord, 1);
    // def_int $i = 0;
    // while ($event_coord = coord) {
    //     $event_coord = ~coord_lineofwalk_radius(coord, 1);
    //     $i = calc($i + 1);
    //     if ($i = 25) {
    //         // after 25 tries, just disappear
    //         npc_anim(emote_cry, 20);
    //         npc_say("I lose more friends that way.");
    //         ~macro_event_disappear;
    //         return(true);
    //     }
    // }
    npc_tele(coord); // osrs
    npc_setmode(playerfollow); // only starts following after teleport? https://youtu.be/HpmsgVshwxg?t=303
}
return(false);

// based off https://web.archive.org/web/20060814111255/http://www.zybez.net/misc.php?id=11
[proc,macro_event_combat_level]()(int)
def_int $combat_level = ~player_combat_level;
if ($combat_level < 11) {
    return(^macro_event_cmb_14);
}
if ($combat_level < 21) {
    return(^macro_event_cmb_29);
}
if ($combat_level < 41) {
    return(^macro_event_cmb_49);
}
if ($combat_level < 61) {
    return(^macro_event_cmb_79);
}
if ($combat_level < 91) {
    return(^macro_event_cmb_120);
}

return(^macro_event_cmb_159);

[proc,macro_event_coord]()(coord)
return(~coord_lineofwalk_radius2(coord, 1));

// for 2x2 npcs
[proc,macro_event_coord_2x2]()(coord)
def_int $i = 0;
def_coord $sw;
def_coord $nw;
def_coord $se;
def_coord $ne;
// basically prevent the npc from spawning into anything it shouldnt. and so it doesnt spawn ontop of player
while ($i < 50) {
    $sw = movecoord(coord, ~random_range(-2, 2), 0, ~random_range(-2, 2));
    $nw = movecoord($sw, 1, 0, 0);
    $se = movecoord($sw, 0, 0, 1);
    $ne = movecoord($sw, 1, 0, 1);
    if (map_blocked($sw) = false & map_blocked($nw) = false & map_blocked($se) = false & map_blocked($ne) = false & lineofwalk($sw, $ne) = true){
        return($sw);
    }
    $i = calc($i + 1);
}
return(null);