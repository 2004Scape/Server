// timer is initiated on login, in scripts\player\scripts\global.rs2
[softtimer,general_macro_events]
// if (afk_event = ^false) {
//     return;
// }
if (~macro_event_active = true) {
    return;
}
// random events arent restricted in wildy?
// https://youtu.be/uPKaH7yg4To?t=421
%macro_event = randominc(enum_getoutputcount(general_macro_events));
def_npc $event = enum(int, npc, general_macro_events, %macro_event);
def_coord $event_coord = ~coord_lineofwalk_radius(coord, 1);

def_int $i = 0;
while ($event_coord = coord) {
    $event_coord = ~coord_lineofwalk_radius(coord, 1);
    $i = calc($i + 1);
    if ($i = 25) {
        // after 25 tries, dont spawn anything
        return;
    }
}
@macro_event_spawn($event, $event_coord);


// swarm of bees
// tangle vine (discontinued)
// strange plant
// drunken dwarf
// genie
// mysterious old man

[label,macro_event_spawn](npc $event, coord $event_coord)
npc_add($event_coord, $event, 100);
spotanim_map(spotanim_86, $event_coord, 124, 0);
sound_synth(smokepuff, 0, 0);

switch_int (%macro_event) {
    case ^swarm_macro_event : @swarm_macro_event_spawn;
    case ^strange_plant_friendly_macro_event : @strange_plant_macro_event_spawn;
    case ^drunken_dwarf_macro_event : @drunken_dwarf_macro_event_spawn;
    case ^genie_macro_event : @genie_macro_event_spawn;
    case ^mysterious_old_man_general_macro_event : @mysterious_old_man_general_macro_event_spawn;
}


// teleports the player to a random location, using the coords in configs/macro_events_teleports.enum
[proc,macro_event_fail_teleport]
def_coord $teleport_coord;
if (map_members = true) {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_members, random(enum_getoutputcount(macro_event_fail_teleports_members)));
} else {
    $teleport_coord = enum(int, coord, macro_event_fail_teleports_free, random(enum_getoutputcount(macro_event_fail_teleports_free)));
}
// https://youtu.be/MS7ADB-hh18
// doesnt if_close
queue(macro_event_fail_teleport, 0, $teleport_coord);

[queue,macro_event_fail_teleport](coord $teleport_coord)
p_telejump($teleport_coord);
// TODO: Confirm this
~teleport_anti_smuggle_checks;

[proc,macro_event_fail_scatter_inv]


// called when the npc needs to be poofed away
[proc,macro_event_disappear]
if (npc_findhero = true) {
    sound_synth(smokepuff2, 0, 0);
}
spotanim_map(spotanim_86, npc_coord, 124, 0);
npc_del;

// check if other macro events already exist
[proc,macro_event_active]()(boolean)
if (%macro_event > 0) { 
    return (true);
}
return (false);