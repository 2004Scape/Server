[label,genie_macro_event_spawn]
npc_say("Greetings <text_gender("master", "mistress")> <displayname>!");
npc_setmode(playerfollow);
%npc_attacking_uid = uid;

[opnpc1,genie_macro_event]
// confirm
if (uid ! %npc_attacking_uid) {
    ~chatnpc(neutral, "Sorry, I'm trying to speak to <displayname>, but <text_gender("he", "she")> seems to be ignoring me.");
    return;
}
if (%macro_event = ^no_macro_event) {
    ~chatnpc(neutral, "Have a nice day, <text_gender("master", "mistress")>.");
    return;
}
%macro_event = ^no_macro_event;
~macro_event_give_reward(lamp, 1);
// delays are taken from this video: https://youtu.be/EN4c2hV65Qk?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
%npc_macro_event_patience = ^patience_5;
npc_settimer(3);
// dialogue taken from osrs. Neutral is a guess.
~chatnpc(neutral, "Ah, so you are there, <text_gender("master", "mistress")> <displayname>. I'm so glad you summoned me. Please take this lamp and make your wish.");

[ai_timer,genie_macro_event]
if (~macro_event_lost = true) {
    return;
}

// warning message https://youtu.be/TCQl_9PALag?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=395
// this video goes 50t after respawn without warning message https://youtu.be/KkyVz0tSOQA?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=176
// 3 warning messages: https://youtu.be/g5k3XuXxyYQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
if (%npc_macro_event_patience = ^patience_1) {
    npc_anim(emote_wave, 20);
    npc_say("Ehem... master <displayname>?");
} else if (%npc_macro_event_patience = ^patience_2) {
    npc_anim(emote_think, 20);
    npc_say("Are you there, master <displayname>?");
} else if (%npc_macro_event_patience = ^patience_3) {
    npc_anim(emote_angry, 20);
    npc_say("You shouldn't ignore a Genie master <displayname>?");
} else if (%npc_macro_event_patience = ^patience_4) { // failing genie: https://youtu.be/MS7ADB-hh18?t=6
    %macro_event = ^no_macro_event;
    npc_say("No one ignores me!");
    npc_anim(human_castteleport, 0);
    npc_delay(1);
    npc_del;
    if (random(2) < 1 & inv_freespace(inv) < inv_size(inv)) { // 50% chance to teleport or scatter inv
        ~macro_event_fail_scatter_inv;
    } else {
        ~macro_event_fail_teleport;
    }
    return;
} else if (%npc_macro_event_patience = ^patience_5) { // wave
    %npc_macro_event_patience = ^patience_6;
    npc_anim(emote_wave, 20);
    npc_settimer(3);
    return;
} else if (%npc_macro_event_patience >= ^patience_6) { // leave
    ~macro_event_disappear;
    return;
}

%npc_macro_event_patience = calc(%npc_macro_event_patience + 1);