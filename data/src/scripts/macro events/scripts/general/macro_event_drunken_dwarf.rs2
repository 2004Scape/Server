[proc,macro_event_drunken_dwarf_spawn] // greet player
// no smoke puff: https://youtu.be/R9eKmC7oLy0?t=122
sound_synth(dwarf_whistle, 0, 0);
npc_anim(dwarf_seq_105, 20);
npc_say("'Ello der <displayname>! *hic*");
%npc_macro_event_target = uid;
npc_setmode(playerfollow);

[ai_timer,macro_event_drunken_dwarf]
if (%npc_int = ^drunken_dwarf_angry) {
    if (~macro_event_lost_hostile = true) {
        // https://youtu.be/PhAjUCWSoOI?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=79
        // https://youtu.be/PhAjUCWSoOI?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=81
        npc_queue(5, 0, 0); // despawn
    }
    return;
}
if (~macro_event_lost = true) {
    npc_queue(5, 0, 0);
    return;
}
// warnings: https://youtu.be/Ww-PkAr4vt4?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=96
// todo: confirm these anims
if (finduid(%npc_macro_event_target) = true) {
    if (%npc_int = 0) {
        npc_anim(dwarf_seq_103, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Aww comeon, talk to ikle me <displayname>!");
    } else if (%npc_int = 1) {
        npc_anim(dwarf_seq_105, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Oi, are you der <displayname>!");
    } else if (%npc_int = 2) {
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Dunt ignore your mate, <displayname>!");
    } else if (%npc_int = 3) {
        npc_anim(dwarf_seq_103, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("I hates yoo <displayname>!");
    } else if (%npc_int = 4) { // fail
        // todo: figure out the message that goes here
        %aggressive_npc = npc_uid; // interupt the player if they're in combat https://youtu.be/tw66JWQzpD0?t=32
        npc_setmode(opplayer2);
        %npc_int = ^drunken_dwarf_angry;
        %macro_event = ^no_macro_event;
        return;
    }
    %npc_int = add(%npc_int, 1);
}

[ai_queue5,macro_event_drunken_dwarf] // despawn
npc_say("Aww, me is all alone.");
npc_delay(2); // osrs
npc_del;

[ai_queue6,macro_event_drunken_dwarf] // say good bye
// Doesnt say goodbye :( https://youtu.be/R9eKmC7oLy0?t=122
npc_del;

[opnpc1,macro_event_drunken_dwarf]
if (uid ! %npc_macro_event_target) {
    ~chatnpc("<p,drunk>You're not my matey!"); // osrs
    return;
}
if (%npc_int = ^drunken_dwarf_happy) {
    ~chatnpc("<p,drunk>'Ave a good 'un buddy!"); // osrs
    return;
}
inv_add(inv, kebab, 1);
inv_add(inv, beer, 1);

%npc_int = ^drunken_dwarf_happy;
%macro_event = ^no_macro_event;

npc_queue(6, 0, 0); // say good bye
npc_settimer(0);
~chatnpc("<p,drunk>I 'new it were you matey! 'Ere, have some ob the good stuff!"); // osrs

// https://youtu.be/8wJZmDwcBKs?t=108
[ai_opplayer2,macro_event_drunken_dwarf]
if (%npc_action_delay > map_clock) return;
if (~npc_can_attack_player = false) {
    npc_setmode(null);
    return;
}

npc_anim(npc_param(attack_anim), 20);
%npc_action_delay = add(map_clock, npc_param(attackrate));
~player_projectile(npc_coord, coord, uid, dwarf_rock_launch, 45, 40, 51, 25, 5, 11, 5);
sound_synth(npc_param(attack_sound), 0, 0);

~npc_set_attack_vars;

if (stat(hitpoints) = 0) {
    return;
}

def_int $attack_roll = ~npc_melee_attack_roll;
def_int $defence_roll = ~player_defence_roll_specific(npc_param(damagetype));
def_int $maxhit = ~npc_melee_maxhit;

// npc_say("Clock: <tostring(map_clock)>, NPC A: <tostring($attack_roll)>, Player D: <tostring($defence_roll)>, NPC Max: <tostring($maxhit)>");

def_int $damage = 0;
if (randominc($attack_roll) > randominc($defence_roll)) {
    $damage = randominc($maxhit);
}
if_close; // close the player interface when taking a hit.
anim(%com_defendanim, 60);
queue(damage_player, 1, $damage);
queue(playerhit_n_retaliate, 1, npc_uid); // this should be a queue* command
