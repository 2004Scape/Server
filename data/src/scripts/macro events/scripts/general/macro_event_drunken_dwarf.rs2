[ai_queue4,macro_event_drunken_dwarf](int $arg) // greet player
// no smoke puff: https://youtu.be/R9eKmC7oLy0?t=122
if (finduid(%npc_attacking_uid) = true) {
    sound_synth(dwarf_whistle, 0, 0);
    npc_anim(dwarf_seq_105, 20);
    npc_say("'Ello der <displayname>! *hic*");
    npc_setmode(playerfollow);
}

[ai_queue5,macro_event_drunken_dwarf](int $arg) // despawn
npc_say("Aww, me is all alone.");
npc_delay(2); // osrs
if (finduid(%npc_attacking_uid) = true) {
    %macro_event = ^no_macro_event;
}
npc_del;

[ai_queue6,macro_event_drunken_dwarf](int $arg) // say good bye
// Doesnt say goodbye :( https://youtu.be/R9eKmC7oLy0?t=122
npc_del;

[opnpc1,macro_event_drunken_dwarf]
if (uid ! %npc_attacking_uid) {
    ~chatnpc("<p,drunk>You're not my matey!"); // osrs
    return;
}
if (%macro_event = ^no_macro_event | %macro_event_uid ! npc_uid) {
    ~chatnpc("<p,drunk>'Ave a good 'un buddy!"); // osrs
    return;
}
inv_add(inv, kebab, 1);
inv_add(inv, beer, 1);
%macro_event = ^no_macro_event;

npc_queue(6, 0, 0); // say good bye
~chatnpc("<p,drunk>I 'new it were you matey! 'Ere, have some ob the good stuff!"); // osrs

[ai_timer,macro_event_drunken_dwarf]
if (~npc_out_of_combat = false) {
    return;
}
if (~macro_event_lost_and_follow = true) { // https://youtu.be/PhAjUCWSoOI?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=79
   // https://youtu.be/PhAjUCWSoOI?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=81
    npc_queue(5, 0, 0); // despawn
    return;
}

// warnings: https://youtu.be/Ww-PkAr4vt4?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=96
// todo: confirm these anims
if (finduid(%npc_attacking_uid) = true) {
    if (%npc_macro_event_status = 1) {
        npc_anim(dwarf_seq_103, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Aww comeon, talk to ikle me <displayname>!");
    } else if (%npc_macro_event_status = 2) {
        npc_anim(dwarf_seq_105, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Oi, are you der <displayname>!");
    } else if (%npc_macro_event_status = 3) {
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("Dunt ignore your mate, <displayname>!");
    } else if (%npc_macro_event_status = 4) {
        npc_anim(dwarf_seq_103, 20);
        sound_synth(dwarf_whistle, 0, 0);
        npc_say("I hates yoo <displayname>!");
    } else if (%npc_macro_event_status >= 5) { // fail
        // todo: figure out the message that goes here
        %aggressive_npc = npc_uid; // interupt the player if they're in combat https://youtu.be/tw66JWQzpD0?t=32
        npc_setmode(opplayer2);
    }

    %npc_macro_event_status = calc(%npc_macro_event_status + 1);
}


// https://youtu.be/8wJZmDwcBKs?t=108
[ai_opplayer2,macro_event_drunken_dwarf]
if (%npc_action_delay > map_clock) return;
if (~npc_can_attack_player = false) {
    npc_setmode(null);
    return;
}

npc_anim(npc_param(attack_anim), 20);
%npc_action_delay = add(map_clock, npc_param(attackrate));
~player_projectile(npc_coord, coord, uid, dwarf_rock_launch, 45, 40, 51, 25, 5, 11, 5);
sound_synth(npc_param(attack_sound), 0, 0);

if (stat(hitpoints) = 0) {
    return;
}

def_int $attack_roll = ~npc_melee_attack_roll;
def_int $defence_roll = ~player_defence_roll_specific(npc_param(damagetype));
def_int $maxhit = ~npc_melee_maxhit;

// npc_say("Clock: <tostring(map_clock)>, NPC A: <tostring($attack_roll)>, Player D: <tostring($defence_roll)>, NPC Max: <tostring($maxhit)>");

def_int $damage = 0;
if (randominc($attack_roll) > randominc($defence_roll)) {
    $damage = randominc($maxhit);
}
if_close; // close the player interface when taking a hit.
// not sure if this is correct
if (getqueue(player_death) < 1) {
    queue(drunken_dwarf_player_defend_anim, 1);
    queue(damage_player, 1, $damage);
    queue(playerhit_n_retaliate, 1, npc_uid); // this should be a queue* command
}
~npc_set_attack_vars;

[queue,drunken_dwarf_player_defend_anim]
anim(%com_defendanim, 0);
