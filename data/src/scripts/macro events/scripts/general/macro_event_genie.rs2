[proc,macro_event_genie_spawn]
spotanim_map(small_smokepuff, npc_coord, 124, 0);
sound_synth(genie_appear, 0, 0);
npc_say("Greetings <text_gender("master", "mistress")> <displayname>!");
%npc_macro_event_target = uid;
// npc_setmode(none); <-- this is current osrs implementation
// npc_queue(4, 0, 2); <-- (and sets mode to playerfollow in ai_queue4)

// this matches this video: https://www.youtube.com/watch?v=el-h9hbSH80&t=293s
npc_queue(4, 0, 20);
npc_setmode(playerfollow);

[ai_queue4,macro_event_genie]
if (finduid(%npc_macro_event_target) = true) {
    if (%macro_event_uid ! npc_uid) {
        npc_queue(5, 0, 2); // just incase
        return;
    }
    // warning message https://youtu.be/TCQl_9PALag?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=395
    // this video goes 50t after respawn without warning message https://youtu.be/KkyVz0tSOQA?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=176
    // 3 warning messages: https://youtu.be/g5k3XuXxyYQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
    if (last_int = 0) {
        npc_anim(emote_wave, 20);
        npc_say("Ehem... master <displayname>?");
    } else if (last_int = 1) {
        npc_anim(emote_think, 20);
        npc_say("Are you there, master <displayname>?");
    } else if (last_int = 2) {
        npc_anim(emote_angry, 20);
        npc_say("You shouldn't ignore a Genie master <displayname>?");
    } else if (last_int = 3) { // failing genie: https://youtu.be/MS7ADB-hh18?t=6
        queue(genie_fail, 0);
        return;
    }
    npc_queue(4, add(last_int, 1), 20);
}

[queue,genie_fail]
if (npc_finduid(%macro_event_uid) = true) {
    if (random(2) < 1 & inv_freespace(inv) < inv_size(inv)) { // 50% chance to teleport or scatter inv
        ~macro_event_fail_scatter_inv;
    } else {
        ~macro_event_fail_teleport;
    }
}

[ai_queue5,macro_event_genie]
// only confirmed for mysterious old man
npc_delay(2);
npc_anim(emote_cry, 20);
npc_say("I lose more friends that way.");
npc_delay(2);
~macro_event_disappear;

[ai_queue6,macro_event_genie]
npc_anim(emote_wave, 20);
npc_delay(2);
~macro_event_disappear;

[opnpc1,macro_event_genie]
if (uid ! %npc_macro_event_target) {
    if (.finduid(%npc_macro_event_target) = true) {
        ~chatnpc("<p,sad>Sorry, I'm trying to talk to <.text_gender("Master", "Mistress")> <.displayname>, but <.text_gender("he", "she")> seems to be ignoring me."); // osrs
    }
    return;
}
if (%macro_event_uid ! npc_uid) {
    ~chatnpc("<p,neutral>Have a nice day, <text_gender("master", "mistress")>.");
    return;
}
%macro_event_uid = null;
%macro_event = ^no_macro_event;
inv_add(inv, lamp, 1);
// delays are taken from this video: https://youtu.be/EN4c2hV65Qk?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
npc_queue(6, 0, 2);
// 2005 https://imgur.com/jrofzN2
~chatnpc("<p,neutral>Ah, so you are there <text_gender("master", "mistress")>. I'm so glad you|summoned me. Please take this lamp, and make your|wish!");

[ai_timer,macro_event_genie]
if (~macro_event_lost = true) {
    // only confirmed for mysterious old man
    npc_queue(5, 0, 0);
    return;
}
if (finduid(%npc_macro_event_target) = true) {
    if (%macro_event_uid = npc_uid) {
        npc_setmode(playerfollow); // this is here because they used ~chatnpc back in the day, which sets the npc
    }
}
