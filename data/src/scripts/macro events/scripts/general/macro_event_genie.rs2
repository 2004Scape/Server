[label,macro_event_genie_spawn]
spotanim_map(spotanim_86, npc_coord, 124, 0);
sound_synth(genie_appear, 0, 0);
npc_say("Greetings <text_gender("master", "mistress")> <displayname>!");
npc_setmode(playerfollow);
%npc_attacking_uid = uid;

[opnpc1,macro_event_genie]
if (uid ! %npc_attacking_uid) {
    ~chatnpc(sad, "Sorry, I'm trying to talk to <text_gender("Master", "Mistress")> <displayname>, but <text_gender("he", "she")> seems to be ignoring me."); // osrs
    return;
}
if (%macro_event = ^no_macro_event) {
    ~chatnpc(neutral, "Have a nice day, <text_gender("master", "mistress")>.");
    return;
}
%macro_event = ^no_macro_event;
~macro_event_give_reward(lamp, 1);
// delays are taken from this video: https://youtu.be/EN4c2hV65Qk?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
%npc_macro_event_status = ^macro_event_wave;
npc_settimer(3);
// dialogue taken from osrs. Neutral is a guess.
~chatnpc(neutral, "Ah, so you are there, <text_gender("master", "mistress")> <displayname>. I'm so glad you summoned me. Please take this lamp and make your wish.");

[ai_timer,macro_event_genie]
if (~macro_event_lost = true) {
    // only confirmed for mysterious old man
    npc_anim(emote_cry, 20);
    npc_say("I lose more friends that way.");
    ~macro_event_disappear;
    return;
}

// warning message https://youtu.be/TCQl_9PALag?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=395
// this video goes 50t after respawn without warning message https://youtu.be/KkyVz0tSOQA?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=176
// 3 warning messages: https://youtu.be/g5k3XuXxyYQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
if (%npc_macro_event_status = 20) {
    npc_anim(emote_wave, 20);
    npc_say("Ehem... master <displayname>?");
} else if (%npc_macro_event_status = 40) {
    npc_anim(emote_think, 20);
    npc_say("Are you there, master <displayname>?");
} else if (%npc_macro_event_status = 60) {
    npc_anim(emote_angry, 20);
    npc_say("You shouldn't ignore a Genie master <displayname>?");
} else if (%npc_macro_event_status = 80) { // failing genie: https://youtu.be/MS7ADB-hh18?t=6
    %macro_event = ^no_macro_event;
    npc_say("No one ignores me!");
    npc_anim(human_castteleport, 0);
    npc_delay(1);
    npc_del;
    if (random(2) < 1 & inv_freespace(inv) < inv_size(inv)) { // 50% chance to teleport or scatter inv
        ~macro_event_fail_scatter_inv;
    } else {
        ~macro_event_fail_teleport;
    }
    return;
} else if (%npc_macro_event_status = ^macro_event_wave) { // wave
    npc_anim(emote_wave, 20);
    npc_settimer(3);
    npc_delay(2);
    ~macro_event_disappear;
    return;
}
%npc_macro_event_status = calc(%npc_macro_event_status + 1);