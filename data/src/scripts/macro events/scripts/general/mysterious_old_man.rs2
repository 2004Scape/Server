[label,mysterious_old_man_general_macro_event_spawn]
npc_anim(emote_bow, 20);
npc_say("Greetings <displayname>!");
npc_setmode(playerfollow);
%npc_attacking_uid = uid;

[opnpc1,mysterious_old_man_general_macro_event]
if (%macro_event = 0) {
    ~chatnpc(neutral, "Goodbye.");
    return;
}
%npc_macro_event_patience = 0;
%macro_event = 0;
~macro_event_give_reward(~mysterious_old_man_general_macro_event_reward);
// dialogue taken from osrs. Neutral is a guess.
npc_delay(3);
npc_anim(emote_wave, 20);
npc_delay(2);
~macro_event_disappear;
~chatnpc(neutral, "Ah, so you are there. I hoped you would talk to me, I get so lonely. Here, have a present! I must be going now though.");
// delays are taken from this video: https://youtu.be/JeQUY1m1wRo?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=137

[proc,mysterious_old_man_general_macro_event_reward]()(namedobj, int)
// drops taken from osrs, confirmed through https://web.archive.org/web/20040817204238/http://www.runescapecommunity.com:80/index.php?showtopic=104849
def_int $random = random(150);
// 10/150 chance for 80, 160, 320, 480, 640
if ($random < 50) return(coins, multiply(pow(2, ~random_range(3, 6)), 10));
if ($random < 82) return(uncut_sapphire, 1); // 32/150 uncut sapphire 
if ($random < 98) return(kebab, 1); // 16/150 kebab
if ($random < 114) return(uncut_emerald, 1); // 16/150 uncut emerald
if ($random < 128) return(spinach_roll, 1); // 14/150 spinach roll
if ($random < 136) return(uncut_ruby, 1); // 8/150 uncut ruby
if ($random < 142) return(coins, 240); // 6/150 240 coins
if ($random < 146) return(cosmic_talisman, 1); // 4/150 cosmic talisma
if ($random < 148) return(uncut_diamond, 1); // 2/150 uncut diamond
if ($random < 149) return(half_key1, 1); // 1/150 half key
if ($random < 150) return(half_key2, 1); // 1/150 half key


[ai_timer,mysterious_old_man_general_macro_event]
if (finduid(%npc_attacking_uid) = false) { // https://youtu.be/T0ulsgorBkY?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT
    npc_anim(emote_cry, 20);
    npc_say("I lose more friends that way.");
    %npc_macro_event_patience = 0;
    ~macro_event_disappear;
    return;
}

// https://youtu.be/_PPxvNjIgq4?t=63
// https://youtu.be/TCQl_9PALag?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=374
%npc_macro_event_patience = calc(%npc_macro_event_patience + 1);

// warning
if (%npc_macro_event_patience = 4) {
    npc_anim(emote_angry, 20);
    npc_say("It's really rude to ignore someone, <displayname>!");
    return;
}
// fail
if (%npc_macro_event_patience = 5) {
    %npc_macro_event_patience = 0;
    %macro_event = 0;
    ~macro_event_fail_teleport;
    ~macro_event_disappear;
    clearsofttimer(general_macro_events);
    return;
}