[proc,macro_event_triffid_spawn]
npc_setmode(none);
npc_anim(seq_348, 0);
%npc_int = ^triffid_growing;
%npc_macro_event_target = uid;
sound_synth(triffid_appear, 0, 0);

[opnpc1,macro_event_triffid_friendly]
if (%npc_macro_event_target ! uid) {
    mes("It's not here for you."); // osrs
    return;
}
if (%npc_int = ^triffid_growing) {
    // doesnt exist in osrs but i found a screenshot https://imgur.com/7xyWFHk
    mes("The fruit isn't ready to be picked yet...");
    return;
}
inv_add(inv, strange_fruit, 1);
sound_synth(pick, 0, 0);
// https://youtu.be/ghfKpXe7N5s?t=24
mes("You pick the fruit from the plant."); // osrs
%macro_event = ^no_macro_event;
npc_anim(seq_350, 0); // take thing away
npc_delay(22); // delayed when despawning in osrs
npc_del;

// this timer runs twice, first is for plant growing, second is for becoming hostile
// 90 ticks https://youtu.be/94bDnUMTMXQ?t=374
[ai_timer,macro_event_triffid_friendly]
if (%npc_int = ^triffid_ready_to_attack) { // turn hostile
    npc_anim(seq_351, 60);
    npc_delay(4);

    npc_changetype(macro_event_triffid_hostile, ^max_32bit_int);
    npc_delay(1);
    npc_settimer(1);
    if (finduid(%npc_macro_event_target) = true) {
        %aggressive_npc = npc_uid; // interupt the player if they're in combat https://youtu.be/tw66JWQzpD0?t=32
        %macro_event = ^no_macro_event;
        npc_setmode(opplayer2);
    }
    return;
}
%npc_int = ^triffid_ready_to_attack;


// deletes if its not in opplayer2 mode
// https://youtu.be/94bDnUMTMXQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=466
// https://youtu.be/t7iOUMQv6Ws?t=148
[ai_timer,macro_event_triffid_hostile]
if (~macro_event_lost_hostile = true) {
    npc_anim(seq_355, 0);
    npc_delay(1);
    npc_del;
}