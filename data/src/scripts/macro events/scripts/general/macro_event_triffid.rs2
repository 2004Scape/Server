[ai_queue4,macro_event_triffid_friendly](int $arg)
npc_setmode(none);
npc_anim(seq_348, 0);
%npc_macro_event_status = ^triffid_growing;
if (finduid(%npc_attacking_uid) = true) {
    sound_synth(triffid_appear, 0, 0);
    %macro_event = ^no_macro_event;
}

[opnpc1,macro_event_triffid_friendly]
if (%npc_attacking_uid ! uid) {
    mes("It's not here for you."); // osrs
    return;
}
if (%npc_macro_event_status = ^triffid_growing) {
    // doesnt exist in osrs but i found a screenshot https://imgur.com/7xyWFHk
    mes("The fruit isn't ready to be picked yet...");
    return;
}
inv_add(inv, strange_fruit, 1);
sound_synth(pick, 0, 0);
// https://youtu.be/ghfKpXe7N5s?t=24
mes("You pick the fruit from the plant."); // osrs
npc_anim(seq_350, 0); // take thing away
npc_delay(22); // delayed when despawning in osrs
npc_del;

// this timer runs twice, first is for plant growing, second is for becoming hostile
// 90 ticks https://youtu.be/94bDnUMTMXQ?t=374
[ai_timer,macro_event_triffid_friendly]
if (%npc_macro_event_status = ^triffid_ready_to_attack) { // turn hostile
    npc_anim(seq_351, 60);
    npc_delay(4);

    npc_changetype(macro_event_triffid_hostile);
    npc_delay(1);
    if (finduid(%npc_attacking_uid) = true) {
        npc_setmode(opplayer2);
    }
}
%npc_macro_event_status = ^triffid_ready_to_attack;


[ai_timer,macro_event_triffid_hostile]
if (npc_getmode = opplayer2) {
    return;
}
// deletes if its not in opplayer2 mode
// https://youtu.be/94bDnUMTMXQ?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT&t=466
// https://youtu.be/t7iOUMQv6Ws?t=148
npc_anim(seq_355, 0);
npc_delay(1);
npc_del;