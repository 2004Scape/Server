[proc,macro_event_mysterious_old_man_general_spawn]
spotanim_map(small_smokepuff, npc_coord, 124, 0);
sound_synth(smokepuff, 0, 0);
npc_anim(emote_bow, 20);
npc_say("Greetings <displayname>!");
%npc_macro_event_target = uid;

// to match genie in this video: https://www.youtube.com/watch?v=el-h9hbSH80&t=293s
npc_setmode(playerfollow);

// other videos:
// https://youtu.be/_PPxvNjIgq4?t=63

// warnings https://youtu.be/Ai89wqupYqA
// https://youtu.be/HpmsgVshwxg?t=267
[ai_timer,macro_event_mysterious_old_man_general]
if (~macro_event_lost = true) {
    // only confirmed for mysterious old man https://youtu.be/HwGAzcmvF9k?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
    npc_queue(5, 0, 0);
    return;
}

if (finduid(%npc_macro_event_target) = true) {
    if (%npc_int = 0) {
        npc_anim(emote_wave, 20);
        npc_say("ehem... Hello <displayname>!");
    } else if (%npc_int = 1) {
        npc_anim(emote_think, 20);
        npc_say("Hello, are you there <displayname>!");
    } else if (%npc_int = 2) {
        npc_anim(emote_angry, 20);
        npc_say("It's really rude to ignore someone, <displayname>!");
    } else if (%npc_int = 3) { // fail
        npc_say("No-one ignores me!");
        npc_anim(human_castteleport, 0);
        %macro_event = ^no_macro_event;
        // https://youtu.be/Ai89wqupYqA?t=45
        queue(macro_event_fail_teleport, 0);
        queue(macro_event_fail_note_inv, 0);
        npc_delay(0);
        npc_del;
        return;
    }
    %npc_int = add(%npc_int, 1);
}

[ai_queue5,macro_event_mysterious_old_man_general] // despawn
npc_anim(emote_cry, 20);
npc_say("I lose more friends that way.");
npc_delay(2);
~macro_event_disappear;

[ai_queue6,macro_event_mysterious_old_man_general] // say goodbye to player
npc_anim(emote_wave, 20);
npc_delay(2);
~macro_event_disappear;

[opnpc1,macro_event_mysterious_old_man_general]
if (uid ! %npc_macro_event_target) {
    // https://youtu.be/U_TRFGTfIY0?t=276
    if (.finduid(%npc_macro_event_target) = true) {
        ~chatnpc("<p,neutral>Sorry, but I'm trying to talk to <.displayname>. However <.text_gender("he", "she")> appears to be ignoring me...");
        return;
    }
    ~chatnpc("<p,neutral>Goodbye.");
    return;
}
if (%npc_int = ^mysterious_old_man_happy) {
    ~chatnpc("<p,neutral>Goodbye.");
    return;
}
%npc_int = ^mysterious_old_man_happy;
%macro_event = ^no_macro_event;
inv_add(inv, ~macro_event_mysterious_old_man_general_reward);
// dialogue taken from osrs. Neutral is a guess.
npc_queue(6, 0, 2); // say goodbye to player
npc_settimer(0);
~chatnpc("<p,neutral>Ah, so you are there. I hoped you would talk to me, I get so lonely. Here, have a present! I must be going now though."); // https://youtu.be/HpmsgVshwxg?t=305 uses chatnpc
// delays are taken from this video: https://youtu.be/JeQUY1m1wRo?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=137

[proc,macro_event_mysterious_old_man_general_reward]()(namedobj, int)
// drops taken from osrs, confirmed through https://web.archive.org/web/20040817204238/http://www.runescapecommunity.com:80/index.php?showtopic=104849

// gives casket instead of main rewards - sudden
// https://web.archive.org/web/20040606142810/http://www.tip.it/runescape/index.php?page=rs2randon.htm
// if ($random < 50) return(coins, multiply(pow(2, ~random_range(3, 6)), 10));
// if ($random < 82) return(uncut_sapphire, 1); // 32/150 uncut sapphire 
// if ($random < 98) return(kebab, 1); // 16/150 kebab
// if ($random < 114) return(uncut_emerald, 1); // 16/150 uncut emerald
// if ($random < 128) return(spinach_roll, 1); // 14/150 spinach roll
// if ($random < 136) return(uncut_ruby, 1); // 8/150 uncut ruby
// if ($random < 142) return(coins, 240); // 6/150 240 coins
// if ($random < 146) return(cosmic_talisman, 1); // 4/150 cosmic talisma
// if ($random < 148) return(uncut_diamond, 1); // 2/150 uncut diamond
// if ($random < 149) return(half_key1, 1); // 1/150 half key
// if ($random < 150) return(half_key2, 1); // 1/150 half key

def_int $random = random(150);
if (map_members = false) {
    $random = random(36);
}
if ($random < 16) { // 16/150 kebab?
    return(kebab, 1);
} else if ($random < 30) {// 14/150 spinach roll
    return(spinach_roll, 1);
} else if ($random < 36) { // 6/150 240 coins
    return(coins, 240);
} else {
    return(casket, 1);
}
