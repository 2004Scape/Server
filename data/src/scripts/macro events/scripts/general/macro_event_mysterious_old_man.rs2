[ai_queue4,macro_event_mysterious_old_man_general](int $arg)
spotanim_map(small_smokepuff, npc_coord, 124, 0);
if (finduid(%npc_attacking_uid) = true) {
    sound_synth(smokepuff, 0, 0);
    npc_anim(emote_bow, 20);
    npc_say("Greetings <displayname>!");
    npc_delay(1);
    npc_setmode(playerfollow);
}

[ai_queue5,macro_event_mysterious_old_man_general](int $arg) // despawn
npc_anim(emote_cry, 20);
npc_say("I lose more friends that way.");
npc_delay(2);
~macro_event_disappear;

[ai_queue6,macro_event_mysterious_old_man_general](int $arg) // say goodbye to player
npc_anim(emote_wave, 20);
npc_delay(2);
~macro_event_disappear;

[opnpc1,macro_event_mysterious_old_man_general]
if (uid ! %npc_attacking_uid) {
    // https://youtu.be/U_TRFGTfIY0?t=276
    ~chatnpc(neutral, "Sorry, but I'm trying to talk to <displayname>. However he appears to be ignoring me...");
    return;
}
if (%macro_event = ^no_macro_event | %macro_event_uid ! npc_uid) {
    ~chatnpc(neutral, "Goodbye.");
    return;
}
%macro_event = ^no_macro_event;
~macro_event_give_reward(~macro_event_mysterious_old_man_general_reward);
// dialogue taken from osrs. Neutral is a guess.
npc_queue(6, 0, 2); // say goodbye to player
~chatnpc(neutral, "Ah, so you are there. I hoped you would talk to me, I get so lonely. Here, have a present! I must be going now though.");
// delays are taken from this video: https://youtu.be/JeQUY1m1wRo?list=PLn23LiLYLb1bQ7Hwp77KoNBjKvpZQTfJT&t=137

[proc,macro_event_mysterious_old_man_general_reward]()(namedobj, int)
// drops taken from osrs, confirmed through https://web.archive.org/web/20040817204238/http://www.runescapecommunity.com:80/index.php?showtopic=104849

// gives casket instead of main rewards - sudden
// https://web.archive.org/web/20040606142810/http://www.tip.it/runescape/index.php?page=rs2randon.htm
// if ($random < 50) return(coins, multiply(pow(2, ~random_range(3, 6)), 10));
// if ($random < 82) return(uncut_sapphire, 1); // 32/150 uncut sapphire 
// if ($random < 98) return(kebab, 1); // 16/150 kebab
// if ($random < 114) return(uncut_emerald, 1); // 16/150 uncut emerald
// if ($random < 128) return(spinach_roll, 1); // 14/150 spinach roll
// if ($random < 136) return(uncut_ruby, 1); // 8/150 uncut ruby
// if ($random < 142) return(coins, 240); // 6/150 240 coins
// if ($random < 146) return(cosmic_talisman, 1); // 4/150 cosmic talisma
// if ($random < 148) return(uncut_diamond, 1); // 2/150 uncut diamond
// if ($random < 149) return(half_key1, 1); // 1/150 half key
// if ($random < 150) return(half_key2, 1); // 1/150 half key

def_int $random = random(150);
if (map_members = false) {
    $random = random(36);
}
if ($random < 16) { // 16/150 kebab?
    return(kebab, 1);
} else if ($random < 30) {// 14/150 spinach roll
    return(spinach_roll, 1);
} else if ($random < 36) { // 6/150 240 coins
    return(coins, 240);
} else {
    return(casket, 1);
}

[ai_timer,macro_event_mysterious_old_man_general]
if (~macro_event_lost_and_follow = true) {
    // only confirmed for mysterious old man https://youtu.be/HwGAzcmvF9k?list=PLn23LiLYLb1Y3P9S9qZbijcJihiD416jT
    npc_queue(5, 0, 0);
    return;
}
// other videos:
// https://youtu.be/_PPxvNjIgq4?t=63

// warnings https://youtu.be/Ai89wqupYqA
// https://youtu.be/HpmsgVshwxg?t=267
if (finduid(%npc_attacking_uid) = true) {
    if (%macro_event = ^no_macro_event) {
        npc_queue(5, 0, 2); // just incase
        return;
    }
    if (%npc_macro_event_status = 1) {
        npc_anim(emote_wave, 20);
        npc_say("ehem... Hello <displayname>!");
    } else if (%npc_macro_event_status = 2) {
        npc_anim(emote_think, 20);
        npc_say("Hello, are you there <displayname>!");
    } else if (%npc_macro_event_status = 3) {
        npc_anim(emote_angry, 20);
        npc_say("It's really rude to ignore someone, <displayname>!");
    } else if (%npc_macro_event_status = 4) { // fail
        // https://youtu.be/Ai89wqupYqA?t=45
        ~macro_event_fail_teleport;
        ~macro_event_fail_note_inv; // not sure if this existed but Sudden and a couple others remember it being a thing
    }
    %npc_macro_event_status = calc(%npc_macro_event_status + 1);
}
