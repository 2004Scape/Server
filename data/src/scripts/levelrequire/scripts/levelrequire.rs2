// todo osrs and 2004 seems to have different level req messages.
// note: we have correct level up messages for 2004.
// https://youtu.be/RHwRNK4ZuOM?t=411
// todo need more info on 2004 messages. all of these are osrs messages.

[label,levelrequire_attack](int $level, int $slot)
if (stat_base(attack) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an attack level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_defence](int $level, int $slot)
if (stat_base(defence) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a defence level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_ranged](int $level, int $slot)
if (stat_base(ranged) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a Ranged level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_magic_and_attack](int $magic_level, int $attack_level, int $slot)
if (stat_base(magic) < $magic_level | stat_base(attack) < $attack_level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a Magic level of <tostring($magic_level)> and an Attack level of <tostring($attack_level)>.");
    return;
}
~equip($slot);

[label,levelrequire_ranged_and_defence](int $ranged_level, int $defence_level, int $slot)
if (stat_base(ranged) < $ranged_level | stat_base(defence) < $defence_level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a ranged level of <tostring($ranged_level)> and a defence level of <tostring($defence_level)>.");
    return;
}
~equip($slot);

[label,levelrequire_dragon_slayer_quest_attack](int $level, int $slot)
if (~levelrequire_dragon_slayer_quest = false) {
    return;
}
if (stat_base(attack) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Attack level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_dragon_slayer_quest_defence](int $level, int $slot)
if (~levelrequire_dragon_slayer_quest = false) {
    return;
}
if (stat_base(defence) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Defence level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_dragon_slayer_quest_ranged_and_defence](int $ranged_level, int $defence_level, int $slot)
if (~levelrequire_dragon_slayer_quest = false) {
    return;
}
@levelrequire_ranged_and_defence($ranged_level, $defence_level, $slot);

[label,levelrequire_heroes_quest_attack](int $level, int $slot)
if (~levelrequire_heroes_quest = false) {
    return;
}
if (stat_base(attack) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Attack level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_legends_quest_defence](int $level, int $slot)
if (~levelrequire_legends_quest = false) {
    return;
}
if (stat_base(defence) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Defence level of <tostring($level)>.");
    return;
}
~equip($slot);

[label,levelrequire_zanaris_quest_attack](int $level, int $slot)
if (~levelrequire_zanaris_quest = false) {
    return;
}
if (stat_base(attack) < $level) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Attack level of <tostring($level)>.");
    return;
}
~equip($slot);

[proc,levelrequire_dragon_slayer_quest]()(boolean)
if (%dragon_progress < ^dragon_complete) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to complete the Dragon Slayer quest first."); // todo confirm for 2004
    return(false);
}
return(true);

[proc,levelrequire_heroes_quest]()(boolean)
if (%hero_progress < ^hero_complete) {
    mes("You have not earned the right to wield this yet.");
    mes("You need to complete the Heroes' Guild entry quest."); // todo confirm for 2004
    return(false);
}
return(true);

[proc,levelrequire_legends_quest]()(boolean)
if (%legends_progress < ^legends_complete) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to complete the Legends' Quest."); // todo confirm for 2004
    return(false);
}
return(true);

[proc,levelrequire_zanaris_quest]()(boolean)
if (%zanaris_progress < ^zanaris_complete) {
    mes("You have not earned the right to wear this yet.");
    mes("You need to complete the Lost City of Zanaris Quest."); // todo confirm for 2004
    return(false);
}
return(true);

[label,levelrequire_iban_staff](int $slot)
// https://oldschool.runescape.wiki/w/Update:Various_tweaks_to_the_game
// > Fixed a bug which meant the Iban-staff could be wielded at level 49, despite stating a requirement of 50.
if (stat(magic) < 50) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have a Magic level of 50.");
    return;
}
if (stat(attack) < 50) {
    mes("You are not a high enough level to use this item.");
    mes("You need to have an Attack level of 50.");
    return;
}
// https://youtu.be/n_ip_vEDbCA?si=0uESoOC98Fvl7HqS&t=2333
mes("You equip the staff of Iban.");
~equip($slot);
